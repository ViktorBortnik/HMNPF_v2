
&ИзменениеИКонтроль("СформироватьФайлыПоРазделу6")
Функция ХМСформироватьФайлыПоРазделу6(ОбъектРодитель, МВТ, ГотовыеФайлы, ДанныеФормы, Протокол)
	//За основу берем ВТ_ПС_Остатки
	Запрос = Новый Запрос; 
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("Организация", ОбъектРодитель.Организация);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ОбъектРодитель.ДатаФормированияРеестра));
	Запрос.УстановитьПараметр("КонецПериодаГраница", Новый Граница(КонецДня(ОбъектРодитель.ДатаФормированияРеестра), ВидГраницы.Включая)); 
	Запрос.УстановитьПараметр("КонецПериодаГодНазад", КонецДня(ДобавитьМесяц(ОбъектРодитель.ДатаФормированияРеестра,-12)));
	Запрос.УстановитьПараметр("ДатаНачалаВремен", '19800101');

	ВзносыФиз = Новый СписокЗначений;
	ВзносыФиз.Добавить(Справочники.уп_ТипыСумм.ВзносыФиз);
	ВзносыФиз.Добавить(Справочники.уп_ТипыСумм.ВзносыТр);  
	Запрос.УстановитьПараметр("ВзносыФиз", ВзносыФиз);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТ_ПС_Остатки.НомерСчета КАК Справочник.уп_ПенсионныеСчета) КАК НомерСчета,
	|	ВТ_ПС_Остатки.Участник КАК Участник,
	|	ВТ_ПС_Остатки.Состояние КАК Состояние,
	|	ВТ_ПС_Остатки.ДатаСостояния КАК ДатаСостояния,
	|	ВТ_ПС_Остатки.ОстатокВРезерве КАК ОстатокВРезерве,
	|	ВТ_ПС_Остатки.ОстатокНачисленныхПенсий КАК ОстатокНачисленныхПенсий,
	|	ВТ_ПС_Остатки.ОстатокВозвратыПенсий КАК ОстатокВозвратыПенсий,  
	|	ВТ_ПС_Остатки.ОстатокОпПенсий КАК ОстатокОпПенсий 
	|ПОМЕСТИТЬ ВТ_Пенсионеры_Входящая
	|ИЗ
	|	ВТ_ПС_Остатки КАК ВТ_ПС_Остатки
	|ГДЕ
	|	(ВТ_ПС_Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ВыплатнойПериод)
	|			ИЛИ ВТ_ПС_Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.НачисленияПриостановлены))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	NULL КАК НомерСчета
	|ПОМЕСТИТЬ ВТ_ВыгруженныеПенсионеры"; 
	РезультатЗапроса = Запрос.Выполнить();  

	#Область ТекстОсновногоЗапроса 
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 500000
	|	ВЫРАЗИТЬ(ВТ_Пенсионеры_Входящая.НомерСчета КАК Справочник.уп_ПенсионныеСчета) КАК НомерСчета,
	|	ВЫРАЗИТЬ(ВТ_Пенсионеры_Входящая.НомерСчета КАК Справочник.уп_ПенсионныеСчета).Владелец КАК ПенсионныйДоговор,
	|	ВЫРАЗИТЬ(ВТ_Пенсионеры_Входящая.НомерСчета КАК Справочник.уп_ПенсионныеСчета).Код КАК НомерСчетаКод,
	|	ВТ_Пенсионеры_Входящая.Участник КАК Участник,
	|	ВТ_Пенсионеры_Входящая.Состояние КАК Состояние,
	|	ВТ_Пенсионеры_Входящая.ДатаСостояния КАК ДатаСостояния,
	|	ЕСТЬNULL(ВТ_Пенсионеры_Входящая.ОстатокВРезерве, 0) КАК ОстатокВРезерве
	|ПОМЕСТИТЬ ВТ_ПодобранныеПенсионеры
	|ИЗ
	|	ВТ_Пенсионеры_Входящая КАК ВТ_Пенсионеры_Входящая
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыгруженныеПенсионеры КАК ВТ_ВыгруженныеПенсионеры
	|		ПО ВТ_Пенсионеры_Входящая.НомерСчета = ВТ_ВыгруженныеПенсионеры.НомерСчета
	|ГДЕ
	|	ВТ_ВыгруженныеПенсионеры.НомерСчета ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыгруженныеПенсионеры.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ ВТ_ВыгруженныеПенсионеры_Временная
	|ИЗ
	|	ВТ_ВыгруженныеПенсионеры КАК ВТ_ВыгруженныеПенсионеры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПодобранныеПенсионеры.НомерСчета
	|ИЗ
	|	ВТ_ПодобранныеПенсионеры КАК ВТ_ПодобранныеПенсионеры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВыгруженныеПенсионеры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыгруженныеПенсионеры_Временная.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ ВТ_ВыгруженныеПенсионеры
	|ИЗ
	|	ВТ_ВыгруженныеПенсионеры_Временная КАК ВТ_ВыгруженныеПенсионеры_Временная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВыгруженныеПенсионеры_Временная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СхемаСчетаСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СхемаСчетаСрезПоследних.Схема КАК Схема,
	|	уп_СхемаСчетаСрезПоследних.ТипПенсии КАК ТипПенсии,
	|	уп_ПенсионныеСхемы.ТипПенсионнойСхемы КАК ТипПенсионнойСхемы
	|ПОМЕСТИТЬ ВТ_ДанныеСхем
	|ИЗ
	|	РегистрСведений.уп_СхемаСчета.СрезПоследних(&КонецПериода, ) КАК уп_СхемаСчетаСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уп_ПенсионныеСхемы КАК уп_ПенсионныеСхемы
	|		ПО уп_СхемаСчетаСрезПоследних.Схема = уп_ПенсионныеСхемы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.Схема КАК Схема,
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора КАК ШаблонДоговора,
	|	уп_ПенсионныеСхемы.ФормаНПО КАК ФормаНПО,
	|	уп_ПенсионныеСхемы.Фондирование КАК Фондирование,
	|	уп_ШаблоныДоговоров.ПорядокИндексацииДляАСВ КАК ПорядокИндексацииДляАСВ,
	|	уп_ШаблоныДоговоров.ПроцентИндексации КАК ПроцентИндексации,
	|	уп_ШаблоныДоговоров.ПояснениеКПорядкуИндексации КАК ПояснениеКПорядкуИндексации,
	|	уп_ШаблоныДоговоров.ПравопреемствоДляАСВ КАК ПравопреемствоДляАСВ,
	|	уп_ШаблоныДоговоров.ГарантированныйПериод КАК ГарантированныйПериод
	|ПОМЕСТИТЬ ВТ_Параметры
	|ИЗ
	|	РегистрСведений.уп_ПараметрыПенсионногоДоговора.СрезПоследних(&КонецПериода, ) КАК уп_ПараметрыПенсионногоДоговораСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уп_ПенсионныеСхемы КАК уп_ПенсионныеСхемы
	|		ПО уп_ПараметрыПенсионногоДоговораСрезПоследних.Схема = уп_ПенсионныеСхемы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уп_ШаблоныДоговоров КАК уп_ШаблоныДоговоров
	|		ПО уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора = уп_ШаблоныДоговоров.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.ФормаВыплаты КАК ФормаВыплаты,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.Банк КАК Банк,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
	|	БанковскиеСчета.НомерСчета КАК НомерРасчетногоСчета,
	|	Банки.Наименование КАК БанкНаименование,
	|	Банки.Код КАК БИК,
	|	Банки.КоррСчет КАК КоррСчет
	|ПОМЕСТИТЬ ВТ_ДанныеЛицевыхСчетов
	|ИЗ
	|	РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(&КонецПериода, ) КАК уп_ЛицевыеСчетаУчастниковСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Банки КАК Банки
	|			ПО БанковскиеСчета.Банк = Банки.Ссылка
	|		ПО уп_ЛицевыеСчетаУчастниковСрезПоследних.РасчетныйСчет = БанковскиеСчета.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
#Вставка
//++https://redmine.npf.com/issues/4274
	|ВЫБРАТЬ
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	СУММА(уп_СчетчикНачислений.КоличествоОборот) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|		уп_СчетчикНачислений.Количество КАК КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&КонецПериода) КАК хм_СрезСчетчик
	|			ПО уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета
	|				И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния
	|				И (хм_СрезСчетчик.ДатаУстановкиСостояния >= &ДатаНачалаВремен)
	|	ГДЕ
	|		уп_СчетчикНачислений.Период <= &КонецПериода
	|		И уп_СчетчикНачислений.Период >= &ДатаНачалаВремен
	|		И хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		хм_СрезСчетчик.НомерСчета,
	|		хм_СрезСчетчик.Количество
	|	ИЗ
	|		РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&КонецПериода) КАК хм_СрезСчетчик
	|	ГДЕ
	|		хм_СрезСчетчик.ДатаУстановкиСостояния >= &ДатаНачалаВремен	
	|
	|) КАК уп_СчетчикНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СчетчикНачислений.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
//--https://redmine.npf.com/issues/4274
#КонецВставки
	|ВЫБРАТЬ
	|	ВТ_ПодобранныеПенсионеры.НомерСчета КАК НомерСчета,
	|	ВТ_ПодобранныеПенсионеры.НомерСчета.Владелец КАК ПенсионныйДоговор,
	|	ВТ_ПодобранныеПенсионеры.НомерСчета.Код КАК НомерСчетаКод,
	|	ВТ_ПодобранныеПенсионеры.Участник КАК Участник,
	|	ВТ_ПодобранныеПенсионеры.ОстатокВРезерве КАК ОстатокВРезерве,
	|	ВТ_УчастникиИндексы.ИДУчастника КАК ИДУчастника,
	|	ВТ_УчастникиИндексы.КодУчастника КАК КодУчастника,
	|	ВТ_ПДИндексы.ИДПД КАК ИДПД, 
	|	ВТ_ПДИндексы.КодПенсионногоДоговора КАК КодПенсионногоДоговора, 
	|	ВТ_ПодобранныеПенсионеры.Состояние КАК Состояние,
	|	ВТ_ПодобранныеПенсионеры.ДатаСостояния КАК ДатаСостояния,
	|	ВТ_ДанныеСхем.Схема КАК Схема,
	|	ЕСТЬNULL(ВТ_ДолиПравопреемников.Правопреемники, """") КАК СтрокаПравопреемники,
	|	ЕСТЬNULL(ВТ_ДолиПравопреемников.Доли, """") КАК СтрокаДоли,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеСхем.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПустаяСсылка)
	|			ТОГДА ВТ_ДанныеСхем.ТипПенсионнойСхемы
	|		ИНАЧЕ ВТ_ДанныеСхем.ТипПенсии
	|	КОНЕЦ КАК ТипПенсии,
	|	уп_РазмерПенсииСрезПоследних.Размер КАК РазмерПенсии,
	|	уп_ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат КАК ПериодичностьВыплат,
	|	уп_ЗадолженностьПоПенсиямОстатки.СуммаПенсииОстаток + уп_ЗадолженностьПоПенсиямОстатки.СуммаДоплатыОстаток КАК СуммаЗадолженности,
	|	уп_ПоступленияКПерерасчетуОстатки.СуммаОстаток КАК СуммаКПерерасчету,
	|	ВТ_ДанныеЛицевыхСчетов.ФормаВыплаты КАК ФормаВыплаты,
	|	ВТ_ДанныеЛицевыхСчетов.Банк КАК Банк,
	|	ВТ_ДанныеЛицевыхСчетов.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ВТ_ДанныеЛицевыхСчетов.РасчетныйСчет КАК РасчетныйСчет,
	|	ВТ_УчастникиИндексы.ФизЛицо КАК ФизЛицо,
	|	уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат КАК КоличествоВыплатНазначено,
	|	уп_СчетчикНачисленийОбороты.КоличествоОборот КАК КоличествоВыплатНачислено,
	|	ВТ_Параметры.ШаблонДоговора КАК ШаблонДоговора,
	|	ВТ_Параметры.ПорядокИндексацииДляАСВ КАК ПорядокИндексацииДляАСВ,
	|	ВТ_Параметры.ПроцентИндексации КАК ПроцентИндексации,
	|	ВТ_Параметры.ПояснениеКПорядкуИндексации КАК ПояснениеКПорядкуИндексации,
	|	ВТ_ДанныеСхем.ТипПенсионнойСхемы КАК ТипПенсионнойСхемы,
	|	ВТ_Параметры.Схема КАК СхемаДоговора,
	|	ВТ_Параметры.ФормаНПО КАК ФормаНПО,
	|	ВТ_Параметры.Фондирование КАК Фондирование,
	|	уп_ДатыОкончанияСрочнойВыплатыСрезПоследних.ДатаОкончанияСрочнойВыплаты КАК ДатаОкончанияСрочнойВыплаты,
	|	ВТ_Параметры.ПравопреемствоДляАСВ КАК ШаблонДоговораПравопреемствоДляАСВ,
	|	ВТ_Параметры.ГарантированныйПериод КАК ШаблонДоговораГарантированныйПериод,
	|	уп_СостоянияПССрезПервых.Период КАК ДатаНачалаВыплат,
	|	ВТ_ДанныеЛицевыхСчетов.БанкНаименование КАК БанкНаименование,
	|	ВТ_ДанныеЛицевыхСчетов.БИК КАК БИК,
	|	ВТ_ДанныеЛицевыхСчетов.КоррСчет КАК КоррСчет,
	|	ВТ_ДанныеЛицевыхСчетов.НомерРасчетногоСчета КАК НомерРасчетногоСчета
	|ПОМЕСТИТЬ ВТ_Пенсионеры_Данные
	|ИЗ
	|	ВТ_ПодобранныеПенсионеры КАК ВТ_ПодобранныеПенсионеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УчастникиИндексы КАК ВТ_УчастникиИндексы
	|		ПО ВТ_ПодобранныеПенсионеры.Участник = ВТ_УчастникиИндексы.Участник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПДИндексы КАК ВТ_ПДИндексы
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета.Владелец = ВТ_ПДИндексы.ПенсионныйДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеСхем КАК ВТ_ДанныеСхем
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = ВТ_ДанныеСхем.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДолиПравопреемников КАК ВТ_ДолиПравопреемников
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = ВТ_ДолиПравопреемников.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсии.СрезПоследних(&КонецПериода, ) КАК уп_РазмерПенсииСрезПоследних
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_РазмерПенсииСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(&КонецПериода, ) КАК уп_ПериодичностьВыплатСрезПоследних
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_ПериодичностьВыплатСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеЛицевыхСчетов КАК ВТ_ДанныеЛицевыхСчетов
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = ВТ_ДанныеЛицевыхСчетов.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ДатыОкончанияСрочнойВыплаты.СрезПоследних(&КонецПериода, ) КАК уп_ДатыОкончанияСрочнойВыплатыСрезПоследних
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_ДатыОкончанияСрочнойВыплатыСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ЗадолженностьПоПенсиям.Остатки(&КонецПериодаГраница, ) КАК уп_ЗадолженностьПоПенсиямОстатки
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_ЗадолженностьПоПенсиямОстатки.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ПоступленияКПерерасчету.Остатки КАК уп_ПоступленияКПерерасчетуОстатки
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_ПоступленияКПерерасчетуОстатки.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоНазначенныхВыплат.СрезПоследних(&КонецПериода, ) КАК уп_КоличествоНазначенныхВыплатСрезПоследних
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_КоличествоНазначенныхВыплатСрезПоследних.НомерСчета
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(&ДатаНачалаВремен, &КонецПериода, Период, ) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Параметры КАК ВТ_Параметры
	|		ПО ВТ_ПодобранныеПенсионеры.ПенсионныйДоговор = ВТ_Параметры.ПенсионныйДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПервых(, Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ВыплатнойПериод)) КАК уп_СостоянияПССрезПервых
	|		ПО ВТ_ПодобранныеПенсионеры.НомерСчета = уп_СостоянияПССрезПервых.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
	|	СУММА(ВЫБОР
	|			КОГДА уп_РезервыОстатки.ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.ИПС)
	|				ТОГДА уп_РезервыОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстатокИПС,
	|	СУММА(ВЫБОР
	|			КОГДА уп_РезервыОстатки.ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.СПС)
	|				ТОГДА уп_РезервыОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстатокСПС
	|ПОМЕСТИТЬ ВТ_ОстаткиПенсионеров
	|ИЗ
	|	РегистрНакопления.уп_Резервы.Остатки(
	|			&КонецПериодаГраница,
	|			НомерСчета В
	|				(ВЫБРАТЬ
	|					ВТ_Пенсионеры_Данные.НомерСчета
	|				ИЗ
	|					ВТ_Пенсионеры_Данные)) КАК уп_РезервыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_РезервыОстатки.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ВыплатыОбороты.НомерСчета КАК НомерСчета,
	|	МАКСИМУМ(ВЫРАЗИТЬ(уп_ВыплатыОбороты.Регистратор КАК Документ.уп_ВыплатныеРеестры).Дата) КАК ДатаПоследнейВыплаты
	|ПОМЕСТИТЬ ВТ_ДатыПоследнейВыплаты
	|ИЗ
	|	РегистрНакопления.уп_Выплаты.Обороты(&КонецПериодаГодНазад, &КонецПериода, Регистратор, ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыПенсий)) КАК уп_ВыплатыОбороты
	|ГДЕ
	|	уп_ВыплатыОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатыОбороты.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ВыплатыОбороты.НомерСчета КАК НомерСчета,
	|	уп_ВыплатыОбороты.СуммаОборот КАК СуммаПоследнейПенсии
	|ПОМЕСТИТЬ ВТ_СуммыПоследнейВыплаты
	|ИЗ
	|	РегистрНакопления.уп_Выплаты.Обороты(&КонецПериодаГодНазад, &КонецПериода, Регистратор, ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыПенсий)) КАК уп_ВыплатыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДатыПоследнейВыплаты КАК ВТ_ДатыПоследнейВыплаты
	|		ПО уп_ВыплатыОбороты.НомерСчета = ВТ_ДатыПоследнейВыплаты.НомерСчета
	|			И уп_ВыплатыОбороты.Регистратор.Дата = ВТ_ДатыПоследнейВыплаты.ДатаПоследнейВыплаты
	|ГДЕ
	|	уп_ВыплатыОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатыОбороты.НомерСчета,
	|	уп_ВыплатыОбороты.СуммаОборот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Пенсионеры_Данные.НомерСчета КАК НомерСчета,
	|	ВТ_Пенсионеры_Данные.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	ВТ_Пенсионеры_Данные.НомерСчетаКод КАК НомерСчетаКод,
	|	ВТ_Пенсионеры_Данные.Участник КАК Участник,
	|	ВТ_Пенсионеры_Данные.ОстатокВРезерве КАК ОстатокВРезерве,
	|	ВТ_Пенсионеры_Данные.ИДУчастника КАК ИДУчастника,
	|	ВТ_Пенсионеры_Данные.КодУчастника КАК КодУчастника,
	|	ВТ_Пенсионеры_Данные.ИДПД КАК ИДПД,
	|	ВТ_Пенсионеры_Данные.КодПенсионногоДоговора КАК КодПенсионногоДоговора,
	|	ВТ_Пенсионеры_Данные.Состояние КАК Состояние,
	|	ВТ_Пенсионеры_Данные.ДатаСостояния КАК ДатаСостояния,
	|	ВТ_Пенсионеры_Данные.Схема КАК Схема,
	|	ВТ_Пенсионеры_Данные.СтрокаПравопреемники КАК СтрокаПравопреемники,
	|	ВТ_Пенсионеры_Данные.СтрокаДоли КАК СтрокаДоли,
	|	ВТ_Пенсионеры_Данные.ТипПенсии КАК ТипПенсии,
	|	ВТ_Пенсионеры_Данные.РазмерПенсии КАК РазмерПенсии,
	|	ВТ_Пенсионеры_Данные.ПериодичностьВыплат КАК ПериодичностьВыплат,
	|	ЕСТЬNULL(ВТ_Пенсионеры_Данные.СуммаЗадолженности, 0) КАК СуммаЗадолженности,
	|	ВТ_Пенсионеры_Данные.СуммаКПерерасчету КАК СуммаКПерерасчету,
	|	ВТ_Пенсионеры_Данные.ФормаВыплаты КАК ФормаВыплаты,
	|	ВТ_Пенсионеры_Данные.Банк КАК Банк,
	|	ВТ_Пенсионеры_Данные.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	ВТ_Пенсионеры_Данные.РасчетныйСчет КАК РасчетныйСчет,
	|	ВТ_Пенсионеры_Данные.ФизЛицо КАК ФизЛицо,
	|	ВТ_Пенсионеры_Данные.КоличествоВыплатНазначено КАК КоличествоВыплатНазначено,
	|	ВТ_Пенсионеры_Данные.КоличествоВыплатНачислено КАК КоличествоВыплатНачислено,
	|	ВТ_Пенсионеры_Данные.ШаблонДоговора КАК ШаблонДоговора,
	|	ЕСТЬNULL(ВТ_Пенсионеры_Данные.ПорядокИндексацииДляАСВ, """") КАК ПорядокИндексацииДляАСВ,
	|	ВТ_Пенсионеры_Данные.ПроцентИндексации КАК ПроцентИндексации,
	|	ВТ_Пенсионеры_Данные.ПояснениеКПорядкуИндексации КАК ПояснениеКПорядкуИндексации,
	|	ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокИПС, 0) КАК ОстатокИПС,
	|	ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокСПС, 0) КАК ОстатокСПС,
	|	ЕСТЬNULL(ВТ_СуммыПоследнейВыплаты.СуммаПоследнейПенсии, 0) КАК СуммаПоследнейПенсии,
	|	АдресаИзТЧ.ЗначенияПолей КАК ЗначенияПолей,
	|	АдресаИзТЧ.Страна КАК Страна,
	|	АдресаИзТЧ.Регион КАК Регион,
	|	АдресаИзТЧ.Город КАК Город,
	|	ЕСТЬNULL(АдресаИзТЧ.Представление, """") КАК Представление,
	|	АдресаПочт.ЗначенияПолейАП КАК ЗначенияПолейАП,
	|	АдресаПочт.СтранаАП КАК СтранаАП,
	|	АдресаПочт.РегионАП КАК РегионАП,
	|	АдресаПочт.ГородАП КАК ГородАП,
	|	ЕСТЬNULL(АдресаПочт.ПредставлениеАП, """") КАК ПредставлениеАП,
	|	ВТ_Пенсионеры_Данные.ДатаНачалаВыплат КАК ДатаНачалаВыплат,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокИПС, 0) <> 0
	|			ТОГДА ""ИПС""
	|		КОГДА ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ДоИсчерпанияСредствНаСчете)
	|				И ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокСПС, 0) = 0
	|			ТОГДА ""ИПС""
	|		КОГДА ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|				И ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокСПС, 0) = 0
	|			ТОГДА ""ИПС""
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаНПО = ЗНАЧЕНИЕ(Перечисление.уп_ФормаРезерваНПО.СПС)
	|			ТОГДА ""СПС""
	|		ИНАЧЕ ""ИПС""
	|	КОНЕЦ КАК ТипСчетаВыплат,
	|	ВЫБОР
	|		КОГДА ВТ_Пенсионеры_Данные.Фондирование = ЗНАЧЕНИЕ(Перечисление.уп_ФондированиеПенсионныхСхем.Фондированная)
	|			ТОГДА ""фондированная""
	|		ИНАЧЕ ""иное""
	|	КОНЕЦ КАК Фондирование,
	|	ВЫБОР
	|		КОГДА ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПожизненныхВыплат)
	|			ТОГДА ""пожизненно""
	|		КОГДА ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|			ТОГДА ""срочно""
	|		КОГДА ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ДоИсчерпаниясредствнаСчете)
	|			ТОГДА ""до исчерпания""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ПериодВыплаты,
	|	ВЫБОР
	|		КОГДА ВТ_Пенсионеры_Данные.ПериодичностьВыплат = ЗНАЧЕНИЕ(Перечисление.уп_ПериодичностьВыплат.Ежегодно)
	|			ТОГДА 12
	|		КОГДА ВТ_Пенсионеры_Данные.ПериодичностьВыплат = ЗНАЧЕНИЕ(Перечисление.уп_ПериодичностьВыплат.Полугодие)
	|			ТОГДА 6
	|		КОГДА ВТ_Пенсионеры_Данные.ПериодичностьВыплат = ЗНАЧЕНИЕ(Перечисление.уп_ПериодичностьВыплат.Ежеквартально)
	|			ТОГДА 3
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПериодичностьВыплатыНП,
	|	ВТ_Пенсионеры_Данные.ДатаОкончанияСрочнойВыплаты КАК ДатаОкончанияСрочнойВыплаты,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_Пенсионеры_Данные.КоличествоВыплатНазначено, 0) - ЕСТЬNULL(ВТ_Пенсионеры_Данные.КоличествоВыплатНачислено, 0) <= 1
	|				И ВТ_Пенсионеры_Данные.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|			ТОГДА ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокИПС, 0) + ЕСТЬNULL(ВТ_ОстаткиПенсионеров.ОстатокСПС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоследняяСрочнаяВыплата,
	|	ВТ_Пенсионеры_Данные.ШаблонДоговораПравопреемствоДляАСВ КАК ШаблонДоговораПравопреемствоДляАСВ,
	|	ЕСТЬNULL(ВТ_Пенсионеры_Данные.ШаблонДоговораГарантированныйПериод, 0) КАК ШаблонДоговораГарантированныйПериод,
	|	ВЫБОР
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ФормыВыплаты.РасчетныйСчет)
	|			ТОГДА 1
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ФормыВыплаты.Почта)
	|			ТОГДА 2
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ФормыВыплаты.ПочтаСДоставкойНаДом)
	|			ТОГДА 2
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ФормыВыплаты.ПочтовыйПереводСУведомлением)
	|			ТОГДА 2
	|		КОГДА ВТ_Пенсионеры_Данные.ФормаВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ФормыВыплаты.ПочтаСДоставкойНаДомУведомлением)
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СпособВыплаты,
	|	ВТ_Пенсионеры_Данные.БанкНаименование КАК БанкНаименование,
	|	ВТ_Пенсионеры_Данные.БИК КАК БИК,
	|	ВТ_Пенсионеры_Данные.КоррСчет КАК КоррСчет,
	|	ВТ_Пенсионеры_Данные.НомерРасчетногоСчета КАК НомерРасчетногоСчета
	|ИЗ
	|	ВТ_Пенсионеры_Данные КАК ВТ_Пенсионеры_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиПенсионеров КАК ВТ_ОстаткиПенсионеров
	|		ПО ВТ_Пенсионеры_Данные.НомерСчета = ВТ_ОстаткиПенсионеров.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыПоследнейВыплаты КАК ВТ_СуммыПоследнейВыплаты
	|		ПО ВТ_Пенсионеры_Данные.НомерСчета = ВТ_СуммыПоследнейВыплаты.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ АдресаИзТЧ КАК АдресаИзТЧ
	|		ПО ВТ_Пенсионеры_Данные.Участник = АдресаИзТЧ.Участник
	|		ЛЕВОЕ СОЕДИНЕНИЕ АдресаПочт КАК АдресаПочт
	|		ПО ВТ_Пенсионеры_Данные.Участник = АдресаПочт.Участник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ДанныеСхем
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Параметры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ДанныеЛицевыхСчетов";


	#КонецОбласти

	ЗапросПроверка = Новый Запрос;
	ЗапросПроверка.МенеджерВременныхТаблиц = МВТ;
	#Область ТекстЗапросаПроверкиОстатка	
	ЗапросПроверка.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВТ_Пенсионеры_Входящая.НомерСчета) КАК Количество
	|ИЗ
	|	ВТ_Пенсионеры_Входящая КАК ВТ_Пенсионеры_Входящая
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыгруженныеПенсионеры КАК ВТ_ВыгруженныеПенсионеры
	|		ПО ВТ_Пенсионеры_Входящая.НомерСчета = ВТ_ВыгруженныеПенсионеры.НомерСчета
	|ГДЕ
	|	ВТ_ВыгруженныеПенсионеры.НомерСчета ЕСТЬ NULL"; 
	#КонецОбласти	

	РазделительЗначений = ОбъектРодитель.РазделительЗначений;
	РазделительСтрок = ОбъектРодитель.РазделительСтрок;
	МаксКолвоСтрок = ОбъектРодитель.МаксСтрокВФайле;

	Если ЗначениеЗаполнено(ОбъектРодитель.ТипФайла) Тогда
		ВременныйФайл = ПолучитьИмяВременногоФайла("."+ОбъектРодитель.ТипФайла);
	Иначе
		ВременныйФайл = ПолучитьИмяВременногоФайла(".txt");
	КонецЕсли;
	Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
		ЗаписьТекста = Новый ЗаписьТекста(ВременныйФайл, "windows-1251", РазделительСтрок, Истина);
	Иначе
		Макет = ПолучитьМакет("NP");
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");   
		ТабДок = Новый ТабличныйДокумент;      
		ТабДок.Вывести(ОбластьШапка);
	КонецЕсли;	

	ФайлЗаписан = Истина;
	НомерФайла = 1;
	НомерСтроки = 0;
	СтрокВФайле = 0;
	ЕстьФайлы = Ложь;

	Пока Истина Цикл
		ПроверкаКоличества = ЗапросПроверка.Выполнить().Выбрать();		
		ПроверкаКоличества.Следующий();
		Если ПроверкаКоличества.Количество = 0 Тогда
			Если НЕ ФайлЗаписан Тогда  
				Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
					ЗаписьТекста.Закрыть(); 
				Иначе 
					ТабДок.Записать(ВременныйФайл, "xlsx");
				КонецЕсли;
				АдресФайла = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ВременныйФайл));
				ГотовыеФайлы.Вставить(АдресФайла, ПолучитьИмяФайла(ОбъектРодитель, "NP", НомерФайла));
				ЕстьФайлы = Истина;
			КонецЕсли;
			Прервать;
		КонецЕсли;
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Выборка_Количество = Выборка.Количество();
		Пока Выборка.Следующий() Цикл
			НомерСтроки = НомерСтроки + 1;
			СтрокВФайле = СтрокВФайле + 1; 

			Если Выборка.ПорядокИндексацииДляАСВ = Перечисления.уп_ПорядокИндексацииДляАСВ.ПерерасчетПоДоходуСверх Тогда
				ПорядокИндексации = "Перерасчет по доходу сверх "+Строка(Выборка.ПроцентИндексации)+" % годовых"; 
			ИначеЕсли Выборка.ПорядокИндексацииДляАСВ = Перечисления.уп_ПорядокИндексацииДляАСВ.НаФиксированныйПроцент Тогда
				ПорядокИндексации = Выборка.ПроцентИндексации+" % годовых";
			ИначеЕсли  Выборка.ПорядокИндексацииДляАСВ = Перечисления.уп_ПорядокИндексацииДляАСВ.Иное Тогда
				ПорядокИндексации =  Выборка.ПояснениеКПорядкуИндексации;
			Иначе 
				ПорядокИндексации = ?(Выборка.ПорядокИндексацииДляАСВ = Перечисления.уп_ПорядокИндексацииДляАСВ.ПустаяСсылка() 
				ИЛИ Выборка.ПорядокИндексацииДляАСВ = "" 
				ИЛИ Выборка.ПорядокИндексацииДляАСВ = Null, "индексация не предусмотрена", Выборка.ПорядокИндексацииДляАСВ);
			КонецЕсли;  

			Если Выборка.ШаблонДоговораПравопреемствоДляАСВ = Перечисления.уп_ПорядокПравопреемства.ГарантированныйПериод Тогда
				ДатаКонцаГарантированногоПериода = ДобавитьМесяц(Выборка.ДатаНачалаВыплат,Выборка.ШаблонДоговораГарантированныйПериод*12)-1;
				ДатаКонцаГарантированногоПериодаСтрока = Формат(ДатаКонцаГарантированногоПериода, "ДФ=dd.MM.yyyy");
			Иначе
				ДатаКонцаГарантированногоПериода = "";
			КонецЕсли;

			Если Выборка.ШаблонДоговораПравопреемствоДляАСВ = Перечисления.уп_ПорядокПравопреемства.Полное
				или Выборка.ШаблонДоговораПравопреемствоДляАСВ = Перечисления.уп_ПорядокПравопреемства.Выплатное Тогда
				УказыватьПравопреемников = Истина;
			ИначеЕсли Выборка.ШаблонДоговораПравопреемствоДляАСВ = Перечисления.уп_ПорядокПравопреемства.ГарантированныйПериод
				И  КонецДня(ОбъектРодитель.ДатаФормированияРеестра)<=ДатаКонцаГарантированногоПериода Тогда
				УказыватьПравопреемников = Истина;
			ИначеЕсли Выборка.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат 
				И Выборка.ОстатокИПС>0 Тогда
				УказыватьПравопреемников = Истина;
			ИначеЕсли Выборка.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.ДоИсчерпанияСредствНаСчете
				И Выборка.ОстатокИПС>0 Тогда
				УказыватьПравопреемников = Истина;
			Иначе
				УказыватьПравопреемников = Ложь;
			Конецесли;

			Если Выборка.СпособВыплаты = 1 Тогда
				//Реквизиты = СокрЛП(Выборка.БанкНаименование)+";"+Выборка.БИК+";"+Выборка.КоррСчет+";"+Выборка.НомерРасчетногоСчета+";"+Выборка.НомерЛицевогоСчета;
				Если СтрДлина(СокрЛП(Выборка.НомерЛицевогоСчета)) = 20 Тогда 
					Реквизиты = СокрЛП(Выборка.БанкНаименование)+";"+Выборка.БИК+";"+Выборка.КоррСчет+";"+СокрЛП(Выборка.НомерЛицевогоСчета)+";";   //это под вопросом
				Иначе	
					Реквизиты = СокрЛП(Выборка.БанкНаименование)+";"+Выборка.БИК+";"+Выборка.КоррСчет+";"+Выборка.НомерРасчетногоСчета+";"+СокрЛП(Выборка.НомерЛицевогоСчета);
				КонецЕсли;
			Иначе
				Реквизиты = "";
			КонецЕсли;
			Если Выборка.СпособВыплаты = 2 Тогда
				//Адрес = ПолучитьСведенияОбАдресе(Выборка, Выборка.Участник, "Основной", Протокол); 
				Адрес = Выборка.ПредставлениеАП; 
				Если НЕ ЗначениеЗаполнено(Адрес) Тогда 
					Адрес = Выборка.Представление;
				КонецЕсли;	
			Иначе
				Адрес = "";
			КонецЕсли;	
			Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
				ЗаписьТекста.ЗаписатьСтроку(
				"NP" + РазделительЗначений +   
				"f"+Формат(Выборка.ИДУчастника,"ЧГ=0;ЧН=") + РазделительЗначений + 
				"d"+Формат(Выборка.ИДПД,"ЧГ=0;ЧН=") + РазделительЗначений + 
				?(Выборка.ТипСчетаВыплат = "СПС", "СПС", ВРЕГ(СокрЛП(Выборка.НомерСчетаКод))) + РазделительЗначений + 
				Выборка.ТипСчетаВыплат + РазделительЗначений +
				ПорядокИндексации + РазделительЗначений +
				Выборка.Фондирование + РазделительЗначений + 
				Выборка.ПериодВыплаты + РазделительЗначений +  
				Строка(Выборка.ПериодичностьВыплатыНП) + РазделительЗначений +
				Формат(Выборка.РазмерПенсии, "ЧДЦ=2; ЧГ=0;ЧН=") + РазделительЗначений + 
				?(Выборка.Состояние = Перечисления.уп_СостоянияПС.НачисленияПриостановлены, Формат(Выборка.ДатаСостояния, "ДФ=dd.MM.yyyy"),"") + РазделительЗначений + 
				Формат(Выборка.СуммаЗадолженности, "ЧДЦ=2; ЧГ=0;ЧН=") + РазделительЗначений +
				?(Выборка.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат, Формат(Выборка.ДатаОкончанияСрочнойВыплаты, "ДФ=dd.MM.yyyy"),"") + РазделительЗначений +  
				?(Строка(Выборка.ПериодВыплаты) = "пожизненно",
				"",
				"остаток") + РазделительЗначений + /// п.6.15 Указания ?(Выборка.ПоследняяСрочнаяВыплата = 0,"",Формат(Выборка.ПоследняяСрочнаяВыплата, "ЧДЦ=2; ЧГ=0;ЧН="))) + РазделительЗначений + 
				Формат(Выборка.ОстатокИПС, "ЧДЦ=2; ЧГ=0;ЧН=") + РазделительЗначений +
				?(Выборка.СуммаКПерерасчету = Null, Формат(0, "ЧДЦ=2; ЧГ=0;ЧН="),Формат(Выборка.СуммаКПерерасчету, "ЧДЦ=2; ЧГ=0;ЧН=")) + РазделительЗначений + 
				?(Выборка.ШаблонДоговораПравопреемствоДляАСВ = Null ИЛИ Выборка.ШаблонДоговораПравопреемствоДляАСВ = Перечисления.уп_ПорядокПравопреемства.ПустаяСсылка(), "нет", Выборка.ШаблонДоговораПравопреемствоДляАСВ) + РазделительЗначений + 
				ДатаКонцаГарантированногоПериодаСтрока + РазделительЗначений + 
				?(УказыватьПравопреемников,Выборка.СтрокаПравопреемники,"") + РазделительЗначений +
				?(УказыватьПравопреемников,Выборка.СтрокаДоли,"") + РазделительЗначений +   
				Выборка.СпособВыплаты + РазделительЗначений + 
				Реквизиты + РазделительЗначений + 
				Адрес
				);
			Иначе
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				ОбластьСтрока.Параметры.ИДУчастника = "f"+Формат(Выборка.ИДУчастника,"ЧГ=0;ЧН=");
				ОбластьСтрока.Параметры.ИДПД = "d"+Формат(Выборка.ИДПД,"ЧГ=0;ЧН=");
				Если Выборка.ТипСчетаВыплат = "СПС" Тогда
					ОбластьСтрока.Параметры.НомерСчетаКод = "СПС";
				КонецЕсли;
				ОбластьСтрока.Параметры.ПорядокИндексации  = ПорядокИндексации;
				Если Выборка.Состояние = Перечисления.уп_СостоянияПС.НачисленияПриостановлены Тогда
					ОбластьСтрока.Параметры.ДатаПриостановки = Выборка.ДатаСостояния;
				КонецЕсли;
				Если  Выборка.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат Тогда
					ОбластьСтрока.Параметры.ДатаОкончанияВыплаты = Выборка.ДатаОкончанияСрочнойВыплаты;
				КонецЕсли;	 
				ОбластьСтрока.Параметры.ДатаКонцаГарантированногоПериодаСтрока = ДатаКонцаГарантированногоПериодаСтрока;
				Если НЕ УказыватьПравопреемников Тогда
					ОбластьСтрока.Параметры.СтрокаПравопреемники = "";
					ОбластьСтрока.Параметры.СтрокаДоли = "";
				КонецЕсли;
				ОбластьСтрока.Параметры.Реквизиты = Реквизиты;
				ОбластьСтрока.Параметры.Адрес = Адрес;  
				ТабДок.Вывести(ОбластьСтрока);
			КонецЕсли;	

			Если СтрокВФайле = МаксКолвоСтрок ИЛИ НомерСтроки = Выборка_Количество Тогда	
				ФайлЗаписан = Ложь;
				Если СтрокВФайле = МаксКолвоСтрок Тогда
					Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
						ЗаписьТекста.Закрыть();  
					Иначе
						ТабДок.Записать(ВременныйФайл, "xlsx");
					КонецЕсли;	
					ФайлЗаписан = Истина;
					АдресФайла = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ВременныйФайл));
					ГотовыеФайлы.Вставить(АдресФайла, ПолучитьИмяФайла(ОбъектРодитель, "NP", НомерФайла));
					ЕстьФайлы = Истина;

					СтрокВФайле = 0;
					НомерФайла = НомерФайла + 1;					

					Если ЗначениеЗаполнено(ОбъектРодитель.ТипФайла) Тогда
						ВременныйФайл = ПолучитьИмяВременногоФайла("."+ОбъектРодитель.ТипФайла);
					Иначе
						ВременныйФайл = ПолучитьИмяВременногоФайла(".txt");
					КонецЕсли;

					Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
						ЗаписьТекста = Новый ЗаписьТекста(ВременныйФайл, "windows-1251", РазделительСтрок, Истина); 
					Иначе 
						Макет = ПолучитьМакет("NP");
						ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
						ОбластьСтрока = Макет.ПолучитьОбласть("Строка");   
						ТабДок = Новый ТабличныйДокумент;      
						ТабДок.Вывести(ОбластьШапка);
					КонецЕсли;	
				КонецЕсли;

				Если НомерСтроки = Выборка_Количество Тогда
					НомерСтроки = 0;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;

	Если НЕ ЕстьФайлы Тогда
		//добавим пустой файл
		Если ЗначениеЗаполнено(ОбъектРодитель.ТипФайла) Тогда
			ВременныйФайл = ПолучитьИмяВременногоФайла("."+ОбъектРодитель.ТипФайла);
		Иначе
			ВременныйФайл = ПолучитьИмяВременногоФайла(".txt");
		КонецЕсли;

		Если ОбъектРодитель.ТипФайла<>"xlsx" Тогда
			ЗаписьТекста = Новый ЗаписьТекста(ВременныйФайл, "windows-1251", РазделительСтрок, Истина);
			ЗаписьТекста.Закрыть();
		Иначе 
			ТабДок = Новый ТабличныйДокумент; 
			ТабДок.Записать(ВременныйФайл, "xlsx");
		КонецЕсли;
		АдресФайла = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ВременныйФайл));
		ГотовыеФайлы.Вставить(АдресФайла, ПолучитьИмяФайла(ОбъектРодитель, "NP", Неопределено));
	КонецЕсли;

КонецФункции
