
&НаСервереБезКонтекста
&ИзменениеИКонтроль("ПолучитьДанные")
Функция ХМПолучитьДанные(Параметры)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Параметры["НачалоПериода"]);
	Запрос.УстановитьПараметр("КонецПериода", Параметры["КонецПериода"]);
	Запрос.УстановитьПараметр("Организация", Параметры["Организация"]);
	Запрос.УстановитьПараметр("СписокСостояний", Параметры["СписокСостояний"]);
	Запрос.УстановитьПараметр("ПоследнийЭлементЗапроса", ?(ЗначениеЗаполнено(Параметры["ПоследнийЭлементЗапроса"]), Параметры["ПоследнийЭлементЗапроса"], Справочники.уп_ПенсионныеСчета.ПустаяСсылка()));
	#Область ТекстЗапроса	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 500000
	|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СостоянияПССрезПоследних.НомерСчета.Участник.уп_ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТ_СписокСчетов
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&КонецПериода, НомерСчета.Владелец.Организация = &Организация) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.НомерСчета > &ПоследнийЭлементЗапроса
	|	И уп_СостоянияПССрезПоследних.Состояние В(&СписокСостояний)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_СписокСчетов.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТ_ФизЛица
	|ИЗ
	|	ВТ_СписокСчетов КАК ВТ_СписокСчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизЛицо,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление,
	|	ФизическиеЛицаКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей
	|ПОМЕСТИТЬ ФактАдресТЧ
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФизЛица КАК ВТ_ФизЛица
	|		ПО ФизическиеЛицаКонтактнаяИнформация.Ссылка = ВТ_ФизЛица.ФизЛицо
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизЛицо,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление,
	|	ФизическиеЛицаКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей
	|ПОМЕСТИТЬ ЮрАдресТЧ
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФизЛица КАК ВТ_ФизЛица
	|		ПО ФизическиеЛицаКонтактнаяИнформация.Ссылка = ВТ_ФизЛица.ФизЛицо
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
#Вставка
//++https://redmine.npf.com/issues/4274
	|ВЫБРАТЬ
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	СУММА(уп_СчетчикНачислений.КоличествоОборот) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|		уп_СчетчикНачислений.Количество КАК КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&КонецПериода) КАК хм_СрезСчетчик
	|			ПО уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета
	|				И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния
	|				И (хм_СрезСчетчик.ДатаУстановкиСостояния >= &НачалоПериода)
	|	ГДЕ
	|		уп_СчетчикНачислений.Период <= &КонецПериода
	|		И уп_СчетчикНачислений.Период >= &НачалоПериода
	|		И хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		хм_СрезСчетчик.НомерСчета,
	|		хм_СрезСчетчик.Количество
	|	ИЗ
	|		РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&КонецПериода) КАК хм_СрезСчетчик
	|	ГДЕ
	|		хм_СрезСчетчик.ДатаУстановкиСостояния >= &НачалоПериода	
	|
	|) КАК уп_СчетчикНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СчетчикНачислений.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
//--https://redmine.npf.com/issues/4274
#КонецВставки
	|ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета.Код КАК КОД,
	|	уп_СостоянияПССрезПоследних.Состояние КАК СОСТОЯНИЕ,
	|	уп_СостоянияПССрезПоследних.НомерСчета.Владелец.Код КАК ДОГОВОР,
	|	уп_СостоянияПССрезПоследних.НомерСчета.Участник.Код КАК КОДУЧ,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК ФАМИЛИЯ,
	|	ФИОФизЛицСрезПоследних.Имя КАК ИМЯ,
	|	ФИОФизЛицСрезПоследних.Отчество КАК ОТЧЕСТВО,
	|	уп_СхемаСчетаСрезПоследних.Схема.Код КАК СХЕМА,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.Банк КАК ПОЛУЧАТЕЛЬ,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерЛицевогоСчета КАК ЛИЦЕВОЙСЧЕТ,
	|	уп_РазмерПенсииСрезПоследних.Размер КАК СУММАВЫПЛАТ,
	|	ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) КАК СРОК,
	|	ЕСТЬNULL(уп_СтартовыеСуммыСрезПоследних.Сумма, 0) КАК СТАРТСУММА,
	|	ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) КАК СЧЕТЧИКВЫПЛАТ,
	|	уп_ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат КАК ПЕРИОДИЧНОСТЬ,
	|	уп_РезервыОстатки.СуммаОстаток КАК СУММАНАСЧЕТЕ,
	|	уп_ДатаОткрытияПС.Период КАК ДАТАОТКРЫТИЯ,
	|	уп_ДатаНачалаВыплатногоПериода.Период КАК ДАТАВЫПЛАТ,
	|	уп_НомераОчередейСрезПоследних.НомерОчереди КАК НОМЕРОЧЕРЕДИ,
	|	ФИОФизЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТ_СписокСчетов.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_СписокСчетов КАК ВТ_СписокСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПоследних(&КонецПериода, ) КАК уп_СостоянияПССрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_СостоянияПССрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&КонецПериода, ) КАК ФИОФизЛицСрезПоследних
	|		ПО ВТ_СписокСчетов.ФизЛицо = ФИОФизЛицСрезПоследних.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(&КонецПериода, ) КАК уп_СхемаСчетаСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_СхемаСчетаСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(&КонецПериода, ) КАК уп_ЛицевыеСчетаУчастниковСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсии.СрезПоследних(&КонецПериода, ) КАК уп_РазмерПенсииСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_РазмерПенсииСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоНазначенныхВыплат.СрезПоследних(&КонецПериода, ) КАК уп_КоличествоНазначенныхВыплатСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_КоличествоНазначенныхВыплатСрезПоследних.НомерСчета
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(&НачалоПериода, &КонецПериода, , ) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(&КонецПериода, ) КАК уп_ПериодичностьВыплатСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_ПериодичностьВыплатСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(&КонецПериода, ) КАК уп_РезервыОстатки
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_РезервыОстатки.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПоследних(&КонецПериода, Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.НакопительныйПериод)) КАК уп_ДатаОткрытияПС
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_ДатаОткрытияПС.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПоследних(&КонецПериода, Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ВыплатнойПериод)) КАК уп_ДатаНачалаВыплатногоПериода
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_ДатаНачалаВыплатногоПериода.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_НомераОчередей.СрезПоследних(&КонецПериода, ) КАК уп_НомераОчередейСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_НомераОчередейСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СтартовыеСуммы.СрезПоследних(&КонецПериода, ) КАК уп_СтартовыеСуммыСрезПоследних
	|		ПО ВТ_СписокСчетов.НомерСчета = уп_СтартовыеСуммыСрезПоследних.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.НомерСчета КАК ПоследнийЭлементЗапроса,
	|	ВТ_Данные.КОД КАК КОД,
	|	ВТ_Данные.СОСТОЯНИЕ КАК СОСТОЯНИЕ,
	|	ВТ_Данные.ДОГОВОР КАК ДОГОВОР,
	|	ВТ_Данные.КОДУЧ КАК КОДУЧ,
	|	ВТ_Данные.ФАМИЛИЯ КАК ФАМИЛИЯ,
	|	ВТ_Данные.ИМЯ КАК ИМЯ,
	|	ВТ_Данные.ОТЧЕСТВО КАК ОТЧЕСТВО,
	|	ВТ_Данные.СХЕМА КАК СХЕМА,
	|	ВТ_Данные.ПОЛУЧАТЕЛЬ КАК ПОЛУЧАТЕЛЬ,
	|	ВТ_Данные.ЛИЦЕВОЙСЧЕТ КАК ЛИЦЕВОЙСЧЕТ,
	|	ВТ_Данные.СУММАВЫПЛАТ КАК СУММАВЫПЛАТ,
	|	ВТ_Данные.СРОК КАК СРОК,
	|	ВТ_Данные.СТАРТСУММА КАК СТАРТСУММА,
	|	ВТ_Данные.СЧЕТЧИКВЫПЛАТ КАК СЧЕТЧИКВЫПЛАТ,
	|	ВТ_Данные.ПЕРИОДИЧНОСТЬ КАК ПЕРИОДИЧНОСТЬ,
	|	ВТ_Данные.СУММАНАСЧЕТЕ КАК СУММАНАСЧЕТЕ,
	|	ВТ_Данные.ДАТАОТКРЫТИЯ КАК ДАТАОТКРЫТИЯ,
	|	ВТ_Данные.ДАТАВЫПЛАТ КАК ДАТАВЫПЛАТ,
	|	ВТ_Данные.НОМЕРОЧЕРЕДИ КАК НОМЕРОЧЕРЕДИ,
	|	ФактАдресТЧ.Представление КАК ФактАдресТЧПредставление,
	|	ФактАдресТЧ.ЗначенияПолей КАК ФактАдресТЧЗначенияПолей,
	|	ЮрАдресТЧ.Представление КАК ЮрАдресТЧПредставление,
	|	ЮрАдресТЧ.ЗначенияПолей КАК ЮрАдресТЧЗначенияПолей
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ФактАдресТЧ КАК ФактАдресТЧ
	|		ПО ВТ_Данные.ФизическоеЛицо = ФактАдресТЧ.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЮрАдресТЧ КАК ЮрАдресТЧ
	|		ПО ВТ_Данные.ФизическоеЛицо = ЮрАдресТЧ.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоследнийЭлементЗапроса";
	#КонецОбласти

	Если Параметры["СписокСостояний"].Количество() = 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И уп_СостоянияПССрезПоследних.Состояние В(&СписокСостояний)", "");
	КонецЕсли;

	Возврат Запрос.Выполнить();
КонецФункции
