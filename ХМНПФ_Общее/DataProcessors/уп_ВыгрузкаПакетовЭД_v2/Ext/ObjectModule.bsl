
&ИзменениеИКонтроль("СформироватьФайлыXML_Форма25")
Функция ХМСформироватьФайлыXML_Форма25(Знач ВходящиеПараметры)

	ПространствоИмен = "http://пф.рф/ВсВО/НПФ/ВСПН-П/2020-05-22";
	ПакетЭД = ВходящиеПараметры.ПакетЭД;
	Организация = ПакетЭД.Организация;

	СписокФайлов = Новый Массив;
	ВыборкаДанных = ПолучитьДанныеПакета_Форма25(ВходящиеПараметры.СписокПачек, ПакетЭД.Дата);

	Пока ВыборкаДанных.Следующий() Цикл

		ОписаниеФайла = ВходящиеПараметры.ДанныеФайлов[ВыборкаДанных.ПачкаЭД];
		СписокФайлов.Добавить(ОписаниеФайла);


		//ПакетЭД = ОписаниеФайла.ПакетЭД;
		//
		//ОписаниеФайла.Вставить("НомерДокумента", уп_ПакетЭД_v2.ПолучитьНовыйНомерДокумента(ПакетЭД.Организация));
		//ОписаниеФайла.Вставить("UUID", ПолучитьГУИД());
		//ОписаниеФайла.Вставить("ИмяФайла", ПолучитьИмяФайла(ПакетЭД, ОписаниеФайла.UUID));	
		//ОписаниеФайла.Вставить("НастройкиЭДО", ПолучитьНастройкиЭДОсПФР(ПакетЭД.Организация));

		Попытка

			#Область ЗаполнениеXDTO	
			СписокТипов = ПолучитьСписокТиповXDTO(ПространствоИмен).ЭДПФР;	
			СписокТипов.Вставить("Суммы", СписокТипов.Запись.Свойства.Получить("Суммы").Тип);
			СписокТипов.Вставить("ПравопреемникСуммы", СписокТипов.Правопреемник.Свойства.Получить("Суммы").Тип);
			СписокПространствИмен = ПолучитьСписокПространствИменПакета(ПространствоИмен);

			ЭДПФР = ФабрикаXDTO.Создать(СписокТипов.ЭДПФР);
			ЭДПФР.ВСПН_П = ФабрикаXDTO.Создать(СписокТипов.ВСПН_П);
			ЭДПФР.СлужебнаяИнформация = ФабрикаXDTO.Создать(СписокТипов.ТипСлужебнаяИнформацияНПФ);

			#Область ВСПН_П
			ВСПН_П = ЭДПФР.ВСПН_П;
			ВСПН_П.Реквизиты 	  = ФабрикаXDTO.Создать(СписокТипов.ТипРеквизитыДокумента);
			ВСПН_П.НПФ 		      = ФабрикаXDTO.Создать(СписокТипов.ТипСведенияНПФ);
			ВСПН_П.СписокСведений = ФабрикаXDTO.Создать(СписокТипов.СписокСведений);

			ЗаполнитьБлокРеквизиты(ВСПН_П.Реквизиты, ПакетЭД, СписокТипов, ОписаниеФайла.НомерДокумента);
			ЗаполнитьБлокНПФ(ВСПН_П.НПФ, ПакетЭД.Организация, СписокТипов);	


			#Область СписокСведений
			НомерПП = 0;
			СписокСведений = ВСПН_П.СписокСведений;

			Данные = ВыборкаДанных.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			//Данные = ПолучитьДанныеПакета_Форма25(ПакетЭД.СоставПакета.Выгрузить(, "ПачкаЭД").ВыгрузитьКолонку("ПачкаЭД"), ПакетЭД.Дата);

			Пока Данные.Следующий() Цикл

				НомерПП = НомерПП + 1;

				Запись = СписокСведений.Запись.Добавить(ФабрикаXDTO.Создать(СписокТипов.Запись));
				Запись.НомерПП = ФабрикаXDTO.Создать(СписокТипов.positiveInteger, НомерПП);		
				Запись.ЗЛ 	   = ФабрикаXDTO.Создать(СписокТипов.ТипЗастрахованноеЛицо);	
				#Область ЗЛ
				ЗЛ = Запись.ЗЛ;
				ЗЛ.ФИО = ФабрикаXDTO.Создать(СписокТипов.ТипФИО);
				ЗЛ.СНИЛС = ФабрикаXDTO.Создать(СписокТипов.ТипСтраховойНомер, Данные.СНИЛС);

				ФИО = ЗЛ.ФИО;
				Если НЕ ПустаяСтрока(Данные.Фамилия) Тогда
					ФИО.Фамилия = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Данные.Фамилия);
				КонецЕсли;
				Если НЕ ПустаяСтрока(Данные.Имя) Тогда
					ФИО.Имя = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Данные.Имя);
				КонецЕсли;
				Если НЕ ПустаяСтрока(Данные.Отчество) Тогда
					ФИО.Отчество = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Данные.Отчество);
				КонецЕсли;
				ФИО.Проверить();
				ЗЛ.Проверить();
				#КонецОбласти			

				Запись.ДатаСмерти = ФабрикаXDTO.Создать(СписокТипов.date, Данные.ДатаСмерти);

				#Область Суммы
				Запись.Суммы = ФабрикаXDTO.Создать(СписокТипов.Суммы);
				Суммы = Запись.Суммы;
				#Область СВ		
				Суммы.СВ = ФабрикаXDTO.Создать(СписокТипов.ТипСуммаНПФ);
				СВ = Суммы.СВ;

				СВ.Сумма = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСВ, 			"ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СВ.ИД 	 = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСВ_ИД, 		"ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СВ.Комп  = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаКомпенсацииСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СВ.ГВосп = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВосполненияСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СВ.ГВозм = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВозмещенияСВ,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СВ.Итого = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСВ + Данные.СуммаСВ_ИД + Данные.СуммаКомпенсацииСВ + Данные.СуммаВосполненияСВ + Данные.СуммаВозмещенияСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));

				СВ.Проверить();
				#КонецОбласти

				#Область ДСВ		
				Суммы.ДСВ = ФабрикаXDTO.Создать(СписокТипов.ТипСуммаНПФ);
				ДСВ = Суммы.ДСВ;

				ДСВ.Сумма = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаДСВ, 			  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				ДСВ.ИД 	  = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаДСВ_ИД, 		  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				ДСВ.Комп  = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаКомпенсацииДСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				ДСВ.ГВосп = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВосполненияДСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				ДСВ.ГВозм = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВозмещенияДСВ,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				ДСВ.Итого = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаДСВ + Данные.СуммаДСВ_ИД + Данные.СуммаКомпенсацииДСВ + Данные.СуммаВосполненияДСВ + Данные.СуммаВозмещенияДСВ, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));

				ДСВ.Проверить();
				#КонецОбласти

				#Область СОФН		
				Суммы.СОФН = ФабрикаXDTO.Создать(СписокТипов.ТипСуммаНПФ);
				СОФН = Суммы.СОФН;

				СОФН.Сумма = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСОФН, 			"ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СОФН.ИД    = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСОФН_ИД, 		"ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СОФН.Комп  = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаКомпенсацииСОФН, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СОФН.ГВосп = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВосполненияСОФН, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СОФН.ГВозм = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВозмещенияСОФН,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				СОФН.Итого = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаСОФН + Данные.СуммаСОФН_ИД + Данные.СуммаКомпенсацииСОФН + Данные.СуммаВосполненияСОФН + Данные.СуммаВозмещенияСОФН, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));

				СОФН.Проверить();
				#КонецОбласти

				#Область МСК		
				Суммы.МСК = ФабрикаXDTO.Создать(СписокТипов.ТипСуммаНПФ);
				МСК = Суммы.МСК;

				МСК.Сумма = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаМСК, 			  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				МСК.ИД    = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаМСК_ИД, 		  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				МСК.Комп  = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаКомпенсацииМСК, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				МСК.ГВосп = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВосполненияМСК, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				МСК.ГВозм = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаВозмещенияМСК,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
				МСК.Итого = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(Данные.СуммаМСК + Данные.СуммаМСК_ИД + Данные.СуммаКомпенсацииМСК + Данные.СуммаВосполненияМСК + Данные.СуммаВозмещенияМСК, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));

				МСК.Проверить();
				#КонецОбласти

				Суммы.ВсегоСПН = Формат(Данные.СуммаВсегоСПН, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД="".""");
				#КонецОбласти

				#Область Решение
				Запись.Решение = ФабрикаXDTO.Создать(СписокТипов.Решение);
				Решение = Запись.Решение;
				#Область Реквизиты
				Решение.Реквизиты = ФабрикаXDTO.Создать(СписокТипов.ТипРеквизитыДокумента);
				Реквизиты = Решение.Реквизиты;

				Реквизиты.Дата  = ФабрикаXDTO.Создать(СписокТипов.date, Данные.РешениеДата);
				#Удаление 
				Реквизиты.Номер = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Данные.РешениеНомер);	
				#КонецУдаления  
				#Вставка 
				// +ХМНПФ 06.10.2020 МилевскийАЮ // СформироватьФайлыXML_Форма25 // №2946
				Реквизиты.Номер = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, СокрЛП(Данные.РешениеНомер));
				// -ХМНПФ 06.10.2020 МилевскийАЮ // СформироватьФайлыXML_Форма25 // №2946
				#КонецВставки
				Реквизиты.Проверить();
				#КонецОбласти

				Решение.Тип 	 = ФабрикаXDTO.Создать(СписокТипов.string, ВРег(Данные.ТипРешения));
				Решение.Источник = ФабрикаXDTO.Создать(СписокТипов.string, ВРег(Данные.Источник));

				#Область Правопреемники
				Решение.Правопреемники = ФабрикаXDTO.Создать(СписокТипов.Правопреемники);
				Правопреемники = Решение.Правопреемники;

				Выборка = Данные.Выбрать();	
				Пока Выборка.Следующий() Цикл
					Правопреемник = ФабрикаXDTO.Создать(СписокТипов.Правопреемник);
					#Область АнкетныеДанные
					Правопреемник.АнкетныеДанные = ФабрикаXDTO.Создать(СписокТипов.АнкетныеДанные);
					АнкетныеДанные = Правопреемник.АнкетныеДанные;

					АнкетныеДанные.ФИО = ФабрикаXDTO.Создать(СписокТипов.ТипФИО);
					ФИО = АнкетныеДанные.ФИО;

					Если НЕ ПустаяСтрока(Выборка.ФамилияПравопреемника) Тогда
						ФИО.Фамилия = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Выборка.ФамилияПравопреемника);
					КонецЕсли;
					Если НЕ ПустаяСтрока(Выборка.ИмяПравопреемника) Тогда
						ФИО.Имя = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Выборка.ИмяПравопреемника);
					КонецЕсли;
					Если НЕ ПустаяСтрока(Выборка.ОтчествоПравопреемника) Тогда
						ФИО.Отчество = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, Выборка.ОтчествоПравопреемника);
					КонецЕсли;
					ФИО.Проверить();

					Если НЕ ПустаяСтрока(Выборка.СНИЛСПравопреемника) Тогда
						АнкетныеДанные.СНИЛС = ФабрикаXDTO.Создать(СписокТипов.ТипСтраховойНомер, Выборка.СНИЛСПравопреемника);
					КонецЕсли;

					АнкетныеДанные.Проверить();
					#КонецОбласти				

					#Область ПлатежныйДокумент
					Правопреемник.ПлатежныйДокумент = ФабрикаXDTO.Создать(СписокТипов.ТипРеквизитыДокумента);
					ПлатежныйДокумент = Правопреемник.ПлатежныйДокумент;

					Если ЗначениеЗаполнено(Выборка.ПлатежныйДокументДата) Тогда
						ПлатежныйДокумент.Дата  = ФабрикаXDTO.Создать(СписокТипов.date, Выборка.ПлатежныйДокументДата);
						ПлатежныйДокумент.Номер = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, НомерОбъектаБезПрефикса(Выборка.ПлатежныйДокументНомер));
					КонецЕсли;

					ПлатежныйДокумент.Проверить();
					#КонецОбласти

					#Область Заявление
					Правопреемник.Заявление = ФабрикаXDTO.Создать(СписокТипов.ТипРеквизитыДокумента);
					Заявление = Правопреемник.Заявление;

					Если ЗначениеЗаполнено(Выборка.ЗаявлениеДата) Тогда
						Заявление.Дата  = ФабрикаXDTO.Создать(СписокТипов.date, Выборка.ЗаявлениеДата);
						Заявление.Номер = ФабрикаXDTO.Создать(СписокТипов.ТипНепустаяСтрока, СокрЛП(Выборка.ЗаявлениеНомер));
					КонецЕсли;

					Заявление.Проверить();
					#КонецОбласти

					#Область Суммы
					Правопреемник.Суммы = ФабрикаXDTO.Создать(СписокТипов.ПравопреемникСуммы);
					Суммы = Правопреемник.Суммы;

					ВсегоСВ   = Выборка.СуммаСВ   + Выборка.СуммаСВ_ИД   + Выборка.СуммаКомпенсацииСВ   + Выборка.СуммаВосполненияСВ   + Выборка.СуммаВозмещенияСВ;
					ВсегоДСВ  = Выборка.СуммаДСВ  + Выборка.СуммаДСВ_ИД  + Выборка.СуммаКомпенсацииДСВ  + Выборка.СуммаВосполненияДСВ  + Выборка.СуммаВозмещенияДСВ;
					ВсегоСОФН = Выборка.СуммаСОФН + Выборка.СуммаСОФН_ИД + Выборка.СуммаКомпенсацииСОФН + Выборка.СуммаВосполненияСОФН + Выборка.СуммаВозмещенияСОФН;
					ВсегоМСК  = Выборка.СуммаМСК  + Выборка.СуммаМСК_ИД  + Выборка.СуммаКомпенсацииМСК  + Выборка.СуммаВосполненияМСК  + Выборка.СуммаВозмещенияМСК;

					Суммы.СВ   	   = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(ВсегоСВ,   "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
					Суммы.ДСВ  	   = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(ВсегоДСВ,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
					Суммы.СОФН 	   = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(ВсегоСОФН, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
					Суммы.МСК  	   = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(ВсегоМСК,  "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));
					Суммы.ВсегоСПН = ФабрикаXDTO.Создать(СписокТипов.ТипДеньги, Формат(ВсегоСВ + ВсегоДСВ + ВсегоСОФН + ВсегоМСК, "ЧН=0.00; ЧДЦ=2; ЧГ=; ЧРД=""."""));

					Суммы.Проверить();
					#КонецОбласти		
					Правопреемник.Проверить();

					Правопреемники.Правопреемник.Добавить(Правопреемник);
				КонецЦикла;

				Правопреемники.Проверить();
				#КонецОбласти		
				Решение.Проверить();
				#КонецОбласти		
				Запись.Проверить();
			КонецЦикла;
			СписокСведений.Проверить();
			#КонецОбласти
			ВСПН_П.КоличествоЗЛ = ФабрикаXDTO.Создать(СписокТипов.positiveInteger, НомерПП);	

			ВСПН_П.Проверить();
			#КонецОбласти

			ЗаполнитьБлокСлужебнаяИнформация(ЭДПФР.СлужебнаяИнформация, ПакетЭД, СписокТипов, ОписаниеФайла.НомерДокумента, ОписаниеФайла.UUID);
			#КонецОбласти

		Исключение
			ОписаниеФайла.Вставить("ОписаниеОшибки", ОписаниеОшибки());
			//Возврат ОписаниеФайла;
			уп_ПакетЭД_v2.УдалитьЗаписьИзРегистраФайлыЭДО(ПакетЭД, ОписаниеФайла.НомерДокумента);
			Продолжить;
		КонецПопытки;

		#Область ЗаполнениеЗаписиXML
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".XML");

		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла);
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписьXML.ЗаписатьНачалоЭлемента("ЭДПФР", ПространствоИмен);
		ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("", ПространствоИмен);
		Для Каждого Строка Из СписокПространствИмен Цикл
			ЗаписьXML.ЗаписатьСоответствиеПространстваИмен(Строка.Ключ, Строка.Значение);
		КонецЦикла;

		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ВСПН_П, "ВСПН-П", ПространствоИмен);
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ЭДПФР.СлужебнаяИнформация, "СлужебнаяИнформация", ПространствоИмен);

		ЗаписьXML.ЗаписатьКонецЭлемента();
		ЗаписьXML.Закрыть();
		#КонецОбласти

		ОписаниеФайла.Вставить("ДвоичныеДанные", Новый ДвоичныеДанные(ИмяВременногоФайла));	
		Попытка УдалитьФайлы(ИмяВременногоФайла); Исключение КонецПопытки;		

		//уп_ПакетЭД_v2.ОчиститьНомераДокументов(ПакетЭД);
		//
		//Возврат ОписаниеФайла;

	КонецЦикла;

	Возврат СписокФайлов;

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьБлок_Исполнитель")
Процедура ХМЗаполнитьБлок_Исполнитель(БлокИсполнитель, ПакетЭД, СписокТипов)
	Исполнитель = ПолучитьДанныеИсполнителя(?(ЗначениеЗаполнено(ПакетЭД.Ответственный.ФизическоеЛицо), ПакетЭД.Ответственный.ФизическоеЛицо, ПакетЭД.Ответственный.ФизЛицо), КонецДня(ПакетЭД.КонецПериода));

	БлокИсполнитель.ФИО = ФабрикаXDTO.Создать(БлокИсполнитель.Тип().Свойства.Получить("ФИО").Тип);	
	Если НЕ Исполнитель = Неопределено Тогда
		ФИО = БлокИсполнитель.ФИО;
		Если НЕ ПустаяСтрока(Исполнитель.Фамилия) Тогда
			ФИО.Фамилия = ФабрикаXDTO.Создать(БлокИсполнитель.Тип().Свойства.Получить("ФИО").Тип.Свойства.Получить("Фамилия").Тип, Исполнитель.Фамилия);
		КонецЕсли;
		Если НЕ ПустаяСтрока(Исполнитель.Имя) Тогда
			ФИО.Имя = ФабрикаXDTO.Создать(БлокИсполнитель.Тип().Свойства.Получить("ФИО").Тип.Свойства.Получить("Имя").Тип, Исполнитель.Имя);
		КонецЕсли;
		Если НЕ ПустаяСтрока(Исполнитель.Отчество) Тогда
			ФИО.Отчество = ФабрикаXDTO.Создать(БлокИсполнитель.Тип().Свойства.Получить("ФИО").Тип.Свойства.Получить("Отчество").Тип, Исполнитель.Отчество);
		КонецЕсли;
		ФИО.Проверить();

		Если ЗначениеЗаполнено(Исполнитель.Телефон) Тогда
			#Удаление 
			БлокИсполнитель.Телефон = ФабрикаXDTO.Создать(СписокТипов.ТипНомерТелефона, Исполнитель.Телефон);
			#КонецУдаления
			#Вставка 
			// +ХМНПФ 01.04.2019 ГригорьевМА // ЗаполнитьБлок_Исполнитель() // №2144			
			Телефон = СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(Исполнитель.Телефон, "+", ""), "(", ""), ")", ""), "-", ""), " ", "");
			БлокИсполнитель.Телефон = ФабрикаXDTO.Создать(СписокТипов.ТипНомерТелефона, Телефон);
			// -ХМНПФ 01.04.2019 ГригорьевМА // ЗаполнитьБлок_Исполнитель() // №2144		
			#КонецВставки					
		Иначе
			ЗафиксироватьОшибку("Не удалось заполнить телефон в блоке исполнитель, т.к. телефон не указан в физ. лице ответственного " + ПакетЭД.Ответственный); 
		КонецЕсли;
	КонецЕсли;

	БлокИсполнитель.Проверить();
КонецПроцедуры

&ИзменениеИКонтроль("СформироватьФайлыExcel")
Процедура ХМСформироватьФайлыExcel(ПараметрыФайла, Протокол)
	Если ТипЗнч(ПараметрыФайла["ПакетЭД"]) = Тип("ДокументОбъект.уп_ПакетЭД_v2") И НЕ ТипЗнч(ПараметрыФайла["ПакетЭД"]) = Тип("ДокументСсылка.уп_ПакетЭД_v2") Тогда
		ВызватьИсключение "Входящий ПакетЭД должен иметь тип уп_ПакетЭД_v2";
	КонецЕсли;

	Если ПараметрыФайла["ПакетЭД"].ПометкаУдаления Тогда
		Протокол.ДобавитьСтроку("Пакет ЭД помечен на удаление: " + ПараметрыФайла["ПакетЭД"].Ссылка);
		Возврат;
	КонецЕсли;

	ВидЭД = ПараметрыФайла["ПакетЭД"].ВидЭД;	
	Если ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма1 Тогда
		СформироватьФайлыExcel_Форма1(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма2 Или 
		ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма3 Или
		ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма4 Или
		ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма5 Или 
		ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма6 
		Тогда
		СформироватьФайлыExcel_Форма2_Форма3_Форма4_Форма5_Форма6(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма10 Тогда
		СформироватьФайлыExcel_Форма10(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма11 Тогда
		СформироватьФайлыExcel_Форма11(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма12 Тогда
		СформироватьФайлыExcel_Форма12(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма14 Тогда
		СформироватьФайлыExcel_Форма14(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма18 Тогда
		СформироватьФайлыExcel_Форма18(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма20 Тогда
		СформироватьФайлыExcel_Форма20(ПараметрыФайла); 
	#Вставка 
		//+ ХМНПФ 23.03.2019 ГригорьевМА //СформироватьФайлыExcel() // №2121
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма22 Тогда
		ХМ_СформироватьФайлыExcel_Форма22(ПараметрыФайла);
		//- ХМНПФ 23.03.2019 ГригорьевМА //СформироватьФайлыExcel() // №2121
	#КонецВставки
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма29 Тогда
		СформироватьФайлыExcel_Форма29(ПараметрыФайла);
	ИначеЕсли ВидЭД = Перечисления.уп_ВидыЭД_v2.Форма31 Тогда
		СформироватьФайлыExcel_Форма31(ПараметрыФайла);	
	КонецЕсли;
КонецПроцедуры

//++ ХМНПФ 25.03.2019 ГригорьевМА //ХМ_СформироватьФайлыExcel_Форма22() // №2121
&НаСервере
Процедура ХМ_СформироватьФайлыExcel_Форма22(ПараметрыВыгрузки)
#Область ОбъявлениеПеременных	
	ПакетЭД 	  		  = ПараметрыВыгрузки.ПакетЭД;
	ВидЭД				  = ПакетЭД.ВидЭД;
	ТаблицаОшибок 		  = ПолучитьТаблицуОшибок();		
	НастройкиЭДО  		  = ПолучитьНастройкиЭДОсПФР(ПакетЭД.Организация, ПакетЭД.Дата);
	
#КонецОбласти
		
	Для Каждого СтрокаПакета Из ПакетЭД.СоставПакета Цикл
		ГУИД = ПолучитьГУИД();
		ИмяФайла = ПолучитьИмяВременногоФайла(".XLSX");	
	
		ТабличныйДокумент = Новый ТабличныйДокумент;
	
		Макет = ПолучитьМакет("ХМ_Excel_Форма22");	
	
		ОбластьШапка  = Макет.ПолучитьОбласть("Шапка");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		ОбластьПодвал  = Макет.ПолучитьОбласть("Подвал");
	
		СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПакетЭД.Организация, ПакетЭД.Дата);
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(ПакетЭД.Организация, ПакетЭД.Дата);
		ДанныеОтветственного = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ПакетЭД.Организация, ПакетЭД.Ответственный.ФизическоеЛицо, ПакетЭД.Дата);
		Исполнитель = ""+ДанныеОтветственного.Фамилия+" "+Лев(ДанныеОтветственного.Имя,1)+". "+Лев(ДанныеОтветственного.Отчество,1)+".";
		ДолжностьОтветственного = ДанныеОтветственного.Должность;

		НомерТаблицы = 0;

		НомерСтроки = 0;
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		НомерТаблицы = НомерТаблицы+1;
		ПенсионныйФонд = СтрокаПакета.Получатель;
		ОбластьШапка.Параметры.ДатаРеестра = Формат( ПакетЭД.Дата,"ДЛФ=DD");
		ОбластьШапка.Параметры.НомерРеестра =  Число(ПакетЭД.Номер);
	    ОбластьШапка.Параметры.НаименованиеФонда = ПакетЭД.Организация.уп_НаименованиеДляПФР;
		ОбластьШапка.Параметры.СтрокаКуда = ВРЕГ(ПенсионныйФонд.НаименованиеПолное);
		ОбластьШапка.Параметры.ИНН = СокрЛП(ПенсионныйФонд.ИНН);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ИтогоСуммаОстаток = 0;
		ИтогоСуммаСВ = 0;
		ИтогоСуммаДСВ = 0;
		ИтогоСуммаСФ = 0;
	    ИтогоСуммаВР = 0;
		ИтогоСуммаМК = 0;
		ИтогоСуммаСВ_ИД = 0;
		ИтогоСуммаДСВ_ИД = 0;
		ИтогоСуммаСФ_ИД = 0;
		ИтогоСуммаВР_ИД = 0;
		ИтогоСуммаМК_ИД = 0;

		Для каждого стр из СтрокаПакета.ПачкаЭД.ДоговорыОПС Цикл
			СуммаОстаток = стр.СуммаВыплаты;
			СуммаСВ = 0;
			СуммаДСВ = 0;
			СуммаСФ = 0;
	    	СуммаВР = 0;
			СуммаМК = 0;
			СуммаСВ_ИД = 0;
			СуммаДСВ_ИД = 0;
			СуммаСФ_ИД = 0;
			СуммаВР_ИД = 0;
			СуммаМК_ИД = 0;
            ИтогоСуммаОстаток = ИтогоСуммаОстаток + СуммаОстаток;
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ДоговорОПС", стр.ДоговорОПС);
			НайденныеСтроки = СтрокаПакета.ПачкаЭД.РасшифровкаСумм.НайтиСтроки(ПараметрыОтбора);
			Для каждого стрСуммы из НайденныеСтроки Цикл
				Если стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ВзносыФиз Тогда
					СуммаСВ = стрСуммы.СуммаВыплаты;
					ИтогоСуммаСВ = ИтогоСуммаСВ + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ДопВзносы Тогда
					СуммаДСВ = стрСуммы.СуммаВыплаты;
					ИтогоСуммаДСВ = ИтогоСуммаДСВ + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ГосПоддержка Тогда
					СуммаСФ = стрСуммы.СуммаВыплаты;
					ИтогоСуммаСФ = ИтогоСуммаСФ + стрСуммы.СуммаВыплаты;
	            ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ВзносыЮр Тогда
					СуммаВР = стрСуммы.СуммаВыплаты;
					ИтогоСуммаВР = ИтогоСуммаВР + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.МатКапитал Тогда
					СуммаМК = стрСуммы.СуммаВыплаты;
					ИтогоСуммаМК = ИтогоСуммаМК + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
					СуммаСВ_ИД = стрСуммы.СуммаВыплаты;
					ИтогоСуммаСВ_ИД = ИтогоСуммаСВ_ИД + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДДопВзносы Тогда
					СуммаДСВ_ИД = стрСуммы.СуммаВыплаты;
					ИтогоСуммаДСВ_ИД = ИтогоСуммаДСВ_ИД + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДГосПоддержка Тогда
					СуммаСФ_ИД = стрСуммы.СуммаВыплаты;
					ИтогоСуммаСФ_ИД = ИтогоСуммаСФ_ИД + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДЮр Тогда
					СуммаВР_ИД = стрСуммы.СуммаВыплаты;
					ИтогоСуммаВР_ИД = ИтогоСуммаВР_ИД + стрСуммы.СуммаВыплаты;
				ИначеЕсли стрСуммы.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
					СуммаМК_ИД = стрСуммы.СуммаВыплаты;
					ИтогоСуммаМК_ИД = ИтогоСуммаМК_ИД + стрСуммы.СуммаВыплаты;
				КонецЕсли;
			КонецЦикла;
			НомерСтроки = НомерСтроки+1;
			ФИО = стр.ЗастрахованноеЛицо.уп_ФизЛицо.ФИО;
			Фам = Лев(ФИО, Найти(ФИО, " ")-1);
			ИО = Прав(ФИО, СтрДлина(ФИО)-Найти(ФИО, " "));
			Имя = Лев(ИО, Найти(ИО, " ")-1);
			Отчество = Прав(ИО, СтрДлина(ИО)-Найти(ИО, " "));
			ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтрока.Параметры.ФамилияСтр = ВРЕГ(Фам);
			ОбластьСтрока.Параметры.ИмяСтр = ВРЕГ(Имя);
			ОбластьСтрока.Параметры.ОтчествоСтр = ВРЕГ(Отчество);
			ОбластьСтрока.Параметры.ДатаРожденияСтр = стр.ЗастрахованноеЛицо.уп_ФизЛицо.ДатаРождения;
			МестоРожденияСтр = СтрЗаменить(стр.ЗастрахованноеЛицо.уп_ФизЛицо.МестоРождения, "0,", "");
	    	ОбластьСтрока.Параметры.МестоРожденияСтр = СокрЛП(СтрЗаменить(МестоРожденияСтр,",",", "));
		    СНИЛС = уп_ОПС.ПреобразоватьНомерПФРДляРеестра(стр.ЗастрахованноеЛицо.уп_ФизЛицо.СтраховойНомерПФР); 
     		ПерваяЧастьСтраховогоНомера = Лев(СНИЛС,9);
    		ВтораяЧастьСтраховогоНомера = Прав(СНИЛС,2);
    		ОбластьСтрока.Параметры.СНИЛС = Число(ПерваяЧастьСтраховогоНомера);
    		ОбластьСтрока.Параметры.КонтрольнаяСумма = Число(ВтораяЧастьСтраховогоНомера);
    		ОбластьСтрока.Параметры.Пол = ВРЕГ(Лев(Строка(стр.ЗастрахованноеЛицо.уп_ФизЛицо.Пол),1));
			ОбластьСтрока.Параметры.СтрокаКуда = ВРЕГ(ПенсионныйФонд.НаименованиеПолное);
			ОбластьСтрока.Параметры.ИНН = СокрЛП(ПенсионныйФонд.ИНН);
			ОбластьСтрока.Параметры.СуммаСВ = СуммаСВ;
			ОбластьСтрока.Параметры.СуммаДСВ = СуммаДСВ;
			ОбластьСтрока.Параметры.СуммаСФ = СуммаСФ;
	    	ОбластьСтрока.Параметры.СуммаОстаток = СуммаОстаток;
			ОбластьСтрока.Параметры.СуммаМК = СуммаМК;
			ОбластьСтрока.Параметры.СуммаСВ_ИД = СуммаСВ_ИД;
			ОбластьСтрока.Параметры.СуммаДСВ_ИД = СуммаДСВ_ИД;
			ОбластьСтрока.Параметры.СуммаСФ_ИД = СуммаСФ_ИД;
			ОбластьСтрока.Параметры.СуммаМК_ИД = СуммаВР_ИД;
	
    		ТабличныйДокумент.Вывести(ОбластьСтрока);

		КонецЦикла;
		ДолжностьИФИО = ХМ_ФункцииОтчетов.ДолжностьИФИОРуководителя(ПакетЭД.Дата);
        ОбластьПодвал.Параметры.ФИОРуководителя = ДолжностьИФИО.ФИО;
        ОбластьПодвал.Параметры.ДолжностьРуководителя = ДолжностьИФИО.Должность;
        ОбластьПодвал.Параметры.ИтогоСуммаСВ = ИтогоСуммаСВ;
		ОбластьПодвал.Параметры.ИтогоСуммаДСВ = ИтогоСуммаДСВ;
		ОбластьПодвал.Параметры.ИтогоСуммаСФ = ИтогоСуммаСФ;
	    ОбластьПодвал.Параметры.ИтогоСуммаОстаток = ИтогоСуммаОстаток;
		ОбластьПодвал.Параметры.ИтогоСуммаМК = ИтогоСуммаМК;
		ОбластьПодвал.Параметры.ИтогоСуммаСВ_ИД = ИтогоСуммаСВ_ИД;
		ОбластьПодвал.Параметры.ИтогоСуммаДСВ_ИД = ИтогоСуммаДСВ_ИД;
		ОбластьПодвал.Параметры.ИтогоСуммаСФ_ИД = ИтогоСуммаСФ_ИД;
		ОбластьПодвал.Параметры.ИтогоСуммаМК_ИД = ИтогоСуммаВР_ИД;

		ТабличныйДокумент.Вывести(ОбластьПодвал);
		ХМ_ФункцииОтчетов.ВывестиИсполнителя(ТабличныйДокумент);
		ТабличныйДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		ПараметрыВыгрузки.СозданныеФайлы.Добавить(Новый Структура("Адрес,ИмяФайла", ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла), ПараметрыВыгрузки.ИДФормы), "НПФ_" + ПенсионныйФонд.ИНН + "_000_" + уп_ПакетЭД_v2.ПолучитьКодДокумента(ПакетЭД.ВидЭД) + "_" + Формат(ТекущаяДата(), "ДФ=""ггггММдд""") + "_" + ГУИД + "." + "XLSX"));			
		
	КонецЦикла;	
	
КонецПроцедуры
//-- ХМНПФ 25.03.2019 ГригорьевМА //ХМ_СформироватьФайлыExcel_Форма22() // №2121

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 05.03.2019 ШадринАВ // СформироватьФайлыXML_Форма20() // №2084
	// ХМНПФ 28.06.2018 КлименкоМИ // СформироватьФайлыXML_Форма1() // №1709 неактуально
	// ХМНПФ 23.03.2019 ГригорьевМА //СформироватьФайлыExcel() // №2121
	// ХМНПФ 25.03.2019 ГригорьевМА //ХМ_СформироватьФайлыExcel_Форма22() // №2121
	// ХМНПФ 29.03.2019 ГригорьевМА // ЗаполнитьБлок_СлужебнаяИнформация() // №2140
	// ХМНПФ 01.04.2019 ГригорьевМА // ЗаполнитьБлок_Исполнитель() // №2144
	// ХМНПФ 17.04.2019 ШадринАВ // СформироватьФайлыXML_Форма25() // №2176
	// ХМНПФ 24.04.2019 ГригорьевМА // ЗаполнитьБлок_СлужебнаяИнформация() // №2189
	// ХМНПФ 06.11.2019 ШадринАВ // СформироватьФайлыXML_Форма12() // №2535
	// ХМНПФ 12.12.2019 ШадринАВ // ЗаполнитьБлок_СлужебнаяИнформация() // №2604
	// ХМНПФ 03.02.2020 МилевскийАЮ // СформироватьФайлыXML_Форма18 // №2652
	// ХМНПФ 07.10.2021 МилевскийАЮ // СформироватьФайлыXML_Форма25_Новая // №3348
КонецПроцедуры

