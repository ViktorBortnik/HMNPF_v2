
&ИзменениеИКонтроль("РасчитатьПроцент")
Процедура ХМРасчитатьПроцент()
	РасчетыЗакончены = Ложь;

	//проверяем состояния
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	пдс_СостоянияПенсионныхСчетовПДССрезПоследних.Состояние КАК Состояние,
	|	пдс_СостоянияПенсионныхСчетовПДССрезПоследних.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ ОбщееСостояние
	|ИЗ
	|	РегистрСведений.пдс_СостоянияПенсионныхСчетовПДС.СрезПоследних(&ДатаДок, НомерСчета = &НомерСчета) КАК пдс_СостоянияПенсионныхСчетовПДССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	пдс_ПричиныЗакрытияПенсионныхСчетовПДССрезПоследних.ПричинаЗакрытия КАК ПричинаЗакрытия,
	|	пдс_ПричиныЗакрытияПенсионныхСчетовПДССрезПоследних.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ ПричиныЗакрытия
	|ИЗ
	|	РегистрСведений.пдс_ПричиныЗакрытияПенсионныхСчетовПДС.СрезПоследних(&ДатаДок, НомерСчета = &НомерСчета) КАК пдс_ПричиныЗакрытияПенсионныхСчетовПДССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбщееСостояние.Состояние КАК Состояние,
	|	ПричиныЗакрытия.ПричинаЗакрытия КАК ПричинаЗакрытия,
	|	ОбщееСостояние.НомерСчета КАК НомерСчета
	|ИЗ
	|	ОбщееСостояние КАК ОбщееСостояние
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПричиныЗакрытия КАК ПричиныЗакрытия
	|		ПО ОбщееСостояние.НомерСчета = ПричиныЗакрытия.НомерСчета";

	Запрос.УстановитьПараметр("ДатаДок", ДатаРасчета);
	Запрос.УстановитьПараметр("НомерСчета", НомерСчета);

	РезультатЗапроса = Запрос.Выполнить();

	ОтказПоСостоянию = Ложь;
	ОтказПоПериоду = Ложь;
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.пдс_СостоянияПСПДС.НачисленияПриостановлены Тогда
			ОтказПоСостоянию = Истина;
			Сообщить("ПС "+НомерСчета+" находится в состоянии "+ВыборкаДетальныеЗаписи.Состояние+", выплата назначена быть не может", СтатусСообщения.Важное);
		КонецЕсли;	
		Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.пдс_СостоянияПСПДС.ВыплатнойПериод
			или ВыборкаДетальныеЗаписи.Состояние = Перечисления.пдс_СостоянияПСПДС.НачисленияПриостановлены Тогда
			ОтказПоПериоду = Истина;
			Сообщить("По ПС "+НомерСчета+" уже назначена пенсия", СтатусСообщения.Важное);
		КонецЕсли;	
	КонецЦикла;

	БазаРасчета = Расшифровка.Итог("Сумма");
	//размер пожизненной пенсии считаем в любом случае - надо для принятия решения
	Если СрокДляРасчетаПожизненнойПенсии<>0 Тогда
		РазмерПожизненнойПенсии = БазаРасчета/СрокДляРасчетаПожизненнойПенсии;
#Вставка
		//ЛыткинАМ //https://redmine.npf.com/issues/4251
		РазмерПожизненнойПенсии = ЦЕЛ(БазаРасчета/СрокДляРасчетаПожизненнойПенсии*100)/100;
#КонецВставки
	Иначе
		РазмерПожизненнойПенсии = 0;
		Если БазаРасчета<>0 Тогда 
			Сообщить("Не указан срок для расчета пожизненной пенсии");
		КонецЕсли;
		РасчетыЗакончены = Истина;
		ПринятоеРешение = 0; 
	КонецЕсли;

	Если ВариантРасчета = 0 Тогда //считаем еще размер срочной пенсии
		Если Срок<>0 Тогда
			Размер = БазаРасчета/Срок;
#Вставка
			//ЛыткинАМ //https://redmine.npf.com/issues/4270
			Размер = ЦЕЛ(БазаРасчета/Срок*100)/100;
#КонецВставки
		Иначе
			Размер = 0;
			Если БазаРасчета<>0 Тогда 
				Сообщить("Не указан срок для расчета срочной выплаты");
			КонецЕсли;
			РасчетыЗакончены = Истина;
			ПринятоеРешение = 0; 
		КонецЕсли;
	КонецЕсли;
	Если РасчетыЗакончены Тогда
		Возврат;
	КонецЕсли;	

	//Если ВариантРасчета = 1 Тогда
	Если ПрожиточныйМинимум<>0 Тогда
		Процент = РазмерПожизненнойПенсии*100/ПрожиточныйМинимум;
		Если Процент>=10 Тогда
			ПринятоеРешение = 1;
		Иначе
			ПринятоеРешение = 2;
		КонецЕсли;
	Иначе
		Процент = 0;
		ПринятоеРешение = 0; 
		Сообщить("Не заполнен прожиточный минимум. Принятие решения о варианте выплаты невозможно.", СтатусСообщения.Важное);
	КонецЕсли;
	//Иначе
	//	ПринятоеРешение = 1;
	//КонецЕсли;

	Если ОтказПоСостоянию или ОтказПоПериоду Тогда
		ПринятоеРешение = 3;
	КонецЕсли;

КонецПроцедуры
