Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбработатьДокументПоступления")
Процедура ХМОбработатьДокументПоступления(ДокСсылка)
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	|	ДоговорыОПС.Ссылка.Дата КАК Период,
	|	ДоговорыОПС.Ссылка.Организация КАК Организация,
	|	ДоговорыОПС.Ссылка.ПенсионныйФонд КАК Плательщик,
	|	ДоговорыОПС.Ссылка.ДатаНачалаПериодаВзносов КАК ДатаНачалаПериодаВзносов,
	|	ДоговорыОПС.Ссылка.ДатаКонцаПериодаВзносов КАК ДатаКонцаПериода,
	|	ДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
	|	ДоговорыОПС.Участник КАК Участник,
	|	&РПН КАК ТипРезерва,
	|	ДоговорыОПС.ТипСуммы КАК ТипСуммы,
	|	ДоговорыОПС.Сумма КАК Сумма,
	|	ДоговорыОПС.СуммаКомпенсации КАК СуммаКомпенсации,
	|	ДоговорыОПС.СуммаВосполнения КАК СуммаВосполнения
	|ПОМЕСТИТЬ ДоговорыОПС
	|ИЗ
	|	Документ.уп_ФинансовыйРеестр.ДоговорыОПС КАК ДоговорыОПС
	|ГДЕ
	|	ДоговорыОПС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДоговорОПС КАК ДоговорОПС,
	|	КОНЕЦПЕРИОДА(уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДатаОбращения, КВАРТАЛ) КАК КварталОбращения,
	|	уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ВключатьДопВзносы КАК ВключатьДопВзносы,
	|	НАЧАЛОПЕРИОДА(уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДатаОбращения, ГОД) КАК НачалоГодаОбращения,
	|	КОНЕЦПЕРИОДА(уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДатаОбращения, ГОД) КАК КонецГодаОбращения
	|ПОМЕСТИТЬ ОбращенияЗаЕВ
	|ИЗ
	|	РегистрСведений.уп_ДатыОбращенияЗаЕдиновременнойВыплатой.СрезПоследних(
	|			&ДатаДок,
	|			ДоговорОПС В
	|				(ВЫБРАТЬ
	|					ДоговорыОПС.ДоговорОПС
	|				ИЗ
	|					ДоговорыОПС)) КАК уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПоступленияОПСОбороты.ДоговорОПС КАК ДоговорОПС,
	|	уп_ПоступленияОПСОбороты.СуммаОборот КАК СуммаОборот,
	|	уп_ПоступленияОПСОбороты.ДатаКонцаПериода КАК ДатаКонцаПериода
	|ПОМЕСТИТЬ ВзносыГосПоддержки
	|ИЗ
	|	РегистрНакопления.уп_ПоступленияОПС.Обороты(
	|			,
	|			&ДатаДок,
	|			Период,
	|			ДоговорОПС В
	|					(ВЫБРАТЬ
	|						ОбращенияЗаЕВ.ДоговорОПС
	|					ИЗ
	|						ОбращенияЗаЕВ
	|					ГДЕ
	|						ОбращенияЗаЕВ.ВключатьДопВзносы = ИСТИНА)
	|				И ТипСуммы = &ГосПоддержка) КАК уп_ПоступленияОПСОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПоступленияОПСОбороты.ДоговорОПС КАК ДоговорОПС,
	|	уп_ПоступленияОПСОбороты.СуммаОборот КАК СуммаОборот,
	|	уп_ПоступленияОПСОбороты.ДатаКонцаПериода КАК ДатаКонцаПериода
	|ПОМЕСТИТЬ ВзносыДСВ
	|ИЗ
	|	РегистрНакопления.уп_ПоступленияОПС.Обороты(
	|			,
	|			&ДатаДок,
	|			Период,
	|			ДоговорОПС В
	|					(ВЫБРАТЬ
	|						ОбращенияЗаЕВ.ДоговорОПС
	|					ИЗ
	|						ОбращенияЗаЕВ
	|					ГДЕ
	|						ОбращенияЗаЕВ.ВключатьДопВзносы = ИСТИНА)
	|				И ТипСуммы В (&ДСВ)) КАК уп_ПоступленияОПСОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбращенияЗаЕВ.ДоговорОПС КАК ДоговорОПС,
	|	ОбращенияЗаЕВ.КварталОбращения КАК КварталОбращения,
	|	ОбращенияЗаЕВ.ВключатьДопВзносы КАК ВключатьДопВзносы,
	|	ОбращенияЗаЕВ.НачалоГодаОбращения КАК НачалоГодаОбращения,
	|	ОбращенияЗаЕВ.КонецГодаОбращения КАК КонецГодаОбращения,
	|	СУММА(ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(ВзносыГосПоддержки.ДатаКонцаПериода, ДЕНЬ) = КОНЕЦПЕРИОДА(ОбращенияЗаЕВ.КонецГодаОбращения, ДЕНЬ)
	|				ТОГДА ВзносыГосПоддержки.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ГосПоддержкаЗаГодОбращения
	|ИЗ
	|	ОбращенияЗаЕВ КАК ОбращенияЗаЕВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВзносыГосПоддержки КАК ВзносыГосПоддержки
	|		ПО ОбращенияЗаЕВ.ДоговорОПС = ВзносыГосПоддержки.ДоговорОПС
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбращенияЗаЕВ.ДоговорОПС,
	|	ОбращенияЗаЕВ.КварталОбращения,
	|	ОбращенияЗаЕВ.ВключатьДопВзносы,
	|	ОбращенияЗаЕВ.НачалоГодаОбращения,
	|	ОбращенияЗаЕВ.КонецГодаОбращения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбращенияЗаЕВ.ДоговорОПС КАК ДоговорОПС,
	|	ОбращенияЗаЕВ.КварталОбращения КАК КварталОбращения,
	|	ОбращенияЗаЕВ.ВключатьДопВзносы КАК ВключатьДопВзносы,
	|	ОбращенияЗаЕВ.НачалоГодаОбращения КАК НачалоГодаОбращения,
	|	ОбращенияЗаЕВ.КонецГодаОбращения КАК КонецГодаОбращения,
	|	СУММА(ВЫБОР
	|			КОГДА ВзносыДСВ.ДатаКонцаПериода <= ОбращенияЗаЕВ.КварталОбращения
	|					И ВзносыДСВ.ДатаКонцаПериода > ОбращенияЗаЕВ.НачалоГодаОбращения
	|				ТОГДА ВзносыДСВ.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ВзносыДСВ.ДатаКонцаПериода <= ОбращенияЗаЕВ.КонецГодаОбращения
	|					И ВзносыДСВ.ДатаКонцаПериода > ОбращенияЗаЕВ.НачалоГодаОбращения
	|				ТОГДА ВзносыДСВ.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДопВзносыЗаГод,
	|	СУММА(ВЫБОР
	|			КОГДА ВзносыДСВ.ДатаКонцаПериода <= ОбращенияЗаЕВ.КонецГодаОбращения
	|					И ВзносыДСВ.ДатаКонцаПериода > ОбращенияЗаЕВ.КварталОбращения
	|				ТОГДА ВзносыДСВ.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДопВзносыПосле
	|ПОМЕСТИТЬ ДСВЗаПериодОбращения
	|ИЗ
	|	ОбращенияЗаЕВ КАК ОбращенияЗаЕВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВзносыДСВ КАК ВзносыДСВ
	|		ПО ОбращенияЗаЕВ.ДоговорОПС = ВзносыДСВ.ДоговорОПС
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбращенияЗаЕВ.ДоговорОПС,
	|	ОбращенияЗаЕВ.КварталОбращения,
	|	ОбращенияЗаЕВ.ВключатьДопВзносы,
	|	ОбращенияЗаЕВ.НачалоГодаОбращения,
	|	ОбращенияЗаЕВ.КонецГодаОбращения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
	|	ДоговорыОПС.Участник КАК Участник,
	|	ДоговорыОПС.ТипСуммы КАК ТипСуммы,
	|	ДоговорыОПС.ТипРезерва КАК ТипРезерва,
	|	ДоговорыОПС.Сумма КАК Сумма,
	|	ДоговорыОПС.СуммаКомпенсации КАК СуммаКомпенсации,
	|	ДоговорыОПС.СуммаВосполнения КАК СуммаВосполнения,
	|	ВЫБОР
	|		КОГДА ДоговорыОПС.ТипСуммы = &ГосПоддержка
	|			ТОГДА ЕСТЬNULL(ГосПоддержкаЗаГодОбращения.Сумма, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаГП,
	|	ВЫБОР
	|		КОГДА ДоговорыОПС.ТипСуммы = &ГосПоддержка
	|			ТОГДА ЕСТЬNULL(ДСВЗаПериодОбращения.Сумма, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДСВ,
	|	ОбращенияЗаЕВ.КварталОбращения КАК КварталОбращения,
	|	ОбращенияЗаЕВ.ВключатьДопВзносы КАК ВключатьДопВзносы,
	|	ОбращенияЗаЕВ.КонецГодаОбращения КАК КонецГодаОбращения,
	|	ОбращенияЗаЕВ.НачалоГодаОбращения КАК НачалоГодаОбращения,
	|	ДоговорыОПС.Период КАК Период,
	|	ДоговорыОПС.Организация КАК Организация,
	|	ДоговорыОПС.Плательщик КАК Плательщик,
	|	ДоговорыОПС.ДатаНачалаПериодаВзносов КАК ДатаНачалаПериодаВзносов,
	|	ДоговорыОПС.ДатаКонцаПериода КАК ДатаКонцаПериода,
	|	ЕстьNULL(ДСВЗаПериодОбращения.ДопВзносыЗаГод,0) КАК ДопВзносыЗаГод,
	|	ЕстьNULL(ДСВЗаПериодОбращения.ДопВзносыПосле,0) КАК ДопВзносыПосле
	|ИЗ
	|	ОбращенияЗаЕВ КАК ОбращенияЗаЕВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыОПС КАК ДоговорыОПС
	|		ПО ОбращенияЗаЕВ.ДоговорОПС = ДоговорыОПС.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГосПоддержкаЗаГодОбращения КАК ГосПоддержкаЗаГодОбращения
	|		ПО ОбращенияЗаЕВ.ДоговорОПС = ГосПоддержкаЗаГодОбращения.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДСВЗаПериодОбращения КАК ДСВЗаПериодОбращения
	|		ПО ОбращенияЗаЕВ.ДоговорОПС = ДСВЗаПериодОбращения.ДоговорОПС";

	Запрос.УстановитьПараметр("Ссылка", ДокСсылка);
	Запрос.УстановитьПараметр("ДатаДок", ДокСсылка.Дата);
	Запрос.УстановитьПараметр("ГосПоддержка", Справочники.уп_ТипыСуммОПС.ГосПоддержка);
	Запрос.УстановитьПараметр("РезервНаследников", Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН);
	Запрос.УстановитьПараметр("РПН", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101000000');

	ДСВ = Новый СписокЗначений;
	ДСВ.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыЮр);
	ДСВ.Добавить(Справочники.уп_ТипыСуммОПС.ДопВзносы);
	Запрос.УстановитьПараметр("ДСВ", ДСВ);
	Если ТипЗнч(ДокСсылка) = Тип("ДокументСсылка.уп_РегистрацияПоступленийОПС") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "уп_ФинансовыйРеестр", "уп_РегистрацияПоступленийОПС");
	КонецЕсли;	

	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	НаборЗаписейРегистра	= РегистрыНакопления.уп_РезервыОПС.СоздатьНаборЗаписей();
	ОтборПоРегистратору = НаборЗаписейРегистра.Отбор.Регистратор;
	ОтборПоРегистратору.Установить(ДокСсылка);
	НаборЗаписейРегистра.Прочитать();
	ТаблицаНабора = НаборЗаписейРегистра.Выгрузить();
	ЕстьИзменения = Ложь;
	Пока Выборка.Следующий() Цикл
		ЗаменаТР = Ложь;
		ЗаменаСуммы = Ложь;
		Сумма = Выборка.Сумма;
		СуммаДоп = 0;
		Если ЗначениеЗаполнено(Выборка.КварталОбращения) Тогда
			Если Выборка.ТипСуммы = Справочники.уп_ТипыСуммОПС.ГосПоддержка Тогда
				#Вставка
				// +ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325	
				Если Выборка.ВключатьДопВзносы Тогда
				// -ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325 
				#КонецВставки
					Если ДокСсылка.ДатаКонцаПериодаВзносов < Выборка.НачалоГодаОбращения Тогда
						ЕстьИзменения = Истина;
						ЗаменаТР = Истина;
						//ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
					ИначеЕсли КонецДня(ДокСсылка.ДатаКонцаПериодаВзносов) = КонецДня(Выборка.КонецГодаОбращения) И Выборка.Сумма>0 Тогда
						//СуммаВРезервЕВ = Мин(ТекСтрокаДоговорыОПС.СуммаДСВ-ТекСтрокаДоговорыОПС.СуммаГП, ТекСтрокаДоговорыОПС.Сумма);
						Если Выборка.ДопВзносыЗаГод = 0 Тогда
							Сообщить("По документу "+ДокСсылка+" по договору ОПС "+Выборка.ДоговорОПС+" сумма гос поддержки за год обращения составляет "+Выборка.Сумма+", а сумма доп. взносов = 0. Резерв не может быть корректно сформирован.");
							СуммаВРезервЕВ = 0;
						Иначе
							Коэффициент = Выборка.Сумма/Выборка.ДопВзносыЗаГод;
							СуммаСФДолжнаОстаться = Окр(Выборка.ДопВзносыПосле*Коэффициент,2);
							СуммаВРезервЕВ = Выборка.Сумма - СуммаСФДолжнаОстаться;
							Если СуммаВРезервЕВ>0 Тогда
								ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
								Сумма = СуммаВРезервЕВ;
								ЗаменаТР = Истина;
								Если СуммаВРезервЕВ<Выборка.Сумма Тогда
									ЗаменаСуммы = Истина;
									СуммаДоп = Выборка.Сумма-СуммаВРезервЕВ;
								КонецЕсли;	
								ЕстьИзменения = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;	
				#Вставка
				// +ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325	
				КонецЕсли;	
				// -ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325
				#КонецВставки				
			Иначе
				Если ДокСсылка.ДатаКонцаПериодаВзносов <= Выборка.КварталОбращения Тогда
					ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
					ЗаменаТР = Истина;
					ЕстьИзменения = Истина;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;
		Если ЕстьИзменения Тогда
			//СтрокаТаблицы = ТаблицаНабора.Найти(Выборка.ДоговорОПС, "ДоговорОПС");
			СтрокиТаблицы = ТаблицаНабора.НайтиСтроки(Новый Структура("ДоговорОПС, ТипРезерва, ТипСуммы, Сумма", Выборка.ДоговорОПС, Выборка.ТипРезерва, Выборка.ТипСуммы, Выборка.Сумма ));
			Если СтрокиТаблицы.Количество() = 1 Тогда
				СтрокаТаблицы = СтрокиТаблицы.Получить(0); 
				Если СтрокаТаблицы.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений Тогда
					Если ЗаменаТР Тогда
						СтрокаТаблицы.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
					КонецЕсли;	
					Если ЗаменаСуммы Тогда
						СтрокаТаблицы.Сумма = Сумма;
						НовСтрТаблицы = ТаблицаНабора.Добавить();
						ЗаполнитьЗначенияСвойств(НовСтрТаблицы,СтрокаТаблицы);
						НовСтрТаблицы.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
						НовСтрТаблицы.Сумма = СуммаДоп; 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	Если ЕстьИзменения Тогда
		НаборЗаписейРегистра.Загрузить(ТаблицаНабора);
		Попытка
			НаборЗаписейРегистра.Записать(Истина);
		Исключение
			#Вставка
			// +ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Ошибка при обработке документа поступления: " + ДокСсылка;
			Сообщение.Сообщить();
			// -ХМНПФ 08.09.2021 ШадринАВ // ОбработатьДокументПоступления() // №3325
			#КонецВставки
		КонецПопытки;	
	КонецЕсли;	
КонецПроцедуры
