
&НаСервере
&ИзменениеИКонтроль("СформироватьНаСервере")
Процедура ХМСформироватьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СостоянияПССрезПоследних.НомерСчета.Владелец КАК Договор
	|ПОМЕСТИТЬ вр_ТабВыплатныеСчета
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&Дата, ) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.Состояние = &Выплатное
	|	%ОтборОрганизация%
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПериодичностьВыплатСрезПоследних.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ вр_ИсключаемыеСчета
	|ИЗ
	|	РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(
	|			&Дата,
	|			НомерСчета В
	|					(ВЫБРАТЬ
	|						вр_ТабВыплатныеСчета.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ТабВыплатныеСчета КАК вр_ТабВыплатныеСчета)
	|				И ПериодичностьВыплат В (&ПериодичностьВыплатНеЕжемесячные)) КАК уп_ПериодичностьВыплатСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_ТабВыплатныеСчета.НомерСчета КАК НомерСчета,
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора.МинимальныйСрокВыплат КАК ШаблонДоговораМинимальныйСрокВыплат,
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.Схема КАК Схема
	|ПОМЕСТИТЬ вр_ПС
	|ИЗ
	|	вр_ТабВыплатныеСчета КАК вр_ТабВыплатныеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПараметрыПенсионногоДоговора.СрезПоследних(&Дата, ) КАК уп_ПараметрыПенсионногоДоговораСрезПоследних
	|		ПО вр_ТабВыплатныеСчета.Договор = уп_ПараметрыПенсионногоДоговораСрезПоследних.ПенсионныйДоговор
	|			И (уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора.ВозможностьПереводаСрочныхВыплатВВыплатыДоИсчерпания)
	|ГДЕ
	|	НЕ вр_ТабВыплатныеСчета.НомерСчета В
	|				(ВЫБРАТЬ
	|					вр_ИсключаемыеСчета.НомерСчета КАК НомерСчета
	|				ИЗ
	|					вр_ИсключаемыеСчета КАК вр_ИсключаемыеСчета)
	|;
	|
#Вставка
//++https://redmine.npf.com/issues/4274
	|ВЫБРАТЬ
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	СУММА(уп_СчетчикНачислений.КоличествоОборот) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|		уп_СчетчикНачислений.Количество КАК КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&Дата, НомерСчета В (ВЫБРАТЬ
	|						вр_ПС.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПС КАК вр_ПС)) КАК хм_СрезСчетчик
	|			ПО уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета
	|				И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния
	|	ГДЕ
	|		уп_СчетчикНачислений.Период <= &Дата
	|	    И уп_СчетчикНачислений.НомерСчета В (ВЫБРАТЬ
	|						вр_ПС.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПС КАК вр_ПС)
	|		И хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		хм_СрезСчетчик.НомерСчета,
	|		хм_СрезСчетчик.Количество
	|	ИЗ
	|		РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&Дата, НомерСчета В (ВЫБРАТЬ
	|						вр_ПС.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПС КАК вр_ПС)) КАК хм_СрезСчетчик
	|
	|) КАК уп_СчетчикНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СчетчикНачислений.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
//--https://redmine.npf.com/issues/4274
#КонецВставки
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_ПС.НомерСчета КАК НомерСчета,
	|	вр_ПС.ШаблонДоговораМинимальныйСрокВыплат - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) КАК ОставшиесяВыплаты,
	|	вр_ПС.Схема КАК Схема,
	|	вр_ПС.ШаблонДоговораМинимальныйСрокВыплат КАК ШаблонДоговораМинимальныйСрокВыплат
	|ПОМЕСТИТЬ вр_ОставшиесяВыплаты
	|ИЗ
	|	вр_ПС КАК вр_ПС
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(
	|				,
	|				&Дата,
	|				,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						вр_ПС.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПС КАК вр_ПС)) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|		ПО вр_ПС.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|ГДЕ
	|	вр_ПС.ШаблонДоговораМинимальныйСрокВыплат - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_ОставшиесяВыплаты.НомерСчета КАК НомерСчета,
	|	ЕСТЬNULL(уп_РезервыОстатки.СуммаОстаток, 0) / вр_ОставшиесяВыплаты.ОставшиесяВыплаты КАК РасчетныйРазмерПенсии,
	|	уп_СостоянияПССрезПервых.Период КАК НачалоВыплатногоПериода,
	|	вр_ОставшиесяВыплаты.Схема КАК Схема,
	|	вр_ОставшиесяВыплаты.ШаблонДоговораМинимальныйСрокВыплат КАК ШаблонДоговораМинимальныйСрокВыплат
	|ПОМЕСТИТЬ вр_ПС2
	|ИЗ
	|	вр_ОставшиесяВыплаты КАК вр_ОставшиесяВыплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(
	|				&Дата,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						вр_ОставшиесяВыплаты.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ОставшиесяВыплаты КАК вр_ОставшиесяВыплаты)) КАК уп_РезервыОстатки
	|		ПО вр_ОставшиесяВыплаты.НомерСчета = уп_РезервыОстатки.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПервых(
	|				,
	|				НомерСчета В
	|						(ВЫБРАТЬ
	|							вр_ОставшиесяВыплаты.НомерСчета КАК НомерСчета
	|						ИЗ
	|							вр_ОставшиесяВыплаты КАК вр_ОставшиесяВыплаты)
	|					И Состояние = &Выплатное) КАК уп_СостоянияПССрезПервых
	|		ПО вр_ОставшиесяВыплаты.НомерСчета = уп_СостоянияПССрезПервых.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_ПС2.НомерСчета КАК НомерСчета,
	|	вр_ПС2.РасчетныйРазмерПенсии КАК РасчетныйРазмерПенсии,
	|	вр_ПС2.Схема КАК Схема,
	|	вр_ПС2.ШаблонДоговораМинимальныйСрокВыплат КАК МинимальныйСрокВыплат
	|ПОМЕСТИТЬ вр_ПС3
	|ИЗ
	|	вр_ПС2 КАК вр_ПС2
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ОбщиеПенсионныеПравила.СрезПоследних(, Правило = &МинимальныйРазмерПенсии) КАК уп_ОбщиеПенсионныеПравилаСрезПоследних
	|		ПО вр_ПС2.НачалоВыплатногоПериода >= уп_ОбщиеПенсионныеПравилаСрезПоследних.Период
	|ГДЕ
	|	вр_ПС2.РасчетныйРазмерПенсии > ЕСТЬNULL(уп_ОбщиеПенсионныеПравилаСрезПоследних.Значение, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вр_ПС3.НомерСчета КАК НомерСчета,
	|	вр_ПС3.РасчетныйРазмерПенсии КАК РазмерВыплаты,
	|	вр_ПС3.Схема КАК Схема,
	|	вр_ПС3.МинимальныйСрокВыплат КАК КоличествоВыплат,
	|	ЗНАЧЕНИЕ(Перечисление.уп_ПериодичностьВыплат.Ежемесячно) КАК ПериодичностьВыплат,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.ФормаВыплаты КАК ФормаВыплаты,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.ТипЛицевогоСчета КАК ТипЛицевогоСчета,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
	|	уп_ЛицевыеСчетаУчастниковСрезПоследних.Банк КАК Получатель,
	|	вр_ПС3.НомерСчета.Участник КАК Участник,
	|	вр_ПС3.НомерСчета.Владелец КАК ПенсионныйДоговор
	|ИЗ
	|	вр_ПС3 КАК вр_ПС3
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(
	|				&Дата,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						вр_ПС3.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПС3 КАК вр_ПС3)) КАК уп_ЛицевыеСчетаУчастниковСрезПоследних
	|		ПО вр_ПС3.НомерСчета = уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета";

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборОрганизация%", "И уп_СостоянияПССрезПоследних.НомерСчета.Владелец.Организация = &Организация");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборОрганизация%", "");
	КонецЕсли;

	Запрос.УстановитьПараметр("Выплатное", Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	Запрос.УстановитьПараметр("Дата", Объект.ДатаФормированияДокументов);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("МинимальныйРазмерПенсии", Справочники.уп_ОбщиеПенсионныеПравила.МинРазмерСрочнойПенсии);

	ПериодичностьВыплатНеЕжемесячные = Новый Массив;
	ПериодичностьВыплатНеЕжемесячные.Добавить(Перечисления.уп_ПериодичностьВыплат.Ежегодно);
	ПериодичностьВыплатНеЕжемесячные.Добавить(Перечисления.уп_ПериодичностьВыплат.Ежеквартально);
	ПериодичностьВыплатНеЕжемесячные.Добавить(Перечисления.уп_ПериодичностьВыплат.Полугодие);
	Запрос.УстановитьПараметр("ПериодичностьВыплатНеЕжемесячные", ПериодичностьВыплатНеЕжемесячные);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Счетчик = 0;
	КолСтрокВТаблице = 20000;
	ДокРаспоряжение = Неопределено;

	Пока Выборка.Следующий() Цикл

		Если Счетчик = КолСтрокВТаблице Или ДокРаспоряжение = Неопределено Тогда

			Если Счетчик = КолСтрокВТаблице Тогда
				ДокРаспоряжение.Записать();
				НоваяСтрока = Объект.НовыеДокументы.Добавить();
				НоваяСтрока.Документ = ДокРаспоряжение.Ссылка;
				Счетчик = 0;
			КонецЕсли;

			ДокРаспоряжение = Документы.уп_Распоряжение.СоздатьДокумент();
			ДокРаспоряжение.Организация = Объект.Организация;
			ДокРаспоряжение.Дата = ?(ЗначениеЗаполнено(Объект.ДатаФормированияДокументов), Объект.ДатаФормированияДокументов, ТекущаяДатаСеанса());
			ДокРаспоряжение.Ответственный = Объект.Ответственный;

		КонецЕсли;

		Счетчик = Счетчик + 1;
		СтрокаТЧ = ДокРаспоряжение.СписокСчетов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
		СтрокаТЧ.НачальнаяДата = ?(ЗначениеЗаполнено(Объект.ДатаФормированияДокументов), Объект.ДатаФормированияДокументов, ТекущаяДатаСеанса()); 

	КонецЦикла;

	Если ДокРаспоряжение<>Неопределено И ДокРаспоряжение.СписокСчетов.Количество() > 0 Тогда
		ДокРаспоряжение.Записать();
		НоваяСтрока = Объект.НовыеДокументы.Добавить();
		НоваяСтрока.Документ = ДокРаспоряжение.Ссылка;		
	КонецЕсли;

КонецПроцедуры
