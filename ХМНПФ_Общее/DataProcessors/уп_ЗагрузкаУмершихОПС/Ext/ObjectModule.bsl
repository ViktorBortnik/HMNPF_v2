
Процедура ХМ_НеТиповыеИзменения()

	// ХМНПФ 09.03.2016 Шадрин А.В. // ЗагрузитьТаблицу() // №261
	
КонецПроцедуры

&ИзменениеИКонтроль("ЗагрузитьТаблицу")
Процедура ХМЗагрузитьТаблицу(ТЗ, Протокол)
	//есть только СНИЛС
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабДляЗагрузки.СНИЛС,
	|	ТабДляЗагрузки.ФИО,
	|	ТабДляЗагрузки.Нпп,
	|	ТабДляЗагрузки.ДатаСмерти
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ТабДляЗагрузки КАК ТабДляЗагрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.СНИЛС КАК СНИЛС,
	|	ИсходныеДанные.ФИО КАК ФИО,
	|	ИсходныеДанные.ДатаСмерти,
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник КАК ЗЛ,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние,
	|	ИсходныеДанные.Нпп КАК Нпп,
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Код КАК ДоговорНомер,
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.ДатаЗаключения КАК ДоговорДата,
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
	|				&ДатаДок,
	|				ДоговорОПС.Организация = &Организация
	|					И ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР В (&СписокСНИЛС)) КАК уп_СостоянияДоговораОПССрезПоследних
	|		ПО ИсходныеДанные.СНИЛС = уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(
	|				&ДатаДок,
	|				ДоговорОПС.Организация = &Организация
	|					И ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР В (&СписокСНИЛС)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
	|		ПО ИсходныеДанные.СНИЛС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР
	|
	|УПОРЯДОЧИТЬ ПО
	|	Нпп,
	|	ДоговорДата УБЫВ
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоговорОПС)
	|ПО
	|	СНИЛС";

	Запрос.УстановитьПараметр("ДатаДок", ДокументОснование.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументОснование.Организация);
	Запрос.УстановитьПараметр("ТабДляЗагрузки", ТЗ);
	Запрос.УстановитьПараметр("СписокСНИЛС", ТЗ.ВыгрузитьКолонку("СНИЛС"));

	Результат = Запрос.Выполнить();
	#Если Клиент Тогда
		Состояние("Поиск соответствий в базе данных");
	#КонецЕсли
	СписокДействующихСостояний = Новый СписокЗначений;
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ЕдиновременнаяВыплата);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
	СписокДействующихСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);

	ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл
		ВыборкаДетальныеЗаписи = ВыборкаИтоги.Выбрать();
		Если ВыборкаДетальныеЗаписи.Количество()<=1 Тогда
			Если ВыборкаДетальныеЗаписи.Следующий() Тогда
				#Если Клиент Тогда
					Состояние(ВыборкаДетальныеЗаписи.Нпп);
				#КонецЕсли
				Если ВыборкаДетальныеЗаписи.ДоговорОПС = NULL Тогда //не найден договор
					Протокол.ДобавитьСтроку("Не найден договор об ОПС с "+ВыборкаДетальныеЗаписи.ФИО+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС);
				Иначе //все в порядке
					Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
						Протокол.ДобавитьСтроку("Договор №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" уже закрыт и не будет внесен в табличную часть!");
						Продолжить;
					ИначеЕсли ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию
						и ВыборкаДетальныеЗаписи.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти Тогда
						#Удаление 
						Протокол.ДобавитьСтроку("Договор №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" уже находится в состоянии подготовка к закрытию по смерти и не будет внесен в табличную часть!");
						Продолжить;							
						#КонецУдаления 
						#Вставка 
						// +ХМНПФ 09.03.2016 Шадрин А.В. // ЗагрузитьТаблицу() // №261
						Протокол.ДобавитьСтроку("Договор №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" уже находится в состоянии подготовка к закрытию по смерти.");
						// -ХМНПФ 09.03.2016 Шадрин А.В. // ЗагрузитьТаблицу() // №261
						#КонецВставки
					КонецЕсли;	

					ДобавитьСтроку(ВыборкаДетальныеЗаписи.ДоговорОПС,ВыборкаДетальныеЗаписи.ЗЛ,ВыборкаДетальныеЗаписи.ДатаСмерти);
				КонецЕсли;
			Иначе
				Протокол.ДобавитьСтроку("Не найден ЗЛ "+ВыборкаИтоги.СНИЛС);
			КонецЕсли;
		Иначе
			ТекДоговор = Неопределено;
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СНИЛС = ВыборкаДетальныеЗаписи.СНИЛС;
				Если  СписокДействующихСостояний.НайтиПоЗначению(ВыборкаДетальныеЗаписи.Состояние)<>Неопределено Тогда
					ТекДоговор = ВыборкаДетальныеЗаписи.ДоговорОПС;
					ДобавитьСтроку(ВыборкаДетальныеЗаписи.ДоговорОПС,ВыборкаДетальныеЗаписи.ЗЛ,ВыборкаДетальныеЗаписи.ДатаСмерти);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТекДоговор = Неопределено Тогда
				Протокол.ДобавитьСтроку("По ЗЛ "+ВыборкаИтоги.СНИЛС+" не найдено ни одного действующего договора.");
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
