
&НаСервере
&ИзменениеИКонтроль("СформироватьТЗДокумента")
Процедура НПФ_СформироватьТЗДокумента(ТЗДокумента, Периодичность)
	
	#Вставка
	//+ХМНПФ 10.01.2023 ХлопцевБА // №3726 //
	ТолькоНовые = Ложь;
	//ПоСпискуСчетов = Ложь;
	Если Объект.ХМ_ТолькоНовые > 0 И ЗначениеЗаполнено(Объект.хм_НачалоПериода) И ЗначениеЗаполнено(Объект.хм_КонецПериода) Тогда
		ТолькоНовые = Истина;	
		//ПоСпискуСчетов = Истина;
	КонецЕсли;	
	//-ХМНПФ 10.01.2023 ХлопцевБА №3726 // 
	#КонецВставки
	ДатаВПериоде = уп_НПФ.ОпределитьДатуВПериоде(Объект.ДатаФормированияДокументов,Периодичность,Объект.ПериодНачисления);
	ДатаКонцаПериода = уп_НПФ.ОпределитьДатуКонцаПериода(Объект.ПериодНачисления,Периодичность);
	
	ЗависимостьМесяцаВыплаты = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Объект.ДатаФормированияДокументов, Новый Структура("Организация", Объект.Организация)).ЗависимостьМесяцаВыплатыНПО;
	
	Если ЗависимостьМесяцаВыплаты = Перечисления.уп_ЗависимостьМесяцаВыплатыНПО.ОтДатыНазначения
		или ЗависимостьМесяцаВыплаты = Перечисления.уп_ЗависимостьМесяцаВыплатыНПО.ОтДатыНачалаВыплат Тогда
		ТекстОтборМесяцВыплаты = " И ПериодичностьВыплатСрезПоследних.НомерМесяцаВПериоде = &НомерМесяцаВПериоде ";
	Иначе
		ТекстОтборМесяцВыплаты = "";
	КонецЕсли;	
	
	#Удаление
	Если (ЗначениеЗаполнено(Объект.ОтборПенсионныйДоговор)) 
		или (ЗначениеЗаполнено(Объект.ОтборГруппаДоговоров)) Тогда
		ОтборДоговор = Истина;
	Иначе
		ОтборДоговор = Ложь;
	КонецЕсли;
	#КонецУдаления
	#Вставка
	ТекстОтборДоговор = "";
	ОтборДоговор = Ложь;

	Если (ЗначениеЗаполнено(Объект.ОтборПенсионныйДоговор)) Тогда
		ТекстОтборДоговор = " И "+?(Объект.хм_ОтборНеПенсионныйДоговор = Истина," НЕ ","")+" НомерСчета.Владелец = (&ПенсионныйДоговор)";
		ОтборДоговор = Истина;
	Иначеесли  (ЗначениеЗаполнено(Объект.ОтборГруппаДоговоров)) Тогда
		ТекстОтборДоговор = " И НомерСчета.Владелец В ИЕРАРХИИ (&ПенсионныйДоговор)";
		ОтборДоговор = Истина; 
	КонецЕсли;
	//+ХМНПФ 10.01.2023 ХлопцевБА // №3726 // 
	Если ТолькоНовые Тогда
		ТекстОтборДоговор = ТекстОтборДоговор + " И НомерСчета.Владелец В (&ПенсионныйДоговор) И НомерСчета В (&ПенсионныйСчет) ";
		ОтборДоговор = Истина;  
	КонецЕсли;   
	//-ХМНПФ 10.01.2023 ХлопцевБА №3726 // 
	#КонецВставки
	Если (ЗначениеЗаполнено(Объект.ОтборВкладчик)) Тогда
		ОтборВкл = Истина;
	Иначе
		ОтборВкл = Ложь;
	КонецЕсли;
	//Бардошина +
	Если ЗначениеЗаполнено(Объект.ОтборПодразделение) Тогда
		ОтборПодразделение = Истина;
	Иначе
		ОтборПодразделение = Ложь;
	КонецЕсли;	
	//Бардошина -	
	
	
	
	Запрос = Новый Запрос;
	Текст = "";
	
	//Бардошина +
	//запрос изменен: расширена секция ГДЕ (состояние = начисления приостановлены и определенная расшифровка) 
	//добавлено поле ЭтоЗадолженность, добавлен отбор по подразделению
	Текст = Текст +  "ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СостоянияПССрезПоследних.ДатаУстановкиСостояния КАК ДатаУстановкиСостояния,
	|	уп_СостоянияПССрезПоследних.Состояние КАК Состояние,
	|	уп_СостоянияПССрезПоследних.Расшифровка КАК Расшифровка
	|ПОМЕСТИТЬ ВТ_ПенсионныеСчета
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаВПериоде, НомерСчета.Владелец.Организация = &Организация
	#Удаление
	|		    "+?(ОтборДоговор," И НомерСчета.Владелец В ИЕРАРХИИ (&ПенсионныйДоговор)","")+?(ОтборВкл," И НомерСчета.Владелец.Вкладчик В ИЕРАРХИИ (&ОтборВкладчик)","")+"
	#КонецУдаления
	#Вставка
	|		    "+?(ОтборДоговор,ТекстОтборДоговор,"")+?(ОтборВкл," И НомерСчета.Владелец.Вкладчик В ИЕРАРХИИ (&ОтборВкладчик)","")+"
	#КонецВставки
	|		    "+?(ОтборПодразделение," И НомерСчета.Владелец.ПодразделениеБУ В ИЕРАРХИИ (&ОтборПодразделение)","")+"
	|) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	(уп_СостоянияПССрезПоследних.Состояние В (&СписокВыплатныхСостояний)
	|			ИЛИ уп_СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.НачисленияПриостановлены)
	|				И уп_СостоянияПССрезПоследних.Расшифровка В (&СписокРасшифровок))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета,
	|	уп_СостоянияПССрезПоследних.ДатаУстановкиСостояния,
	|	уп_СостоянияПССрезПоследних.Состояние,
	|	уп_СостоянияПССрезПоследних.Расшифровка
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&НачТекПериода, НомерСчета.Владелец.Организация = &Организация
	#Удаление
	|		    "+?(ОтборДоговор," И НомерСчета.Владелец В ИЕРАРХИИ (&ПенсионныйДоговор)","")+?(ОтборВкл," И НомерСчета.Владелец.Вкладчик В ИЕРАРХИИ (&ОтборВкладчик)","")+"
	#КонецУдаления
	#Вставка
	|		    "+?(ОтборДоговор,ТекстОтборДоговор,"")+?(ОтборВкл," И НомерСчета.Владелец.Вкладчик В ИЕРАРХИИ (&ОтборВкладчик)","")+"
	#КонецВставки
	|		    "+?(ОтборПодразделение," И НомерСчета.Владелец.ПодразделениеБУ В ИЕРАРХИИ (&ОтборПодразделение)","")+"
	|) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ВыплатнойПериод)
	|	И уп_СостоянияПССрезПоследних.НомерСчета.Участник.уп_ФизЛицо.уп_ДатаСмерти МЕЖДУ &НачТекПериода И &ДатаВПериоде
	|	И &ВключатьСчетаУмершихВТекущемПериоде
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПенсионныеСчета.НомерСчета КАК НомерСчета,
	|	МИНИМУМ(ВТ_ПенсионныеСчета.ДатаУстановкиСостояния) КАК ДатаУстановкиСостояния
	|ПОМЕСТИТЬ ВТ_МаксПериоды
	|ИЗ
	|	ВТ_ПенсионныеСчета КАК ВТ_ПенсионныеСчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПенсионныеСчета.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_МаксПериоды.НомерСчета КАК НомерСчета,
	|	ВТ_МаксПериоды.ДатаУстановкиСостояния КАК ДатаУстановкиСостояния,
	|	ВТ_ПенсионныеСчета.Состояние КАК Состояние,
	|	ВТ_ПенсионныеСчета.Расшифровка КАК Расшифровка
	|ПОМЕСТИТЬ СостоянияПССрезПоследних
	|ИЗ
	|	ВТ_ПенсионныеСчета КАК ВТ_ПенсионныеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МаксПериоды КАК ВТ_МаксПериоды
	|		ПО ВТ_ПенсионныеСчета.НомерСчета = ВТ_МаксПериоды.НомерСчета
	|			И ВТ_ПенсионныеСчета.ДатаУстановкиСостояния = ВТ_МаксПериоды.ДатаУстановкиСостояния
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	ВЫБРАТЬ
	|	СостоянияПССрезПоследних.НомерСчета,
	|	СостоянияПССрезПоследних.НомерСчета.Участник КАК Участник,
	//|	СостоянияПССрезПоследних.Период
	|	СостоянияПССрезПоследних.ДатаУстановкиСостояния КАК Период,
	|	ВЫБОР
	|		КОГДА СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.НачисленияПриостановлены)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоЗадолженность
	#Вставка
	|	, Очередность.НомерОчереди КАК НомерОчереди
	#КонецВставки
	|ПОМЕСТИТЬ ТаблицаСчетов
	|ИЗ
	|	СостоянияПССрезПоследних КАК СостоянияПССрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПериодичностьВыплатСрезПоследних.НомерСчета КАК НомерСчета
	|		ИЗ
	|			РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(&ДатаВПериоде, ) КАК ПериодичностьВыплатСрезПоследних
	|		ГДЕ
	|			ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат = &Периодичность "+ТекстОтборМесяцВыплаты+"
	|           ) КАК Периодичность
	|		ПО СостоянияПССрезПоследних.НомерСчета = Периодичность.НомерСчета";
	Если ЗначениеЗаполнено(Объект.ОтборПолучатель) Тогда
		Текст = Текст + "
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета КАК НомерСчета,
		|			ЛицевыеСчетаУчастниковСрезПоследних.Участник КАК Участник
		|		ИЗ
		|			РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(&ДатаДок, Организация = &Организация) КАК ЛицевыеСчетаУчастниковСрезПоследних
		|		ГДЕ
		|			ЛицевыеСчетаУчастниковСрезПоследних.Банк В ИЕРАРХИИ(&Банк)) КАК Получатели
		|		ПО СостоянияПССрезПоследних.НомерСчета = Получатели.НомерСчета
		|         И  СостоянияПССрезПоследних.НомерСчета.Участник = Получатели.Участник";
	КонецЕсли;
	#Вставка
	//+ХМНПФ 28.02.2021 ХлопцевБА // Дополнительный отбор // №3086
	Если ЗначениеЗаполнено(Объект.ОтборУчастник) Тогда
		Текст = Текст + "
		| ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|	ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета КАК НомерСчета,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Участник КАК Участник
		| ИЗ
		|	РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(
		|			&ДатаДок,
		|			Организация = &Организация
		|				И Участник = &отборУчастник) КАК ЛицевыеСчетаУчастниковСрезПоследних) КАК Участники
		| ПО (СостоянияПССрезПоследних.НомерСчета = Участники.НомерСчета)
		|	И (СостоянияПССрезПоследних.НомерСчета.Участник = Участники.Участник)";
	КонецЕсли;
	//-ХМНПФ 28.02.2021  ХлопцевБА // Дополнительный отбор // №3086
	
	//+ХМНПФ 28.02.2021 ХлопцевБА // Дополнительный отбор // №3086
	Если Объект.ТолькоНаПочту ИЛИ ЭтотОбъект["ТолькоЧерезРасчетныйСчет"] Тогда
		Текст = Текст + "
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|	ВложенныйЗапрос.НомерСчета КАК НомерСчета,
		|	ВложенныйЗапрос.Участник КАК Участник,
		|	ВложенныйЗапрос.ФормаВыплаты КАК текФормаВыплаты
		|ИЗ
		|	(ВЫБРАТЬ
		|		уп_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета КАК НомерСчета,
		|		уп_ЛицевыеСчетаУчастниковСрезПоследних.Участник КАК Участник,
		|		уп_ЛицевыеСчетаУчастниковСрезПоследних.ФормаВыплаты КАК ФормаВыплаты
		|	ИЗ
		|		РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(&ДатаДок, Организация = &Организация) КАК уп_ЛицевыеСчетаУчастниковСрезПоследних) КАК ВложенныйЗапрос
		|ГДЕ
		|	ВложенныйЗапрос.ФормаВыплаты В(&мФормаВыплатыПочта)) КАК ФормаВыплаты
		|ПО (СостоянияПССрезПоследних.НомерСчета = ФормаВыплаты.НомерСчета)";
	КонецЕсли;
	//-ХМНПФ 28.02.2021  ХлопцевБА // Дополнительный отбор // №3086
	
	//+ХМНПФ 28.02.2021  ХлопцевБА // Для первой выплаты вывел "НомерОчереди" // №3086
	#КонецВставки
	Если Объект.ИспользоватьОтборОчередностьВыплаты Тогда
		Текст = Текст + "
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НомераОчередейСрезПоследних.НомерСчета КАК НомерСчета
		#Вставка
		|	, НомераОчередейСрезПоследних.НомерОчереди КАК НомерОчереди
		#КонецВставки
		|		ИЗ
		|			РегистрСведений.уп_НомераОчередей.СрезПоследних(&ДатаДок, ) КАК НомераОчередейСрезПоследних
		|		ГДЕ
		|			НомераОчередейСрезПоследних.НомерОчереди = &НомерОчереди) КАК Очередность
		|		ПО СостоянияПССрезПоследних.НомерСчета = Очередность.НомерСчета";
	#Вставка
	Иначе
		Текст = Текст + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НомераОчередейСрезПоследних.НомерСчета КАК НомерСчета,
		|	        НомераОчередейСрезПоследних.НомерОчереди КАК НомерОчереди
		|		ИЗ
		|			РегистрСведений.уп_НомераОчередей.СрезПоследних(&ДатаДок, ) КАК НомераОчередейСрезПоследних
		//|		ГДЕ
		//|			НомераОчередейСрезПоследних.НомерОчереди = &НомерОчереди) КАК Очередность
		|			) КАК Очередность
		|		ПО СостоянияПССрезПоследних.НомерСчета = Очередность.НомерСчета";
	#КонецВставки
	КонецЕсли;
	#Вставка
	//-ХМНПФ 28.02.2021  ХлопцевБА // Для первой выплаты вывел "НомерОчереди" // №3086
	#КонецВставки
	Текст = Текст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСчетов.НомерСчета,
	|	ТаблицаСчетов.Участник,
	|	ТаблицаСчетов.Период,
	|	ТаблицаСчетов.ЭтоЗадолженность
	#Вставка
	|	, ТаблицаСчетов.НомерОчереди
	#КонецВставки
	|ИЗ
	|	ТаблицаСчетов КАК ТаблицаСчетов "+ ?(Объект.НачислятьЗаПрошлыеПериоды,"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСчетов.НомерСчета,
	|	ТаблицаСчетов.Участник,
	|	ТаблицаСчетов.Период,
	|	ТаблицаСчетов.ЭтоЗадолженность
	#Вставка
	|	, ТаблицаСчетов.НомерОчереди КАК НомерОчереди
	#КонецВставки
	|ПОМЕСТИТЬ СписокСчетовБезНачислений
	|ИЗ
	|	ТаблицаСчетов КАК ТаблицаСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(
	//|				&ДатаНачалаПрошлогоПериода,
	|				&ДатаНачалаВремен,
	|				&ДатаКонцаПрошлогоПериода,
	|				Период,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ТаблицаСчетов.НомерСчета
	|					ИЗ
	|						ТаблицаСчетов КАК ТаблицаСчетов)) КАК уп_СчетчикНачисленийОбороты
	|		ПО ТаблицаСчетов.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|ГДЕ
	|	уп_СчетчикНачисленийОбороты.НомерСчета ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетовБезНачислений.НомерСчета,
	|	СписокСчетовБезНачислений.Участник,
	|	НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, &ПериодичностьИмя) КАК Период,
	|	СписокСчетовБезНачислений.ЭтоЗадолженность
	#Вставка
	|	, СписокСчетовБезНачислений.НомерОчереди
	#КонецВставки
	|ИЗ
	|	СписокСчетовБезНачислений КАК СписокСчетовБезНачислений
	//
	|ГДЕ
	|   НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, &ПериодичностьИмя)<>&НачТекПериода
	//
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Период ПЕРИОДАМИ(&ПериодичностьИмя, , &ДатаКонцаПрошлогоПериода)","");	
	//Бардошина -					
	
	//Запрос.УстановитьПараметр("ДатаВПериоде", ДатаВПериоде);
	Запрос.УстановитьПараметр("ДатаВПериоде", ДатаКонцаПериода);
	Запрос.УстановитьПараметр("НачТекПериода", уп_НПФ.ОпределитьДатуНачалаПериода(Объект.ПериодНачисления,Периодичность));
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	//ОПА НПО 2018.06.15 +
	НомерМесяцаВПериоде = уп_НПОСервер.ОпределитьНомерМесяцаВПериоде(Периодичность, Объект.ДатаФормированияДокументов);
	Запрос.УстановитьПараметр("НомерМесяцаВПериоде", НомерМесяцаВПериоде);
	//ОПА НПО 2018.06.13 -
	СписокВыплатныхСостояний = Новый списокЗначений;
	СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены);
	Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
	//Бардошина +
	СписокРасшифровок = Новый СписокЗначений;
	СписокРасшифровок.Добавить(Перечисления.уп_РасшифровкаСостоянийПС.НеПрошёлПеререгистрациюПриостановлен);
	СписокРасшифровок.Добавить(Перечисления.уп_РасшифровкаСостоянийПС.НеПрошёлПеререгистрациюПриостановленСКомпенсацией);
	СписокРасшифровок.Добавить(Перечисления.уп_РасшифровкаСостоянийПС.Приостановлен);
	СписокРасшифровок.Добавить(Перечисления.уп_РасшифровкаСостоянийПС.ПриостановленСКомпенсацией);
#Вставка
	//ЛыткинАМ //Окончание пенсионных оснований не требует компенсации
#КонецВставки
#Удаление
	СписокРасшифровок.Добавить(Перечисления.уп_РасшифровкаСостоянийПС.ОкончаниеПенсионныхОснований);
#КонецУдаления
	Запрос.УстановитьПараметр("СписокРасшифровок", СписокРасшифровок);
	Запрос.УстановитьПараметр("ОтборПодразделение", Объект.ОтборПодразделение);
	//Бардошина -
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДатаДок", Объект.ДатаФормированияДокументов);
	Запрос.УстановитьПараметр("Банк", Объект.ОтборПолучатель);
	#Вставка
	//+ХМНПФ 24.03.2022 ХлопцевБА // Дополнительный отбор // 
	мФормаВыплатыПочта = Новый Массив;
	Если Объект.ТолькоНаПочту Тогда
		мФормаВыплатыПочта.Добавить(Перечисления.уп_ФормыВыплаты.Почта);
		мФормаВыплатыПочта.Добавить(Перечисления.уп_ФормыВыплаты.ПочтаСДоставкойНаДом);
		мФормаВыплатыПочта.Добавить(Перечисления.уп_ФормыВыплаты.ПочтовыйПереводСУведомлением);
		мФормаВыплатыПочта.Добавить(Перечисления.уп_ФормыВыплаты.ПочтаСДоставкойНаДомУведомлением);
	КонецЕсли;                
	Если ЭтотОбъект["ТолькоЧерезРасчетныйСчет"] Тогда
		мФормаВыплатыПочта.Добавить(Перечисления.уп_ФормыВыплаты.РасчетныйСчет);
	КонецЕсли;                
	Запрос.УстановитьПараметр("мФормаВыплатыПочта", мФормаВыплатыПочта);
	//-ХМНПФ 24.03.2022  ХлопцевБА // Дополнительный отбор // 
	//+ХМНПФ 28.02.2021 ХлопцевБА // Дополнительный отбор // №3086
	Запрос.УстановитьПараметр("ОтборУчастник", Объект.ОтборУчастник);
	//-ХМНПФ 28.02.2021  ХлопцевБА // Дополнительный отбор // №3086
	#КонецВставки
	Если ЗначениеЗаполнено(Объект.ОтборГруппаДоговоров) Тогда
		ЗначениеОтбораДоговора = Объект.ОтборГруппаДоговоров;
	Иначе
		ЗначениеОтбораДоговора = Объект.ОтборПенсионныйДоговор;
	КонецЕсли;
	#Вставка
	//+ХМНПФ 10.01.2023 ХлопцевБА // №3726 // 
	Если ТолькоНовые Тогда
		Если ЗначениеЗаполнено(Объект.ОтборПенсионныйДоговор) Тогда   
			
			ПенсионныйДоговор = Объект.ОтборПенсионныйДоговор;
			ГруппаДоговоров   = Неопределено;
			ЗначениеОтбора = хм_ПрочиеПроцедуры.ХМ_ПолучитьСписокДоговоров(Объект.ХМ_НачалоПериода, Объект.ХМ_КонецПериода,ПенсионныйДоговор,ГруппаДоговоров);
			ЗначениеОтбораДоговора = ЗначениеОтбора.СписокДоговоров;  

		ИначеЕсли ЗначениеЗаполнено(Объект.ОтборГруппаДоговоров) Тогда  
			ПенсионныйДоговор = Неопределено;
			ГруппаДоговоров   = Объект.ОтборГруппаДоговоров;
			ЗначениеОтбора = хм_ПрочиеПроцедуры.ХМ_ПолучитьСписокДоговоров(Объект.ХМ_НачалоПериода, Объект.ХМ_КонецПериода,ПенсионныйДоговор,ГруппаДоговоров);
			ЗначениеОтбораДоговора = ЗначениеОтбора.СписокДоговоров;  
		Иначе           
			ПенсионныйДоговор = Неопределено;
			ГруппаДоговоров   = Неопределено;
			ЗначениеОтбора = хм_ПрочиеПроцедуры.ХМ_ПолучитьСписокДоговоров(Объект.ХМ_НачалоПериода, Объект.ХМ_КонецПериода,ПенсионныйДоговор,ГруппаДоговоров);
			ЗначениеОтбораДоговора = ЗначениеОтбора.СписокДоговоров;  
		КонецЕсли; 

		ЗначениеОтбораПС = ЗначениеОтбора.СписокСчетов;   
		
	КонецЕсли;   
	Запрос.УстановитьПараметр("ПенсионныйСчет", ЗначениеОтбораПС);
	//-ХМНПФ 10.01.2023 ХлопцевБА // №3726 // 
	#КонецВставки
	Запрос.УстановитьПараметр("ПенсионныйДоговор", ЗначениеОтбораДоговора);
	Запрос.УстановитьПараметр("ОтборВкладчик", Объект.ОтборВкладчик);
	Запрос.УстановитьПараметр("НомерОчереди", Объект.ОтборОчередностьВыплаты);
	Запрос.УстановитьПараметр("ВключатьСчетаУмершихВТекущемПериоде", Объект.ВключатьСчетаУмершихВТекущемПериоде);
	
	Если Объект.НачислятьЗаПрошлыеПериоды Тогда
		ДатаНачалаПериода 			= уп_НПФ.ОпределитьДатуНачалаПериода(Объект.ПериодНачисления,Периодичность);
		Коэффициент 				= уп_НПФ.ЧисловойКоэффициентПериода(Периодичность);
		ДатаНачалаПрошлогоПериода   = ДобавитьМесяц(ДатаНачалаПериода, -Коэффициент);
		ДатаКонцаПрошлогоПериода    = ДобавитьМесяц(ДатаКонцаПериода, -Коэффициент);
		
		ПериодичностьИмя = "";
		
		Если Периодичность = Перечисления.уп_ПериодичностьВыплат.Ежемесячно Тогда
			ПериодичностьИмя = "МЕСЯЦ";
		ИначеЕсли Периодичность = Перечисления.уп_ПериодичностьВыплат.Ежеквартально Тогда
			ПериодичностьИмя = "Квартал";
		ИначеЕсли Периодичность = Перечисления.уп_ПериодичностьВыплат.Полугодие Тогда
			ПериодичностьИмя = "Полугодие"; 
		Иначе
			ПериодичностьИмя = "Год";		
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ДатаНачалаПрошлогоПериода", ДатаНачалаПрошлогоПериода);
		Запрос.УстановитьПараметр("ДатаКонцаПрошлогоПериода", ДатаКонцаПрошлогоПериода);
		Запрос.УстановитьПараметр("ДатаНачалаВремен", '19800101');
		
		Текст = СтрЗаменить(Текст, "&ПериодичностьИмя", ПериодичностьИмя); 

	КонецЕсли;
	
	Запрос.Текст = Текст;
	
	ВыборкаПакет = Запрос.ВыполнитьПакет();
	//ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
	ТаблицаСчетов = ВыборкаПакет[4].Выгрузить();
	
	ТаблицаСчетов.Колонки.Добавить("НомерНачисления", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
	ТаблицаСчетов.Колонки.Добавить("НачисленоРаннее", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
	
	ТаблицаДоплат = Новый ТаблицаЗначений;
	ТаблицаДоплат.Колонки.Добавить("НомерСчета", Новый ОписаниеТипов("СправочникСсылка.уп_ПенсионныеСчета"));
	ТаблицаДоплат.Колонки.Добавить("КоличествоНачисленныхДоплат", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
	
	ИмяРасчетаПенсий = уп_НПОСервер.ПолучитьИмяМодуляРасчетаПенсий(); 
	Если СтрНайти(ИмяРасчетаПенсий, "Ошибка") <> 0 Тогда 
		Сообщить("Расчет невозможен. "+ИмяРасчетаПенсий);  
		Возврат;
	Иначе
		МодульРасчетаПенсий = ОбщегоНазначения.ОбщийМодуль(ИмяРасчетаПенсий);   //по умолчанию  уп_ОбщегоНазначенияУчетРезервов
	КонецЕсли;
	Если Объект.НачислятьЗаПрошлыеПериоды Тогда
		
		ВыборкаПоПрошлымПериодам = ВыборкаПакет[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Период" ,"Все");
		ТаблицаСчетовПрошлыхПериодов = ТаблицаСчетов.Скопировать();
		
		ТаблицаСчетовПрошлыхПериодов.Очистить();
		
		Пока ВыборкаПоПрошлымПериодам.Следующий() Цикл
			
			ВыборкаПоСчетам = ВыборкаПоПрошлымПериодам.Выбрать();
			
			Пока ВыборкаПоСчетам.Следующий() Цикл 
				
				НайденнаяСтрока = ТаблицаСчетовПрошлыхПериодов.Найти(ВыборкаПоСчетам.НомерСчета, "НомерСчета");
				
			    Если НайденнаяСтрока = Неопределено Тогда
					 НоваяСтрока = ТаблицаСчетовПрошлыхПериодов.Добавить();
					 ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСчетам);
				КонецЕсли;
				
			КонецЦикла;
			
			Для каждого Строка Из ТаблицаСчетовПрошлыхПериодов Цикл;
                Строка.Период = ВыборкаПоПрошлымПериодам.Период;
				Строка.НомерНачисления = Строка.НомерНачисления + 1;
			КонецЦикла;
			
			//ТаблицаСчетовПрошлыхПериодов.ЗаполнитьЗначения(ВыборкаПоПрошлымПериодам.Период, "Период"); 
			#Вставка
			//+ХМНПФ 20.12.2023 ХлопцевБА // №3730  
			Если Объект.ВыплачиватьОстатокВПоследнююПенсию Тогда
				пОрганизация = Новый Структура;
				пОрганизация.Вставить("Организация",Объект.Организация);
				пОрганизация.Вставить("ВыплачиватьОстатокВПоследнююПенсию", Объект.ВыплачиватьОстатокВПоследнююПенсию); 
			Иначе
				пОрганизация = Объект.Организация;
			КонецЕсли;
			//-ХМНПФ 20.12.2023 ХлопцевБА // №3730    
			#КонецВставки
			//уп_ОбщегоНазначенияУчетРезервов.СформироватьТЗНачисления(Объект.Организация,ТЗДокумента, Периодичность, ТаблицаСчетовПрошлыхПериодов, Объект.ДатаФормированияДокументов, ВыборкаПоПрошлымПериодам.Период,, ТаблицаДоплат);
			#Удаление
			МодульРасчетаПенсий.СформироватьТЗНачисления(Объект.Организация,ТЗДокумента, Периодичность, ТаблицаСчетовПрошлыхПериодов, Объект.ДатаФормированияДокументов, ВыборкаПоПрошлымПериодам.Период,, ТаблицаДоплат);
			#КонецУдаления
			#Вставка
			МодульРасчетаПенсий.СформироватьТЗНачисления(пОрганизация,ТЗДокумента, Периодичность, ТаблицаСчетовПрошлыхПериодов, Объект.ДатаФормированияДокументов, ВыборкаПоПрошлымПериодам.Период,, ТаблицаДоплат);
			#КонецВставки
		КонецЦикла;
		
		Для каждого Строка Из ТаблицаСчетовПрошлыхПериодов Цикл
			
			СтруктураПоиска = Новый Структура("НомерСчета, Участник");
			СтруктураПоиска.НомерСчета = Строка.НомерСчета;
			СтруктураПоиска.Участник = Строка.Участник;
			
			МассивСтрокТаблСчетов = ТаблицаСчетов.НайтиСтроки(СтруктураПоиска);
			
			Если МассивСтрокТаблСчетов.Количество() > 0  Тогда
				МассивСтрокТаблСчетов[0].НомерНачисления =  Строка.НомерНачисления+1;
				МассивСтрокТаблСчетов[0].НачисленоРаннее =  Строка.НачисленоРаннее;
			КонецЕсли;
			
		КонецЦикла
		
	КонецЕсли;
	
	//Бардошина +
	//получение остатков в РН "Задолженность по пенсиям" по тем, кому было сделано восстановление выплат
	//ТЗОстаткиПоЗадолженности = уп_ОбщегоНазначенияУчетРезервов.ПолучитьТаблицуОстатковПоЗадолженности(ТаблицаСчетов, Объект.ДатаФормированияДокументов, Объект.ПериодНачисления);
	#Удаление
	ТЗОстаткиПоЗадолженности = МодульРасчетаПенсий.ПолучитьТаблицуОстатковПоЗадолженности(ТаблицаСчетов, Объект.ДатаФормированияДокументов, Объект.ПериодНачисления);
	//Бардошина -
	МодульРасчетаПенсий.СформироватьТЗНачисления(Объект.Организация,ТЗДокумента, Периодичность, ТаблицаСчетов, Объект.ДатаФормированияДокументов, Объект.ПериодНачисления, ТЗОстаткиПоЗадолженности, ТаблицаДоплат); //Бардошина+-
	#КонецУдаления
	#Вставка
	ТЗОстаткиПоЗадолженности = уп_ОбщегоНазначенияУчетРезервов.ПолучитьТаблицуОстатковПоЗадолженности(ТаблицаСчетов, Объект.ДатаФормированияДокументов, Объект.ПериодНачисления);
	
	//+ХМНПФ 20.12.2023 ХлопцевБА // №3730 
	Если Объект.ВыплачиватьОстатокВПоследнююПенсию Тогда
		пОрганизация = Новый Структура;
		пОрганизация.Вставить("Организация",Объект.Организация);
		пОрганизация.Вставить("ВыплачиватьОстатокВПоследнююПенсию", Объект.ВыплачиватьОстатокВПоследнююПенсию); 
	Иначе
		пОрганизация = Объект.Организация;
	КонецЕсли;
	//-ХМНПФ 20.12.2023 ХлопцевБА // №3730    

	уп_ОбщегоНазначенияУчетРезервов.СформироватьТЗНачисления(пОрганизация,ТЗДокумента, Периодичность, ТаблицаСчетов, Объект.ДатаФормированияДокументов, Объект.ПериодНачисления, ТЗОстаткиПоЗадолженности, ТаблицаДоплат); //Бардошина+-
	#КонецВставки
КонецПроцедуры

&НаКлиенте
Процедура НПФ_ТолькоНаПочтуПриИзмененииПосле(Элемент)
	
	Объект.КоличествоВПачке = 25000;
	Объект.Получатель = Ложь;
	Объект.ГруппаДоговоров = Ложь;
	Объект.ОчередностьВыплаты = Ложь;
	Объект.Вкладчик = Ложь;
	Объект.ПенсионныйДоговор = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НПФ_хм_ТолькоНовыеПриИзмененииПосле(Элемент)
	Если Объект.ХМ_ТолькоНовые Тогда
		Объект.НачислятьЗаПрошлыеПериоды = Не Объект.ХМ_ТолькоНовые;
		Элементы.НачислятьЗаПрошлыеПериоды.ТолькоПросмотр = Объект.ХМ_ТолькоНовые;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура хмНДФЛ(Команда)
	ОткрытьФорму("Обработка.уп_НачислениеНДФЛ.Форма.уф_Форма");
КонецПроцедуры

&НаСервере
Процедура НПФ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.ОтражатьНБТекущимПериодом.Доступность = Ложь;
	Объект.ОтражатьНБТекущимПериодом = Истина;   
	ХМДополнитьФорму();
КонецПроцедуры

&НаКлиенте
&После("КнопкаСформировать")
Процедура НПФ_КнопкаСформировать(Команда)
	Попытка
		spVoice = Новый COMОбъект("SAPI.SpVoice");
		spVoice.Speak("Начисление пенсии завершено");
		spVoice = Неопределено;
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
&После("КнопкаПровести")
Процедура НПФ_КнопкаПровести(Команда)
	Попытка
		spVoice = Новый COMОбъект("SAPI.SpVoice");
		spVoice.Speak("Проведение пенсии завершено");
		spVoice = Неопределено;
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаписатьДокументы")
Процедура ХМЗаписатьДокументы(ТЗ)

	//сортировка по признакам
	ПризнакиСортировки = "Пусто, ПодразделениеБУ"; //Это если не сортировать  
	Если Объект.ТипВкладчика Тогда
		ПризнакиСортировки = ПризнакиСортировки + ",ПенсионныйДоговор,ТипВкладчика";
	КонецЕсли;	
	если Объект.Получатель тогда
		ПризнакиСортировки = ПризнакиСортировки + ",Получатель";
	конецесли;
	если Объект.Вкладчик тогда
		ПризнакиСортировки = ПризнакиСортировки + ",Вкладчик";
	конецесли;
	если Объект.ГруппаДоговоров тогда
		ПризнакиСортировки = ПризнакиСортировки + ",ГруппаДоговора";
	конецесли;
	если Объект.ПенсионныйДоговор И НЕ Объект.ТипВкладчика тогда
		ПризнакиСортировки = ПризнакиСортировки + ",ПенсионныйДоговор";
	конецесли;
	если Объект.ОчередностьВыплаты тогда
		ПризнакиСортировки = ПризнакиСортировки + ",ОчередностьВыплаты";
	конецесли;
	ТЗ.Сортировать(ПризнакиСортировки);

	//ФормаИндикацииХодаЗагрузки = ПолучитьОбщуюФорму("ХодВыполненияОбработкиДанных");
	//ФормаИндикацииХодаЗагрузки.НаименованиеОбработкиДанных = "Начисление пенсии.";
	//ФормаИндикацииХодаЗагрузки.Открыть();
	//
	//ФормаИндикацииХодаЗагрузки.МаксимальноеЗначение = ТЗ.Количество()-1;
	//ФормаИндикацииХодаЗагрузки.КомментарийОбработкиДанных = "Записываем данные ";
	//ФормаИндикацииХодаЗагрузки.КомментарийЗначения = "Записано данных:";
	//ФормаИндикацииХодаЗагрузки.Значение = 0;

	ТПодразделение = ""; ТПолучатель=""; ТГруппаДоговора=""; ТПенсионныйДоговор=""; ТОчередностьВыплаты=""; НачислениеПенсий="";
	ТВкладчик = ""; ТТипВкладчика = "";
	СчетчикСтрокВДокументе = 0;
	Для Н=0 По ТЗ.Количество()-1 Цикл
		ТекСтрока = ТЗ.Получить(Н);
		СоответствуетДокументу=Истина;
		Если ТПодразделение <> ТекСтрока.ПодразделениеБУ Тогда
			СоответствуетДокументу=Ложь; 
		ИначеЕсли Объект.ТипВкладчика И (ТТипВкладчика <> ТекСтрока.ТипВкладчика 
			ИЛИ (ТТипВкладчика = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо И ТПенсионныйДоговор<>ТекСтрока.ПенсионныйДоговор)) Тогда
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.Получатель и (ТПолучатель <> ТекСтрока.Получатель) Тогда 
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.Вкладчик и (ТВкладчик <> ТекСтрока.Вкладчик) Тогда 
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.ГруппаДоговоров и (ТГруппаДоговора <> ТекСтрока.ГруппаДоговора) Тогда 
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.ПенсионныйДоговор и (ТПенсионныйДоговор <> ТекСтрока.ПенсионныйДоговор) Тогда 
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.ОчередностьВыплаты и (ТОчередностьВыплаты <> ТекСтрока.ОчередностьВыплаты) Тогда 
			СоответствуетДокументу=Ложь;
		ИначеЕсли Объект.ПоПачкам и (СчетчикСтрокВДокументе>=Объект.КоличествоВПачке) Тогда 
			СоответствуетДокументу=Ложь;
		КонецЕсли;


		если (Н=0) или не СоответствуетДокументу тогда
			если Н>0 тогда
				НачислениеПенсий.ОбменДанными.Загрузка = Истина;
				НачислениеПенсий.Записать();
				НовоеНачисление = Объект.НовыеНачисления.Добавить();
				НовоеНачисление.Документ = НачислениеПенсий.Ссылка;
			конецесли;

			ТПодразделение = ТекСтрока.ПодразделениеБУ;
			если Объект.ТипВкладчика тогда
				ТТипВкладчика = ТекСтрока.ТипВкладчика;
				ТПенсионныйДоговор = ТекСтрока.ПенсионныйДоговор;
			конецесли;
			если Объект.Получатель тогда
				ТПолучатель = ТекСтрока.Получатель;
			конецесли;
			если Объект.Вкладчик тогда
				ТВкладчик = ТекСтрока.Вкладчик;
			конецесли;
			если Объект.ГруппаДоговоров тогда
				ТГруппаДоговора = ТекСтрока.ГруппаДоговора;
			конецесли;
			если Объект.ПенсионныйДоговор тогда
				ТПенсионныйДоговор = ТекСтрока.ПенсионныйДоговор;
			конецесли;
			если Объект.ОчередностьВыплаты тогда
				ТОчередностьВыплаты = ТекСтрока.ОчередностьВыплаты;
			конецесли;

			НачислениеПенсий = Документы.уп_НачислениеПенсий.СоздатьДокумент();
			НачислениеПенсий.Организация = Объект.Организация;
			//УстановитьНомерДокумента(НачислениеПенсий);
			НачислениеПенсий.ПериодНачисления = Объект.ПериодНачисления;
			НачислениеПенсий.ДокументРегистрации = Объект.ДокументРегистрации;
			НачислениеПенсий.ОтражатьВБухгалтерскомУчете = Объект.ОтражатьВБухгалтерскомУчете;
			//НачислениеПенсий.ОтражатьВНалоговомУчете = ОтражатьВНалоговомУчете;
			НачислениеПенсий.уп_ВидДеятельности = Объект.уп_ВидДеятельности;
			НачислениеПенсий.ПодразделениеОрганизации = ТПодразделение;
			НачислениеПенсий.ОтражатьНБТекущимПериодом = Объект.ОтражатьНБТекущимПериодом;
			НачислениеПенсий.Ответственный = Объект.Ответственный;
			СчетчикСтрокВДокументе = 0;

			СоответствиеДокументу = Новый Соответствие();
			если Объект.Получатель тогда
				СоответствиеДокументу.Вставить("Получатель",ТекСтрока.Получатель);
			конецесли;
			если Объект.ГруппаДоговоров тогда
				СоответствиеДокументу.Вставить("ГруппаДоговора",ТекСтрока.ГруппаДоговора);
			конецесли;
			если Объект.ПенсионныйДоговор тогда
				СоответствиеДокументу.Вставить("ПенсионныйДоговор",ТекСтрока.ПенсионныйДоговор);
			конецесли;
			если Объект.ОчередностьВыплаты тогда
				СоответствиеДокументу.Вставить("ОчередностьВыплаты",ТекСтрока.ОчередностьВыплаты);
			конецесли;


			если (Объект.Переключатель1=0) и (Объект.ОчередностьВыплаты) тогда
				ЧислоЗаписи = РегистрыСведений.уп_ДатыОчередей.Получить(Новый Структура("НомерОчереди",ТОчередностьВыплаты)).ДатаВМесяце;;
				если НЕ ЗначениеЗаполнено(ЧислоЗаписи) тогда
					ЧислоЗаписи = 1;
				конецесли;				
				МесяцЗаписи = Месяц(Объект.ДатаФормированияДокументов);
				ГодЗаписи = Год(Объект.ДатаФормированияДокументов);
				ДатаЗаписи = Дата(ГодЗаписи, МесяцЗаписи, ЧислоЗаписи); 
				НачислениеПенсий.Дата = ДатаЗаписи;
			иначе
				НачислениеПенсий.Дата = Объект.ДатаФормированияДокументов;
			конецесли;
			//уп_ОсновнаяПоставкаСервер.УстановитьНомерДокумента(НачислениеПенсий);
		конецесли;	

		//запись строки документа
		//Бардошина +
		Если ТекСтрока.ЭтоЗадолженность Тогда
			СтрокаДокумента = НачислениеПенсий.НачислениеЗадолженностиПоПриостановкам.Добавить();
			СтрокаДокумента.НомерСчета = ТекСтрока.НомерСчета;
			#Вставка
			// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
			СтрокаДокумента.Участник = ТекСтрока.Участник;
			СтрокаДокумента.ПенсионныйДоговор = ТекСтрока.ПенсионныйДоговор;
			// -ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
			#КонецВставки
			#Удаление 
			СтрокаДокумента.Участник = ТекСтрока.НомерСчета.Участник;
			СтрокаДокумента.ПенсионныйДоговор = ТекСтрока.НомерСчета.Владелец;				
			#КонецУдаления
			СтрокаДокумента.Сумма = ТекСтрока.Сумма;
			СтрокаДокумента.Доплата = ТекСтрока.Доплата; 
			СтрокаДокумента.Пенсия = ТекСтрока.Пенсия;
			СтрокаДокумента.НачалоПериодаНачисления = ТекСтрока.НачалоПериода;
			СтрокаДокумента.КонецПериодаНачисления =  ТекСтрока.КонецПериода;
			СтрокаДокумента.Периодичность = ТекСтрока.Периодичность;
		Иначе	
			СтрокаДокумента = НачислениеПенсий.СписокПенсионеров.Добавить();
			СчетчикСтрокВДокументе = СчетчикСтрокВДокументе + 1;
			СтрокаДокумента.НомерСчета = ТекСтрока.НомерСчета;
			#Вставка
			// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
			СтрокаДокумента.Участник = ТекСтрока.Участник;
			СтрокаДокумента.ПенсионныйДоговор = ТекСтрока.ПенсионныйДоговор;
			// -ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
			#КонецВставки
			#Удаление 
			СтрокаДокумента.Участник = ТекСтрока.НомерСчета.Участник;
			СтрокаДокумента.ПенсионныйДоговор = ТекСтрока.НомерСчета.Владелец;
			#КонецУдаления
			СтрокаДокумента.Схема = ТекСтрока.Схема;
			СтрокаДокумента.ТипПенсии = ТекСтрока.ТипПенсии;
			СтрокаДокумента.Сумма = ТекСтрока.Сумма;
			СтрокаДокумента.Доплата = ТекСтрока.Доплата; 
			СтрокаДокумента.Пенсия = ТекСтрока.Пенсия; 
			СтрокаДокумента.НачалоПериодаНачисления = ТекСтрока.НачалоПериода;
			СтрокаДокумента.КонецПериодаНачисления =  ТекСтрока.КонецПериода;
			СтрокаДокумента.Периодичность = ТекСтрока.Периодичность;
			СтрокаДокумента.ЭтоВосстановлениеЗадолженности = ТекСтрока.ЭтоВосстановлениеЗадолженности;
		КонецЕсли;
		//Бардошина -

		//ФормаИндикацииХодаЗагрузки.Значение = Н;

		Если Н = ТЗ.Количество()-1 Тогда 
			НачислениеПенсий.ОбменДанными.Загрузка = Истина;
			НачислениеПенсий.Записать();
			НовоеНачисление = Объект.НовыеНачисления.Добавить();
			НовоеНачисление.Документ = НачислениеПенсий.Ссылка;
		КонецЕсли;
	КонецЦикла;	

	//ФормаИндикацииХодаЗагрузки.Закрыть();

КонецПроцедуры

Процедура ХМДополнитьФорму() 
	
	СвойстваГруппы = Новый Структура();
	СвойстваГруппы.Вставить("ВставитьПередЭлементом",Элементы.ГруппаПерваяПравая);
	СвойстваГруппы.Вставить("Группировка",ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	СвойстваГруппы.Вставить("Отображение",ОтображениеОбычнойГруппы.СлабоеВыделение);
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьОбычнуюГруппу(ЭтаФорма,"хм_ГруппаПериод",Элементы.ГруппаПервая,СвойстваГруппы);
    Элементы.Переместить(Элементы.ГруппаПерваяЛевая,Элементы["хм_ГруппаПериод"]);
	СвойстваГруппы = Новый Структура();
	СвойстваГруппы.Вставить("Группировка",ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	СвойстваГруппы.Вставить("Отображение",ОтображениеОбычнойГруппы.СильноеВыделение);
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьОбычнуюГруппу(ЭтаФорма,"хм_ГруппаПрошлыеНачисления",Элементы["хм_ГруппаПериод"],СвойстваГруппы);
	
	СвойстваГруппы = Новый Структура();
	СвойстваГруппы.Вставить("Группировка",ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	СвойстваГруппы.Вставить("Отображение",ОтображениеОбычнойГруппы.СлабоеВыделение);
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьОбычнуюГруппу(ЭтаФорма,"хм_ВыбПериод",Элементы["хм_ГруппаПрошлыеНачисления"],СвойстваГруппы);
    	
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	СвойстваПоля.Вставить("ВставитьПередЭлементом",Элементы["хм_ВыбПериод"]);
	СвойстваПоля.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"хм_ТолькоНовые",Элементы["хм_ГруппаПрошлыеНачисления"],"Объект.хм_ТолькоНовые",СвойстваПоля);
	
	уп_ДинамическиеЭлементыСервер.ХМ_УставноитьДействие(Элементы["хм_ТолькоНовые"],"ПриИзменении","НПФ_хм_ТолькоНовыеПриИзмененииПосле");
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Заголовок", "от");  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"хм_НачалоПериода",Элементы["хм_ВыбПериод"],"Объект.хм_НачалоПериода",СвойстваПоля);
   	СвойстваПоля.Вставить("Заголовок", "до");  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"хм_КонецПериода",Элементы["хм_ВыбПериод"],"Объект.хм_КонецПериода",СвойстваПоля);	

	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	СвойстваПоля.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ВыплачиватьОстатокВПоследнююПенсию",Элементы.ГруппаПерваяПравая,"Объект.ВыплачиватьОстатокВПоследнююПенсию",СвойстваПоля);	
	
   	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	СвойстваПоля.Вставить("ВставитьПередЭлементом",Элементы.ГруппаПачки);
	СвойстваПоля.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"хм_ОтборНеПенсионныйДоговор",Элементы.ГруппаТретья,"Объект.хм_ОтборНеПенсионныйДоговор",СвойстваПоля);  
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("ВставитьПередЭлементом",Элементы.ГруппаОчередность); 
	СвойстваПоля.Вставить("Заголовок","Участник"); 
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ОтборУчастник",Элементы.ГруппаЧетыре,"Объект.ОтборУчастник",СвойстваПоля);
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	СвойстваПоля.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ТолькоНаПочту",Элементы.ГруппаОчередность,"Объект.ТолькоНаПочту",СвойстваПоля);
	уп_ДинамическиеЭлементыСервер.ХМ_УставноитьДействие(Элементы["ТолькоНаПочту"],"ПриИзменении","НПФ_ТолькоНаПочтуПриИзмененииПосле");
	
	уп_ДинамическиеЭлементыСервер.ХМ_ДобавитьРеквизит(ЭтаФорма,"ТолькоЧерезРасчетныйСчет",Новый ОписаниеТипов("Булево"),,"Только через расчетный счет");
    уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ТолькоЧерезРасчетныйСчет",Элементы.ГруппаОчередность,"ТолькоЧерезРасчетныйСчет",СвойстваПоля);	
	
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьКомандуСКнопкой(ЭтаФорма,"хмНДФЛ","НДФЛ",Элементы.ФормаКоманднаяПанель);
		
КонецПроцедуры
