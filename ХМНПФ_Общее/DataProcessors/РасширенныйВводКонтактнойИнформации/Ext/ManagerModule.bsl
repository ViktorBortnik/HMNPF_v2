
&ИзменениеИКонтроль("АдресВСтарыйСписокПолей")
Функция ХМАдресВСтарыйСписокПолей(XDTOАдрес, РасширенныйСоставПолей)
	Список = Новый СписокЗначений;
	
	ПространствоИмен = УправлениеКонтактнойИнформациейЛокализация.ПространствоИмен();
	XDTOТип = XDTOАдрес.Тип();
	Если XDTOТип = ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
		
		// Страна с кодом
		ДобавитьЗначение(Список, "Страна", XDTOАдрес.Страна);
		Если ПустаяСтрока(XDTOАдрес.Страна) Тогда
			КодСтраны = "";
		Иначе
			Страна = Справочники.СтраныМира.НайтиПоНаименованию(XDTOАдрес.Страна, Истина);
			КодСтраны = СокрЛП(Страна.Код);
		КонецЕсли;
		ДобавитьЗначение(Список, "КодСтраны", КодСтраны);
		
		Если Не РаботаСАдресамиКлиентСервер.ЭтоОсновнаяСтрана(XDTOАдрес.Страна) Тогда
			Возврат Список;
		КонецЕсли;
		
		АдресРФ = XDTOАдрес.Состав;
		
	ИначеЕсли XDTOТип = ФабрикаXDTO.Тип(РаботаСАдресами.ПространствоИмен(), "АдресРФ") Тогда
		АдресРФ = XDTOАдрес;
		
	Иначе
		Возврат Список;
		
	КонецЕсли;
	
	ДобавитьЗначение(Список, "Индекс", ПочтовыйИндексАдреса(АдресРФ) );
	
	ДобавитьЗначение(Список, "Регион", АдресРФ.СубъектРФ);
	ДобавитьЗначение(Список, "КодРегиона", РаботаСАдресами.КодРегиона(АдресРФ.СубъектРФ) );
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "РегионСокращение", Сокращение(АдресРФ.СубъектРФ));
	КонецЕсли;
	
	Район = РайонАдреса(АдресРФ);
	ДобавитьЗначение(Список, "Район", Район);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "РайонСокращение", Сокращение(Район));
	КонецЕсли;
	
	ДобавитьЗначение(Список, "Город", АдресРФ.Город);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "ГородСокращение", Сокращение(АдресРФ.Город));
	КонецЕсли;
	
	// преобразование в КЛАДР
	НаселенныйПункт                 = АдресРФ.НаселПункт;
	Улица                           = АдресРФ.Улица;
	ДополнительныйЭлементАдреса     = НайтиДополнительныйЭлементАдреса(АдресРФ).Значение;
	ПодчиненныйЭлементЭлементАдреса = ДополнительныйЭлементАдреса(АдресРФ,
		РаботаСАдресами.XPathДополнительногоОбъектаАдресации(91));
	
	ЕстьДополнительныйЭлементАдреса     = ЗначениеЗаполнено(ДополнительныйЭлементАдреса);
	ЕстьПодчиненныйЭлементЭлементАдреса = ЗначениеЗаполнено(ПодчиненныйЭлементЭлементАдреса);
	
	Если ЕстьДополнительныйЭлементАдреса Тогда
		
		Если ЕстьПодчиненныйЭлементЭлементАдреса Тогда
			НаселенныйПункт = ДополнительныйЭлементАдреса;
			Улица = ПодчиненныйЭлементЭлементАдреса;
		Иначе
			Улица = ДополнительныйЭлементАдреса;
		КонецЕсли;
		
	ИначеЕсли ЕстьПодчиненныйЭлементЭлементАдреса Тогда
		Улица = ПодчиненныйЭлементЭлементАдреса;
	КонецЕсли;
	#Вставка		
	// +ХМНПФ 22.11.2019 ШадринАВ // АдресВСтарыйСписокПолей() // №2575
	Если НЕ ЗначениеЗаполнено(АдресРФ.Улица) И ЗначениеЗаполнено(ДополнительныйЭлементАдреса) Тогда
		Улица = ДополнительныйЭлементАдреса; // №2584
	ИначеЕсли ЗначениеЗаполнено(АдресРФ.Улица) И ЗначениеЗаполнено(ДополнительныйЭлементАдреса) Тогда
		Улица = ДополнительныйЭлементАдреса + ","+АдресРФ.Улица;  // №2584
	ИначеЕсли ЗначениеЗаполнено(АдресРФ.Улица) Тогда
		Улица = АдресРФ.Улица;
	КонецЕсли;
	// -ХМНПФ 22.11.2019 ШадринАВ // АдресВСтарыйСписокПолей() // №2575
	#КонецВставки
	
	ДобавитьЗначение(Список, "НаселенныйПункт", НаселенныйПункт);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "НаселенныйПунктСокращение", Сокращение(НаселенныйПункт));
	КонецЕсли;

	ДобавитьЗначение(Список, "Улица", Улица);
	
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "УлицаСокращение", Сокращение(Улица));
	КонецЕсли;
	
	// Дом и корпус
	ЗданияИПомещения = ЗданияИПомещенияАдреса(АдресРФ);
	
	Если ЗданияИПомещения.Здания.Количество() = 0 Тогда
		ДобавитьЗначение(Список, "ТипДома", "");
		ДобавитьЗначение(Список, "Дом",     "");
	Иначе
		СтрокаОбъекта = ЗданияИПомещения.Здания[0];
		ДобавитьЗначение(Список, "ТипДома", СтрокаОбъекта.Тип,      Ложь);
		ДобавитьЗначение(Список, "Дом",     СтрокаОбъекта.Значение, Ложь);
		
		ЗданияИПомещения.Удалить(СтрокаОбъекта);
		Если ЗданияИПомещения.Здания.Количество() > 0 Тогда
			
			ПараметрыОбъекта = ЗначениеЗданияИлиПомещения(ЗданияИПомещения.Здания, РаботаСАдресамиПовтИсп.ВариантыДанныхСтроение(), Ложь);
			Если ПараметрыОбъекта.Количество() = 0 Тогда
				ДобавитьЗначение(Список, "ТипКорпуса", "");
				ДобавитьЗначение(Список, "Корпус",     "");
			ИначеЕсли ПараметрыОбъекта.Количество() = 1 Тогда
				СтрокаОбъекта  = ПараметрыОбъекта[0];
				ДобавитьЗначение(Список, "ТипКорпуса", СтрокаОбъекта.Тип,      Ложь);
				ДобавитьЗначение(Список, "Корпус",     СтрокаОбъекта.Значение, Ложь);
			Иначе
				ТипКорпуса  = ПараметрыОбъекта[0].Тип;
				ЗначениеКорпуса = "";
				Разделитель = "";
				Сокращения = РаботаСАдресами.СокращенияОбъектовАдресацииАдресаРФ();
				Для Каждого СтрокаОбъекта Из ПараметрыОбъекта Цикл
					#Удаление 
					ИмяЗдания = ?(ЗначениеЗаполнено(Сокращения[СтрокаОбъекта.Тип]), Сокращения[СтрокаОбъекта.Тип], СтрокаОбъекта.Тип);
					ЗначениеКорпуса  = ЗначениеКорпуса  + Разделитель + ИмяЗдания + " " + СтрокаОбъекта.Значение;
					Разделитель = ", " ;
					#КонецУдаления  
					#Вставка 
					// +ХМНПФ 20.11.2019 ШадринАВ // АдресВСтарыйСписокПолей() // №2567
					ДобавитьЗначение(Список, ?(СтрокаОбъекта.Тип="Корпус", "ТипКорпуса", "Тип"+СтрокаОбъекта.Тип), СтрокаОбъекта.Тип, РасширенныйСоставПолей);
					ДобавитьЗначение(Список, СтрокаОбъекта.Тип, СтрокаОбъекта.Значение, РасширенныйСоставПолей);	
					// -ХМНПФ 20.11.2019 ШадринАВ // АдресВСтарыйСписокПолей() // №2567
					#КонецВставки
				КонецЦикла;
				#Удаление 
				ДобавитьЗначение(Список, "ТипКорпуса", ТипКорпуса,      РасширенныйСоставПолей);
				ДобавитьЗначение(Список, "Корпус",     ЗначениеКорпуса  , РасширенныйСоставПолей);
				#КонецУдаления  
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыОбъекта = ЗначениеЗданияИлиПомещения(ЗданияИПомещения.Помещения, РаботаСАдресамиПовтИсп.ВариантыДанныхПомещение(), РасширенныйСоставПолей);
	Если ПараметрыОбъекта.Количество() = 0 Тогда
		ДобавитьЗначение(Список, "ТипКвартиры", "");
		ДобавитьЗначение(Список, "Квартира",    "");
	ИначеЕсли ПараметрыОбъекта.Количество() = 1 Тогда
		СтрокаОбъекта  = ПараметрыОбъекта[0];
		ДобавитьЗначение(Список, "ТипКвартиры", СтрокаОбъекта.Тип,      РасширенныйСоставПолей);
		ДобавитьЗначение(Список, "Квартира",    СтрокаОбъекта.Значение, РасширенныйСоставПолей);
	Иначе
		ТипПомещения  = ПараметрыОбъекта[0].Тип;
		ЗначениеПомещения = "";
		Разделитель = "";
		Сокращения = РаботаСАдресами.СокращенияОбъектовАдресацииАдресаРФ();
		Для Каждого СтрокаОбъекта Из ПараметрыОбъекта Цикл
			ИмяПомещения = ?(ЗначениеЗаполнено(Сокращения[СтрокаОбъекта.Тип]), Сокращения[СтрокаОбъекта.Тип], СтрокаОбъекта.Тип);
			ЗначениеПомещения = ЗначениеПомещения + Разделитель + ИмяПомещения + " " + СтрокаОбъекта.Значение;
			Разделитель = ", " ;
		КонецЦикла;
		ДобавитьЗначение(Список, "ТипКвартиры", ТипПомещения,      РасширенныйСоставПолей);
		ДобавитьЗначение(Список, "Квартира",    ЗначениеПомещения, РасширенныйСоставПолей);
	КонецЕсли;
	
	Возврат Список;
КонецФункции
