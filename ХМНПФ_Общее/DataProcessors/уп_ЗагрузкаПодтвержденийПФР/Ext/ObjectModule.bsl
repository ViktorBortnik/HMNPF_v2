
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 30.03.2017 ШадринАВ // РазобратьТаблицу() // №902
	// ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
КонецПроцедуры

&ИзменениеИКонтроль("РазобратьТаблицу")
Процедура ХМРазобратьТаблицу(ТЗ, ТипТаблицы, Протокол, Файл)
	
	//создаем документ
	НовыйДокумент = Документы.уп_ПодтверждениеПФР.СоздатьДокумент();
	НовыйДокумент.Дата = ДатаФормированияДокументов;
	НовыйДокумент.ДатаПоступленияСредств = ДатаПоступленияСредств;
	НовыйДокумент.Организация = Организация;
	Если ЗначениеЗаполнено(Комментарий) Тогда
		НовыйДокумент.Комментарий = Комментарий;
	Иначе
		НовыйДокумент.Комментарий = Файл;
	КонецЕсли;
	НовыйДокумент.Ответственный = Ответственный;
	
	Если КорректироватьДанныеЗЛ Тогда
		ДокКорректировки = Документы.уп_КорректировкаДанныхЗЛПоРеестрам.СоздатьДокумент();
		ДокКорректировки.Дата = ДатаФормированияДокументов;
		ДокКорректировки.Организация = Организация;
		ДокКорректировки.Ответственный = Ответственный;
		//ДокКорректировки.ФинансовыйРеестр = ФинансовыйРеестр;
		ДокКорректировки.Комментарий = Файл;
	Иначе
		ДокКорректировки = Неопределено;
	КонецЕсли;
	

	Если ТипТаблицы = 1 Тогда //есть номер и дата договора

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТабДляЗагрузки.СНИЛС,
		|	ТабДляЗагрузки.НомерДоговора,
		|	ТабДляЗагрузки.ДатаЗаключения,
		|	ТабДляЗагрузки.Фамилия,
		|	ТабДляЗагрузки.Имя,
		|	ТабДляЗагрузки.Отчество,
		|	ТабДляЗагрузки.Контрагент,
		|	ТабДляЗагрузки.ДатаРождения,
		|	ТабДляЗагрузки.МестоРождения,
		|	ТабДляЗагрузки.Нпп
		|ПОМЕСТИТЬ ИсходныеДанные
		|ИЗ
		|	&ТабДляЗагрузки КАК ТабДляЗагрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ДоговорыОПС.Ссылка КАК ДоговорОПС,
		|	ИсходныеДанные.СНИЛС,
		|	ИсходныеДанные.НомерДоговора,
		|	ИсходныеДанные.ДатаЗаключения,
		|	ИсходныеДанные.Фамилия,
		|	ИсходныеДанные.Имя,
		|	ИсходныеДанные.Отчество,
		|	ИсходныеДанные.Контрагент,
		|	ИсходныеДанные.ДатаРождения,
		|	ИсходныеДанные.МестоРождения,
		|	ИсходныеДанные.Нпп
		|ПОМЕСТИТЬ ДоговорыОПС
		|ИЗ
		|	ИсходныеДанные КАК ИсходныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уп_ДоговорыОПС КАК уп_ДоговорыОПС
		|		ПО ИсходныеДанные.СНИЛС = уп_ДоговорыОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР
		#Удаление
		|			И (НАЧАЛОПЕРИОДА(ИсходныеДанные.ДатаЗаключения, ДЕНЬ) = НАЧАЛОПЕРИОДА(уп_ДоговорыОПС.ДатаЗаключения, ДЕНЬ))
		#КонецУдаления
		#Вставка
		// +ХМНПФ 30.03.2017 ШадринАВ // РазобратьТаблицу() // №902
		#КонецВставки
		|			И ИсходныеДанные.НомерДоговора = уп_ДоговорыОПС.Код
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыОПС.СНИЛС,
		|	ДоговорыОПС.НомерДоговора,
		|	ДоговорыОПС.ДатаЗаключения,
		|	ДоговорыОПС.Фамилия,
		|	ДоговорыОПС.Имя,
		|	ДоговорыОПС.Отчество,
		|	ДоговорыОПС.Контрагент,
		|	ДоговорыОПС.ДатаРождения,
		|	ДоговорыОПС.МестоРождения,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """") КАК ФамилияРегистр,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Имя, """") КАК ИмяРегистр,
		|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Отчество, """") КАК ОтчествоРегистр,
		|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
		|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник КАК ЗЛ,
		|	уп_СостоянияДоговораОПССрезПоследних.Состояние,
		|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения КАК ДатаРожденияБаза,
		|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.МестоРождения КАК МестоРожденияБаза,
		#Вставка
		// +ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
		|	ХМ_ПричиныЗакрытия.ПричинаЗакрытия КАК ХМ_ПричинаЗакрытия,
		// -ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
		#КонецВставки
		|	ДоговорыОПС.Нпп КАК Нпп
		|ИЗ
		|	ДоговорыОПС КАК ДоговорыОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
		|				&ДатаДок,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговорыОПС.ДоговорОПС
		|						ИЗ
		|							ДоговорыОПС КАК ДоговорыОПС)) КАК уп_СостоянияДоговораОПССрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаДок, ) КАК ФИОФизЛицСрезПоследних
		|			ПО уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо = ФИОФизЛицСрезПоследних.ФизическоеЛицо
		|		ПО ДоговорыОПС.ДоговорОПС = уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС
		#Вставка
		// +ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(
		|				&ДатаДок,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговорыОПС.ДоговорОПС
		|						ИЗ
		|							ДоговорыОПС КАК ДоговорыОПС)) КАК ХМ_ПричиныЗакрытия
		|		ПО ДоговорыОПС.ДоговорОПС = ХМ_ПричиныЗакрытия.ДоговорОПС
		// -ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
		#КонецВставки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Нпп";
		
		Запрос.УстановитьПараметр("ДатаДок", ДатаФормированияДокументов);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ТабДляЗагрузки", ТЗ);
		
		Результат = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
        Всего = ВыборкаДетальныеЗаписи.Количество();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			#Если Клиент Тогда
			Состояние("Обработка строки "+ВыборкаДетальныеЗаписи.Нпп+" из "+Всего);
			#КонецЕсли
			Если ВыборкаДетальныеЗаписи.ДоговорОПС = NULL Тогда //не найден договор
				ФИО = ВыборкаДетальныеЗаписи.Фамилия+" "+ВыборкаДетальныеЗаписи.Имя+" "+ВыборкаДетальныеЗаписи.Отчество;
				Протокол.ДобавитьСтроку("Не найден договор об ОПС №"+ВыборкаДетальныеЗаписи.НомерДоговора+" от "+ВыборкаДетальныеЗаписи.ДатаЗаключения+" с "+ФИО+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС);
            ИначеЕсли ВыборкаДетальныеЗаписи.ЗЛ = NULL Тогда //не найден контрагент
				Протокол.ДобавитьСтроку("Не найден ЗЛ "+ВыборкаДетальныеЗаписи.СНИЛС);
			ИначеЕсли ВыборкаДетальныеЗаписи.ДоговорОПС = NULL Тогда //не найден договор
				ФИО = ВыборкаДетальныеЗаписи.Фамилия+" "+ВыборкаДетальныеЗаписи.Имя+" "+ВыборкаДетальныеЗаписи.Отчество;
				Протокол.ДобавитьСтроку("Не найден договор об ОПС №"+ВыборкаДетальныеЗаписи.НомерДоговора+" от "+ВыборкаДетальныеЗаписи.ДатаЗаключения+" с "+ФИО+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС);
			Иначе //все в порядке
			#Удаление
				Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
			#КонецУдаления
			#Вставка
				// +ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
				Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт
					И ВыборкаДетальныеЗаписи.ХМ_ПричинаЗакрытия<>Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЗакрытПоОтказуИзПФР Тогда
					// +ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
			#КонецВставки
					Протокол.ДобавитьСтроку("Договор №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" уже закрыт и не будет внесен в табличную часть!");
					Продолжить;
				КонецЕсли;	
				Если ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР
					#Вставка
					// +ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
					И ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.ПриостановленПоОтказуИзПФР
					И ВыборкаДетальныеЗаписи.ХМ_ПричинаЗакрытия<>Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЗакрытПоОтказуИзПФР
					// -ХМНПФ 16.03.2018 ШадринАВ // РазобратьТаблицу() // №1432
					#КонецВставки
					и ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.Заключен  Тогда
					Протокол.ДобавитьСтроку("Обратите внимание!! Состояние договора №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" = "+ВыборкаДетальныеЗаписи.Состояние);
					Продолжить;
				КонецЕсли;
				НовСтр = НовыйДокумент.ДоговорыОПС.Добавить();
				НовСтр.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				НовСтр.Участник = ВыборкаДетальныеЗаписи.ДоговорОПС.Участник;
				НовСтр.ПредыдущийСтраховщик = ВыборкаДетальныеЗаписи.Контрагент;
				
				Если КорректироватьДанныеЗЛ Тогда
					ИзмененоФИО = Ложь;
					ИзмененоДР = Ложь;
					ИзмененоМР = Ложь;
					//проверим дату рождения
					Если НачалоДня(ВыборкаДетальныеЗаписи.ДатаРождения)<>НачалоДня(ВыборкаДетальныеЗаписи.ДатаРожденияБаза) Тогда
						ИзмененоДР = Истина;
					КонецЕсли;
					//проверим место рождения
					Если ЗначениеЗаполнено(СокрЛП(ВыборкаДетальныеЗаписи.МестоРождения)) Тогда
						Если ВРЕГ(СокрЛП(ВыборкаДетальныеЗаписи.МестоРожденияБаза))<>ВРЕГ(СокрЛП(ВыборкаДетальныеЗаписи.МестоРождения)) Тогда
							ИзмененоМР = Истина;
						КонецЕсли;
					КонецЕсли;
					//проверим ФИО
					Если СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Фамилия))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ФамилияРегистр))
						ИЛИ СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Имя))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ИмяРегистр))
						ИЛИ СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Отчество))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ОтчествоРегистр)) Тогда
						ИзмененоФИО = Истина;
						Протокол.ДобавитьСтроку("Обратите внимание!! У физ. лица со СНИЛС "+ВыборкаДетальныеЗаписи.СНИЛС+" ФИО в реестре "+ВыборкаДетальныеЗаписи.Фамилия+" "+ВыборкаДетальныеЗаписи.Имя+" "+ВыборкаДетальныеЗаписи.Отчество+" <> ФИО в базе данных "+ВыборкаДетальныеЗаписи.ФамилияРегистр+" "+ВыборкаДетальныеЗаписи.ИмяРегистр+" "+ВыборкаДетальныеЗаписи.ОтчествоРегистр);
					КонецЕсли;	
					
					Если (ИзмененоФИО ИЛИ ИзмененоДР ИЛИ ИзмененоМР) И КорректироватьДанныеЗЛ Тогда
						НовСтрКорректировки = ДокКорректировки.ЗастрахованныеЛица.Добавить();
						НовСтрКорректировки.Корректировать = Истина;
						НовСтрКорректировки.ЗЛ = ВыборкаДетальныеЗаписи.ДоговорОПС.Участник;
						НовСтрКорректировки.ИзмененоФИО = ИзмененоФИО;
						НовСтрКорректировки.ФИОПоБазе = СокрЛП(ВыборкаДетальныеЗаписи.ФамилияРегистр)+" "+СокрЛП(ВыборкаДетальныеЗаписи.ИмяРегистр)+" "+СокрЛП(ВыборкаДетальныеЗаписи.ОтчествоРегистр);
						НовСтрКорректировки.ФИОПоРеестру = СокрЛП(ВыборкаДетальныеЗаписи.Фамилия)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Имя)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Отчество);
						НовСтрКорректировки.ИзмененаДР = ИзмененоДР;
						НовСтрКорректировки.ДРПоБазе = ВыборкаДетальныеЗаписи.ДатаРожденияБаза;
						НовСтрКорректировки.ДРПоРеестру = ВыборкаДетальныеЗаписи.ДатаРождения;
						НовСтрКорректировки.ИзмененоМестоРождения = ИзмененоМР;
						НовСтрКорректировки.МРПоБазе = ВыборкаДетальныеЗаписи.МестоРожденияБаза;
						НовСтрКорректировки.МРПоРеестру = ВыборкаДетальныеЗаписи.МестоРождения;
					КонецЕсли;	
				КонецЕсли;
				
			КонецЕсли;	
		КонецЦикла;
	Иначе //есть только СНИЛС
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТабДляЗагрузки.СНИЛС,
			|	ТабДляЗагрузки.НомерДоговора,
			|	ТабДляЗагрузки.ДатаЗаключения,
			|	ТабДляЗагрузки.Фамилия,
			|	ТабДляЗагрузки.Имя,
			|	ТабДляЗагрузки.Отчество,
			|	ТабДляЗагрузки.Контрагент,
			|	ТабДляЗагрузки.ДатаРождения,
			|	ТабДляЗагрузки.МестоРождения,
			|	ТабДляЗагрузки.Нпп
			|ПОМЕСТИТЬ ИсходныеДанные
			|ИЗ
			|	&ТабДляЗагрузки КАК ТабДляЗагрузки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ДоговорыОПС.Ссылка КАК ДоговорОПС,
			|	ИсходныеДанные.СНИЛС,
			|	ИсходныеДанные.НомерДоговора,
			|	ИсходныеДанные.ДатаЗаключения,
			|	ИсходныеДанные.Фамилия,
			|	ИсходныеДанные.Имя,
			|	ИсходныеДанные.Отчество,
			|	ИсходныеДанные.Контрагент,
			|	ИсходныеДанные.ДатаРождения,
			|	ИсходныеДанные.МестоРождения,
			|	ИсходныеДанные.Нпп
			|ПОМЕСТИТЬ ДоговорыОПС
			|ИЗ
			|	ИсходныеДанные КАК ИсходныеДанные
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уп_ДоговорыОПС КАК уп_ДоговорыОПС
			|		ПО ИсходныеДанные.СНИЛС = уп_ДоговорыОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДоговорыОПС.СНИЛС КАК СНИЛС,
			|	ДоговорыОПС.НомерДоговора,
			|	ДоговорыОПС.ДатаЗаключения,
			|	ДоговорыОПС.Фамилия,
			|	ДоговорыОПС.Имя,
			|	ДоговорыОПС.Отчество,
			|	ДоговорыОПС.Контрагент,
			|	ДоговорыОПС.ДатаРождения,
			|	ДоговорыОПС.МестоРождения,
			|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
			|	Контрагенты.Ссылка КАК ЗЛ,
			|	уп_СостоянияДоговораОПССрезПоследних.Состояние,
			|	ДоговорыОПС.Нпп КАК Нпп,
			|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.Код КАК ДоговорНомер,
			|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС.ДатаЗаключения КАК ДоговорДата
			|ИЗ
			|	ДоговорыОПС КАК ДоговорыОПС
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
			|		ПО ДоговорыОПС.СНИЛС = Контрагенты.уп_ФизЛицо.СтраховойНомерПФР
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
			|				&ДатаДок,
			|				ДоговорОПС.Организация = &Организация
			|					И ДоговорОПС В
			|						(ВЫБРАТЬ
			|							ДоговорыОПС.ДоговорОПС
			|						ИЗ
			|							ДоговорыОПС КАК ДоговорыОПС)) КАК уп_СостоянияДоговораОПССрезПоследних
			|		ПО ДоговорыОПС.ДоговорОПС = уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС
			|
			|УПОРЯДОЧИТЬ ПО
			|	Нпп,
			|	ДоговорДата УБЫВ
			|ИТОГИ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоговорОПС)
			|ПО
			|	СНИЛС";
		Запрос.УстановитьПараметр("ДатаДок", ДатаФормированияДокументов);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ТабДляЗагрузки", ТЗ);

		Результат = Запрос.Выполнить();

		ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Всего = ВыборкаИтоги.Количество();
		Нпп = 0;
		Пока ВыборкаИтоги.Следующий() Цикл
			Нпп = Нпп+1;
			#Если Клиент Тогда
			Состояние("Обработка строки "+Нпп+" из "+Всего);
			#КонецЕсли
			ВыборкаДетальныеЗаписи = ВыборкаИтоги.Выбрать();
			Если ВыборкаДетальныеЗаписи.Следующий() Тогда
				Если ВыборкаДетальныеЗаписи.ЗЛ = NULL Тогда //не найден контрагент
					Протокол.ДобавитьСтроку("Не найден ЗЛ "+ВыборкаДетальныеЗаписи.СНИЛС);
				ИначеЕсли ВыборкаДетальныеЗаписи.ДоговорОПС = NULL Тогда //не найден договор
					ФИО = ВыборкаДетальныеЗаписи.Фамилия+" "+ВыборкаДетальныеЗаписи.Имя+" "+ВыборкаДетальныеЗаписи.Отчество;
					Протокол.ДобавитьСтроку("Не найден договор об ОПС №"+ВыборкаДетальныеЗаписи.НомерДоговора+" от "+ВыборкаДетальныеЗаписи.ДатаЗаключения+" с "+ФИО+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС);
				Иначе //все в порядке
					Если ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
						Для н=2 По ВыборкаДетальныеЗаписи.Количество() Цикл
							Если ВыборкаДетальныеЗаписи.Следующий() Тогда
								Если ВыборкаДетальныеЗаписи.Состояние <> Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
									Прервать;
								КонецЕсли;
							КонецЕсли;
							н=н+1;
						КонецЦикла;	
						Если (н = ВыборкаДетальныеЗаписи.Количество() ИЛИ ВыборкаДетальныеЗаписи.Количество()=1) И ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
							Протокол.ДобавитьСтроку("Договор №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" уже закрыт и не будет внесен в табличную часть!");
							Продолжить;
						КонецЕсли;

					КонецЕсли;	
					Если ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР
						и ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.Заключен  Тогда
						Протокол.ДобавитьСтроку("Обратите внимание!! Состояние договора №"+ВыборкаДетальныеЗаписи.ДоговорОПС.Код+" с "+ВыборкаДетальныеЗаписи.ДоговорОПС.Участник.Наименование+", страховой номер ПФР "+ВыборкаДетальныеЗаписи.СНИЛС+" = "+ВыборкаДетальныеЗаписи.Состояние);
						Продолжить;
					КонецЕсли;	
					НовСтр = НовыйДокумент.ДоговорыОПС.Добавить();
					НовСтр.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
					НовСтр.Участник = ВыборкаДетальныеЗаписи.ДоговорОПС.Участник;
					НовСтр.ПредыдущийСтраховщик = ВыборкаДетальныеЗаписи.Контрагент;
					
					Если КорректироватьДанныеЗЛ Тогда
						ИзмененоФИО = Ложь;
						ИзмененоДР = Ложь;
						ИзмененоМР = Ложь;
						//проверим дату рождения
						Если НачалоДня(ВыборкаДетальныеЗаписи.ДатаРождения)<>НачалоДня(ВыборкаДетальныеЗаписи.ДатаРожденияБаза) Тогда
							ИзмененоДР = Истина;
						КонецЕсли;
						//проверим место рождения
						Если ЗначениеЗаполнено(СокрЛП(ВыборкаДетальныеЗаписи.МестоРождения)) Тогда
							Если ВРЕГ(СокрЛП(ВыборкаДетальныеЗаписи.МестоРожденияБаза))<>ВРЕГ(СокрЛП(ВыборкаДетальныеЗаписи.МестоРождения)) Тогда
								ИзмененоМР = Истина;
							КонецЕсли;
						КонецЕсли;
						//проверим ФИО
						Если СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Фамилия))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ФамилияРегистр))
							ИЛИ СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Имя))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ИмяРегистр))
							ИЛИ СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.Отчество))<>СокрЛП(ВРЕГ(ВыборкаДетальныеЗаписи.ОтчествоРегистр)) Тогда
							ИзмененоФИО = Истина;
							Протокол.ДобавитьСтроку("Обратите внимание!! У физ. лица со СНИЛС "+ВыборкаДетальныеЗаписи.СНИЛС+" ФИО в реестре "+ВыборкаДетальныеЗаписи.Фамилия+" "+ВыборкаДетальныеЗаписи.Имя+" "+ВыборкаДетальныеЗаписи.Отчество+" <> ФИО в базе данных "+ВыборкаДетальныеЗаписи.ФамилияРегистр+" "+ВыборкаДетальныеЗаписи.ИмяРегистр+" "+ВыборкаДетальныеЗаписи.ОтчествоРегистр);
						КонецЕсли;	
						
						Если (ИзмененоФИО ИЛИ ИзмененоДР ИЛИ ИзмененоМР) И КорректироватьДанныеЗЛ Тогда
							НовСтрКорректировки = ДокКорректировки.ЗастрахованныеЛица.Добавить();
							НовСтрКорректировки.Корректировать = Истина;
							НовСтрКорректировки.ЗЛ = ВыборкаДетальныеЗаписи.ДоговорОПС.Участник;
							НовСтрКорректировки.ИзмененоФИО = ИзмененоФИО;
							НовСтрКорректировки.ФИОПоБазе = СокрЛП(ВыборкаДетальныеЗаписи.ФамилияРегистр)+" "+СокрЛП(ВыборкаДетальныеЗаписи.ИмяРегистр)+" "+СокрЛП(ВыборкаДетальныеЗаписи.ОтчествоРегистр);
							НовСтрКорректировки.ФИОПоРеестру = СокрЛП(ВыборкаДетальныеЗаписи.Фамилия)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Имя)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Отчество);
							НовСтрКорректировки.ИзмененаДР = ИзмененоДР;
							НовСтрКорректировки.ДРПоБазе = ВыборкаДетальныеЗаписи.ДатаРожденияБаза;
							НовСтрКорректировки.ДРПоРеестру = ВыборкаДетальныеЗаписи.ДатаРождения;
							НовСтрКорректировки.ИзмененоМестоРождения = ИзмененоМР;
							НовСтрКорректировки.МРПоБазе = ВыборкаДетальныеЗаписи.МестоРожденияБаза;
							НовСтрКорректировки.МРПоРеестру = ВыборкаДетальныеЗаписи.МестоРождения;
						КонецЕсли;	
					КонецЕсли;
				КонецЕсли;
			Иначе
				Протокол.ДобавитьСтроку("Не найден ЗЛ "+ВыборкаИтоги.СНИЛС);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
	Если НовыйДокумент.ДоговорыОПС.Количество() > 0 Тогда
		НовыйДокумент.Записать();
		НовоеНачисление = НовыеНачисления.Добавить();
		НовоеНачисление.Документ = НовыйДокумент.Ссылка;
	Иначе
		Сообщить("Документ не создан, т.к. нет строк в табличной части (файл: " + Файл + " ).");
	КонецЕсли;
	
	Если КорректироватьДанныеЗЛ Тогда
		Если ДокКорректировки.ЗастрахованныеЛица.Количество()>0 Тогда
			ДокКорректировки.Записать();
			НовоеНачисление = НовыеНачисления.Добавить();
			НовоеНачисление.Документ = ДокКорректировки.Ссылка;
			Протокол.ДобавитьСтроку("Записан документ "+ДокКорректировки.Ссылка);
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры
