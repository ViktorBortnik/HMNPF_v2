
&НаСервере
&ИзменениеИКонтроль("ЗаполнитьНаСервереПерерасчет")
Процедура ХМЗаполнитьНаСервереПерерасчет()
	Объект.СписокСчетов.Очистить();

	ИмяМодуляПК = уп_НПОСервер.ПолучитьИмяМодуляПенсионногоКалькулятора();
	Если ИмяМодуляПК<>Неопределено Тогда 
		Если СтрНайти(ИмяМодуляПК, "Ошибка") <> 0 Тогда
			Сообщить("Расчет не может быть произведен. "+ИмяМодуляПК);
			Возврат;
		Иначе
			МодульРасчетаПК = ОбщегоНазначения.ОбщийМодуль(ИмяМодуляПК);
			МодульРасчетаПК.ЗаполнитьСчетаПК(Объект, ТаблицаДожития); 
		КонецЕсли;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
		|	уп_СостоянияПССрезПоследних.НомерСчета.Владелец КАК ПенсионныйДоговор
		|ПОМЕСТИТЬ ТаблицаПС
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
		|			&ДатаРасчета,
		|			НомерСчета.Участник = &Участник
		|				И НомерСчета.Владелец.Организация = &Организация) КАК уп_СостоянияПССрезПоследних
		|ГДЕ
		|	уп_СостоянияПССрезПоследних.Состояние В(&СписокСостояний)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СхемаСчетаСрезПоследних.НомерСчета КАК НомерСчета,
		|	уп_СхемаСчетаСрезПоследних.Схема КАК Схема,
		|	уп_СхемаСчетаСрезПоследних.ТипПенсии КАК ТипПенсии
		|ПОМЕСТИТЬ ТаблицаСхем
		|ИЗ
		|	РегистрСведений.уп_СхемаСчета.СрезПоследних(
		|			&ДатаРасчета,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)) КАК уп_СхемаСчетаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
		|	уп_РезервыОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ ТаблицаОстатков
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&ДатаРасчета,
		|			Организация = &Организация
		|				И НомерСчета В
		|					(ВЫБРАТЬ
		|						ТаблицаПС.НомерСчета
		|					ИЗ
		|						ТаблицаПС)
		|				И ТипРезерва В (&СписокТиповРезерва)) КАК уп_РезервыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ПенсионныйДоговор КАК ПенсионныйДоговор,
		|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора.МинимальныйСрокВыплат КАК ШаблонДоговораМинимальныйСрокВыплат
		|ПОМЕСТИТЬ ТаблицаУсловийДоговора
		|ИЗ
		|	РегистрСведений.уп_ПараметрыПенсионногоДоговора.СрезПоследних(
		|			&ДатаРасчета,
		|			ПенсионныйДоговор В
		|				(ВЫБРАТЬ
		|					ТаблицаПС.ПенсионныйДоговор
		|				ИЗ
		|					ТаблицаПС)) КАК уп_ПараметрыПенсионногоДоговораСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СчетчикНачисленийОбороты.НомерСчета КАК НомерСчета,
		|	уп_СчетчикНачисленийОбороты.КоличествоОборот КАК КоличествоСделанныхВыплат
		|ПОМЕСТИТЬ КолСделанныхВыплат
		|ИЗ
#Вставка
//++https://redmine.npf.com/issues/4274
	|		(ВЫБРАТЬ уп_СчетчикНачислений.НомерСчета, Максимум(естьNUll(хм_СрезСчетчик.Количество,0)) + СУММА(ВЫБОР КОГДА хм_СрезСчетчик.ДатаУстановкиСостояния есть NUll Тогда естьNUll(уп_СчетчикНачислений.Количество,0) ИНАЧЕ 0 КОНЕЦ) КАК КоличествоОборот
	|		ИЗ РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаРасчета,НомерСчета В (ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)) КАК хм_СрезСчетчик
	|		ПО (уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета)
	|			И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния 
	|			И хм_СрезСчетчик.ДатаУстановкиСостояния  >= &ДатаНачалаВремен	
	|		ГДЕ уп_СчетчикНачислений.Период >= &ДатаНачалаВремен 
	|			И уп_СчетчикНачислений.Период <= &ДатаРасчета
	|			И уп_СчетчикНачислений.НомерСчета В (ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)
	|		СГРУППИРОВАТЬ ПО уп_СчетчикНачислений.НомерСчета
	|		) КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
		|	РегистрНакопления.уп_СчетчикНачислений.Обороты(
		|			&ДатаНачалаВремен,
		|			&ДатаРасчета,
		|			Период,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПС.НомерСчета КАК НомерСчета,
		|	ЕстьNULL(ТаблицаОстатков.СуммаОстаток,0) КАК СуммаОстаток,
		|	ТаблицаСхем.Схема КАК Схема,
		|	ТаблицаСхем.ТипПенсии КАК ТипПенсии,
		|	ЕстьNULL(ТаблицаУсловийДоговора.ШаблонДоговораМинимальныйСрокВыплат,0) КАК МинимальныйСрокВыплат,
		|	ЕСТЬNULL(КолСделанныхВыплат.КоличествоСделанныхВыплат, 0) КАК КоличествоСделанныхВыплат
		|ИЗ
		|	ТаблицаПС КАК ТаблицаПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСхем КАК ТаблицаСхем
		|		ПО ТаблицаПС.НомерСчета = ТаблицаСхем.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
		|		ПО ТаблицаПС.НомерСчета = ТаблицаОстатков.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаУсловийДоговора КАК ТаблицаУсловийДоговора
		|		ПО ТаблицаПС.ПенсионныйДоговор = ТаблицаУсловийДоговора.ПенсионныйДоговор
		|		ЛЕВОЕ СОЕДИНЕНИЕ КолСделанныхВыплат КАК КолСделанныхВыплат
		|		ПО ТаблицаПС.НомерСчета = КолСделанныхВыплат.НомерСчета";

		Запрос.УстановитьПараметр("ДатаРасчета", Объект.ДатаРасчета);
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены);
		Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		СписокТиповРезерва = Новый СписокЗначений;
		СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ИПС);
		СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.СПС);
		Запрос.УстановитьПараметр("СписокТиповРезерва", СписокТиповРезерва);
		Запрос.УстановитьПараметр("Участник", Объект.Участник);
		Запрос.УстановитьПараметр("ДатаНачалаВремен", '19900101');

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Коэф = уп_НПФ.ЧисловойКоэффициентПериода(Объект.ПериодичностьВыплат);

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НовСтр = Объект.СписокСчетов.Добавить();
			НовСтр.НомерСчета = ВыборкаДетальныеЗаписи.НомерСчета;
			НовСтр.СхемаИсходная = ВыборкаДетальныеЗаписи.Схема;
			НовСтр.СхемаДляВыплат = ВыборкаДетальныеЗаписи.Схема;
			Если ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.Смешанная Тогда
				НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.ТипПенсии;
			Иначе
				НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемы;
			КонецЕсли;
			НовСтр.СуммаНаСчете = ВыборкаДетальныеЗаписи.СуммаОстаток;
			НовСтр.КоличествоСделанныхВыплат = ВыборкаДетальныеЗаписи.КоличествоСделанныхВыплат;
			Если НовСтр.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат Тогда
				НовСтр.КоличествоВыплат = ВыборкаДетальныеЗаписи.МинимальныйСрокВыплат/Коэф-ВыборкаДетальныеЗаписи.КоличествоСделанныхВыплат;
				НовСтр.МинимальныйСрок = ВыборкаДетальныеЗаписи.МинимальныйСрокВыплат;
			КонецЕсли;
			Если Объект.ИспользоватьУвеличенныйРазмерПервойВыплаты Тогда
				НовСтр.СуммаДоплаты = ВыборкаДетальныеЗаписи.СуммаОстаток*Объект.ПроцентОтОстаткаНаСчете/100;
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьПерерасчет")
Процедура ХМЗаполнитьПерерасчет()
	Объект.СписокСчетов.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СостоянияПССрезПоследних.НомерСчета.Владелец КАК ПенсионныйДоговор
	|ПОМЕСТИТЬ ТаблицаПС
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
	|			&ДатаРасчета,
	|			НомерСчета.Участник = &Участник
	|				И НомерСчета.Владелец.Организация = &Организация) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.Состояние В(&СписокСостояний)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СхемаСчетаСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СхемаСчетаСрезПоследних.Схема КАК Схема,
	|	уп_СхемаСчетаСрезПоследних.ТипПенсии КАК ТипПенсии
	|ПОМЕСТИТЬ ТаблицаСхем
	|ИЗ
	|	РегистрСведений.уп_СхемаСчета.СрезПоследних(
	|			&ДатаРасчета,
	|			НомерСчета В
	|				(ВЫБРАТЬ
	|					ТаблицаПС.НомерСчета
	|				ИЗ
	|					ТаблицаПС)) КАК уп_СхемаСчетаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
	|	уп_РезервыОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	РегистрНакопления.уп_Резервы.Остатки(
	|			&ДатаРасчета,
	|			Организация = &Организация
	|				И НомерСчета В
	|					(ВЫБРАТЬ
	|						ТаблицаПС.НомерСчета
	|					ИЗ
	|						ТаблицаПС)
	|				И ТипРезерва В (&СписокТиповРезерва)) КАК уп_РезервыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора.МинимальныйСрокВыплат КАК ШаблонДоговораМинимальныйСрокВыплат
	|ПОМЕСТИТЬ ТаблицаУсловийДоговора
	|ИЗ
	|	РегистрСведений.уп_ПараметрыПенсионногоДоговора.СрезПоследних(
	|			&ДатаРасчета,
	|			ПенсионныйДоговор В
	|				(ВЫБРАТЬ
	|					ТаблицаПС.ПенсионныйДоговор
	|				ИЗ
	|					ТаблицаПС)) КАК уп_ПараметрыПенсионногоДоговораСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СчетчикНачисленийОбороты.НомерСчета КАК НомерСчета,
	|	уп_СчетчикНачисленийОбороты.КоличествоОборот КАК КоличествоСделанныхВыплат
	|ПОМЕСТИТЬ КолСделанныхВыплат
	|ИЗ
#Вставка
//++https://redmine.npf.com/issues/4274
	|		(ВЫБРАТЬ уп_СчетчикНачислений.НомерСчета, Максимум(естьNUll(хм_СрезСчетчик.Количество,0)) + СУММА(ВЫБОР КОГДА хм_СрезСчетчик.ДатаУстановкиСостояния есть NUll Тогда естьNUll(уп_СчетчикНачислений.Количество,0) ИНАЧЕ 0 КОНЕЦ) КАК КоличествоОборот
	|		ИЗ РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаРасчета,НомерСчета В (ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)) КАК хм_СрезСчетчик
	|		ПО (уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета)
	|			И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния 
	|			И хм_СрезСчетчик.ДатаУстановкиСостояния  >= &ДатаНачалаВремен	
	|		ГДЕ уп_СчетчикНачислений.Период >= &ДатаНачалаВремен 
	|			И уп_СчетчикНачислений.Период <= &ДатаРасчета
	|			И уп_СчетчикНачислений.НомерСчета В (ВЫБРАТЬ
		|					ТаблицаПС.НомерСчета
		|				ИЗ
		|					ТаблицаПС)
	|		СГРУППИРОВАТЬ ПО уп_СчетчикНачислений.НомерСчета
	|		) КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|	РегистрНакопления.уп_СчетчикНачислений.Обороты(
	|			&ДатаНачалаВремен,
	|			&ДатаРасчета,
	|			Период,
	|			НомерСчета В
	|				(ВЫБРАТЬ
	|					ТаблицаПС.НомерСчета
	|				ИЗ
	|					ТаблицаПС)) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПС.НомерСчета КАК НомерСчета,
	|	ЕСТЬNULL(ТаблицаОстатков.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ТаблицаСхем.Схема КАК Схема,
	|	ТаблицаСхем.ТипПенсии КАК ТипПенсии,
	|	ЕСТЬNULL(ТаблицаУсловийДоговора.ШаблонДоговораМинимальныйСрокВыплат, 0) КАК МинимальныйСрокВыплат,
	|	ЕСТЬNULL(КолСделанныхВыплат.КоличествоСделанныхВыплат, 0) КАК КоличествоСделанныхВыплат
	|ИЗ
	|	ТаблицаПС КАК ТаблицаПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСхем КАК ТаблицаСхем
	|		ПО ТаблицаПС.НомерСчета = ТаблицаСхем.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО ТаблицаПС.НомерСчета = ТаблицаОстатков.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаУсловийДоговора КАК ТаблицаУсловийДоговора
	|		ПО ТаблицаПС.ПенсионныйДоговор = ТаблицаУсловийДоговора.ПенсионныйДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ КолСделанныхВыплат КАК КолСделанныхВыплат
	|		ПО ТаблицаПС.НомерСчета = КолСделанныхВыплат.НомерСчета
	|ГДЕ
	|	ТаблицаПС.НомерСчета В(&СписокПС)";

	Запрос.УстановитьПараметр("ДатаРасчета", Объект.ДатаРасчета);
	СписокСостояний = Новый СписокЗначений;
	СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены);
	Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	СписокТиповРезерва = Новый СписокЗначений;
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ИПС);
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.СПС);
	Запрос.УстановитьПараметр("СписокТиповРезерва", СписокТиповРезерва);
	Запрос.УстановитьПараметр("Участник", Объект.Участник);
	Запрос.УстановитьПараметр("СписокПС", Объект.Заявление.СписокСчетов.Выгрузить().ВыгрузитьКолонку("НомерСчета"));
	Запрос.УстановитьПараметр("ДатаНачалаВремен", '19900101');

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Коэф = уп_НПФ.ЧисловойКоэффициентПериода(Объект.ПериодичностьВыплат);

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = Объект.СписокСчетов.Добавить();
		НовСтр.НомерСчета = ВыборкаДетальныеЗаписи.НомерСчета;
		НовСтр.СхемаИсходная = ВыборкаДетальныеЗаписи.Схема;
		НовСтр.СхемаДляВыплат = ВыборкаДетальныеЗаписи.Схема;
		НовСтр.СуммаНаСчете = ВыборкаДетальныеЗаписи.СуммаОстаток;
		НовСтр.КоличествоСделанныхВыплат = ВыборкаДетальныеЗаписи.КоличествоСделанныхВыплат;
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТипПенсии) Тогда
			НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.ТипПенсии;
		ИначеЕсли ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.Смешанная Тогда
			НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемыДляПрогнозов;
		Иначе
			НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемы;
		КонецЕсли;	
		Если НовСтр.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат Тогда
			НовСтр.КоличествоВыплат = ВыборкаДетальныеЗаписи.МинимальныйСрокВыплат/Коэф-ВыборкаДетальныеЗаписи.КоличествоСделанныхВыплат;
			НовСтр.МинимальныйСрок = ВыборкаДетальныеЗаписи.МинимальныйСрокВыплат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
