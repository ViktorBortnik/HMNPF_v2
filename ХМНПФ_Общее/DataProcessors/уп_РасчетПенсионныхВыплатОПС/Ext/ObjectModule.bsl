//31.08.2017 ШадринАВ // Новая ХМ_ПечататьРасчетНаСервере() // №1142 // ! нужно отслеживать и синхронизировать с ПечататьРасчетНаСервере() из модуля формы

Функция ХМ_ПечататьРасчетНаСервере() Экспорт
    ТабДокумент = Новый ТабличныйДокумент;

	//Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	Макет = ЭтотОбъект.ПолучитьМакет("Макет");

	ОбластьОтчет = Макет.ПолучитьОбласть("Отчет");
	Если ЗначениеЗаполнено(ЭтотОбъект.Заявление) Тогда
		Если ТипЗнч(ЭтотОбъект.Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОЕдиновременнойВыплате")  Тогда
			ОбластьОтчет.Параметры.Картинка1 = "V";
			ОбластьОтчет.Параметры.Картинка6 = "V";
		ИначеЕсли ТипЗнч(ЭтотОбъект.Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты")  Тогда
				ОбластьОтчет.Параметры.Картинка2 = "V";
				ОбластьОтчет.Параметры.Картинка4 = "V";
		ИначеЕсли ТипЗнч(ЭтотОбъект.Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииНЧТП")  Тогда
				ОбластьОтчет.Параметры.Картинка3 = "V";
				ОбластьОтчет.Параметры.Картинка5 = "V";
		КонецЕсли;
		ОбластьОтчет.Параметры.ДатаЗаявления = ЭтотОбъект.Заявление.Дата;
	Конецесли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ДоговорОПС) Тогда
		ФИО = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(ЭтотОбъект.ДатаРасчета, Новый Структура("ФизическоеЛицо", ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо));
		ОбластьОтчет.Параметры.ФИО = ФИО.Фамилия+" "+ФИО.Имя+" "+ФИО.Отчество;
		ОбластьОтчет.Параметры.СНИЛС = ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР;
		ОбластьОтчет.Параметры.НомерДоговора = СокрЛП(ЭтотОбъект.ДоговорОПС.Код);
		ОбластьОтчет.Параметры.ДатаДоговора = Формат(ЭтотОбъект.ДоговорОПС.ДатаЗаключения,"ДФ=dd.MM.yyyy");
		ОбластьОтчет.Параметры.ДатаРождения = Формат(ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения,"ДФ=dd.MM.yyyy");
		ДатаРасчетаВозраста = ?(ЗначениеЗаполнено(ЭтотОбъект.ПравоНаНакопительнуюПенсию),ЭтотОбъект.ПравоНаНакопительнуюПенсию,?(ЗначениеЗаполнено(ЭтотОбъект.ДатаРасчета),ЭтотОбъект.ДатаРасчета,ТекущаяДата()));
		//ОбластьОтчет.Параметры.ВозрастЛет = ?(ЗначениеЗаполнено(ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),уп_ОПС.РазобратьРазностьДат(ДатаРасчетаВозраста,ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),"");
		ОбластьОтчет.Параметры.ВозрастНаДатуРасчета = ?(ЗначениеЗаполнено(ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),"Возраст (лет): "+уп_ОПС.РазобратьРазностьДат(ЭтотОбъект.ДатаРасчета,ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),"");
		ОбластьОтчет.Параметры.ВозрастНаДатуГосПенсии = ?(ЗначениеЗаполнено(ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),"Возраст (лет): "+уп_ОПС.РазобратьРазностьДат(ДатаРасчетаВозраста,ЭтотОбъект.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения),"");
	КонецЕсли;
	
	
	ОбластьОтчет.Параметры.ДатаРасчета = Формат(ЭтотОбъект.ДатаРасчета,"ДФ=dd.MM.yyyy");;
	ОбластьОтчет.Параметры.ДатаГосПенсии = Формат(ЭтотОбъект.ПравоНаНакопительнуюПенсию,"ДФ=dd.MM.yyyy");
	Если ЭтотОбъект.ВариантРасчета = 2 Тогда
		ОбластьОтчет.Параметры.СрокСП = ЭтотОбъект.СрокСП;
	Иначе
		ОбластьОтчет.Параметры.СрокПП = ЭтотОбъект.СрокПП;
	КонецЕсли;
	ОбластьОтчет.Параметры.СуммаПН = ЭтотОбъект.СуммаОснЧасти+ЭтотОбъект.СуммаДопЧасти;
	СВ = 0;
	ДСВ = 0;
	МК = 0;
	БазаПП = 0;
	БазаСП = 0;
	//Для Каждого стр из Расшифровка Цикл
	//	Если стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.ВзносыФиз
	//		или стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
	//		СВ = СВ+стр.Сумма;
	//	ИначеЕсли стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.МатКапитал
	//		или стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
	//		МК = МК+стр.Сумма;
	//	Иначе
	//		ДСВ = ДСВ+стр.Сумма;
	//	КонецЕсли;	
	//КонецЦикла;	
	ОбластьОтчет.Параметры.СВ = ЭтотОбъект.СуммаОснЧасти;
	ОбластьОтчет.Параметры.ДСВ = ЭтотОбъект.СуммаДопЧасти-ЭтотОбъект.СуммаМК;
	ОбластьОтчет.Параметры.МК = ЭтотОбъект.СуммаМК;
	Если ЭтотОбъект.ВариантРасчета = 2 Тогда
		ОбластьОтчет.Параметры.СуммаПН_ПП = 0;
		ОбластьОтчет.Параметры.СуммаПН_СП = ЭтотОбъект.Расшифровка.Итог("Сумма");
	Иначе
		ОбластьОтчет.Параметры.СуммаПН_ПП = ЭтотОбъект.Расшифровка.Итог("Сумма");
		ОбластьОтчет.Параметры.СуммаПН_СП = 0;
	КонецЕсли;
	Областьотчет.Параметры.РазмерПП = ЭтотОбъект.РазмерПП;
	ОбластьОтчет.Параметры.РазмерСП = ЭтотОбъект.РазмерСП;
	ОбластьОтчет.Параметры.РазмерГП = ЭтотОбъект.РазмерГП;
	//01.07.2024 ЛыткинАМ // ХМХМ_ПечататьРасчетНаСервере //https://redmine.npf.com/issues/3891
	Если ЭтотОбъект.ДатаРасчета<'20240701' Тогда  
		ОбластьОтчет.Параметры.ИмяОбъектаСравнения = "Размер назначенной страховой пенсии, руб.";
		ОбластьОтчет.Параметры.РазмерГП = ЭтотОбъект.РазмерГП;
	Иначе
		ОбластьОтчет.Параметры.ИмяОбъектаСравнения = "Прожиточный минимум пенсионера в целом по Российской Федерации, руб.";
		ОбластьОтчет.Параметры.РазмерГП = ЭтотОбъект.ПрожиточныйМинимум;
	КонецЕсли;	
	
	ОбластьОтчет.Параметры.Процент = ЭтотОбъект.Процент;
	Если ЭтотОбъект.ПринятоеРешение>0 Тогда
		Если  ЭтотОбъект.ПринятоеРешение = 1 Тогда
			Областьотчет.Параметры.Картинка7 = "V";
		ИначеЕсли  ЭтотОбъект.ПринятоеРешение = 4 Тогда
			Областьотчет.Параметры.Картинка7 = "V";
		Иначе
			Если ЭтотОбъект.РазмерПП>0 Тогда
				ОбластьОтчет.Параметры.Картинка9 = "V";
			КонецЕсли;
			Если ЭтотОбъект.РазмерСП>0 Тогда
				ОбластьОтчет.Параметры.Картинка8 = "V";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьОтчет);
	// +ХМНПФ 11.12.2015 ШадринАВ // ОсновныеДействияФормыПечать() // №70
	ХМ_ПодвалОтчета(ТабДокумент);
	// -ХМНПФ 11.12.2015 ШадринАВ // ОсновныеДействияФормыПечать() // №70
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.АвтоМасштаб = Истина;
	Возврат ТабДокумент; 
КонецФункции	

Процедура ХМ_ПечатьИзРешенияНаСервере(РешениеОбъект)
	
	ЭтотОбъект.Заявление = РешениеОбъект.ДокументОснование;
	ЭтотОбъект.ДоговорОПС = РешениеОбъект.ДоговорОПС;
	ЭтотОбъект.ЗЛ = ЭтотОбъект.ДоговорОПС.Участник;
	ЭтотОбъект.ДатаРасчета = РешениеОбъект.Дата;
	ЭтотОбъект.РазмерГП = РешениеОбъект.ХМ_РазмерГП;
	ЭтотОбъект.ДатаГосПенсии = РешениеОбъект.ХМ_ДатаГосПенсии;
	//01.07.2024 ЛыткинАМ // ХМХМ_ПечататьРасчетНаСервере //https://redmine.npf.com/issues/3891
	ЭтотОбъект.ПрожиточныйМинимум = РегистрыСведений.пдс_ПрожиточныйМинимум.ПолучитьПоследнее(ЭтотОбъект.ДатаРасчета, Новый Структура()).Размер; 
	// +ХМНПФ 27.05.2020 ШадринАВ // ЗаполнитьПоОстаткам() // №2361
	ЭтотОбъект.ПравоНаНакопительнуюПенсию = РешениеОбъект.ДокументОснование.СтрахЧастьС;
	// -ХМНПФ 27.05.2020 ШадринАВ // ЗаполнитьПоОстаткам() // №2361
	Если ТипЗнч(РешениеОбъект) = Тип("ДокументСсылка.уп_РешениеОЕдиновременнойВыплате")  Тогда
		Если РешениеОбъект.НеУчитыватьДопВзносы Тогда
			ЭтотОбъект.ВариантРасчета = 0;  //только страховые взносы
		Иначе
			ЭтотОбъект.ВариантРасчета = 1;  //страховые + доп. взносы
		КонецЕсли;
		ЭтотОбъект.СрокПП = РешениеОбъект.ХМ_СрокПП;
		ЭтотОбъект.ПринятоеРешение = 1;
		// +ХМНПФ 11.05.2021 ШадринАВ // ХМ_ПечатьИзРешенияНаСервере() // №3207
		//// +ХМНПФ 15.05.2018 КлименкоМИ // ХМ_ПечатьИзРешенияНаСервере() // №1582
		////--ЭтотОбъект.Расшифровка.Загрузить(РешениеОбъект.РасшифровкаСуммы.Выгрузить(,"ТипСуммы,Сумма,СуммаГарантированная"));
		//ТЗИзРешения = РешениеОбъект.РасшифровкаСуммы.Выгрузить().Скопировать();
		//Для каждого Стр из ТЗИзРешения Цикл
		//	Стр.Сумма = Стр.Сумма + Стр.СуммаКомпенсации + Стр.СуммаВосполнения + Стр.СуммаВозмещения;
		//КонецЦикла;
		//ЭтотОбъект.Расшифровка.Загрузить(ТЗИзРешения);
		//// -ХМНПФ 15.05.2018 КлименкоМИ // ХМ_ПечатьИзРешенияНаСервере() // №1582
		ТЗОстатков = ХМ_ПолучитьТЗОстатковНаДатуЗаявления();
		ЭтотОбъект.Расшифровка.Загрузить(ТЗОстатков);
		// -ХМНПФ 11.05.2021 ШадринАВ // ХМ_ПечатьИзРешенияНаСервере() // №3207
		Если ЭтотОбъект.СрокПП<>0 Тогда
			ЭтотОбъект.РазмерПП = ЭтотОбъект.Расшифровка.Итог("Сумма")/ЭтотОбъект.СрокПП;
		КонецЕсли;
	ИначеЕсли ТипЗнч(РешениеОбъект) = Тип("ДокументСсылка.уп_РешениеОНазначенииСрочнойВыплаты")  Тогда
		ЭтотОбъект.ВариантРасчета = 2;  //только доп. взносы
		ЭтотОбъект.РазмерСП = РешениеОбъект.РазмерСрочнойВыплаты;
		ЭтотОбъект.СрокСП = РешениеОбъект.Срок;
		ЭтотОбъект.ПринятоеРешение = 2;
		ЭтотОбъект.Расшифровка.Загрузить(РешениеОбъект.Остатки.Выгрузить(,"ТипСуммы,Сумма,СуммаГарантированная"));
	ИначеЕсли ТипЗнч(РешениеОбъект) = Тип("ДокументСсылка.уп_РешениеОНазначенииНЧТП")  Тогда
		Если РешениеОбъект.УчитыватьДопВзносы Тогда
			ЭтотОбъект.ВариантРасчета = 1;  //страховые + доп. взносы
		Иначе
			ЭтотОбъект.ВариантРасчета = 0;  //только страховые взносы
		КонецЕсли;	
		ЭтотОбъект.РазмерПП = РешениеОбъект.РазмерВыплаты;
		ЭтотОбъект.СрокПП = РешениеОбъект.СрокПожизненнойПенсии;
		ЭтотОбъект.ПринятоеРешение = 2;
		ЭтотОбъект.Расшифровка.Загрузить(РешениеОбъект.Остатки.Выгрузить(,"ТипСуммы,Сумма,СуммаГарантированная"));
	КонецЕсли;
	ЭтотОбъект.СуммаОснЧасти = ХМ_ФункцииОтчетов.ПолучитьСуммаОснЧасти(ЭтотОбъект.Расшифровка.Выгрузить(,"ТипСуммы,Сумма"));
	ЭтотОбъект.СуммаДопЧасти = ХМ_ФункцииОтчетов.ПолучитьСуммаДопЧасти(ЭтотОбъект.Расшифровка.Выгрузить(,"ТипСуммы,Сумма"));
	ЭтотОбъект.СуммаМК = ХМ_ФункцииОтчетов.ПолучитьСуммаМК(ЭтотОбъект.Расшифровка.Выгрузить(,"ТипСуммы,Сумма"));
	//01.07.2024 ЛыткинАМ // ХМХМ_ПечататьРасчетНаСервере //https://redmine.npf.com/issues/3891
	хм_РасчетПоПрожМинимумуПенсионера = ?(ЗначениеЗаполнено(ЭтотОбъект.Заявление),ЭтотОбъект.Заявление.Дата,ЭтотОбъект.ДатаРасчета) >= Дата(2024,7,1,0,0,0);

	Если хм_РасчетПоПрожМинимумуПенсионера = Истина Тогда
		// +ХМНПФ 15.01.2025 ЛыткинАМ // Учтен прожиточный минимум // №4005
		ПрожиточныйМинимум = РегистрыСведений.пдс_ПрожиточныйМинимум.ПолучитьПоследнее(?(ЗначениеЗаполнено(ЭтотОбъект.Заявление),ЭтотОбъект.Заявление.Дата,ЭтотОбъект.ДатаРасчета), Новый Структура()).Размер; 
		ЭтотОбъект.Процент = ЭтотОбъект.РазмерПП*100/ПрожиточныйМинимум;
	ИначеЕсли ЭтотОбъект.РазмерГП<>0 Тогда
		ЭтотОбъект.Процент = ЭтотОбъект.РазмерПП*100/(ЭтотОбъект.РазмерГП+ЭтотОбъект.РазмерПП);
	КонецЕсли;

КонецПроцедуры

// ХМНПФ 18.04.2017 ШадринАВ // Новая ХМ_ПолучитьСрокПП() // №955
Функция ХМ_ПолучитьСрокПП(ХМ_ДатаДляРасчета)
	Возврат хм_ПрочиеПроцедуры.ПолучитьСрокПП(ХМ_ДатаДляРасчета, ЗЛ, ДатаГосПенсии);
КонецФункции // ХМ_ПолучитьСрокПП()

// ХМНПФ 14.01.2016 ШадринАВ // ХМ_ПечатьИзРешения() // №133 *
Процедура ХМ_ПечатьИзРешения(РешениеОбъект) Экспорт
	ХМ_ПечатьИзРешенияНаСервере(РешениеОбъект.Ссылка);
	ТабДокументРешения = ХМ_ПечататьРасчетНаСервере();
	РешениеОбъект.ДополнительныеСвойства.Вставить("ТабДокументРешения",ТабДокументРешения);
КонецПроцедуры

// ХМНПФ 11.12.2015 ШадринАВ // Новая ХМ_ПодвалОтчета() // №70 *
Процедура ХМ_ПодвалОтчета(ТабДокумент)
	//Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("ХМ_Макет");
	Макет = ЭтотОбъект.ПолучитьМакет("ХМ_Макет");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ДолжностьФИО = ХМ_ФункцииОтчетов.ДолжностьИФИОРуководителя();
	ОбластьПодвал.Параметры.ДолжностьОтветственного = ДолжностьФИО.Должность;
	ОбластьПодвал.Параметры.ФИООтветственного = ДолжностьФИО.ФИО;
	ОбластьПодвал.Параметры.Исполнитель = ПараметрыСеанса.ТекущийПользователь;
	ТабДокумент.Вывести(ОбластьПодвал);
КонецПроцедуры

// ХМНПФ 11.05.2021 ШадринАВ // Новая ХМ_ПолучитьТЗОстатковНаДатуЗаявления() // №3207
Функция ХМ_ПолучитьТЗОстатковНаДатуЗаявления()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_РезервыОПСОстатки.ТипСуммы КАК ТипСуммы,
		|	СУММА(уп_РезервыОПСОстатки.СуммаОстаток) КАК Сумма
		|ИЗ
		|	РегистрНакопления.уп_РезервыОПС.Остатки(
		|			&ДатаЗаявления,
		|			ДоговорОПС = &ДоговорОПС
		|				И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений)
		|				И Истина) КАК уп_РезервыОПСОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_РезервыОПСОстатки.ТипСуммы";
	
	Запрос.УстановитьПараметр("ДатаЗаявления", ЭтотОбъект.Заявление.Дата);
	Запрос.УстановитьПараметр("ДоговорОПС", ЭтотОбъект.ДоговорОПС);
	
	Если ТипЗнч(ЭтотОбъект.Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииНЧТП") И НЕ ЭтотОбъект.Заявление.УчитыватьДопВзносы Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Истина","И ТипСуммы В(ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз),ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))");
	ИначеЕсли ТипЗнч(ЭтотОбъект.Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОЕдиновременнойВыплате") И ЭтотОбъект.Заявление.НеУчитыватьДопВзносы Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"И Истина","И ТипСуммы В(ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз),ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))");
	КонецЕсли;
	
	ТЗОстатков = Запрос.Выполнить().Выгрузить();
	Возврат ТЗОстатков;
КонецФункции // ХМ_ПолучитьТЗОстатковНаДатуЗаявления()

Процедура ХМ_НеТиповыеИзменения()
	// !!! НЕОБХОДИМО СИНХРОНИЗИРОВАТЬ АЛГОРИТМЫ с Обработкой ХМ_РасчетПенсионныхВыплатОПС
	
	// ХМНПФ 14.12.2015 ЛыткинАМ // ЗаполнитьПоОстаткам() // №СрокПожизненнойПенсии
	// ХМНПФ 09.01.2017 ШадринАВ // ЗаполнитьПоОстаткам() // №743
	// ХМНПФ 18.04.2017 ШадринАВ // Новая ХМ_ПолучитьСрокПП() // №955
	// ХМНПФ 31.08.2017 ШадринАВ // Новая ХМ_ПечататьРасчетНаСервере() // №1142 // ! нужно отслеживать и синхронизировать с ПечататьРасчетНаСервере() из модуля формы
	// ХМНПФ 14.01.2016 ШадринАВ // ХМ_ПечатьИзРешения() // №133 *
	// ХМНПФ 14.01.2016 ШадринАВ // ХМ_ПечатьИзРешенияНаСервере() // №133 *
	// ХМНПФ 11.12.2015 ШадринАВ // Новая ХМ_ПодвалОтчета() // №70 * дубль в модуле формы
	// ХМНПФ 17.01.2017 ШадринАВ // РасчитатьПроцент() // №753
	// ХМНПФ 01.02.2018 ШадринАВ // ЗаполнитьПоОстаткам() // №1319
	// ХМНПФ 15.05.2018 КлименкоМИ // ХМ_ПечатьИзРешенияНаСервере() // №1582
	// ХМНПФ 15.05.2019 ГригорьевМА // Изменен макет Макет текст стр.6 "Заявление на накопительную часть трудовой пенсии" заменен на "Заявление на накопительную пенсию" // №2322
	// ХМНПФ 27.05.2020 ШадринАВ // ЗаполнитьПоОстаткам() // №2361	
	
	// !!! НЕОБХОДИМО СИНХРОНИЗИРОВАТЬ АЛГОРИТМЫ с Обработкой ХМ_РасчетПенсионныхВыплатОПС
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьПоОстаткам")
Процедура ХМЗаполнитьПоОстаткам()
	Расшифровка.Очистить();

	ДатаЗаявления = ДатаРасчета;
	Если ЗначениеЗаполнено(Заявление) И 
		(ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииНЧТП") 
		или ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОЕдиновременнойВыплате")	) Тогда  

		ДатаЗаявления = ?(Заявление.Дата>ДатаРасчета,Заявление.Дата,ДатаРасчета);
		ДатаГосПенсии = Заявление.СтрахЧастьС;
		ПравоНаНакопительнуюПенсию = Заявление.ПравоНаНакопительнуюПенсию;
		РазмерГП = Заявление.РазмерГосПенсии;
	КонецЕсли;
	Если ЗначениеЗаполнено(Заявление) И 
		ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты")  Тогда
		ПравоНаНакопительнуюПенсию = Заявление.ПравоНаНакопительнуюПенсию;
		#Вставка			
		// +ХМНПФ 01.02.2018 ШадринАВ // ЗаполнитьПоОстаткам() // №1319
		ДатаГосПенсии = Заявление.СтрахЧастьС;
		// -ХМНПФ 01.02.2018 ШадринАВ // ЗаполнитьПоОстаткам() // №1319
		#КонецВставки		
	КонецЕсли;	
	#Вставка			
	// +ХМНПФ 27.05.2020 ШадринАВ // ЗаполнитьПоОстаткам() // №2361
	Если ЗначениеЗаполнено(Заявление) Тогда
		ПравоНаНакопительнуюПенсию = Заявление.СтрахЧастьС;
	КонецЕсли;	
	// -ХМНПФ 27.05.2020 ШадринАВ // ЗаполнитьПоОстаткам() // №2361
    #КонецВставки
	
	//заполняем табличную часть
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВложенныйЗапрос.ТипСуммы,
	|	СУММА(ВложенныйЗапрос.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ВложенныйЗапрос.ТекОстаток) КАК ТекОстаток
	|ПОМЕСТИТЬ РезервыОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_РезервыОПСОстатки.ТипСуммы КАК ТипСуммы,
	|		уп_РезервыОПСОстатки.СуммаОстаток КАК СуммаОстаток,
	|		0 КАК ТекОстаток
	|	ИЗ
	|		РегистрНакопления.уп_РезервыОПС.Остатки(
	|				&ДатаОстатков,
	|				ДоговорОПС = &ДоговорОПС
	|					И ТипРезерва = &Накопительный) КАК уп_РезервыОПСОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		уп_РезервыОПСОстатки.ТипСуммы,
	|		0,
	|		уп_РезервыОПСОстатки.СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.уп_РезервыОПС.Остатки(
	|				&ДатаЗаявления,
	|				ДоговорОПС = &ДоговорОПС
	|					И ТипРезерва = &Накопительный) КАК уп_РезервыОПСОстатки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипСуммы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(РезервыОстатки.ТипСуммы, уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы) КАК ТипСуммы,
	|	ЕСТЬNULL(РезервыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(РезервыОстатки.ТекОстаток, 0) КАК ТекОстаток,
	|	ЕСТЬNULL(уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток, 0) КАК СуммаГарантированная,
	|	ЕСТЬNULL(уп_СуммыГарантийногоВосполненияОбороты.СуммаОборот, 0) КАК СуммаВосполнения
	|ИЗ
	|	РезервыОстатки КАК РезервыОстатки
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(&ДатаОстатков, ДоговорОПС = &ДоговорОПС) КАК уп_ТекущаяСуммаГарантированияОстатки
	|		ПО РезервыОстатки.ТипСуммы = уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СуммыГарантийногоВосполнения.Обороты(&ДатаОстатков, &ТекДата, Период, ДоговорОПС = &ДоговорОПС) КАК уп_СуммыГарантийногоВосполненияОбороты
	|		ПО РезервыОстатки.ТипСуммы = уп_СуммыГарантийногоВосполненияОбороты.ТипСуммы";


	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаРасчета);
	Запрос.УстановитьПараметр("ДатаЗаявления", ДатаЗаявления);
	Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	СуммаДляРасчетаСП = 0;
	СуммаОснЧасти = 0;
	СуммаДопЧасти = 0;
	СуммаМК = 0;
	СуммаГарантированияОснЧасти = 0;
	СуммаГарантированияДопЧасти = 0;
	СуммаВозмещенияОснЧасти = 0;
	СуммаВозмещенияДопЧасти = 0;
	Пока Выборка.Следующий() Цикл
		Если ВариантРасчета = 1 Тогда
			ДобавитьСтроку(Выборка.ТипСуммы,(Выборка.СуммаОстаток+Выборка.СуммаВосполнения),(Выборка.ТекОстаток+Выборка.СуммаВосполнения),Выборка.СуммаГарантированная);
		КонецЕсли;
		Если ВариантРасчета = 2 Тогда //срочная выплата
			Если Выборка.ТипСуммы<>Справочники.уп_ТипыСуммОПС.ВзносыФиз 
				и Выборка.ТипСуммы<>Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
				ДобавитьСтроку(Выборка.ТипСуммы,(Выборка.СуммаОстаток+Выборка.СуммаВосполнения),(Выборка.ТекОстаток+Выборка.СуммаВосполнения),Выборка.СуммаГарантированная);
			КонецЕсли;	
		КонецЕсли;
		Если ВариантРасчета = 0 Тогда
			Если Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.ВзносыФиз 
				или Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
				ДобавитьСтроку(Выборка.ТипСуммы,(Выборка.СуммаОстаток+Выборка.СуммаВосполнения),(Выборка.ТекОстаток+Выборка.СуммаВосполнения),Выборка.СуммаГарантированная);
			КонецЕсли;	
		КонецЕсли;
		Если Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.ВзносыФиз 
			или Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
			СуммаОснЧасти = СуммаОснЧасти+Выборка.СуммаОстаток+Выборка.СуммаВосполнения;
			СуммаГарантированияОснЧасти = СуммаГарантированияОснЧасти+Выборка.СуммаГарантированная;
			СуммаВозмещенияОснЧасти = СуммаВозмещенияОснЧасти + Выборка.СуммаВосполнения;
		Иначе
			СуммаДопЧасти = СуммаДопЧасти + Выборка.СуммаОстаток+Выборка.СуммаВосполнения;
			СуммаГарантированияДопЧасти = СуммаГарантированияДопЧасти+Выборка.СуммаГарантированная;
			Если Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.МатКапитал 
				или Выборка.ТипСуммы=Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
				СуммаМК = СуммаМК+Выборка.СуммаОстаток+Выборка.СуммаВосполнения;
			КонецЕсли;
			СуммаВозмещенияДопЧасти = СуммаВозмещенияДопЧасти + Выборка.СуммаВосполнения;
		КонецЕсли;
	КонецЦикла;

	//номинал
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_НоминалВзносовОПСОбороты.ТипСуммы,
	|	уп_НоминалВзносовОПСОбороты.СуммаОборот
	|ИЗ
	|	РегистрНакопления.уп_НоминалВзносовОПС.Обороты(
	|			&НачалоВремен,
	|			&ДатаОстатков,
	|			Период,
	|			ДоговорОПС = &ДоговорОПС
	|				) КАК уп_НоминалВзносовОПСОбороты";

	Запрос.УстановитьПараметр("ДатаОстатков", ДатаРасчета);
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("НачалоВремен", '20000101');

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	НоминалОснЧасти = 0;
	НоминалДопЧасти = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.ТипСуммы=Справочники.уп_ТипыСуммОПС.ВзносыФиз 
			или ВыборкаДетальныеЗаписи.ТипСуммы=Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
			НоминалОснЧасти = НоминалОснЧасти+ВыборкаДетальныеЗаписи.СуммаОборот;
		Иначе
			НоминалДопЧасти = НоминалДопЧасти + ВыборкаДетальныеЗаписи.СуммаОборот;
		КонецЕсли;
	КонецЦикла;


	СрокСП = 120;
	//Если ЗначениеЗаполнено(Заявление) Тогда
	//	Если ЗначениеЗаполнено(Заявление.СтрахЧастьС) Тогда
	//		ДатаГосПенсии = Заявление.СтрахЧастьС;
	//	Иначе
	//		ДатаГосПенсии = Неопределено;
	//	КонецЕсли;	
	//Иначе
	//	ДатаГосПенсии = Неопределено;
	//КонецЕсли;	
	ВариантРасчетаПериодаТ = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(ТекущаяДата(), Новый Структура("Организация", ДоговорОПС.Организация)).ВариантРасчетаЛетДосрочности;
	//+ АДаниленко 08.08.2019
	Если ВариантРасчетаПериодаТ = 0 ИЛИ ВариантРасчетаПериодаТ = 1 Тогда
		СрокПП = уп_ОПС.СрокПожизненнойПенсии(ЗЛ, ДатаРасчета, ПравоНаНакопительнуюПенсию, ВариантРасчетаПериодаТ);
	ИначеЕсли ВариантРасчетаПериодаТ = 2 ИЛИ ВариантРасчетаПериодаТ = 3 Тогда
		СрокПП = уп_ОПС.СрокПожизненнойПенсии_2019(?(ЗначениеЗаполнено(Заявление), Заявление, ДоговорОПС), ДатаРасчета, ВариантРасчетаПериодаТ = 2, Ложь, ЗначениеЗаполнено(Заявление));
	Иначе
		СрокПП = 0 ;
	КонецЕсли;
	//- АДаниленко 08.08.2019
	#Вставка
	// +ХМНПФ 25.01.2016 ЛыткинАМ // ЗаполнитьПоОстаткам() // №СрокПожизненнойПенсии
	ХМ_ДатаДляРасчета = ?(ЗначениеЗаполнено(Заявление),Заявление.Дата,ДатаРасчета);  // ХМНПФ 15.06.2017 ШадринАВ // ЗаполнитьПоОстаткам() // №1043
	//Если ХМ_ДатаДляРасчета>=Дата(2017,1,1,0,0,0) Тогда // ХМНПФ 09.01.2017 ШадринАВ // ЗаполнитьПоОстаткам() // №743
	СрокПП = ХМ_ПолучитьСрокПП(ХМ_ДатаДляРасчета);
	// +ХМНПФ 18.07.2019 ШадринАВ // ЗаполнитьПоОстаткам() // №2360
	//ИначеЕсли ХМ_ДатаДляРасчета<Дата(2017,1,1,0,0,0) И ХМ_ДатаДляРасчета>=Дата(2016,1,1,0,0,0) Тогда
	//	СрокПП = 234;		
	//Иначе
	//	СрокПП = 228;		
	//КонецЕсли;
	// -ХМНПФ 18.07.2019 ШадринАВ // ЗаполнитьПоОстаткам() // №2360
	// -ХМНПФ 25.01.2016 ЛыткинАМ // ЗаполнитьПоОстаткам() // №СрокПожизненнойПенсии  
	#КонецВставки
	Если ВариантРасчета = 2 Тогда
		РазмерСП = Расшифровка.Итог("Сумма")/СрокСП;
	Иначе
		Если СрокПП<>0 Тогда
			РазмерПП = (Расшифровка.Итог("Сумма"))/СрокПП;
		КонецЕсли;	
	КонецЕсли;
	РасчитатьПроцент();
КонецПроцедуры

&ИзменениеИКонтроль("РасчитатьПроцент")
Процедура ХМРасчитатьПроцент()

	//проверяем состояния

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ ОбщееСостояние
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаДок, ДоговорОПС = &ДоговорОПС) КАК уп_СостоянияДоговораОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС,
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия
	|ПОМЕСТИТЬ ПричиныЗакрытия
	|ИЗ
	|	РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаДок, ДоговорОПС = &ДоговорОПС) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС,
	|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ СостояниеДопЧасти
	|ИЗ
	|	РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаДок, ДоговорОПС = &ДоговорОПС) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС,
	|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ СостояниеОснЧасти
	|ИЗ
	|	РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаДок, ДоговорОПС = &ДоговорОПС) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбщееСостояние.ДоговорОПС,
	|	ОбщееСостояние.Состояние,
	|	ПричиныЗакрытия.ПричинаЗакрытия,
	|	СостояниеДопЧасти.Состояние КАК СостояниеДопЧасти,
	|	СостояниеОснЧасти.Состояние КАК СостояниеОснЧасти
	|ИЗ
	|	ОбщееСостояние КАК ОбщееСостояние
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостояниеОснЧасти КАК СостояниеОснЧасти
	|		ПО ОбщееСостояние.ДоговорОПС = СостояниеОснЧасти.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостояниеДопЧасти КАК СостояниеДопЧасти
	|		ПО ОбщееСостояние.ДоговорОПС = СостояниеДопЧасти.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПричиныЗакрытия КАК ПричиныЗакрытия
	|		ПО ОбщееСостояние.ДоговорОПС = ПричиныЗакрытия.ДоговорОПС";

	Запрос.УстановитьПараметр("ДатаДок", ДатаРасчета);
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);

	РезультатЗапроса = Запрос.Выполнить();

	ОтказПоСостоянию = Ложь;
	ОтказПоНЧТП = Ложь;
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если (ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт
			или ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию) Тогда
			ОтказПоСостоянию = Истина;
			Сообщить("Договор "+ДоговорОПС+" находится в состоянии "+ВыборкаДетальныеЗаписи.Состояние+", выплата назначена быть не может", СтатусСообщения.Важное);
		КонецЕсли;	
		Если ВыборкаДетальныеЗаписи.СостояниеОснЧасти = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП Тогда
			ОтказПоНЧТП = Истина;
			Сообщить("По договору "+ДоговорОПС+" уже назначена накопительная пенсия", СтатусСообщения.Важное);
		КонецЕсли;	
	КонецЦикла;

	БазаРасчета = Расшифровка.Итог("Сумма");

	Если ВариантРасчета = 2 Тогда

		Если СрокСП<>0 Тогда
			РазмерСП = БазаРасчета/СрокСП;
		Иначе
			РазмерСП = 0;
			Если БазаРасчета<>0 Тогда
				Сообщить("Не указан срок срочной выплаты.");
			КонецЕсли;
		КонецЕсли;
		ПринятоеРешение = 2;
	Иначе
		РасчетПоВсемОстаткам = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(ДатаРасчета, Новый Структура("Организация", ДоговорОПС.Организация)).Расчет5ПроцентногоБарьераПоПолномуОстатку;
		Если СрокПП<>0 Тогда
			РазмерПП = БазаРасчета/СрокПП;
			Если РасчетПоВсемОстаткам Тогда
				РазмерПенсииДляСравненения = (СуммаОснЧасти+СуммаДопЧасти)/СрокПП;
			Иначе
				РазмерПенсииДляСравненения = РазмерПП;
			КонецЕсли;	
		Иначе
			РазмерПП = 0;
			РазмерПенсииДляСравненения = 0;
			Сообщить("Не указан предположительный срок пожизненной выплаты.");
		КонецЕсли;

		ОбщийРазмерП = РазмерПенсииДляСравненения;

		Если ДатаРасчета<'20240701' Тогда

			Если РазмерГП<>0 Тогда
				Процент = ОбщийРазмерП*100/(РазмерГП+РазмерПенсииДляСравненения);
				Если Процент>5 Тогда
					Если ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОЕдиновременнойВыплате") Тогда
						ПринятоеРешение = 3;
					Иначе
						ПринятоеРешение = 2;
					КонецЕсли;
				Иначе
					Если ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОЕдиновременнойВыплате") Тогда
						ПринятоеРешение = 1; 
					#Вставка 
					// +ХМНПФ 17.01.2017 ШадринАВ // РасчитатьПроцент() // №753
					ИначеЕсли ТипЗнч(Заявление) = Тип("ДокументСсылка.уп_ЗаявлениеЗЛОНазначенииНЧТП") Тогда
						ПринятоеРешение = 1;
					// -ХМНПФ 17.01.2017 ШадринАВ // РасчитатьПроцент() // №753
					#КонецВставки
					ИначеЕсли НЕ ЗначениеЗаполнено(Заявление) Тогда
						ПринятоеРешение = 1;
					Иначе
						ПринятоеРешение = 4;
					КонецЕсли;	
				КонецЕсли;
			Иначе
				#Вставка 
				// +ХМНПФ 22.02.2019 ШадринАВ // РасчитатьПроцент() // №2060
				Сообщить("Внимание!!! Не заполнен 'Размер гос. пенсии'. Возможны ошибки при расчете и при вынесении решения!",СтатусСообщения.ОченьВажное);
				// -ХМНПФ 22.02.2019 ШадринАВ // РасчитатьПроцент() // №2060
				#КонецВставки
				Сообщить("Не заполнен размер гос. пенсии. Принятие решения о варианте выплаты невозможно.", СтатусСообщения.Важное);
				Процент = 0;
				ПринятоеРешение = 0;
			КонецЕсли;
		Иначе //считаем по прожиточному минимуму   
			Если ПрожиточныйМинимум<>0 Тогда
				Процент = РазмерПенсииДляСравненения*100/ПрожиточныйМинимум; 
				Если Процент>10 Тогда 
					ПринятоеРешение = 2;
				Иначе
					ПринятоеРешение = 1;
				КонецЕсли;	
			Иначе 
				Сообщить("Не заполнен размер прожиточного минимума. Принятие решения о варианте выплаты невозможно.", СтатусСообщения.Важное);
				Процент = 0;
				ПринятоеРешение = 0;
			КонецЕсли;	
		КонецЕсли;

	КонецЕсли;

	Если ОтказПоСостоянию Тогда
		ПринятоеРешение = 4;
	КонецЕсли;
	Если ВариантРасчета<>2 И ОтказПоНЧТП Тогда
		ПринятоеРешение = 4;
	КонецЕсли;	

КонецПроцедуры
