
&НаСервере
&ИзменениеИКонтроль("СформироватьСервер")
Функция ХМСформироватьСервер(ДокументРезультат)

	РабочаяДата=ТекущаяДата();
	СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Организация, РабочаяДата);
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Объект.Организация, РабочаяДата);
	РасшифровкаПодписи = Руководители.РуководительПредставление;
	ВалютаПечати = Константы.ВалютаРегламентированногоУчета.Получить();
	ПредставлениеОрганизации = СведенияОбОрг.Представление;

	КолОтчетов =100;

	НомерОтчета=1;
	флагОчисткиТабДокумента=0;

	Если Объект.ТипРешения = 0 Тогда
		Если Объект.Дата1>='20150101' Тогда
			//Макет = Документы.уп_РешениеОЕдиновременнойВыплате.ПолучитьМакет("ПФ_MXL_РешениеОВыплате2015");
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.уп_РешениеОЕдиновременнойВыплате.ПФ_MXL_РешениеОВыплате2015");
		Иначе
			Макет = Документы.уп_РешениеОЕдиновременнойВыплате.ПолучитьМакет("РешениеОВыплате");
		КонецЕсли;
	Иначе
		Если Объект.Дата1>='20150101' Тогда
			//Макет = Документы.уп_РешениеОЕдиновременнойВыплате.ПолучитьМакет("ПФ_MXL_РешениеОДопВыплате2015");
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.уп_РешениеОЕдиновременнойВыплате.ПФ_MXL_РешениеОДопВыплате2015");
		Иначе
			Макет = Документы.уп_РешениеОЕдиновременнойВыплате.ПолучитьМакет("РешениеОДопВыплате");
		КонецЕсли;
	КонецЕсли;

	ДокументРезультат.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_РешениеОЕдиновременнойВыплате.Ссылка,
	|	уп_РешениеОЕдиновременнойВыплате.Номер КАК НомерРешения,
	|	уп_РешениеОЕдиновременнойВыплате.Дата КАК Дата,
	|	уп_РешениеОЕдиновременнойВыплате.ДокументОснование,
	|	уп_РешениеОЕдиновременнойВыплате.ДокументОснование.Номер КАК НомерЗаявления,
	|	уп_РешениеОЕдиновременнойВыплате.ДокументОснование.Дата КАК ДатаЗаявления,
	|	уп_РешениеОЕдиновременнойВыплате.СуммаНаСчете,
	|	уп_РешениеОЕдиновременнойВыплате.Участник,
	|	ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество КАК ФИО,
	|	уп_РешениеОЕдиновременнойВыплате.ДокументОснование.ДатаРегистрации КАК ДатаРегистрации,
	|	уп_РешениеОЕдиновременнойВыплате.ДоговорОПС.НомерЛицевогоСчета КАК НомерСчета,
	|	уп_РешениеОЕдиновременнойВыплате.ТипЕдиновременнойВыплаты,
	|	уп_РешениеОЕдиновременнойВыплате.Участник.уп_ФизЛицо КАК ФЛ,
	|	уп_РешениеОЕдиновременнойВыплате.ДоговорОПС.Филиал КАК Филиал
	|ПОМЕСТИТЬ ТаблицаРешений
	|ИЗ
	|	Документ.уп_РешениеОЕдиновременнойВыплате КАК уп_РешениеОЕдиновременнойВыплате
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизическихЛиц.СрезПоследних(
	|				&ДатаКон,
	|				ФизическоеЛицо В
	|					(ВЫБРАТЬ
	|						уп_РешениеОЕдиновременнойВыплате.Участник.уп_ФизЛицо
	|					ИЗ
	|						Документ.уп_РешениеОЕдиновременнойВыплате КАК уп_РешениеОЕдиновременнойВыплате
	|					ГДЕ
	|						уп_РешениеОЕдиновременнойВыплате.Дата МЕЖДУ &ДатаНач И &ДатаКон)) КАК ФИОФизЛицСрезПоследних
	|		ПО уп_РешениеОЕдиновременнойВыплате.Участник.уп_ФизЛицо = ФИОФизЛицСрезПоследних.ФизическоеЛицо
	|ГДЕ
	|	уп_РешениеОЕдиновременнойВыплате.Проведен = ИСТИНА
	|	И уп_РешениеОЕдиновременнойВыплате.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И уп_РешениеОЕдиновременнойВыплате.Организация = &Организация
	|	И уп_РешениеОЕдиновременнойВыплате.ДополнительнаяВыплата = &ДопВыплата
	|	И уп_РешениеОЕдиновременнойВыплате.ДоначислениеПоПереданномуВзносуИзПФР = &Доначисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка,
	|	ФизическиеЛицаКонтактнаяИнформация.Тип КАК Тип,
	|	ФизическиеЛицаКонтактнаяИнформация.Вид КАК Вид,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Адрес
	|ПОМЕСТИТЬ ТаблицаАдресов
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаРешений.ФЛ
	|			ИЗ
	|				ТаблицаРешений)
	|	И ФизическиеЛицаКонтактнаяИнформация.Тип = &Тип
	|	И ФизическиеЛицаКонтактнаяИнформация.Вид = &Вид
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРешений.Ссылка КАК Ссылка,
	|	ТаблицаРешений.НомерРешения КАК НомерРешения,
	|	ТаблицаРешений.Дата КАК Дата,
	|	ТаблицаРешений.ДокументОснование КАК ДокументОснование,
	|	ТаблицаРешений.НомерЗаявления КАК НомерЗаявления,
	|	ТаблицаРешений.ДатаЗаявления КАК ДатаЗаявления,
	|	ТаблицаРешений.СуммаНаСчете КАК СуммаНаСчете,
	|	ТаблицаРешений.Участник КАК Участник,
	|	ТаблицаРешений.ФИО КАК ФИО,
	|	ТаблицаРешений.ДатаРегистрации КАК ДатаРегистрации,
	|	ТаблицаРешений.НомерСчета КАК НомерСчета,
	|	ТаблицаРешений.ТипЕдиновременнойВыплаты КАК ТипЕдиновременнойВыплаты,
	|	ТаблицаРешений.Филиал КАК Филиал,
	|	ТаблицаРешений.ФЛ КАК ФЛ,
	|	ТаблицаАдресов.Адрес КАК Адрес
	|ИЗ
	|	ТаблицаРешений КАК ТаблицаРешений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаАдресов КАК ТаблицаАдресов
	|		ПО ТаблицаРешений.ФЛ = ТаблицаАдресов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	НомерРешения";

	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Объект.Дата2));
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Объект.Дата1));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Если Объект.ТипРешения = 0 Тогда
		Запрос.УстановитьПараметр("ДопВыплата", ЛОЖЬ);
		Запрос.УстановитьПараметр("Доначисление", ЛОЖЬ);
	ИначеЕсли Объект.ТипРешения = 1 Тогда
		Запрос.УстановитьПараметр("ДопВыплата", ИСТИНА);
		Запрос.УстановитьПараметр("Доначисление", ЛОЖЬ);
	Иначе
		Запрос.УстановитьПараметр("ДопВыплата", ЛОЖЬ);
		Запрос.УстановитьПараметр("Доначисление", ИСТИНА);
	КонецЕсли;
	Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);


	Результат = Запрос.Выполнить();
	Область = Макет.ПолучитьОбласть("Решение");
	#Удаление
	ОбластьКудаКому = Макет.ПолучитьОбласть("ОбластьКудаКому");
	#КонецУдаления
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		Если флагОчисткиТабДокумента = 1 И Объект.ПечатьБезПредварительногоПросмотра Тогда
			//отправляем на печать
			ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
			ДокументРезультат.Автомасштаб = Истина;
			ДокументРезультат.Напечатать(ЛОЖЬ);
			ДокументРезультат.Очистить();
			флагОчисткиТабДокумента = 0;
		КонецЕсли;	
		Область.Параметры.Заполнить(Выборка);
		Область.Параметры.Число = Формат(Выборка.Дата, "ДФ=дд");
		Область.Параметры.Месяц = Сред(Формат(Выборка.Дата, "ДЛФ=дд"), 3, СтрДлина(Строка(Формат(Выборка.Дата, "ДЛФ=дд")))-10);
		Область.Параметры.Год = Формат(Выборка.Дата, "ДФ=гггг");
		Область.Параметры.НаименованиеФонда = ПредставлениеОрганизации;

		Если Объект.ТипРешения = 0 Тогда	
			Если НЕ ЗначениеЗаполнено(Выборка.ДатаРегистрации) Тогда
				Область.Параметры.ДатаРегистрации = Выборка.ДатаЗаявления;
			КонецЕсли;
			Если Выборка.ТипЕдиновременнойВыплаты = 0 Тогда
				Область.Параметры.Подпункт = "а";
			Иначе
				Область.Параметры.Подпункт = "б";
			КонецЕсли;
		КонецЕсли;

		ПараметрыПрописиНаРусском = ВалютаПечати.ПараметрыПрописи;
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "1", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "2", "0");
		ПараметрыПрописиНаРусском = СтрЗаменить(ПараметрыПрописиНаРусском, "3", "0");
		Область.Параметры.Сумма=Выборка.СуммаНаСчете; //ММВ Добавлена сумма числом (рядом с сумой прописью и отцентрировали)
		Область.Параметры.РублиПрописью = ЧислоПрописью(Цел(Выборка.СуммаНаСчете), "L=ru_RU; НП=Ложь; НД=Ложь", ПараметрыПрописиНаРусском);
		Область.Параметры.Копейки      = Формат(Цел((Выборка.СуммаНаСчете-Цел(Выборка.СуммаНаСчете))*100), "ЧЦ=2; ЧН=00");

		СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОЕдиновременнойВыплате.ПустаяСсылка(),Объект.Организация,Выборка.Филиал,Пользователи.ТекущийПользователь(),"Решение",РабочаяДата);
		Если ЗначениеЗаполнено(СведенияОбОтветственном.Фамилия) Тогда
			Область.Параметры.ДолжностьРуководителя = СведенияОбОтветственном.Должность;
			Область.Параметры.ФИОРуководителя = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
		Иначе
			Область.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
			Область.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
		КонецЕсли;	
		//Область.Параметры.РасшифровкаПодписи = РасшифровкаПодписи;

		ДокументРезультат.Вывести(Область);
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		
		#Удаление
		Если Объект.ПечататьОборот Тогда
			Куда = Выборка.Адрес;
			Кому = Выборка.ФИО;
			ОбластьКудаКому.Рисунки.ТекстКудаКому.Текст = "Кому: "+Кому+Символы.ПС+"Куда: "+Куда;
			ДокументРезультат.Вывести(ОбластьКудаКому);
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
        #КонецУдаления
	
		НомерОтчета = НомерОтчета+1;
		Если НомерОтчета>КолОтчетов Тогда
			флагОчисткиТабДокумента = 1;
			НомерОтчета = 1;
		КонецЕсли;	
	КонецЦикла;

	ДокументРезультат.ОтображатьСетку = Ложь;
	ДокументРезультат.Защита = Ложь;
	ДокументРезультат.ТолькоПросмотр = Истина;
	ДокументРезультат.ОтображатьЗаголовки = Ложь;
	ДокументРезультат.Автомасштаб = Истина;

	Возврат ДокументРезультат;

КонецФункции
