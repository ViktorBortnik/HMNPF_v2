
&НаСервере
Процедура ХМПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_УстановитьДействияФормы() // №2628
	ХМ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("ТипЭлемента", "ГруппаФормы");
	ПараметрыЭлемента.Вставить("Родитель", Элементы.Группа3);
	ХМ_Группа = ХМ_РаботаСФормами.НовоеПолеФормы(ЭтаФорма.Элементы, "ХМ_Группа", НСтр("ru = 'Группа'"), ПараметрыЭлемента);
	ХМ_Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;	
	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("Вид", ВидПоляФормы.ПолеФлажка);
	ПараметрыЭлемента.Вставить("ТипЭлемента", "ПолеФормы");
	ПараметрыЭлемента.Вставить("Родитель", ХМ_Группа);
	ПараметрыЭлемента.Вставить("ПутьКДанным", "Объект.ХМ_ТолькоНовые");
	ХМ_ТолькоНовые = ХМ_РаботаСФормами.НовоеПолеФормы(ЭтаФорма.Элементы, "ХМ_ТолькоНовые", НСтр("ru = 'Только новые'"), ПараметрыЭлемента);
	ХМ_ТолькоНовые.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ХМ_ТолькоНовые.УстановитьДействие("ПриИзменении", "ХМ_ТолькоНовыеПриИзменении");

	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("Вид", ВидПоляФормы.ПолеВвода);
	ПараметрыЭлемента.Вставить("ТипЭлемента", "ПолеФормы");
	ПараметрыЭлемента.Вставить("Родитель", ХМ_Группа);
	ПараметрыЭлемента.Вставить("ПутьКДанным", "Объект.ХМ_НачалоПериода");
	ХМ_НачалоПериода = ХМ_РаботаСФормами.НовоеПолеФормы(ЭтаФорма.Элементы, "ХМ_НачалоПериода", НСтр("ru = 'от'"), ПараметрыЭлемента);
	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("Вид", ВидПоляФормы.ПолеВвода);
	ПараметрыЭлемента.Вставить("ТипЭлемента", "ПолеФормы");
	ПараметрыЭлемента.Вставить("Родитель", ХМ_Группа);
	ПараметрыЭлемента.Вставить("ПутьКДанным", "Объект.ХМ_КонецПериода");
	ХМ_КонецПериода = ХМ_РаботаСФормами.НовоеПолеФормы(ЭтаФорма.Элементы, "ХМ_КонецПериода", НСтр("ru = 'до'"), ПараметрыЭлемента);
	
	Элементы.НовыеНачисления.УстановитьДействие("ПриАктивизацииСтроки", "ХМ_НовыеНачисленияПриАктивизацииСтроки");

КонецПроцедуры

&НаКлиенте
Процедура ХМ_ТолькоНовыеПриИзменении(Элемент)

    Объект.НачислятьЗаПрошлыеПериоды = Не Объект.ХМ_ТолькоНовые;
	Элементы.НачислятьЗаПрошлыеПериоды.ТолькоПросмотр = Объект.ХМ_ТолькоНовые;

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("СформироватьНаСервере")
Процедура ХМСформироватьНаСервере()

	СтруктураОбр = Новый Структура;

	Для Каждого Реквизит Из Обработки.уп_НачислениеПенсийОПС.Создать().Метаданные().Реквизиты Цикл

		СтруктураОбр.Вставить(Реквизит.Имя, Объект[Реквизит.Имя]);

	КонецЦикла;

	СтруктураОбр.Вставить("НовыеНачисления", Объект.НовыеНачисления.Выгрузить());

	АдресОбъекта = ПоместитьВоВременноеХранилище(СтруктураОбр, ЭтаФорма.УникальныйИдентификатор);

	Парам = Новый Массив;
	Парам.Добавить(СтруктураОбр);
	Парам.Добавить(АдресОбъекта);
#Удаление 
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("уп_ОПС.СформироватьТЗДокумента", Парам);
#КонецУдаления
#Вставка 
	// +ХМНПФ 17.01.2020 ШадринАВ // СформироватьНаСервере() // ОПС_Начисления_прош_пер
	Парам.Добавить(хм_РегистрацияСобытийПривелигирован.ПолучитьЗначениеИмяПККлиента());
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("уп_ОПС.ХМ_СформироватьТЗДокумента", Парам);
	// -ХМНПФ 17.01.2020 ШадринАВ // СформироватьНаСервере() // ОПС_Начисления_прош_пер
#КонецВставки
	Идентификатор = ФоновоеЗадание.УникальныйИдентификатор;

КонецПроцедуры

// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_ПриСозданииНаСервере() // №2628
&НаСервере
Процедура ХМ_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ХМ_РаботаСФормами.Обработкауп_НачислениеПенсийОПСуф_ФормаПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Объект.Переключатель1 = 1;
	Объект.НачислятьЗаПрошлыеПериоды = Истина;
	Объект.ХМ_КоличествоПрошлыхПериодов = 3;
	Элементы.НовыеНачисления.Подвал = Истина;
КонецПроцедуры // ХМ_ПриСозданииНаСервере()

// ХМНПФ 13.05.2020 ШадринАВ // Новая ХМ_НовыеНачисленияПриАктивизацииСтроки() // №2628
&НаКлиенте
Процедура ХМ_НовыеНачисленияПриАктивизацииСтроки(Элемент)
	НомерСтроки = 0;
	Сумма = 0;
	ХМ_НовыеНачисленияПриАктивизацииСтрокиНаСервере(НомерСтроки, Сумма);
	Элементы.НовыеНачисления.ПодчиненныеЭлементы.НовыеНачисленияДокументСуммаДокумента.ТекстПодвала = Сумма;
	Элементы.НовыеНачисления.ПодчиненныеЭлементы.НовыеНачисленияНомерСтроки.ТекстПодвала = НомерСтроки;
КонецПроцедуры

// ХМНПФ 13.05.2020 ШадринАВ // Новая ХМ_НовыеНачисленияПриАктивизацииСтрокиНаСервере() // №2628
&НаСервере
Процедура ХМ_НовыеНачисленияПриАктивизацииСтрокиНаСервере(НомерСтроки, Сумма)
	Для каждого ДанныеСтроки Из Объект.НовыеНачисления Цикл
		Сумма = Сумма + ДанныеСтроки.Документ.СуммаДокумента;	
		НомерСтроки = НомерСтроки + ДанныеСтроки.Документ.ДоговорыОПС.Количество();	
	КонецЦикла;
КонецПроцедуры

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ+ 21.10.2015 ШадринАВ // новое поле КоличествоПрошлыхПериодов // ОПС_Начисления_прош_пер
	// ХМНПФ 17.01.2020 ШадринАВ // Модуль // №2628
	// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_УстановитьДействияФормы() // №2628
	// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_ПриСозданииНаСервере() // №2628
	// ХМНПФ 17.01.2020 ШадринАВ // СформироватьНаСервере() // ОПС_Начисления_прош_пер
	// ХМНПФ 13.05.2020 ШадринАВ // Новая ХМ_НовыеНачисленияПриАктивизацииСтроки() // №2628
	// ХМНПФ 13.05.2020 ШадринАВ // Новая ХМ_НовыеНачисленияПриАктивизацииСтрокиНаСервере() // №2628
КонецПроцедуры
