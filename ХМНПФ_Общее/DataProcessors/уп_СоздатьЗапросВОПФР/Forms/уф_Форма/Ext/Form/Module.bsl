
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691			
КонецПроцедуры

&НаСервереБезКонтекста
&ИзменениеИКонтроль("ПолучитьЗаявления")
Функция ХМПолучитьЗаявления(Отбор)
	Запрос = Новый Запрос;
#Область ПараметрыЗапроса	
	Запрос.УстановитьПараметр("Организация",   Отбор.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Отбор.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",  ?(ЗначениеЗаполнено(Отбор.КонецПериода), КонецДня(Отбор.КонецПериода), КонецДня(ТекущаяДата())));
	Запрос.УстановитьПараметр("БезРешений",    ?(Отбор.БезРешений = Неопределено, Истина, Отбор.БезРешений));	
	Запрос.УстановитьПараметр("ВключитьСВ",    Отбор.ТипЗаявления = 0 ИЛИ Отбор.ТипЗаявления = 3);
	Запрос.УстановитьПараметр("ВключитьНЧТП",  Отбор.ТипЗаявления = 1 ИЛИ Отбор.ТипЗаявления = 3);
	Запрос.УстановитьПараметр("ВключитьЕВ",    Отбор.ТипЗаявления = 2 ИЛИ Отбор.ТипЗаявления = 3);
	Запрос.УстановитьПараметр("БезЗапросов",   Отбор.ОтборПоЗапросам = 0 ИЛИ Отбор.ОтборПоЗапросам = 2);
	Запрос.УстановитьПараметр("Просроченные",  Отбор.ОтборПоЗапросам = 1 ИЛИ Отбор.ОтборПоЗапросам = 2);	
#КонецОбласти	
#Область ТекстЗапроса
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостояниеЗапросаВОПФРСрезПоследних.ЗапросВОПФР КАК ЗапросВОПФР,
		|	уп_СостояниеЗапросаВОПФРСрезПоследних.Регистратор КАК Заявление,
		|	уп_СостояниеЗапросаВОПФРСрезПоследних.Период КАК Период,
		|	уп_СостояниеЗапросаВОПФРСрезПоследних_2.Состояние КАК Состояние
		|ПОМЕСТИТЬ ВТ_Запросы_ОбщийПул
		|ИЗ
		|	РегистрСведений.уп_СостояниеЗапросаВОПФР.СрезПоследних(
		|			,
		|			Регистратор ССЫЛКА Документ.уп_ЗаявлениеЗЛОЕдиновременнойВыплате
		|				ИЛИ Регистратор ССЫЛКА Документ.уп_ЗаявлениеЗЛОНазначенииНЧТП
		|				ИЛИ Регистратор ССЫЛКА Документ.уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты
		|				ИЛИ Регистратор ССЫЛКА Документ.уп_ЗапросВОПФР) КАК уп_СостояниеЗапросаВОПФРСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеЗапросаВОПФР.СрезПоследних(, ) КАК уп_СостояниеЗапросаВОПФРСрезПоследних_2
		|		ПО уп_СостояниеЗапросаВОПФРСрезПоследних.ЗапросВОПФР = уп_СостояниеЗапросаВОПФРСрезПоследних_2.ЗапросВОПФР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запросы_ОбщийПул.Заявление КАК Заявление,
		|	МАКСИМУМ(ВТ_Запросы_ОбщийПул.Период) КАК Период
		|ПОМЕСТИТЬ ВТ_МаксДатыЗапросов
		|ИЗ
		|	ВТ_Запросы_ОбщийПул КАК ВТ_Запросы_ОбщийПул
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Запросы_ОбщийПул.Заявление
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запросы_ОбщийПул.ЗапросВОПФР КАК ЗапросВОПФР,
		|	ВТ_Запросы_ОбщийПул.Заявление КАК Заявление,
		|	ВТ_Запросы_ОбщийПул.Состояние КАК Состояние
		|ПОМЕСТИТЬ ВТ_Запросы
		|ИЗ
		|	ВТ_Запросы_ОбщийПул КАК ВТ_Запросы_ОбщийПул
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МаксДатыЗапросов КАК ВТ_МаксДатыЗапросов
		|		ПО ВТ_Запросы_ОбщийПул.Заявление = ВТ_МаксДатыЗапросов.Заявление
		|			И ВТ_Запросы_ОбщийПул.Период = ВТ_МаксДатыЗапросов.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Ссылка КАК Заявление,
		|	НЕ уп_РешениеОбОтказеВЕдиновременнойВыплате.Ссылка ЕСТЬ NULL
		|		ИЛИ НЕ уп_РешениеОЕдиновременнойВыплате.Ссылка ЕСТЬ NULL КАК ЕстьРешение,
		|	ВТ_Запросы_Просроченные.ЗапросВОПФР КАК ПросроченныйЗапрос,
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Дата КАК Дата,
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Организация КАК Организация,
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.ДоговорОПС КАК ДоговорОПС,
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.ДоговорОПС.Участник КАК ЗастрахованноеЛицо,
		|	уп_ЗаявлениеЗЛОЕдиновременнойВыплате.ДоговорОПС.Участник.уп_ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТ_Заявления_ОбщийПул
		|ИЗ
		|	Документ.уп_ЗаявлениеЗЛОЕдиновременнойВыплате КАК уп_ЗаявлениеЗЛОЕдиновременнойВыплате
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОбОтказеВЕдиновременнойВыплате КАК уп_РешениеОбОтказеВЕдиновременнойВыплате
		|		ПО уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Ссылка = уп_РешениеОбОтказеВЕдиновременнойВыплате.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОбОтказеВЕдиновременнойВыплате.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОЕдиновременнойВыплате КАК уп_РешениеОЕдиновременнойВыплате
		|		ПО уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Ссылка = уп_РешениеОЕдиновременнойВыплате.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОЕдиновременнойВыплате.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы_Просроченные
		|		ПО уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Ссылка = ВТ_Запросы_Просроченные.Заявление
		|			И (ЗНАЧЕНИЕ(Перечисление.уп_СостоянияЗапросаВОПФР.Просрочен) = ВТ_Запросы_Просроченные.Состояние)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы
		|		ПО уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Ссылка = ВТ_Запросы.Заявление
		|ГДЕ
		|	&ВключитьЕВ
		|	И уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Организация = &Организация
	#Удаление
		|	И уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	#КонецУдаления
	#Вставка
	// +ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691		
	|	И уп_ЗаявлениеЗЛОЕдиновременнойВыплате.ДатаРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	// -ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691
	#КонецВставки
		|	И (&Просроченные
		|				И НЕ ВТ_Запросы_Просроченные.Заявление ЕСТЬ NULL
		|			ИЛИ &БезЗапросов
		|				И ВТ_Запросы.Заявление ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.Ссылка,
		|	НЕ уп_РешениеОбОтказеНЧТП.Ссылка ЕСТЬ NULL
		|		ИЛИ НЕ уп_РешениеОНазначенииНЧТП.Ссылка ЕСТЬ NULL,
		|	ВТ_Запросы_Просроченные.ЗапросВОПФР,
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.Дата,
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.Организация,
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.ДоговорОПС,
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.ДоговорОПС.Участник,
		|	уп_ЗаявлениеЗЛОНазначенииНЧТП.ДоговорОПС.Участник.уп_ФизЛицо
		|ИЗ
		|	Документ.уп_ЗаявлениеЗЛОНазначенииНЧТП КАК уп_ЗаявлениеЗЛОНазначенииНЧТП
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОбОтказеНЧТП КАК уп_РешениеОбОтказеНЧТП
		|		ПО уп_ЗаявлениеЗЛОНазначенииНЧТП.Ссылка = уп_РешениеОбОтказеНЧТП.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОбОтказеНЧТП.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОНазначенииНЧТП КАК уп_РешениеОНазначенииНЧТП
		|		ПО уп_ЗаявлениеЗЛОНазначенииНЧТП.Ссылка = уп_РешениеОНазначенииНЧТП.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОНазначенииНЧТП.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы_Просроченные
		|		ПО уп_ЗаявлениеЗЛОНазначенииНЧТП.Ссылка = ВТ_Запросы_Просроченные.Заявление
		|			И (ЗНАЧЕНИЕ(Перечисление.уп_СостоянияЗапросаВОПФР.Просрочен) = ВТ_Запросы_Просроченные.Состояние)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы
		|		ПО уп_ЗаявлениеЗЛОНазначенииНЧТП.Ссылка = ВТ_Запросы.Заявление
		|ГДЕ
		|	&ВключитьНЧТП
		|	И уп_ЗаявлениеЗЛОНазначенииНЧТП.Организация = &Организация
	#Удаление
		|	И уп_ЗаявлениеЗЛОНазначенииНЧТП.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	#КонецУдаления
	#Вставка
	// +ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691		
	|	И уп_ЗаявлениеЗЛОНазначенииНЧТП.ДатаРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	// -ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691
	#КонецВставки
		|	И (&Просроченные
		|				И НЕ ВТ_Запросы_Просроченные.Заявление ЕСТЬ NULL
		|			ИЛИ &БезЗапросов
		|				И ВТ_Запросы.Заявление ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Ссылка,
		|	НЕ уп_РешениеОбОтказеСВ.Ссылка ЕСТЬ NULL
		|		ИЛИ НЕ уп_РешениеОНазначенииСрочнойВыплаты.Ссылка ЕСТЬ NULL,
		|	ВТ_Запросы_Просроченные.ЗапросВОПФР,
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Дата,
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Организация,
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.ДоговорОПС,
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.ДоговорОПС.Участник,
		|	уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.ДоговорОПС.Участник.уп_ФизЛицо
		|ИЗ
		|	Документ.уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты КАК уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОбОтказеСВ КАК уп_РешениеОбОтказеСВ
		|		ПО уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Ссылка = уп_РешениеОбОтказеСВ.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОбОтказеСВ.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_РешениеОНазначенииСрочнойВыплаты КАК уп_РешениеОНазначенииСрочнойВыплаты
		|		ПО уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Ссылка = уп_РешениеОНазначенииСрочнойВыплаты.ДокументОснование
		|			И (ЛОЖЬ = уп_РешениеОНазначенииСрочнойВыплаты.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы_Просроченные
		|		ПО уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Ссылка = ВТ_Запросы_Просроченные.Заявление
		|			И (ЗНАЧЕНИЕ(Перечисление.уп_СостоянияЗапросаВОПФР.Просрочен) = ВТ_Запросы_Просроченные.Состояние)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Запросы КАК ВТ_Запросы
		|		ПО уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Ссылка = ВТ_Запросы.Заявление
		|ГДЕ
		|	&ВключитьСВ
		|	И уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Организация = &Организация
	#Удаление
		|	И уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	#КонецУдаления
	#Вставка
	// +ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691		
	|	И уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.ДатаРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
	// -ХМНПФ 19.02.2020 МилевскийАЮ // ПолучитьЗаявления() // №2691
	#КонецВставки
		|	И (&Просроченные
		|				И НЕ ВТ_Запросы_Просроченные.Заявление ЕСТЬ NULL
		|			ИЛИ &БезЗапросов
		|				И ВТ_Запросы.Заявление ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Заявления_ОбщийПул.Заявление КАК Заявление,
		|	ВТ_Заявления_ОбщийПул.ПросроченныйЗапрос КАК ПросроченныйЗапрос,
		|	МАКСИМУМ(ВТ_Заявления_ОбщийПул.ЕстьРешение) КАК ЕстьРешение,
		|	ВТ_Заявления_ОбщийПул.Дата КАК Дата,
		|	ВТ_Заявления_ОбщийПул.Организация КАК Организация,
		|	ВТ_Заявления_ОбщийПул.ДоговорОПС КАК ДоговорОПС,
		|	ВТ_Заявления_ОбщийПул.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
		|	ВТ_Заявления_ОбщийПул.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	ВТ_Заявления_ОбщийПул КАК ВТ_Заявления_ОбщийПул
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Заявления_ОбщийПул.Заявление,
		|	ВТ_Заявления_ОбщийПул.ПросроченныйЗапрос,
		|	ВТ_Заявления_ОбщийПул.Дата,
		|	ВТ_Заявления_ОбщийПул.Организация,
		|	ВТ_Заявления_ОбщийПул.ДоговорОПС,
		|	ВТ_Заявления_ОбщийПул.ЗастрахованноеЛицо,
		|	ВТ_Заявления_ОбщийПул.ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.Заявление КАК Заявление,
		|	ВТ_Данные.Дата КАК Дата,
		|	ВТ_Данные.Организация КАК Организация,
		|	ВТ_Данные.ДоговорОПС КАК ДоговорОПС,
		|	ВТ_Данные.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
		|	ВТ_Данные.ФизЛицо КАК ФизЛицо,
		|	ЛОЖЬ КАК Просрочен
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|ГДЕ
		|	ВТ_Данные.ПросроченныйЗапрос ЕСТЬ NULL
		|	И (&БезРешений
		|				И НЕ ВТ_Данные.ЕстьРешение
		|			ИЛИ НЕ &БезРешений)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Данные.Заявление КАК Заявление,
		|	ВТ_Данные.ПросроченныйЗапрос.ОПФР КАК ОПФР,
		|	ВТ_Данные.Дата КАК Дата,
		|	ВТ_Данные.Организация КАК Организация,
		|	ВТ_Данные.ДоговорОПС КАК ДоговорОПС,
		|	ВТ_Данные.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
		|	ВТ_Данные.ФизЛицо КАК ФизЛицо,
		|	ИСТИНА КАК Просрочен
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|ГДЕ
		|	НЕ ВТ_Данные.ПросроченныйЗапрос ЕСТЬ NULL
		|	И (&БезРешений
		|				И НЕ ВТ_Данные.ЕстьРешение
		|			ИЛИ НЕ &БезРешений)";
	
	Если НЕ ЗначениеЗаполнено(Отбор.Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И уп_ЗаявлениеЗЛОЕдиновременнойВыплате.Организация = &Организация", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И уп_ЗаявлениеЗЛОНазначенииНЧТП.Организация = &Организация", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты.Организация = &Организация", "");
	КонецЕсли;
#КонецОбласти

	ПакетДанных = Запрос.ВыполнитьПакет();
	
	Данные = Новый Структура;
	Данные.Вставить("БезЗапросов", ПакетДанных[5].Выгрузить());
	Данные.Вставить("Просроченные", ПакетДанных[6].Выгрузить());
	
	Возврат Данные;	
КонецФункции
