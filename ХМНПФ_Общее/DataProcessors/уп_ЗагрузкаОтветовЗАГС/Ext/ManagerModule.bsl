
&ИзменениеИКонтроль("ЗагрузитьОтветы")
Функция ХМЗагрузитьОтветы(СтруктураПараметров, ПриЗагрузкеВозниклиОшибки)
	РезультатЗагрузки = НовыйРезультатЗагрузки();

	ВыборкаОрганизаций = СтруктураПараметров.ТаблицаСервиса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаОрганизаций.Следующий() Цикл
		//получим настройки обмена
		НастройкиОбмена = уп_ЕГРЗАГССервер.ПолучитьНастройкиОбмена(ВыборкаОрганизаций.Организация);

		Если НастройкиОбмена.ПорядокОбмена = Перечисления.уп_ПорядокПолученияСведенийОСмертиВЕГРЗАГС.СервисАгредатор Тогда 

			Ошибки = уп_ЕГРЗАГССервер.ПроверитьНастройкиАгредатор(НастройкиОбмена); 
			Если Ошибки.Количество() > 0 Тогда
				ПриЗагрузкеВозниклиОшибки = Истина;
				РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиПодключения";
				Для Каждого Ошибка Из Ошибки Цикл
					РезультатЗагрузки.СообщенияПользователю.Добавить(Ошибка);
				КонецЦикла; 
				Продолжить;
			КонецЕсли;

			Попытка   
				HTTPСоединение = РегистрыСведений.уп_НастройкиОбменаСЕГРЗАГС.СоздатьHTTPСоединение(НастройкиОбмена);
			Исключение
				РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиПодключения";
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации("Ошибка подключения к сервису ", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
				РезультатЗагрузки.СообщенияПользователю.Добавить("Ошибка подключения к серверу: "+ПодробноеПредставлениеОшибки);
				ПриЗагрузкеВозниклиОшибки = Истина;
				Продолжить; 
			КонецПопытки;

			СервисПолученияДанных = СокрЛП(НастройкиОбмена.СервисПередачиДанных);
			Если НЕ Лев(СервисПолученияДанных, 1) = "/" Тогда
				СервисПолученияДанных = "/" + СервисПолученияДанных;
			КонецЕсли;	 
			Если Не Прав(СервисПолученияДанных, 1) = "/" Тогда
				СервисПолученияДанных = СервисПолученияДанных + "/";
			КонецЕсли;

			КонтрагентЗапрос = ВыборкаОрганизаций.Выбрать(); 
			ИДЗапросов = Новый Массив;

			Данные = Новый ТаблицаЗначений;
			Данные.Колонки.Добавить("ДокументЗапрос");
			Данные.Колонки.Добавить("Контрагент");
			Данные.Колонки.Добавить("ИДЗапроса");
			Данные.Колонки.Добавить("ТипЗапроса");
			Данные.Колонки.Добавить("ТипАГС");
			Данные.Колонки.Добавить("ИДЗапросаВСервисе");
			Данные.Колонки.Добавить("Организация");
			Данные.Колонки.Добавить("Фамилия");
			Данные.Колонки.Добавить("Имя");
			Данные.Колонки.Добавить("Отчество");
			Данные.Колонки.Добавить("ДатаРождения");
			Данные.Колонки.Добавить("ВидДУЛКодМВД");
			Данные.Колонки.Добавить("СерияДУЛ");
			Данные.Колонки.Добавить("НомерДУЛ");
			Данные.Колонки.Добавить("Адрес");

			Пока КонтрагентЗапрос.Следующий() Цикл
				Если ИДЗапросов.Найти(КонтрагентЗапрос.ИДЗапросаВСервисе) = Неопределено Тогда
					ИДЗапросов.Добавить(КонтрагентЗапрос.ИДЗапросаВСервисе);
				КонецЕсли;
				НоваяСтрокаДанных = Данные.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаДанных, КонтрагентЗапрос);
			КонецЦикла;

			Для Каждого ИДЗапросаВСервисе из ИДЗапросов Цикл

				ИДЗапросаВСервисе = СтрЗаменить(ИДЗапросаВСервисе, Символ(160), ""); 

				СтрокаПодключения = СервисПолученияДанных + ИДЗапросаВСервисе;

				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("json");
				HTTPЗапрос = Новый HTTPЗапрос(СтрокаПодключения);
				HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос, ИмяВременногоФайла);

				Если HTTPОтвет.КодСостояния = 403 Тогда
					РезультатЗагрузки.СообщенияПользователю.Добавить("Сервис недоступен: forbidden 403");
					Возврат РезультатЗагрузки;	
				КонецЕсли;

				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.ОткрытьФайл(ИмяВременногоФайла);
				СтруктураОтвета = уп_ЕГРЗАГССервер.ЗаполнитьСтруктуруИзОтветаJSON(ЧтениеJSON);	
				ЧтениеJSON.Закрыть();

				Если СтруктураОтвета.status = 200 Или СтруктураОтвета.status = 201 Тогда

					Если Не СтруктураОтвета.response.ags_type = "07" Тогда
						РезультатЗагрузки.СообщенияПользователю.Добавить(
						"По ИД запроса в сервисе " + ИДЗапросаВСервисе + " формат не поддерживается."); 
						Продолжить;
					КонецЕсли;     

					Для Каждого ДокОтвет Из СтруктураОтвета.response.ags_responses Цикл  

						Отбор = Новый Структура;
						Отбор.Вставить("ИДЗапроса", ДокОтвет.doc_id); 
						НайденныеСтроки = Данные.НайтиСтроки(Отбор);
						Если НайденныеСтроки.Количество() = 0 Тогда
							Продолжить;
						КонецЕсли;                   

						КонтрагентЗапрос = НайденныеСтроки[0];
						ДанныеОтвета = ПолучитьОписаниеОтветаАгредатор(ДокОтвет);

						Если ДанныеОтвета.Статус = Перечисления.уп_СтатусыЗапросовВОчередиЗАГС.Ошибка Тогда

							ЗаписьСостояние = РегистрыСведений.уп_СостоянияЗапросовЗАГС.СоздатьМенеджерЗаписи();
							ЗаполнитьЗначенияСвойств(ЗаписьСостояние, КонтрагентЗапрос); 
							ЗаписьСостояние.Состояние   = Перечисления.уп_СостоянияЗапросовЗАГС.ОшибкаПолученияИнформации;
							ЗаписьСостояние.ТекстОшибки = ДанныеОтвета.ОписаниеОшибки;
							ЗаписьСостояние.Период      = ТекущаяДата();	
							ЗаписьСостояние.Записать(Истина); 
							Продолжить;

						КонецЕсли;

						ОтветЗАГС = Документы.уп_ОтветЗАГС.СоздатьДокумент();
						ОтветЗАГС.Дата = ТекущаяДата();
						ЗаполнитьЗначенияСвойств(ОтветЗАГС, КонтрагентЗапрос);
						ОтветЗАГС.ДатаСмерти    = ДанныеОтвета.ДатаСмерти;
						ОтветЗАГС.ЗначенияПолей = ДанныеОтвета.ЗначенияПолей;
						ОтветЗАГС.НетДанных     = ДанныеОтвета.НетДанных;
						ОтветЗАГС.Ответственный = Пользователи.ТекущийПользователь();   

						Если ЗначениеЗаполнено(ОтветЗАГС.ДатаСмерти) Тогда //данные свидельства
							ДанныеСв = ДанныеОтвета.ДанныеСв; 
							ДопДанныеПФ = ДанныеОтвета.ДопДанныеПФ; 
							ЗаполнитьЗначенияСвойств(ОтветЗАГС, ДанныеСв); 
							ЗаполнитьЗначенияСвойств(ОтветЗАГС, ДопДанныеПФ);
						КонецЕсли;

						Если ЗначениеЗаполнено(ОтветЗАГС.ДатаСмерти) И НастройкиОбмена.ИспользоватьАвтоидентификацию Тогда

							СовпадениеФИО = Ложь;
							СовпадениеДР = Ложь;
							СовпадениеПаспорта = Ложь;

							СтрокаЗапроса = КонтрагентЗапрос.ДокументЗапрос.СписокКонтрагентов.Найти(КонтрагентЗапрос.Контрагент);

							НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
							НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ФИО;
							НовСтр.ДанныеЗАГС = ДанныеОтвета.ФИО;
							НовСтр.ДанныеКонтрагента = СокрЛП(СтрокаЗапроса.Фамилия+" "+СтрокаЗапроса.Имя+" "+СтрокаЗапроса.Отчество);
							Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
								ВРЕГ(НовСтр.ДанныеЗАГС) = ВРЕГ(НовСтр.ДанныеКонтрагента) Тогда
								НовСтр.Совпадение = Истина;
								СовпадениеФИО = Истина;
							КонецЕсли;

							НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
							НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ДатаРождения;
							НовСтр.ДанныеЗАГС = ДанныеОтвета.ДатаРождения;
							НовСтр.ДанныеКонтрагента = Формат(СтрокаЗапроса.ДатаРождения, "ДФ=dd.MM.yyyy");
							Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
								НовСтр.ДанныеЗАГС = НовСтр.ДанныеКонтрагента Тогда
								НовСтр.Совпадение = Истина;
								СовпадениеДР = Истина;
							КонецЕсли;	

							НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
							НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ПаспортныеДанные;
							НовСтр.ДанныеЗАГС = ДанныеОтвета.ДанныеДок;
							НовСтр.ДанныеКонтрагента = СокрЛП(СтрокаЗапроса.ВидДУЛ.КодМВД+", "+СтрокаЗапроса.СерияДУЛ+" "+СтрокаЗапроса.НомерДУЛ);
							Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
								СтрЗаменить(НовСтр.ДанныеЗАГС, " ", "") = СтрЗаменить(НовСтр.ДанныеКонтрагента, " ", "") Тогда
								НовСтр.Совпадение = Истина;
								СовпадениеПаспорта = Истина;
							КонецЕсли;

							НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
							НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.Адрес;
							НовСтр.ДанныеЗАГС = ДанныеОтвета.Адрес;
							СтрокаКонтрактнойИнформации = КонтрагентЗапрос.Контрагент.уп_ФизЛицо.КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица, "Вид");
							Если СтрокаКонтрактнойИнформации <> Неопределено Тогда
								НовСтр.ДанныеКонтрагента = СтрокаКонтрактнойИнформации.Представление;
							КонецЕсли;
							Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
								ВРЕГ(НовСтр.ДанныеЗАГС) = ВРЕГ(НовСтр.ДанныеКонтрагента) Тогда
								НовСтр.Совпадение = Истина;
							КонецЕсли;

							Если СовпадениеФИО И СовпадениеДР И СовпадениеПаспорта Тогда
								ОтветЗАГС.СтатусКонтрагента = 1;
							КонецЕсли;
						КонецЕсли;

						ОтветЗАГС.Записать();

					КонецЦикла; 

				ИначеЕсли СтруктураОтвета.status = 100 Тогда
					РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет статус: ожидает отправки.");
				ИначеЕсли СтруктураОтвета.status = 102 Тогда
					РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет статус: отправлен.");
				Иначе

					Если СтруктураОтвета.Свойство("info") Тогда
						ОписаниеОшибки = "status: " + СтруктураОтвета.status + ", info: " + СтруктураОтвета.info;
					ИначеЕсли СтруктураОтвета.Свойство("title") Тогда
						ОписаниеОшибки = "status: " + СтруктураОтвета.status + ", title: " + СтруктураОтвета.title;
					Иначе
						ОписаниеОшибки = "status: " + СтруктураОтвета.status;
					КонецЕсли;

					РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет ошибку:" + ОписаниеОшибки);

					Отбор = Новый Структура;
					Отбор.Вставить("ИДЗапросаВСервисе", ИДЗапросаВСервисе); 
					НайденныеСтроки = Данные.НайтиСтроки(Отбор);

					Для Каждого КонтрагентЗапрос Из НайденныеСтроки Цикл
						ПараметрыЗаписи = Новый Структура;
						ПараметрыЗаписи.Вставить("ДокументЗапрос", КонтрагентЗапрос.ДокументЗапрос);
						ПараметрыЗаписи.Вставить("Контрагент", КонтрагентЗапрос.Контрагент);
						ПараметрыЗаписи.Вставить("Состояние", Перечисления.уп_СостоянияЗапросовЗАГС.ОшибкаПолученияИнформации);
						ПараметрыЗаписи.Вставить("ИДЗапроса", КонтрагентЗапрос.ИДЗапроса);
						ПараметрыЗаписи.Вставить("ИДЗапросаВСервисе", КонтрагентЗапрос.ИДЗапросаВСервисе);
						ПараметрыЗаписи.Вставить("ТипЗапроса", КонтрагентЗапрос.ТипЗапроса);
						ПараметрыЗаписи.Вставить("ТипАГС", КонтрагентЗапрос.ТипАГС);
						ПараметрыЗаписи.Вставить("ТекстОшибки", ОписаниеОшибки);

						ЗаписатьДанныеВРегистр(ПараметрыЗаписи);
					КонецЦикла;

				КонецЕсли;

			КонецЦикла;

			Если НЕ РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиПодключения" Тогда
				РезультатЗагрузки.РезультатВыполнения = "УспешнаяЗагрузка";
			КонецЕсли;

		ИначеЕсли НастройкиОбмена.ПорядокОбмена = Перечисления.уп_ПорядокПолученияСведенийОСмертиВЕГРЗАГС.СервисХИквадрат Тогда 

			Если НЕ ЗначениеЗаполнено(НастройкиОбмена.HOST)
				ИЛИ НЕ ЗначениеЗаполнено(НастройкиОбмена.PORT)
				ИЛИ НЕ ЗначениеЗаполнено(НастройкиОбмена.СервисПолученияДанных)
				ИЛИ НЕ ЗначениеЗаполнено(НастройкиОбмена.APIKEY) Тогда

				РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиПодключения";
				РезультатЗагрузки.СообщенияПользователю.Добавить("Для организации "+ВыборкаОрганизаций.Организация+ "Не заполнены настройки подключения к сервису получения данных.");
				ПриЗагрузкеВозниклиОшибки = Истина;

				Продолжить;

			КонецЕсли;

			HTTPСоединение = РегистрыСведений.уп_НастройкиОбменаСЕГРЗАГС.СоздатьHTTPСоединение(НастройкиОбмена);
			СервисПолученияДанных = СокрЛП(НастройкиОбмена.СервисПолученияДанных);
			Если Не Лев(СервисПолученияДанных, 1) = "/" Тогда
				СервисПолученияДанных = "/" + СервисПолученияДанных;
			КонецЕсли;	

			КонтрагентЗапрос = ВыборкаОрганизаций.Выбрать(); 
			ИДЗапросов = Новый Массив;

			Данные = Новый ТаблицаЗначений;
			Данные.Колонки.Добавить("ДокументЗапрос");
			Данные.Колонки.Добавить("Контрагент");
			Данные.Колонки.Добавить("ИДЗапроса");
			Данные.Колонки.Добавить("ТипЗапроса");
			Данные.Колонки.Добавить("ТипАГС");
			Данные.Колонки.Добавить("ИДЗапросаВСервисе");
			Данные.Колонки.Добавить("Организация");
			Данные.Колонки.Добавить("Фамилия");
			Данные.Колонки.Добавить("Имя");
			Данные.Колонки.Добавить("Отчество");
			Данные.Колонки.Добавить("ДатаРождения");
			Данные.Колонки.Добавить("ВидДУЛКодМВД");
			Данные.Колонки.Добавить("СерияДУЛ");
			Данные.Колонки.Добавить("НомерДУЛ");
			Данные.Колонки.Добавить("Адрес");

			Пока КонтрагентЗапрос.Следующий() Цикл
				Если ИДЗапросов.Найти(КонтрагентЗапрос.ИДЗапросаВСервисе) = Неопределено Тогда
					ИДЗапросов.Добавить(КонтрагентЗапрос.ИДЗапросаВСервисе);
				КонецЕсли;
				НоваяСтрокаДанных = Данные.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаДанных, КонтрагентЗапрос);
			КонецЦикла;

			Для Каждого ИДЗапросаВСервисе из ИДЗапросов Цикл

				ИДЗапросаВСервисе = СтрЗаменить(ИДЗапросаВСервисе, Символ(160), ""); 

				СтрокаПодключения = СервисПолученияДанных + "?apiKey=" + НастройкиОбмена.APIKEY + "&id=" + ИДЗапросаВСервисе;

				ЗаголовокHTTP = Новый Соответствие();
				ЗаголовокHTTP.Вставить("Content-Type", "application/json; charset=utf-8");
				ЗаголовокHTTP.Вставить("X-API-KEY", НастройкиОбмена.APIKEY);

				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("json");
				HTTPЗапрос = Новый HTTPЗапрос(СтрокаПодключения);
				HTTPЗапрос.Заголовки = ЗаголовокHTTP;
				HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос, ИмяВременногоФайла);

				Если HTTPОтвет.КодСостояния = 403 Тогда
					РезультатЗагрузки.СообщенияПользователю.Добавить("Сервис недоступен: forbidden 403");
					Возврат РезультатЗагрузки;	
				КонецЕсли;

				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.ОткрытьФайл(ИмяВременногоФайла);
				СтруктураОтвета = уп_ЕГРЗАГССервер.ЗаполнитьСтруктуруИзОтветаJSON(ЧтениеJSON);	
				ЧтениеJSON.Закрыть();

				Если СтруктураОтвета.success Тогда
					Если СтруктураОтвета.data.status = "ANSWERED" Тогда

						Для Каждого ДокОтвет Из СтруктураОтвета.data.response Цикл 

							Отбор = Новый Структура;
							Отбор.Вставить("ИДЗапроса", ДокОтвет.id); 
							НайденныеСтроки = Данные.НайтиСтроки(Отбор);
							Если НайденныеСтроки.Количество() = 0 Тогда
								Продолжить;
							КонецЕсли;                   

							КонтрагентЗапрос = НайденныеСтроки[0];
							ДанныеОтвета = ПолучитьОписаниеОтветаХиКвадрат(ДокОтвет);

							//надо создать документ
							ОтветЗАГС = Документы.уп_ОтветЗАГС.СоздатьДокумент();
							ОтветЗАГС.Дата = ТекущаяДата();
							ЗаполнитьЗначенияСвойств(ОтветЗАГС, КонтрагентЗапрос);
							ОтветЗАГС.Ответственный = Пользователи.ТекущийПользователь();
							ОтветЗАГС.ЗначенияПолей = ДанныеОтвета["ЗначенияПолей"];

							Если ЗначениеЗаполнено(ДанныеОтвета["ДатаСмерти"]) Тогда

								ОтветЗАГС.ДатаСмерти = ДанныеОтвета["ДатаСмерти"];

								Если ЗначениеЗаполнено(ОтветЗАГС.ДатаСмерти) Тогда //данные свидельства
									ДанныеСв = ДанныеОтвета.ДанныеСв;  
									ДопДанныеПФ = ДанныеОтвета.ДопДанныеПФ; 
									ЗаполнитьЗначенияСвойств(ОтветЗАГС, ДанныеСв);
									ЗаполнитьЗначенияСвойств(ОтветЗАГС, ДопДанныеПФ);
								КонецЕсли;

								Если НастройкиОбмена.ИспользоватьАвтоидентификацию Тогда

									СовпадениеФИО = Ложь;
									СовпадениеДР = Ложь;
									СовпадениеПаспорта = Ложь;

									НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
									НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ФИО;
									НовСтр.ДанныеЗАГС = ДанныеОтвета.ФИО;
									НовСтр.ДанныеКонтрагента = СокрЛП(КонтрагентЗапрос.Фамилия+" "+КонтрагентЗапрос.Имя+" "+КонтрагентЗапрос.Отчество);
									Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
										ВРЕГ(НовСтр.ДанныеЗАГС) = ВРЕГ(НовСтр.ДанныеКонтрагента) Тогда
										НовСтр.Совпадение = Истина;
										СовпадениеФИО = Истина;
									КонецЕсли; 

									НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
									НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ДатаРождения;
									НовСтр.ДанныеЗАГС = ДанныеОтвета.ДатаРождения;
									НовСтр.ДанныеКонтрагента = Формат(КонтрагентЗапрос.ДатаРождения,"ДФ=dd.MM.yyyy");
									Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
										НовСтр.ДанныеЗАГС = НовСтр.ДанныеКонтрагента Тогда
										НовСтр.Совпадение = Истина;
										СовпадениеДР = Истина;
									КонецЕсли;	

									НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
									НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.ПаспортныеДанные;
									НовСтр.ДанныеЗАГС = ДанныеОтвета.ДанныеДок;
#Вставка                    
//https://redmine.npf.com/issues/4470
									Если НЕ КонтрагентЗапрос.ВидДУЛКодМВД = Null Тогда
#КонецВставки
									НовСтр.ДанныеКонтрагента = СокрЛП(КонтрагентЗапрос.ВидДУЛКодМВД+", "+КонтрагентЗапрос.СерияДУЛ+" "+КонтрагентЗапрос.НомерДУЛ);
									Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
										НовСтр.ДанныеЗАГС = НовСтр.ДанныеКонтрагента Тогда
										НовСтр.Совпадение = Истина;
										СовпадениеПаспорта = Истина;
									КонецЕсли;
#Вставка                    
									КонецЕсли;
#КонецВставки

									НовСтр = ОтветЗАГС.ПоляИдентификации.Добавить();
									НовСтр.ПолеИдентификации = Справочники.уп_ПоляИдентификацииКонтрагента.Адрес;
									НовСтр.ДанныеЗАГС = ДанныеОтвета.Адрес;
									НовСтр.ДанныеКонтрагента = КонтрагентЗапрос.Адрес;
									Если ЗначениеЗаполнено(НовСтр.ДанныеЗАГС) И ЗначениеЗаполнено(НовСтр.ДанныеКонтрагента) И
										ВРЕГ(НовСтр.ДанныеЗАГС) = ВРЕГ(НовСтр.ДанныеКонтрагента) Тогда
										НовСтр.Совпадение = Истина;
									КонецЕсли;

									Если СовпадениеФИО И СовпадениеДР И СовпадениеПаспорта Тогда
										ОтветЗАГС.СтатусКонтрагента = 1;
									КонецЕсли;	
								КонецЕсли;	
							КонецЕсли;	

							ОтветЗАГС.НетДанных = ДанныеОтвета["НетДанных"];

							ОтветЗАГС.Записать();

						КонецЦикла;     

					ИначеЕсли СтруктураОтвета.data.status = "PUT" Тогда
						РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет статус: ожидает отправки.");
					ИначеЕсли СтруктураОтвета.data.status = "SENT" Тогда
						РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет статус: отправлен.");

					ИначеЕсли СтруктураОтвета.data.status = "ERROR" Тогда                      

						РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет статус: ошибка.");

						Отбор = Новый Структура;
						Отбор.Вставить("ИДЗапросаВСервисе", ИДЗапросаВСервисе); 
						НайденныеСтроки = Данные.НайтиСтроки(Отбор);

						Для Каждого КонтрагентЗапрос Из НайденныеСтроки Цикл
							ПараметрыЗаписи = Новый Структура;
							ПараметрыЗаписи.Вставить("ДокументЗапрос",    КонтрагентЗапрос.ДокументЗапрос);
							ПараметрыЗаписи.Вставить("Контрагент",        КонтрагентЗапрос.Контрагент);
							ПараметрыЗаписи.Вставить("Состояние",         Перечисления.уп_СостоянияЗапросовЗАГС.ОшибкаПолученияИнформации);
							ПараметрыЗаписи.Вставить("ИДЗапроса",         КонтрагентЗапрос.ИДЗапроса);
							ПараметрыЗаписи.Вставить("ИДЗапросаВСервисе", КонтрагентЗапрос.ИДЗапросаВСервисе);
							ПараметрыЗаписи.Вставить("ТипЗапроса",        КонтрагентЗапрос.ТипЗапроса);
							ПараметрыЗаписи.Вставить("ТипАГС",            КонтрагентЗапрос.ТипАГС);
							ПараметрыЗаписи.Вставить("ТекстОшибки",       "статус: ошибка");

							ЗаписатьДанныеВРегистр(ПараметрыЗаписи);
						КонецЦикла;

					КонецЕсли;

				Иначе

					//РезультатЗагрузки.СообщенияПользователю.Добавить("По контрагенту " + КонтрагентЗапрос.Контрагент + " ошибка обращения к сервису.");
					//РезультатЗагрузки.СообщенияПользователю.Добавить(СтруктураОтвета.error);
					//РезультатЗагрузки.СообщенияПользователю.Добавить(КонтрагентЗапрос.ИДЗапроса);
					//ПриЗагрузкеВозниклиОшибки = Истина;

					//////////////////

					Если СтруктураОтвета.Свойство("error") Тогда
						ОписаниеОшибки = СтруктураОтвета.error;
					Иначе
						ОписаниеОшибки = "ошибка получения информации";
					КонецЕсли;

					РезультатЗагрузки.СообщенияПользователю.Добавить("Запрос в сервисе с номером " + ИДЗапросаВСервисе + " имеет ошибку: " + ОписаниеОшибки);

					Отбор = Новый Структура;
					Отбор.Вставить("ИДЗапросаВСервисе", ИДЗапросаВСервисе); 
					НайденныеСтроки = Данные.НайтиСтроки(Отбор);

					Для Каждого КонтрагентЗапрос Из НайденныеСтроки Цикл
						ПараметрыЗаписи = Новый Структура;
						ПараметрыЗаписи.Вставить("ДокументЗапрос",    КонтрагентЗапрос.ДокументЗапрос);
						ПараметрыЗаписи.Вставить("Контрагент",        КонтрагентЗапрос.Контрагент);
						ПараметрыЗаписи.Вставить("Состояние",         Перечисления.уп_СостоянияЗапросовЗАГС.ОшибкаПолученияИнформации);
						ПараметрыЗаписи.Вставить("ИДЗапроса",         КонтрагентЗапрос.ИДЗапроса);
						ПараметрыЗаписи.Вставить("ИДЗапросаВСервисе", КонтрагентЗапрос.ИДЗапросаВСервисе);
						ПараметрыЗаписи.Вставить("ТипЗапроса",        КонтрагентЗапрос.ТипЗапроса);
						ПараметрыЗаписи.Вставить("ТипАГС",            КонтрагентЗапрос.ТипАГС);
						ПараметрыЗаписи.Вставить("ТекстОшибки",       "статус: ошибка");

						ЗаписатьДанныеВРегистр(ПараметрыЗаписи);
					КонецЦикла;

					ПриЗагрузкеВозниклиОшибки = Истина;

				КонецЕсли;

			КонецЦикла;

			Если Не РезультатЗагрузки.РезультатВыполнения = "ЕстьОшибкиПодключения" Тогда
				РезультатЗагрузки.РезультатВыполнения = "УспешнаяЗагрузка";
			КонецЕсли;   

		КонецЕсли;
	КонецЦикла;

	Возврат РезультатЗагрузки;

КонецФункции
