
&НаСервере
&ИзменениеИКонтроль("СформироватьНаСервере")
Процедура ХМСформироватьНаСервере()
	       
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета
		|ПОМЕСТИТЬ вр_ТабВыплатныеСчета
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&Дата, ) КАК уп_СостоянияПССрезПоследних
		|ГДЕ
		|	уп_СостоянияПССрезПоследних.Состояние В(&СписокВыплатных)
		|	И уп_СостоянияПССрезПоследних.НомерСчета.Владелец.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СхемаСчетаСрезПоследних.НомерСчета КАК НомерСчета
		|ПОМЕСТИТЬ вр_ПенсСчета
		|ИЗ
		|	РегистрСведений.уп_СхемаСчета.СрезПоследних(
		|			&Дата,
		|			НомерСчета В
		|					(ВЫБРАТЬ
		|						вр_ТабВыплатныеСчета.НомерСчета КАК НомерСчета
		|					ИЗ
		|						вр_ТабВыплатныеСчета КАК вр_ТабВыплатныеСчета)
		|				И (Схема.ТипПенсионнойСхемы = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
		|					ИЛИ ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат))
		|				И Схема.ФиксированныеВыплаты) КАК уп_СхемаСчетаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
#Вставка
//++https://redmine.npf.com/issues/4274
	|ВЫБРАТЬ
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	СУММА(уп_СчетчикНачислений.КоличествоОборот) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|		уп_СчетчикНачислений.Количество КАК КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&Дата, НомерСчета В (ВЫБРАТЬ
	|						вр_ПенсСчета.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК хм_СрезСчетчик
	|			ПО уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета
	|				И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния
	|	ГДЕ
	|		уп_СчетчикНачислений.Период <= &Дата
	|	    И уп_СчетчикНачислений.НомерСчета В (ВЫБРАТЬ
	|						вр_ПенсСчета.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПенсСчета КАК вр_ПенсСчета)
	|		И хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		хм_СрезСчетчик.НомерСчета,
	|		хм_СрезСчетчик.Количество
	|	ИЗ
	|		РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&Дата, НомерСчета В (ВЫБРАТЬ
	|						вр_ПенсСчета.НомерСчета КАК НомерСчета
	|					ИЗ
	|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК хм_СрезСчетчик
	|
	|) КАК уп_СчетчикНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СчетчикНачислений.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
//--https://redmine.npf.com/issues/4274
#КонецВставки
		|ВЫБРАТЬ
		|	вр_ПенсСчета.НомерСчета КАК НомерСчета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) > 0
		|			ТОГДА (ЕСТЬNULL(уп_РезервыОстатки.СуммаОстаток, 0) - ЕСТЬNULL(уп_ПоступленияКПерерасчетуОстатки.СуммаОстаток, 0)) / (ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ЕСТЬNULL(уп_РазмерПенсииСрезПоследних.Размер, 0) КАК РазмерПенсии,
		|	вр_ПенсСчета.НомерСчета.Участник КАК Участник,
		|	вр_ПенсСчета.НомерСчета.Владелец КАК ПенсионныйДоговор
		|ИЗ
		|	вр_ПенсСчета КАК вр_ПенсСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(
		|				&Дата,
		|				НомерСчета В
		|						(ВЫБРАТЬ
		|							вр_ПенсСчета.НомерСчета КАК НомерСчета
		|						ИЗ
		|							вр_ПенсСчета КАК вр_ПенсСчета)
		|					И НЕ ТипРезерва В (&РезервыНачислений)) КАК уп_РезервыОстатки
		|		ПО вр_ПенсСчета.НомерСчета = уп_РезервыОстатки.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоНазначенныхВыплат.СрезПоследних(
		|				&Дата,
		|				НомерСчета В
		|					(ВЫБРАТЬ
		|						вр_ПенсСчета.НомерСчета КАК НомерСчета
		|					ИЗ
		|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК уп_КоличествоНазначенныхВыплатСрезПоследних
		|		ПО вр_ПенсСчета.НомерСчета = уп_КоличествоНазначенныхВыплатСрезПоследних.НомерСчета
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(
		|				,
		|				&Дата,
		|				,
		|				НомерСчета В
		|					(ВЫБРАТЬ
		|						вр_ПенсСчета.НомерСчета КАК НомерСчета
		|					ИЗ
		|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
		|		ПО вр_ПенсСчета.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсии.СрезПоследних(
		|				&Дата,
		|				НомерСчета В
		|					(ВЫБРАТЬ
		|						вр_ПенсСчета.НомерСчета КАК НомерСчета
		|					ИЗ
		|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК уп_РазмерПенсииСрезПоследних
		|		ПО вр_ПенсСчета.НомерСчета = уп_РазмерПенсииСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ПоступленияКПерерасчету.Остатки(
		|				&Дата,
		|				НомерСчета В
		|					(ВЫБРАТЬ
		|						вр_ПенсСчета.НомерСчета КАК НомерСчета
		|					ИЗ
		|						вр_ПенсСчета КАК вр_ПенсСчета)) КАК уп_ПоступленияКПерерасчетуОстатки
		|		ПО вр_ПенсСчета.НомерСчета = уп_ПоступленияКПерерасчетуОстатки.НомерСчета
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) > 0
		|				ТОГДА ЕСТЬNULL(уп_РезервыОстатки.СуммаОстаток, 0) / (ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0))
		|			ИНАЧЕ 0
		|		КОНЕЦ > ЕСТЬNULL(уп_РазмерПенсииСрезПоследних.Размер, 0)";
	
	//Если ЗначениеЗаполнено(Объект.Организация) Тогда
	//	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборОрганизация%", "И уп_СостоянияПССрезПоследних.НомерСчета.Владелец.Организация = &Организация");
	//Иначе
	//	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборОрганизация%", "");
	//КонецЕсли;
	СписокВыплатных = Новый СписокЗначений;
	СписокВыплатных.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	Если ВключатьВРасчетПриостановленныеПС Тогда 
		СписокВыплатных.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены);
	КонецЕсли;	
	Запрос.УстановитьПараметр("СписокВыплатных", СписокВыплатных);
	Запрос.УстановитьПараметр("Дата", Объект.ДатаФормированияДокументов);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	РезервыНачислений = Новый Массив;
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковИПС);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковСПС);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
	РезервыНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
	Запрос.УстановитьПараметр("РезервыНачислений", РезервыНачислений); 
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Счетчик = 0;
	КолСтрокВТаблице = 20000;
	
	ТабСчетов = Новый ТаблицаЗначений;
	ТабСчетов.Колонки.Добавить("НомерСчета");
	ТабСчетов.Колонки.Добавить("Участник");
	ТабСчетов.Колонки.Добавить("ПенсионныйДоговор");
	ТабСчетов.Колонки.Добавить("Сумма");
	
	Пока Выборка.Следующий() Цикл
		Если Счетчик = КолСтрокВТаблице Тогда
			НоваяСтрока = Объект.НовыеДокументы.Добавить();
			НоваяСтрока.Документ = СформироватьДокументИндексации(ТабСчетов);
			Счетчик = 0;
			ТабСчетов.Очистить();
		КонецЕсли;	
		Счетчик = Счетчик + 1;
		НовСтр = ТабСчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
	КонецЦикла;	
	
	Если ТабСчетов.Количество() > 0 Тогда
		НоваяСтрока = Объект.НовыеДокументы.Добавить();
		НоваяСтрока.Документ = СформироватьДокументИндексации(ТабСчетов);
	КонецЕсли;
	
КонецПроцедуры
