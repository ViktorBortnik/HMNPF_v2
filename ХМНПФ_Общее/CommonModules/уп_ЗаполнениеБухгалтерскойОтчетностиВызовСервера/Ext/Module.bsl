// ХМНПФ 27.10.2016 ШадринАВ // Новая ХМ_ПолучитьП1004_ФормаОтчета2013Кв3_ОПС5() // №614
Функция ХМ_ПолучитьП1004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения)
	Организация = КонтекстИсполнения.Организация;
	мДатаНачалаПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	мДатаКонцаПериодаОтчета = КонтекстИсполнения.КонецПериодаОтчета;
	
	СписокСостояний=Новый СписокЗначений();
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ЕдиновременнаяВыплата);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);
	// этих состояний нет в типовом варианте
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Заключен);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
	
	З = Новый Запрос();
	З.Текст =  "ВЫБРАТЬ РАЗЛИЧНЫЕ
	           |	ЗаявлениеОНаличииПравопреемников.Ссылка,
	           |	ЗаявлениеОНаличииПравопреемников.Ссылка.ДоговорОПС
	           |ИЗ
	           |	Документ.уп_ЗаявлениеОНаличииПравопреемников.Правопреемники КАК ЗаявлениеОНаличииПравопреемников
	           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК СостоянияДоговораОПС
	           |		ПО ЗаявлениеОНаличииПравопреемников.Ссылка.ДоговорОПС = СостоянияДоговораОПС.ДоговорОПС
	           |ГДЕ
	           |	ЗаявлениеОНаличииПравопреемников.Ссылка.ХМ_ДатаЗанесенияВБД МЕЖДУ &ДатаНачала И &ДатаКонца
	           |	И СостоянияДоговораОПС.Состояние В(&СписокСостояний)";
	
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("СписокСостояний",СписокСостояний);
	З.УстановитьПараметр("Организация",Организация);
	
	Выборка=З.Выполнить().Выбрать();
	Результат = Выборка.Количество();
	Возврат Результат;
КонецФункции

// ХМНПФ 24.11.2016 ШадринАВ // Новая ХМ_ПолучитьП3004_ФормаОтчета2013Кв3_ОПС5() // №676
Функция ХМ_ПолучитьП3004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения)
	мДатаНачалаПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	мДатаКонцаПериодаОтчета = КонтекстИсполнения.КонецПериодаОтчета;
	З = Новый Запрос();
	З.Текст =  "ВЫБРАТЬ
	           |	ВыплатыОПС.Период,
	           |	ВыплатыОПС.ДоговорОПС,
	           |	ВыплатыОПС.Участник,
	           |	СУММА(ВыплатыОПС.СуммаОборот) КАК СуммаВыплаты
	           |ПОМЕСТИТЬ Выплаты
	           |ИЗ
	           |	РегистрНакопления.уп_ВыплатыОПС.Обороты(
	           |			&ДатаНачала,
	           |			&ДатаКонца,
	           |			Регистратор,
	           |			ТипВыплаты = &ПравопреемникиОПС
	           |				И Участник.ЮридическоеФизическоеЛицо = &ФизЛицо) КАК ВыплатыОПС
	           |ГДЕ
	           |	ВыплатыОПС.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
	           |	И НЕ ВЫРАЗИТЬ(ВыплатыОПС.Регистратор КАК Документ.уп_ВыплатныеРеестры).ХМ_ВыплатаПоВозвратам
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	ВыплатыОПС.Период,
	           |	ВыплатыОПС.ДоговорОПС,
	           |	ВыплатыОПС.Участник
	           |;
	           |
	           |////////////////////////////////////////////////////////////////////////////////
	           |ВЫБРАТЬ
	           |	ПрочиеНачисленияОПС.Период,
	           |	ПрочиеНачисленияОПС.ДоговорОПС,
	           |	ПрочиеНачисленияОПС.Участник,
	           |	ВЫБОР
	           |		КОГДА ПрочиеНачисленияОПС.Регистратор ССЫЛКА Документ.уп_РешениеОВыплатеСредствПенсионныхНакоплений
	           |			ТОГДА ВЫРАЗИТЬ(ПрочиеНачисленияОПС.Регистратор КАК Документ.уп_РешениеОВыплатеСредствПенсионныхНакоплений).ДополнительнаяВыплата
	           |		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеНачисленияОПС.Регистратор КАК Документ.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС).ДополнительнаяВыплата
	           |	КОНЕЦ КАК ДопВыплата,
	           |	СУММА(ПрочиеНачисленияОПС.СуммаПриход) КАК СуммаНачисления
	           |ПОМЕСТИТЬ Начисление
	           |ИЗ
	           |	РегистрНакопления.уп_ПрочиеНачисленияОПС.Обороты(
	           |			&НачислениеДатаНачала,
	           |			&ДатаКонца,
	           |			Регистратор,
	           |			ТипВыплаты = &ПравопреемникиОПС
	           |				И Участник.ЮридическоеФизическоеЛицо = &ФизЛицо) КАК ПрочиеНачисленияОПС
	           |ГДЕ
	           |	(ПрочиеНачисленияОПС.Регистратор ССЫЛКА Документ.уп_РешениеОВыплатеСредствПенсионныхНакоплений
	           |			ИЛИ ПрочиеНачисленияОПС.Регистратор ССЫЛКА Документ.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС)
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	ПрочиеНачисленияОПС.Период,
	           |	ПрочиеНачисленияОПС.ДоговорОПС,
	           |	ПрочиеНачисленияОПС.Участник,
	           |	ВЫБОР
	           |		КОГДА ПрочиеНачисленияОПС.Регистратор ССЫЛКА Документ.уп_РешениеОВыплатеСредствПенсионныхНакоплений
	           |			ТОГДА ВЫРАЗИТЬ(ПрочиеНачисленияОПС.Регистратор КАК Документ.уп_РешениеОВыплатеСредствПенсионныхНакоплений).ДополнительнаяВыплата
	           |		ИНАЧЕ ВЫРАЗИТЬ(ПрочиеНачисленияОПС.Регистратор КАК Документ.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС).ДополнительнаяВыплата
	           |	КОНЕЦ
	           |;
	           |
	           |////////////////////////////////////////////////////////////////////////////////
	           |ВЫБРАТЬ
	           |	Начисление.ДоговорОПС КАК ДоговорОПС,
	           |	Начисление.Участник КАК Правопреемник,
	           |	Начисление.ДопВыплата,
	           |	Начисление.СуммаНачисления,
	           |	Выплаты.СуммаВыплаты,
	           |	1 КАК Колво
	           |ПОМЕСТИТЬ НачисленоИВыплачено
	           |ИЗ
	           |	Начисление КАК Начисление
	           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выплаты КАК Выплаты
	           |		ПО Начисление.Участник = Выплаты.Участник
	           |			И Начисление.ДоговорОПС = Выплаты.ДоговорОПС
	           |			И Начисление.Период < Выплаты.Период
	           |			И (Начисление.СуммаНачисления = Выплаты.СуммаВыплаты
	           |				ИЛИ Выплаты.ДоговорОПС.Наименование = ""0714-ПС-00512"")
	           |;
	           |
	           |////////////////////////////////////////////////////////////////////////////////
	           |ВЫБРАТЬ
	           |	НачисленоИВыплачено.ДопВыплата КАК ДопВыплата,
	           |	СУММА(НачисленоИВыплачено.Колво) КАК Колво
	           |ИЗ
	           |	НачисленоИВыплачено КАК НачисленоИВыплачено
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	НачисленоИВыплачено.ДопВыплата";
	
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("НачислениеДатаНачала",ДобавитьМесяц(мДатаНачалаПериодаОтчета,-2));
	З.УстановитьПараметр("ПравопреемникиОПС",Перечисления.уп_ТипыВыплат.ВыплатыНаследникамОПС);
	З.УстановитьПараметр("ФизЛицо",Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	
	ТЗ = З.Выполнить().Выгрузить();
	Показатели = Новый Структура;
	Для каждого стр Из ТЗ Цикл
		Если стр.ДопВыплата Тогда
			Показатели.Вставить("ДопВыплата",стр.Колво);
		Иначе
			Показатели.Вставить("Выплата",стр.Колво);
		КонецЕсли;
	КонецЦикла;
	Попытка
		Выплата = Строка(Показатели.Выплата);
	Исключение
		Выплата = "0";
	КонецПопытки;
	Попытка
		ДопВыплата = Строка(Показатели.ДопВыплата);
	Исключение
		ДопВыплата = "0";
	КонецПопытки;
	Результат = Выплата+" / "+ДопВыплата;
	Возврат Результат;
КонецФункции

// ХМНПФ 25.11.2016 ШадринАВ // Новая ХМ_ПолучитьП3303_ФормаОтчета2013Кв3_ОПС5() // №670
Функция ХМ_ПолучитьП3303_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения)
	Организация = КонтекстИсполнения.Организация;
	мДатаНачалаПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	мДатаКонцаПериодаОтчета = КонтекстИсполнения.КонецПериодаОтчета;
	
	СписокПричинЗакрытия=Новый СписокЗначений();
	СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.НеВступилВСилу);
	СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти);
	
	З = Новый Запрос();
	З.Текст =  "ВЫБРАТЬ РАЗЛИЧНЫЕ
	           |	ЗакрытиеДоговораОПС.ДоговорОПС.Участник
	           |ИЗ
	           |	Документ.уп_ЗакрытиеДоговораОПС.ДоговорыОПС КАК ЗакрытиеДоговораОПС
	           |ГДЕ
	           |	ЗакрытиеДоговораОПС.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	           |	И ЗакрытиеДоговораОПС.Ссылка.ПричинаЗакрытия В(&ПричинаЗакрытия)
	           |	И ЗакрытиеДоговораОПС.Ссылка.Проведен
	           |	И НЕ ЗакрытиеДоговораОПС.Ссылка.ХМ_СписокУмершихНеОтПФР";
	
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ПричинаЗакрытия",СписокПричинЗакрытия);
	
	Выборка=З.Выполнить().Выбрать();
	Результат = Выборка.Количество();
	Возврат Результат;
КонецФункции

// ХМНПФ 28.11.2016 ШадринАВ // Новая ХМ_ПолучитьП2004_ФормаОтчета2013Кв3_ОПС5() // №683
Функция ХМ_ПолучитьП2004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения)
	мДатаНачалаПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	мДатаКонцаПериодаОтчета = КонтекстИсполнения.КонецПериодаОтчета;
	
	З = Новый Запрос();
	З.Текст =  "ВЫБРАТЬ
	           |	ВыплатыОПС.Период,
	           |	ВыплатыОПС.ДоговорОПС,
	           |	СУММА(ВыплатыОПС.СуммаОборот) КАК СуммаОборот
	           |ПОМЕСТИТЬ ВыплатыЕДВ
	           |ИЗ
	           |	РегистрНакопления.уп_ВыплатыОПС.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ЕдиновременнаяВыплатаОПС)) КАК ВыплатыОПС
	           |ГДЕ
	           |	ВыплатыОПС.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
	           |	И НЕ ВЫРАЗИТЬ(ВыплатыОПС.Регистратор КАК Документ.уп_ВыплатныеРеестры).ХМ_ВыплатаПоВозвратам
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	ВыплатыОПС.Период,
	           |	ВыплатыОПС.ДоговорОПС
	           |;
	           |
	           |////////////////////////////////////////////////////////////////////////////////
	           |ВЫБРАТЬ
	           |	РешенияЕДВ.ДоговорОПС,
	           |	РешенияЕДВ.ДополнительнаяВыплата
	           |		ИЛИ РешенияЕДВ.ДоначислениеПоПереданномуВзносуИзПФР КАК ЕДВ_ДОП,
	           |	МАКСИМУМ(РешенияЕДВ.Дата) КАК Дата
	           |ПОМЕСТИТЬ ПоследниеРешенияЕДВ
	           |ИЗ
	           |	Документ.уп_РешениеОЕдиновременнойВыплате КАК РешенияЕДВ
	           |ГДЕ
	           |	РешенияЕДВ.Проведен
	           |	И РешенияЕДВ.Дата <= &ДатаКонца
	           |	И РешенияЕДВ.ДоговорОПС В
	           |			(ВЫБРАТЬ
	           |				ВыплатыЕДВ.ДоговорОПС
	           |			ИЗ
	           |				ВыплатыЕДВ КАК ВыплатыЕДВ)
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	РешенияЕДВ.ДоговорОПС,
	           |	РешенияЕДВ.ДополнительнаяВыплата
	           |		ИЛИ РешенияЕДВ.ДоначислениеПоПереданномуВзносуИзПФР
	           |
	           |ИНДЕКСИРОВАТЬ ПО
	           |	Дата
	           |;
	           |
	           |////////////////////////////////////////////////////////////////////////////////
	           |ВЫБРАТЬ
	           |	ПоследниеРешенияЕДВ.ДоговорОПС КАК ДоговорОПС,
	           |	РешенияЕДВ.Участник.Наименование КАК Участник,
	           |	ВЫБОР
	           |		КОГДА РешенияЕДВ.ДополнительнаяВыплата
	           |				ИЛИ РешенияЕДВ.ДоначислениеПоПереданномуВзносуИзПФР
	           |			ТОГДА 1
	           |		ИНАЧЕ 0
	           |	КОНЕЦ КАК ЕДВДоп,
	           |	ВЫБОР
	           |		КОГДА РешенияЕДВ.ДополнительнаяВыплата
	           |				ИЛИ РешенияЕДВ.ДоначислениеПоПереданномуВзносуИзПФР
	           |			ТОГДА 0
	           |		ИНАЧЕ 1
	           |	КОНЕЦ КАК ЕДВ,
	           |	0 КАК НЧТП,
	           |	0 КАК СВ,
	           |	ВыплатыЕДВ.СуммаОборот КАК Сумма
	           |ИЗ
	           |	ПоследниеРешенияЕДВ КАК ПоследниеРешенияЕДВ
	           |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уп_РешениеОЕдиновременнойВыплате КАК РешенияЕДВ
	           |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыплатыЕДВ КАК ВыплатыЕДВ
	           |			ПО РешенияЕДВ.ДоговорОПС = ВыплатыЕДВ.ДоговорОПС
	           |				И РешенияЕДВ.СуммаНаСчете = ВыплатыЕДВ.СуммаОборот
	           |		ПО ПоследниеРешенияЕДВ.ДоговорОПС = РешенияЕДВ.ДоговорОПС
	           |			И ПоследниеРешенияЕДВ.Дата = РешенияЕДВ.Дата
	           |			И (ПоследниеРешенияЕДВ.ЕДВ_ДОП = (РешенияЕДВ.ДополнительнаяВыплата
	           |				ИЛИ РешенияЕДВ.ДоначислениеПоПереданномуВзносуИзПФР))
	           |
	           |ОБЪЕДИНИТЬ ВСЕ
	           |
	           |ВЫБРАТЬ
	           |	ВыплатыОПС.ДоговорОПС,
	           |	ВыплатыОПС.Участник.Наименование,
	           |	0,
	           |	0,
	           |	ВЫБОР
	           |		КОГДА ВыплатыОПС.ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыПенсийОПС)
	           |			ТОГДА 1
	           |		ИНАЧЕ 0
	           |	КОНЕЦ,
	           |	ВЫБОР
	           |		КОГДА ВыплатыОПС.ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.СрочнаяПенсияОПС)
	           |			ТОГДА 1
	           |		ИНАЧЕ 0
	           |	КОНЕЦ,
	           |	СУММА(ВыплатыОПС.СуммаОборот)
	           |ИЗ
	           |	РегистрНакопления.уп_ВыплатыОПС.Обороты(&ДатаНачала, &ДатаКонца, Регистратор, ТипВыплаты В (ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыПенсийОПС), ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.СрочнаяПенсияОПС))) КАК ВыплатыОПС
	           |ГДЕ
	           |	ВыплатыОПС.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
	           |	И НЕ ВЫРАЗИТЬ(ВыплатыОПС.Регистратор КАК Документ.уп_ВыплатныеРеестры).ХМ_ВыплатаПоВозвратам
	           |
	           |СГРУППИРОВАТЬ ПО
	           |	ВыплатыОПС.ДоговорОПС,
	           |	ВыплатыОПС.Участник.Наименование,
	           |	ВЫБОР
	           |		КОГДА ВыплатыОПС.ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыПенсийОПС)
	           |			ТОГДА 1
	           |		ИНАЧЕ 0
	           |	КОНЕЦ,
	           |	ВЫБОР
	           |		КОГДА ВыплатыОПС.ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.СрочнаяПенсияОПС)
	           |			ТОГДА 1
	           |		ИНАЧЕ 0
	           |	КОНЕЦ
	           |
	           |УПОРЯДОЧИТЬ ПО
	           |	Участник";
	
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	
	ТЗ = З.Выполнить().Выгрузить();
	ЕдвДОП = Строка(ТЗ.Итог("ЕДВДоп"));
	ОснВыплата = Строка(ТЗ.Итог("ЕДВ")+ТЗ.Итог("НЧТП")+ТЗ.Итог("СВ"));
	Результат = ОснВыплата + " / " + ЕдвДОП;
	Возврат Результат;
КонецФункции

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 27.10.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №614
	// ХМНПФ 24.11.2016 ШадринАВ // ЗаполнитьОтчетОПС5СФормаОтчета2013Кв3() // №676
	// ХМНПФ 24.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №676
	// ХМНПФ 25.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №670
	// ХМНПФ 28.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №683
	// ХМНПФ 19.04.2017 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №945
	
	// ХМНПФ 27.10.2016 ШадринАВ // Новая ХМ_ПолучитьП1004_ФормаОтчета2013Кв3_ОПС5() // №614
	// ХМНПФ 24.11.2016 ШадринАВ // Новая ХМ_ПолучитьП3004_ФормаОтчета2013Кв3_ОПС5() // №676
	// ХМНПФ 25.11.2016 ШадринАВ // Новая ХМ_ПолучитьП3303_ФормаОтчета2013Кв3_ОПС5() // №670
	// ХМНПФ 28.11.2016 ШадринАВ // Новая ХМ_ПолучитьП2004_ФормаОтчета2013Кв3_ОПС5() // №683
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьОтчетОПС5СФормаОтчета2013Кв3")
Процедура ХМЗаполнитьОтчетОПС5СФормаОтчета2013Кв3(ПараметрыОтчета, Контейнер)
	Перем РассчитанныеПоказатели; // Таблица для сбора сумм показателей по организациям

	Если ПараметрыОтчета.Свойство("ТаблицаРасшифровки") И ЗначениеЗаполнено(ПараметрыОтчета.ТаблицаРасшифровки) Тогда
		ТаблицаРасшифровки = ПараметрыОтчета.ТаблицаРасшифровки;
	Иначе
		ТаблицаРасшифровки = Новый ТаблицаЗначений;
		ТаблицаРасшифровки.Колонки.Добавить("ИмяПоказателя");
		ТаблицаРасшифровки.Колонки.Добавить("НаименованиеПоказателя");
		ТаблицаРасшифровки.Колонки.Добавить("ЗнакОперации");
		ТаблицаРасшифровки.Колонки.Добавить("НаименованиеСлагаемого");
		ТаблицаРасшифровки.Колонки.Добавить("Сумма");
		ТаблицаРасшифровки.Колонки.Добавить("ИмяРаздела");
		ТаблицаРасшифровки.Колонки.Добавить("ДополнительныеПараметры");
	КонецЕсли;

	ОчиститьРазделРасшифровки(ТаблицаРасшифровки, "ПолеТабличногоДокументаФормаОтчета");

	РассчитанныеПоказатели = Новый ТаблицаЗначений;
	РассчитанныеПоказатели.Колонки.Добавить("Раздел");
	РассчитанныеПоказатели.Колонки.Добавить("Показатель");
	РассчитанныеПоказатели.Колонки.Добавить("Сумма");

	МногострочныеПоказатели = Новый Структура;
	// Вспомогательные значения.
	ОсновнаяОрганизация = ПараметрыОтчета.Организация;
	ГруппаОрганизаций = ПараметрыОтчета.ГруппаОрганизаций;

	ДатаНачалаПериодаОтчета = ПараметрыОтчета.ДатаНачалаПериодаОтчета;
	ДатаКонцаПериодаОтчета  = ПараметрыОтчета.ДатаКонцаПериодаОтчета;

	ДатаНачалаПериодаПредыдущегоГода = ДобавитьМесяц(ДатаНачалаПериодаОтчета, -12);
	ДатаКонцаПериодаПредыдущегоГода  = ДобавитьМесяц(ДатаКонцаПериодаОтчета, -12);

	ДатаНачалаПредыдущегоГода = НачалоГода(ДобавитьМесяц(ДатаНачалаПериодаОтчета, -12));
	ДатаКонцаПредыдущегоГода  = КонецГода(ДатаНачалаПредыдущегоГода);

	ДатаНачалаГодаПредшествующегоПредыдущему = НачалоГода(ДобавитьМесяц(ДатаНачалаПредыдущегоГода, -12));
	ДатаКонцаГодаПредшествующегоПредыдущему  = КонецГода(ДатаНачалаГодаПредшествующегоПредыдущему);

	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);

	ПолныйСписокОрганизаций = Новый Массив;

	Если ДоступныеОрганизации.Найти(ОсновнаяОрганизация) <> Неопределено Тогда
		ПолныйСписокОрганизаций.Добавить(ОсновнаяОрганизация);
	КонецЕсли;

	Если ПараметрыОтчета.Свойство("ГруппаОрганизаций") Тогда
		Для Каждого Организация Из ПараметрыОтчета.ГруппаОрганизаций Цикл
			Если ДоступныеОрганизации.Найти(Организация.Значение) <> Неопределено Тогда
				ПолныйСписокОрганизаций.Добавить(Организация.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);

	Для Каждого Организация Из ПолныйСписокОрганизаций Цикл

		КонтекстИсполнения = Новый Структура;
		КонтекстИсполнения.Вставить("Организация", Организация);
		КонтекстИсполнения.Вставить("ТаблицаРасшифровки", ТаблицаРасшифровки);
		КонтекстИсполнения.Вставить("РассчитанныеПоказатели", РассчитанныеПоказатели);
		КонтекстИсполнения.Вставить("МногострочныеПоказатели", Новый Структура);
		КонтекстИсполнения.Вставить("КонецОтчетногоПериода", ПараметрыОтчета.ДатаКонцаПериодаОтчета);

		ДатаКонцаПредыдущегоПериода = ДобавитьМесяц(ДатаКонцаПериодаОтчета, -12);
		Если НачалоМесяца(ДатаКонцаПериодаОтчета) = ДатаНачалаПериодаОтчета Тогда
			СтрПериод           = "За " + Формат(ДатаКонцаПериодаОтчета, "ДФ = 'ММММ гггг'") + "г.";
			СтрПериодПрошлыйГод = "За " + Формат(ДатаКонцаПредыдущегоПериода, "ДФ = 'ММММ гггг'") + "г.";
		Иначе
			СтрПериод           = "За " + Формат(ДатаНачалаПериодаОтчета, "ДФ = 'ММММ гггг'") +" - " + Формат(ДатаКонцаПериодаОтчета, "ДФ = 'ММММ гггг'") + "г.";
			СтрПериодПрошлыйГод = "За " + Формат(ДатаНачалаПериодаПредыдущегоГода, "ДФ = 'ММММ гггг'") +" - " + Формат(ДатаКонцаПредыдущегоПериода, "ДФ = 'ММММ гггг'") + "г.";
		КонецЕсли;

		КонтекстИсполнения.Вставить("НачалоПериодаОтчета", ДатаНачалаПериодаОтчета);
		КонтекстИсполнения.Вставить("КонецПериодаОтчета", ДатаКонцаПериодаОтчета);
		ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС(КонтекстИсполнения);
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

	#Удаление 
	РассчитанныеПоказатели.Свернуть("Раздел, Показатель", "Сумма");
	#КонецУдаления
	#Вставка 
	// +ХМНПФ 24.11.2016 ШадринАВ // ЗаполнитьОтчетОПС5СФормаОтчета2013Кв3() // №676
	#КонецВставки
	Для Каждого СтрокаПоказателя Из РассчитанныеПоказатели Цикл
		Контейнер[СтрокаПоказателя.Раздел][СтрокаПоказателя.Показатель] = СтрокаПоказателя.Сумма;
	КонецЦикла;

	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Контейнер", Контейнер);
	СтруктураДанных.Вставить("ТаблицаРасшифровки", ТаблицаРасшифровки);
	ПоместитьВоВременноеХранилище(СтруктураДанных, ПараметрыОтчета.АдресВоВременномХранилище);

КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС")
Процедура ХМЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС(КонтекстИсполнения)

	Организация = КонтекстИсполнения.Организация;
	ТаблицаРасшифровки = КонтекстИсполнения.ТаблицаРасшифровки;
	РассчитанныеПоказатели = КонтекстИсполнения.РассчитанныеПоказатели;

	ИмяРаздела = "ПолеТабличногоДокументаФормаОтчета";
	ДополнительныеПараметрыРасшифровки = Новый Структура("Организация", Организация);

	НачалоПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	КонецПериодаОтчета  = КонтекстИсполнения.КонецПериодаОтчета;
	мДатаНачалаПериодаОтчета = КонтекстИсполнения.НачалоПериодаОтчета;
	мДатаКонцаПериодаОтчета = КонтекстИсполнения.КонецПериодаОтчета;

	//Графа = КонтекстИсполнения.Графа;
	//ЗаголовокГрафы = КонтекстИсполнения.ЗаголовокГрафы;

	ОбщиеПараметрыРасшифровки = Новый Структура;
	ОбщиеПараметрыРасшифровки.Вставить("Организация", Организация);
	ОбщиеПараметрыРасшифровки.Вставить("НачалоПериода", НачалоПериодаОтчета);
	ОбщиеПараметрыРасшифровки.Вставить("КонецПериода", КонецПериодаОтчета);

	//
	//************************Строка 100+
	СписокСостояний=Новый СписокЗначений();
	//СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
	//ЕдиноврВыпл
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ЕдиновременнаяВыплата);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
	//~ЕдиноврВыпл
	//СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);
	З=Новый Запрос("ВЫБРАТЬ
	|	СостоянияДоговораОПССрезПоследних.ДоговорОПС КАК Договор
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
	|			&ДатаНачала,
	|			ДоговорОПС.Организация = &Организация
	|				И ДоговорОПС.ДатаЗаключения < &ДатаНачала) КАК СостоянияДоговораОПССрезПоследних
	|ГДЕ
	|	СостоянияДоговораОПССрезПоследних.Состояние В(&Состояния)");
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("Состояния",СписокСостояний);
	З.УстановитьПараметр("Организация",Организация);
	//Состояние("Выполнение запроса по строке 100 раздел 1");
	Результат=З.Выполнить().Выбрать();
	П1003 = Результат.Количество();

	//УстановитьЗначениеПоказателя("П1003", П1003);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П1003", П1003);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П1003","Строка 100 ""Застрахованные лица, заключившие договоры об обязательном пенсионном страховании на начало года"", графа ""Количество (человек)""","+","Количество элементов справочника ДоговорыОПС с датой заключения<Даты начала периода отчета и СостоянияДоговораОПС (регистр сведений) на дату начала периода отчета ОжиданиеПоступленияСредств, НакопительныйПериод, Выплатной период", П1003, ИмяРаздела);
	//************************Строка 100-

	//************************Строка 400+
	З.УстановитьПараметр("ДатаНачала",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("Состояния",СписокСостояний);
	З.УстановитьПараметр("Организация",Организация);
	//Состояние("Выполнение запроса по строке 400 раздел 1");
	Результат=З.Выполнить().Выбрать();
	П4003 = Результат.Количество();

	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П4003", П4003);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П4003","Строка 400 ""Застрахованные лица, заключившие договоры об обязательном пенсионном страховании на конец отчетного периода"", графа ""Количество (человек)""","+","Количество элементов справочника ДоговорыОПС с СостоянияДоговораОПС (регистр сведений) на дату конца периода отчета ОжиданиеПоступленияСредств, НакопительныйПериод, Выплатной период", П4003, ИмяРаздела);
	//************************Строка 400-

	//************************Строка 200+

	З=Новый Запрос("ВЫБРАТЬ
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние,
	|	ЕСТЬNULL(уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия, &ПустаяПричина) КАК ПричинаЗакрытия
	|ПОМЕСТИТЬ СостоянияНаКонец
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_СостоянияДоговораОПССрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация В (&Организация)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
	|		ПО уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС
	|ГДЕ
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние В(&СписокПослеПоступленияСредств)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ СостоянияНаНачало
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаНачала, ДоговорОПС.Организация = &Организация) КАК уп_СостоянияДоговораОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНаКонец.ДоговорОПС,
	|	СостоянияНаКонец.Состояние КАК СостояниеНаКонец,
	|	ЕСТЬNULL(СостоянияНаНачало.Состояние, ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговоровОПС.Заключен)) КАК СостояниеНаНачало,
	|	СостоянияНаКонец.ПричинаЗакрытия,
	|	ВЫБОР
	|		КОГДА уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ПредыдущийСтраховщик = &ПФР
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ПредыдущийСтраховщик ССЫЛКА Справочник.Контрагенты
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ NULL
	|			КОНЕЦ
	|	КОНЕЦ КАК ПредСтраховщик
	|ПОМЕСТИТЬ ПолнаяТаблицаСостояний
	|ИЗ
	|	СостоянияНаКонец КАК СостоянияНаКонец
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНаНачало КАК СостоянияНаНачало
	|		ПО СостоянияНаКонец.ДоговорОПС = СостоянияНаНачало.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПредыдущиеСтраховщикиПоДоговорамОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних
	|		ПО СостоянияНаКонец.ДоговорОПС = уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ДоговорОПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПолнаяТаблицаСостояний.ДоговорОПС КАК ДоговорОПС,
	|	ПолнаяТаблицаСостояний.СостояниеНаКонец,
	|	ПолнаяТаблицаСостояний.СостояниеНаНачало,
	|	ПолнаяТаблицаСостояний.ПредСтраховщик КАК ПредСтраховщик
	|ИЗ
	|	ПолнаяТаблицаСостояний КАК ПолнаяТаблицаСостояний
	|ГДЕ
	|	ПолнаяТаблицаСостояний.СостояниеНаНачало В(&СписокДоПоступленияСредств)
	|	И ПолнаяТаблицаСостояний.СостояниеНаКонец В(&СписокПослеПоступленияСредств)
	|	И НЕ(ПолнаяТаблицаСостояний.СостояниеНаНачало = &ПриостановленПФР
	|				И ПолнаяТаблицаСостояний.СостояниеНаКонец = &Закрыт)
	|	И ПолнаяТаблицаСостояний.ПричинаЗакрытия <> &ПоОтказуПФР 
	#Вставка
	// +ХМНПФ 19.04.2017 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №945
	|	И ПолнаяТаблицаСостояний.ПричинаЗакрытия <> &НеВступилВСилу
	// -ХМНПФ 19.04.2017 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №945
	#КонецВставки
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДоговорОПС)
	|ПО
	|	ПредСтраховщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПолнаяТаблицаСостояний
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНаНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНаКонец");	
	#Вставка
	// +ХМНПФ 19.04.2017 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №945
	З.УстановитьПараметр("НеВступилВСилу",Перечисления.уп_ПричиныЗакрытияДоговораОПС.НеВступилВСилу);
	// -ХМНПФ 19.04.2017 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №945
	#КонецВставки
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("Организация",Организация);
	З.УстановитьПараметр("ПриостановленПФР",Перечисления.уп_СостоянияДоговоровОПС.ПриостановленПоОтказуИзПФР);
	З.УстановитьПараметр("Закрыт",Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
	З.УстановитьПараметр("ПоОтказуПФР",Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЗакрытПоОтказуИзПФР);
	З.УстановитьПараметр("ПустаяПричина",Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПустаяСсылка());
	З.УстановитьПараметр("ПФР",Константы.уп_ПенсионныйФондРФ.Получить());

	СписокДо = Новый СписокЗначений;
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Заключен);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПриостановленПоОтказуИзПФР);

	СписокПосле = Новый СписокЗначений;
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод);
	//СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);
	//ЕдиноврВыпл
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ЕдиновременнаяВыплата);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
	//~ЕдиноврВыпл

	З.УстановитьПараметр("СписокДоПоступленияСредств",СписокДо);
	З.УстановитьПараметр("СписокПослеПоступленияСредств",СписокПосле);

	//ОбработкаПрерыванияПользователя();
	//Состояние("Выполнение запроса по строке 200 раздел 1");
	Результат=З.Выполнить();

	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);


	П2003 = 0;
	П2103 = 0;
	П2203 = 0;
	Пока Выборка.Следующий() Цикл
		//Состояние("Заполнение строк 200-220");
		П2003 = П2003 + Выборка.ДоговорОПС;
		//Если Выборка.ПредыдущийСтраховщик = Константы.уп_ПенсионныйФондРФ.Получить() Тогда
		Если ЗначениеЗаполнено(Выборка.ПредСтраховщик) Тогда
			Если Выборка.ПредСтраховщик Тогда
				П2103 = П2103 + Выборка.ДоговорОПС;
			Иначе
				П2203 = П2203 + Выборка.ДоговорОПС;
			КонецЕсли;
		Иначе
			П2203 = П2203 + Выборка.ДоговорОПС;
			ВыборкаДоговоров = Выборка.Выбрать();
			Пока ВыборкаДоговоров.Следующий() Цикл
				Сообщить("По договору ОПС "+ВыборкаДоговоров.ДоговорОПС+" от "+ВыборкаДоговоров.ДоговорОПС.ДатаЗаключения+" не зарегистрирован предыдущий страховщик.");
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;

	//УстановитьЗначениеПоказателя("П2003", П2003);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П2003", П2003);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П2003","Строка 200 ""Застрахованные лица, заключившие договоры об обязательном пенсионном страховании в течение отчетного периода"", графа ""Количество (человек)""","+","Количество договоров, получивших подтверждение из ПФР в течение отчетного периода", П2003, ИмяРаздела);

	//УстановитьЗначениеПоказателя("П2103", П2103);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П2103", П2103);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П2103","Строка 210 ""в результате реализации права застрахованных лиц на переход в негосударственный пенсионный фонд из Пенсионного фонда Российской Федерации"", графа ""Количество (человек)""","+","Из строки 200, те у которых в реквизите ДокументДоговорОПС.ПредыдущийСтраховщик стоит константа ПенсионныйФондРФ", П2103, ИмяРаздела);

	//УстановитьЗначениеПоказателя("П2203", П2203);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П2203", П2203);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П2203","Строка 220 ""в результате реализации права застрахованных лиц на переход в негосударственный пенсионный фонд из других негосударственных пенсионных фондов"", графа ""Количество (человек)""","+","Из строки 200, те у которых в реквизите ДокументДоговорОПС.ПредыдущийСтраховщик не стоит константа ПенсионныйФондРФ", П2203, ИмяРаздела);
	////************************Строка 200-



	//************************Строка 300+

	З=Новый Запрос("ВЫБРАТЬ
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ СостоянияНаКонец
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_СостоянияДоговораОПССрезПоследних
	|ГДЕ
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние В(&СписокПослеЗакрытия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС,
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние
	|ПОМЕСТИТЬ СостоянияНаНачало
	|ИЗ
	|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаНачала, ДоговорОПС.Организация = &Организация) КАК уп_СостоянияДоговораОПССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНаКонец.ДоговорОПС,
	|	СостоянияНаКонец.Состояние КАК СостояниеНаКонец,
	|	ЕСТЬNULL(СостоянияНаНачало.Состояние, ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговоровОПС.Заключен)) КАК СостояниеНаНачало
	|ПОМЕСТИТЬ ПолнаяТаблицаСостояний
	|ИЗ
	|	СостоянияНаКонец КАК СостоянияНаКонец
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНаНачало КАК СостоянияНаНачало
	|		ПО СостоянияНаКонец.ДоговорОПС = СостоянияНаНачало.ДоговорОПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПолнаяТаблицаСостояний.ДоговорОПС) КАК ДоговорОПС,
	|	уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних.СледующийСтраховщик КАК СледующийСтраховщик,
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия КАК ПричинаЗакрытия
	|ИЗ
	|	ПолнаяТаблицаСостояний КАК ПолнаяТаблицаСостояний
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СледующиеСтраховщикиПоДоговорамОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних
	|		ПО ПолнаяТаблицаСостояний.ДоговорОПС = уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних.ДоговорОПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
	|		ПО ПолнаяТаблицаСостояний.ДоговорОПС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС
	|ГДЕ
	|	ПолнаяТаблицаСостояний.СостояниеНаНачало В(&СписокДоЗакрытия)
	|	И ПолнаяТаблицаСостояний.СостояниеНаКонец В(&СписокПослеЗакрытия)
	|	И уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия В(&СписокПричинЗакрытия)
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия,
	|	уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних.СледующийСтраховщик
	|ИТОГИ
	|	СУММА(ДоговорОПС)
	|ПО
	|	ПричинаЗакрытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПолнаяТаблицаСостояний
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНаНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНаКонец");

	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("Организация",Организация);

	СписокДо = Новый СписокЗначений;
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Заключен);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПриостановленПоОтказуИзПФР);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
	//ЕдиноврВыпл
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ЕдиновременнаяВыплата);
	СписокДо.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
	//~ЕдиноврВыпл

	СписокПосле = Новый СписокЗначений;
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
	СписокПосле.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);

	З.УстановитьПараметр("СписокДоЗакрытия",СписокДо);
	З.УстановитьПараметр("СписокПослеЗакрытия",СписокПосле);

	СписокПричинЗакрытия = Новый СписокЗначений;
	СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.Досрочное);
	СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПереходДосрочный);
	//ЕдиноврВыпл
	//СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЕдиновременнаяВыплата);
	//~ЕдиноврВыпл
	СписокПричинЗакрытия.Добавить(Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти);
	З.УстановитьПараметр("СписокПричинЗакрытия",СписокПричинЗакрытия);

	//ОбработкаПрерыванияПользователя();
	//Состояние("Выполнение запроса по строке 300 раздел 1");
	Результат=З.Выполнить();
	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	//П3003 = 0;
	П3103 = 0;
	П3203 = 0;
	Пока Выборка.Следующий() Цикл
		//Состояние("Заполнение строк 300-320");
		Если Выборка.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.Досрочное или Выборка.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПереходДосрочный Тогда
			ВыборкаСтраховщиков = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСтраховщиков.Следующий() Цикл
				Если ВыборкаСтраховщиков.СледующийСтраховщик = Константы.уп_ПенсионныйФондРФ.Получить() Тогда
					П3103 = П3103 + ВыборкаСтраховщиков.ДоговорОПС;
				Иначе
					П3203 = П3203 + ВыборкаСтраховщиков.ДоговорОПС;
				КонецЕсли;				

			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;
	//************************Строка 300-


	//************************Строка 310+//

	//УстановитьЗначениеПоказателя("П3103", П3103);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П3103", П3103);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П3103","Строка 310 ""в связи с реализацией права застрахованных лиц на переход из негосударственного пенсионного фонда в Пенсионный фонд Российской Федерации"", графа ""Количество (человек)""","+","Из строки 300 те, у которых в документе ""Закрытие договоров ОПС"" стоит Досрочное расторжение, а в качестве пенсионного фонда стоит константа ПенсионныйФондРФ", П3103, ИмяРаздела);
	//************************Строка 310-


	//************************Строка 320+

	//УстановитьЗначениеПоказателя("П3203", П3203);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П3203", П3203);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П3203","Строка 320 ""в связи с реализацией права застрахованных лиц на переход из негосударственного пенсионного фонда в другие негосударственные пенсионные фонды"", графа ""Количество (человек)""","+","Из строки 300 те, у которых в документе ""Закрытие договоров ОПС"" стоит Досрочное расторжение, а в качестве пенсионного фонда не стоит константа ПенсионныйФондРФ", П3203, ИмяРаздела);
	//************************Строка 320-

	////************************Строка 321,...+

	//********************************************************************************
	#Вставка 
	// +ХМНПФ 25.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №670
	П3303 = ХМ_ПолучитьП3303_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П3303", П3303);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П3303","Строка 330 ""в связи со смертью застрахованных лиц"", графа ""Количество (человек)""","+","Из строки 300 те, у которых в документе ""Закрытие договоров ОПС"" стоит причина ""По смерти"" и нет флага ""Список умерших не от ПФР""", П3303, ИмяРаздела);
	// -ХМНПФ 25.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №670
	#КонецВставки
	//************************Строка 100+

	З = Новый Запрос();
	З.Текст =  "ВЫБРАТЬ
	|	уп_ОткрытиеДоговораОПСПравопреемники.НомерСтроки,
	|	уп_ОткрытиеДоговораОПСПравопреемники.Ссылка.Дата,
	|	""Договор"" КАК ТипДокумента,
	|	уп_ОткрытиеДоговораОПСПравопреемники.Ссылка,
	|	уп_ОткрытиеДоговораОПСПравопреемники.Ссылка.ДоговорОПС
	|ПОМЕСТИТЬ Правопреемники
	|ИЗ
	|	Документ.уп_ОткрытиеДоговораОПС.Правопреемники КАК уп_ОткрытиеДоговораОПСПравопреемники
	|ГДЕ
	|	уп_ОткрытиеДоговораОПСПравопреемники.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уп_ЗаявлениеОНаличииПравопреемниковПравопреемники.НомерСтроки,
	|	уп_ЗаявлениеОНаличииПравопреемниковПравопреемники.Ссылка.Дата,
	|	""Заявление"",
	|	уп_ЗаявлениеОНаличииПравопреемниковПравопреемники.Ссылка,
	|	уп_ЗаявлениеОНаличииПравопреемниковПравопреемники.Ссылка.ДоговорОПС
	|ИЗ
	|	Документ.уп_ЗаявлениеОНаличииПравопреемников.Правопреемники КАК уп_ЗаявлениеОНаличииПравопреемниковПравопреемники
	|ГДЕ
	|	уп_ЗаявлениеОНаличииПравопреемниковПравопреемники.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правопреемники.ДоговорОПС КАК ДоговорОПС,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Правопреемники.Ссылка) КАК Ссылка
	|ИЗ
	|	Правопреемники КАК Правопреемники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаКонца, ДоговорОПС.Организация = &Организация) КАК уп_СостоянияДоговораОПССрезПоследних
	|		ПО Правопреемники.ДоговорОПС = уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС
	|ГДЕ
	|	уп_СостоянияДоговораОПССрезПоследних.Состояние В(&СписокСостояний)
	|
	|СГРУППИРОВАТЬ ПО
	|	Правопреемники.ДоговорОПС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорОПС";

	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);	
	З.УстановитьПараметр("СписокСостояний",СписокСостояний);
	З.УстановитьПараметр("Организация",Организация);

	//ОбработкаПрерыванияПользователя();
	//Состояние("Выполнение запроса по строке 100 раздел 2");
	Результат=З.Выполнить();
	Выборка = Результат.Выбрать();
    #Удаление 
	П1004 = Выборка.Количество();	
	#КонецУдаления  
	#Вставка 
	// +ХМНПФ 27.10.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №614
	П1004 = ХМ_ПолучитьП1004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения);
	// -ХМНПФ 27.10.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №614
	#КонецВставки
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П1004", П1004);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П1004","Строка 100 ""Застрахованные лица, подавшие в негосударственный пенсионный фонд заявления о правопреемниках"", графа ""Количество (человек)""","+","Количество договоров из строки 400 - если есть документ ЗаявлениеОНаличииПравопреемников, либо заполнена табличная часть Правопреемники в документе ОткрытиеДоговораОПС", П1004, ИмяРаздела);
	//************************Строка 100-


	//************************Строка 200+

	//сделаем по выплатам
	З=Новый Запрос("ВЫБРАТЬ
	|	ВыплатыОПСОбороты.Участник,
	|	СУММА(ВыплатыОПСОбороты.СуммаОборот) КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.уп_ВыплатыОПС.Обороты(
	|			&ДатаНачала,
	|			&ДатаКонца,
	|			Период,
	|			Организация = &Организация
	|				И ТипВыплаты В (&СписокТиповВыплат)) КАК ВыплатыОПСОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыплатыОПСОбороты.Участник");

	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	СписокТиповВыплат = Новый СписокЗначений;
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.ЕдиновременнаяВыплатаОПС);
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.ВозвратНакопительнойПенсииОПС);
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.ВозвратЕдиновременнойВыплатыОПС);
	СписокТиповВыплат.Добавить(Перечисления.уп_ТипыВыплат.ВозвратСрочнойПенсииОПС);
	З.УстановитьПараметр("СписокТиповВыплат",СписокТиповВыплат);
	З.УстановитьПараметр("Организация",Организация);

	//ОбработкаПрерыванияПользователя();
	//Состояние("Выполнение запроса по строке 200 раздел 2");
	Результат = З.Выполнить().Выбрать();
	#Удаление 
	П2004 = Результат.Количество();	
	#КонецУдаления
	#Вставка 
	// +ХМНПФ 28.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №683
	П2004 = ХМ_ПолучитьП2004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения);
	// -ХМНПФ 28.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №683
	
	#КонецВставки
	//УстановитьЗначениеПоказателя("П2004", П2004);
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П2004", П2004);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П2004","Строка 200 ""Застрахованные лица, получающие накопительную часть трудовой пенсии"", графа ""Количество (человек)""","+","Количество элементов справочника ДоговорыОПС, у которых СостоянияДоговораОПС на дату конца периода отчета ВыплатнойПериод или Обязательства выполнены", П2004, ИмяРаздела);
	//************************Строка 200-


	//************************Строка 300+
	З=Новый Запрос("ВЫБРАТЬ
	|	ВыплатыОПСОбороты.Участник,
	|	СУММА(ВыплатыОПСОбороты.СуммаОборот) КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.уп_ВыплатыОПС.Обороты(
	|			&ДатаНачала,
	|			&ДатаКонца,
	|			Период,
	|			Организация = &Организация
	|				И ТипВыплаты = &ПравопреемникиОПС
	|				И Участник.ЮридическоеФизическоеЛицо = &ФизЛицо) КАК ВыплатыОПСОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыплатыОПСОбороты.Участник");
	//З.УстановитьПараметр("ДатаНачала",'00010101');
	З.УстановитьПараметр("ДатаНачала",мДатаНачалаПериодаОтчета);
	З.УстановитьПараметр("ДатаКонца",мДатаКонцаПериодаОтчета);
	З.УстановитьПараметр("ПравопреемникиОПС",Перечисления.уп_ТипыВыплат.ВыплатыНаследникамОПС);
	З.УстановитьПараметр("ФизЛицо",Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	З.УстановитьПараметр("Организация",Организация);

	//ОбработкаПрерыванияПользователя();
	//Состояние("Выполнение запроса по строке 300 раздел 2");
	Результат = З.Выполнить().Выбрать();
	#Удаление 
	П3004 = Результат.Количество();	
	#КонецУдаления 
	#Вставка 
	// +ХМНПФ 24.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №676
	П3004 = ХМ_ПолучитьП3004_ФормаОтчета2013Кв3_ОПС5(КонтекстИсполнения);
	// -ХМНПФ 24.11.2016 ШадринАВ // ЗаполнитьФормаОтчета5ОПС_2013Кв3_5ОПС() // №676
    #КонецВставки
	УстановитьЗначениеПоказателя(РассчитанныеПоказатели, ИмяРаздела, "П3004", П3004);
	ДобавитьСтрокуРасшифровки(ТаблицаРасшифровки, "П3004","Строка 300 ""Правопреемники умерших застрахованных лиц получившие выплаты"", графа ""Количество (человек)""","+","Количество правопреемников, получивших выплату, по регистру ВыплатыОПС", П3004, ИмяРаздела);
	//************************Строка 300-

КонецПроцедуры
