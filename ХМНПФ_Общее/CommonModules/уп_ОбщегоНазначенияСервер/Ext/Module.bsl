Процедура НПФ_ХМ_СоздатьРСПоРазовымПП_НПОВместоНаСервере(МассивВыплатныхРеестров) Экспорт
	
	
	
	ВыплатныеРеестрыСРС =  ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВыплатныхРеестров,"РасчетныйСчет",Истина);
		
	Для каждого СтрокаНач Из ВыплатныеРеестрыСРС Цикл
		Если Не ЗначениеЗаполнено(СтрокаНач.Значение) Тогда
			ОбъектНач = СтрокаНач.Ключ.ПолучитьОбъект();
			
			ВыгрузкаТЧ = ОбъектНач.СписокПенсионеров.Выгрузить(,"Участник,НомерЛицевогоСчета");
			ВыгрузкаТЧ.Свернуть("Участник,НомерЛицевогоСчета");
			
			Если ВыгрузкаТЧ.Количество() = 1 Тогда
				
				НомерЛицевогоСчета = ОбъектНач.СписокПенсионеров[0].НомерЛицевогоСчета;
				Участник = ОбъектНач.СписокПенсионеров[0].Участник;
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Банки.Ссылка
				|ИЗ
				|	Справочник.Банки КАК Банки
				|ГДЕ
				|	Банки.ХМ_Контрагент = &ХМ_Контрагент";
				
				Запрос.УстановитьПараметр("ХМ_Контрагент", ОбъектНач.Получатель);
				
				РезультатЗапроса = Запрос.Выполнить();
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				
				Если ВыборкаДетальныеЗаписи.Количество() = 1 Тогда
					
					ВыборкаДетальныеЗаписи.Следующий();
					ПолучательБанк = ВыборкаДетальныеЗаписи.Ссылка;
					
					Запрос = Новый Запрос;
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	БанковскиеСчета.Ссылка
					|ИЗ
					|	Справочник.БанковскиеСчета КАК БанковскиеСчета
					|ГДЕ
					|	БанковскиеСчета.Владелец = &Владелец
					|	И БанковскиеСчета.НомерСчета = &НомерСчета
					|	И БанковскиеСчета.Банк = &Банк";
					
					Запрос.УстановитьПараметр("Банк", ПолучательБанк);
					Запрос.УстановитьПараметр("Владелец", Участник);
					Запрос.УстановитьПараметр("НомерСчета", НомерЛицевогоСчета);
					
					РезультатЗапроса = Запрос.Выполнить();
					
					ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
					
					Если ВыборкаДетальныеЗаписи.Следующий() Тогда
						ОбъектНач.РасчетныйСчет = ВыборкаДетальныеЗаписи.Ссылка;
					Иначе
						БанковскийСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
						БанковскийСчет.Наименование = НомерЛицевогоСчета + ", " + ПолучательБанк.Наименование;
						БанковскийСчет.Владелец = Участник;
						БанковскийСчет.НомерСчета = НомерЛицевогоСчета;
						БанковскийСчет.Банк = ПолучательБанк;
						БанковскийСчет.ВалютаДенежныхСредств = Справочники.Валюты.НайтиПоНаименованию("руб.");
						БанковскийСчет.ВидСчета = "Расчетный";
						БанковскийСчет.Записать();
						ОбъектНач.РасчетныйСчет = БанковскийСчет.Ссылка;
					КонецЕсли;
					ОбъектНач.Получатель = Участник;
					ОбъектНач.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 	
	КонецЦикла;
	
КонецПроцедуры

&Перед("npo_НачислениеПенсийОбработкаПроведения")
Процедура ХМnpo_НачислениеПенсийОбработкаПроведения(Источник, Отказ, РежимПроведения)
	Если Источник.ДополнительныеСвойства.Свойство("МассивСообщений") = Ложь Тогда
		Источник.ДополнительныеСвойства.Вставить("МассивСообщений",Новый Массив);	
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("npo_НачислениеПенсийОбработкаПроведения")
Процедура ХМnpo_НачислениеПенсийОбработкаПроведения1(Источник, Отказ, РежимПроведения)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_НачислениеПенсийСписокПенсионеров.НомерСчета КАК НомерСчета,
	|	МАКСИМУМ(уп_НачислениеПенсийСписокПенсионеров.НачалоПериодаНачисления) КАК НачалоПериодаНачисления
	|ПОМЕСТИТЬ СписокСчетовСДатами
	|ИЗ
	|	Документ.уп_НачислениеПенсий.СписокПенсионеров КАК уп_НачислениеПенсийСписокПенсионеров
	|ГДЕ
	|	уп_НачислениеПенсийСписокПенсионеров.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_НачислениеПенсийСписокПенсионеров.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеПенсийСписокПенсионеров.НомерСчета КАК НомерСчета,
	|	НачислениеПенсийСписокПенсионеров.Сумма КАК Сумма,
	|	НачислениеПенсийСписокПенсионеров.Схема КАК Схема,
	|	НачислениеПенсийСписокПенсионеров.НачалоПериодаНачисления КАК НачалоПериодаНачисления,
	|	НачислениеПенсийСписокПенсионеров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА НачислениеПенсийСписокПенсионеров.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПустаяСсылка)
	|			ТОГДА НачислениеПенсийСписокПенсионеров.Схема.ТипПенсионнойСхемы
	|		ИНАЧЕ НачислениеПенсийСписокПенсионеров.ТипПенсии
	|	КОНЕЦ КАК СхемаТипПенсионнойСхемы
	|ПОМЕСТИТЬ СписокСчетов
	|ИЗ
	|	Документ.уп_НачислениеПенсий.СписокПенсионеров КАК НачислениеПенсийСписокПенсионеров
	|ГДЕ
	|	НачислениеПенсийСписокПенсионеров.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетов.НомерСчета КАК НомерСчета,
	|	СписокСчетов.Сумма КАК Сумма,
	|	СписокСчетов.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ СписокСчетовИтог
	|ИЗ
	|	СписокСчетов КАК СписокСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокСчетовСДатами КАК СписокСчетовСДатами
	|		ПО СписокСчетов.НомерСчета = СписокСчетовСДатами.НомерСчета
	|			И СписокСчетов.НачалоПериодаНачисления = СписокСчетовСДатами.НачалоПериодаНачисления
	|			И (СписокСчетов.СхемаТипПенсионнойСхемы = &ТипСхемы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(РезервыОстатки.СуммаОстаток, 0)) КАК СуммаОстаток,
	|	СписокСчетовИтог.НомерСчета КАК НомерСчета,
	|	СУММА(СписокСчетовИтог.Сумма) КАК Сумма,
	|	МАКСИМУМ(СписокСчетовИтог.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ СуммыНаСчетах
	|ИЗ
	|	СписокСчетовИтог КАК СписокСчетовИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(
	|				&МомВремени,
	|				НомерСчета В
	|						(ВЫБРАТЬ
	|							СписокСчетовИтог.НомерСчета КАК НомерСчета
	|						ИЗ
	|							СписокСчетовИтог КАК СписокСчетовИтог)
	|					И ТипРезерва В (&СписокТиповРезерва)) КАК РезервыОстатки
	|		ПО СписокСчетовИтог.НомерСчета = РезервыОстатки.НомерСчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокСчетовИтог.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_СхемаСчетаСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СхемаСчетаСрезПоследних.Схема КАК Схема
	|ПОМЕСТИТЬ СхемыСчетов
	|ИЗ
	|	РегистрСведений.уп_СхемаСчета.СрезПоследних(
	|			&МомВремени,
	|			НомерСчета В
	|				(ВЫБРАТЬ
	|					СуммыНаСчетах.НомерСчета КАК НомерСчета
	|				ИЗ
	|					СуммыНаСчетах КАК СуммыНаСчетах
	|				ГДЕ
	|					СуммыНаСчетах.СуммаОстаток < СуммыНаСчетах.Сумма)) КАК уп_СхемаСчетаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(уп_ПенсионныеСхемыИсточникиВыплат_СПС.НомерСтроки) КАК НомерСтроки,
	|	уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка КАК Ссылка,
	|	СхемыСчетов.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ СхемыСчетов1
	|ИЗ
	|	СхемыСчетов КАК СхемыСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.уп_ПенсионныеСхемы.ИсточникиВыплат_СПС КАК уп_ПенсионныеСхемыИсточникиВыплат_СПС
	|		ПО СхемыСчетов.Схема = уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка
	|ГДЕ
	|	уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка В
	|			(ВЫБРАТЬ
	|				СхемыСчетов.Схема КАК Схема
	|			ИЗ
	|				СхемыСчетов КАК СхемыСчетов)
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка,
	|	СхемыСчетов.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СхемыСчетов1.Ссылка КАК Ссылка,
	|	СхемыСчетов1.НомерСчета КАК НомерСчета,
	|	СхемыСчетов1.НомерСчета.Владелец.ПенсионныйСчет КАК СчетДоговора
	|ПОМЕСТИТЬ СчетаДоговора
	|ИЗ
	|	СхемыСчетов1 КАК СхемыСчетов1
	|ГДЕ
	|	СхемыСчетов1.НомерСтроки > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(уп_РезервыОстатки.СуммаОстаток, 0) КАК СуммаНаСчетеДог,
	|	СчетаДоговора.СчетДоговора КАК СчетДоговора,
	|	СчетаДоговора.НомерСчета КАК НомерСчета
	|ПОМЕСТИТЬ СуммаНаСчетахДоговоров
	|ИЗ
	|	СчетаДоговора КАК СчетаДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(
	|				&МомВремени,
	|				НомерСчета В
	|						(ВЫБРАТЬ
	|							СчетаДоговора.СчетДоговора КАК СчетДоговора
	|						ИЗ
	|							СчетаДоговора КАК СчетаДоговора)
	|					И ТипРезерва В (&СписокТиповРезерва)) КАК уп_РезервыОстатки
	|		ПО СчетаДоговора.СчетДоговора = уп_РезервыОстатки.НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыНаСчетах.НомерСчета КАК НомерСчета,
	|	СуммыНаСчетах.СуммаОстаток КАК СуммаОстаток,
	|	СуммыНаСчетах.Сумма КАК Сумма,
	|	СуммыНаСчетах.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(СуммаНаСчетахДоговоров.СуммаНаСчетеДог, 0) КАК СуммаНаСчетеДог,
	|	ЕСТЬNULL(СуммаНаСчетахДоговоров.СчетДоговора, ЗНАЧЕНИЕ(Справочник.уп_ПенсионныеСчета.ПустаяСсылка)) КАК СчетДоговора
	|ИЗ
	|	СуммыНаСчетах КАК СуммыНаСчетах
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммаНаСчетахДоговоров КАК СуммаНаСчетахДоговоров
	|		ПО СуммыНаСчетах.НомерСчета = СуммаНаСчетахДоговоров.НомерСчета";

	Запрос.УстановитьПараметр("МомВремени", Новый Граница(Источник.МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	Запрос.УстановитьПараметр("ТипСхемы", Перечисления.уп_ТипыПенсионныхСхем.ПожизненныхВыплат);
	СписокТиповРезерва = Новый СписокЗначений;
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ИПС);
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.СПС);
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ПожизненныхВыплат);
	Запрос.УстановитьПараметр("СписокТиповРезерва", СписокТиповРезерва);

	НеХватаетСледВыплат = Новый Массив;

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаОстаток < Выборка.Сумма Тогда

			Если ЗначениеЗаполнено(Выборка.СчетДоговора) Тогда
				Если Выборка.СуммаНаСчетеДог < Выборка.Сумма Тогда
					//НеХватает = Выборка.Сумма - Выборка.СуммаОстаток;
					НеХватаетНаСчетеДоговора = Выборка.Сумма - Выборка.СуммаНаСчетеДог;
					НС = "№ " + Выборка.НомерСтроки + "    ";
					НеХватаетСледВыплат.Добавить(НС + "на счете договора " + Выборка.СчетДоговора + "    " + Строка(НеХватаетНаСчетеДоговора));
				КонецЕсли;
				Продолжить;
			КонецЕсли;

			НеХватает = Выборка.Сумма - Выборка.СуммаОстаток;
			НС = "№ " + Выборка.НомерСтроки + "    ";
			НеХватаетСледВыплат.Добавить(НС + Выборка.НомерСчета + "    " + Строка(НеХватает));
			ТекстСообщения = "По счету "+Выборка.НомерСчета+" нехватает средств на следующую выплату "+Строка(НеХватает);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Источник.ЭтотОбъект,,,);
		КонецЕсли;
	КонецЦикла;

	Если НеХватаетСледВыплат.Количество() > 0 Тогда
		Источник.ДополнительныеСвойства.Вставить("НеХватаетСледВыплат", НеХватаетСледВыплат);
	КонецЕсли;

	//предупреждение об окончании срока выплат по срочной пенсии
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
#Вставка 
	// +ХМНПФ 24.12.2024 ЛыткинАМ // Разганичить начисление и задолженность
	|	ЛОЖЬ КАК ЭтоЗадолдженность,
#КонецВставки 
	|	уп_НачислениеПенсийСписокПенсионеров.НомерСчета КАК НомерСчета,
	|	МАКСИМУМ(уп_НачислениеПенсийСписокПенсионеров.КонецПериодаНачисления) КАК КонецПериодаНачисления
	|ПОМЕСТИТЬ СписокСчетов
	|ИЗ
	|	Документ.уп_НачислениеПенсий.СписокПенсионеров КАК уп_НачислениеПенсийСписокПенсионеров
	|ГДЕ
	|	уп_НачислениеПенсийСписокПенсионеров.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_НачислениеПенсийСписокПенсионеров.НомерСчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
#Вставка 
	// +ХМНПФ 24.12.2024 ЛыткинАМ // Разганичить начисление и задолженность
	|	ИСТИНА,
#КонецВставки 
	|	уп_НачислениеПенсийnpo_НачислениеЗадолженностиПоПриостановкам.НомерСчета,
	|	МАКСИМУМ(уп_НачислениеПенсийnpo_НачислениеЗадолженностиПоПриостановкам.КонецПериодаНачисления)
	|ИЗ
	|	Документ.уп_НачислениеПенсий.НачислениеЗадолженностиПоПриостановкам КАК уп_НачислениеПенсийnpo_НачислениеЗадолженностиПоПриостановкам
	|ГДЕ
	|	уп_НачислениеПенсийnpo_НачислениеЗадолженностиПоПриостановкам.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_НачислениеПенсийnpo_НачислениеЗадолженностиПоПриостановкам.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетов.НомерСчета КАК НомерСчета,
#Вставка 
	// +ХМНПФ 24.12.2024 ЛыткинАМ // Разганичить начисление и задолженность
	|	СписокСчетов.ЭтоЗадолдженность КАК ЭтоЗадолдженность,
#КонецВставки 
	|	СписокСчетов.КонецПериодаНачисления КАК КонецПериодаНачисления
	|ПОМЕСТИТЬ СписокСчетовПоСрочным
	|ИЗ
	|	СписокСчетов КАК СписокСчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(&КонецМесяцаНачисления, ) КАК уп_СхемаСчетаСрезПоследних
	|		ПО СписокСчетов.НомерСчета = уп_СхемаСчетаСрезПоследних.НомерСчета
	|ГДЕ
	|	уп_СхемаСчетаСрезПоследних.Схема.ТипПенсионнойСхемы = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|	ИЛИ уп_СхемаСчетаСрезПоследних.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокСчетовПоСрочным.НомерСчета КАК НомерСчета,
	|	СписокСчетовПоСрочным.КонецПериодаНачисления КАК КонецПериодаНачисления,
#Вставка 
	// +ХМНПФ 24.12.2024 ЛыткинАМ // Разганичить начисление и задолженность
	|	СписокСчетовПоСрочным.ЭтоЗадолдженность КАК ЭтоЗадолдженность,
#КонецВставки 
	|	npo_ДатыОкончанияСрочнойВыплатыСрезПоследних.ДатаОкончанияСрочнойВыплаты КАК ДатаОкончанияСрочнойВыплаты,
	|	ПРЕДСТАВЛЕНИЕ(СписокСчетовПоСрочным.НомерСчета) КАК НомерСчетаТекст
	|ИЗ
	|	СписокСчетовПоСрочным КАК СписокСчетовПоСрочным
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_ДатыОкончанияСрочнойВыплаты.СрезПоследних(
	|				&КонецМесяцаНачисления,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						СписокСчетовПоСрочным.НомерСчета КАК НомерСчета
	|					ИЗ
	|						СписокСчетовПоСрочным КАК СписокСчетовПоСрочным)) КАК npo_ДатыОкончанияСрочнойВыплатыСрезПоследних
	|		ПО СписокСчетовПоСрочным.НомерСчета = npo_ДатыОкончанияСрочнойВыплатыСрезПоследних.НомерСчета
	|ГДЕ
	|	СписокСчетовПоСрочным.КонецПериодаНачисления >= npo_ДатыОкончанияСрочнойВыплатыСрезПоследних.ДатаОкончанияСрочнойВыплаты";

	Запрос.УстановитьПараметр("Ссылка",  Источник.Ссылка);
	Запрос.УстановитьПараметр("КонецМесяцаНачисления", КонецМесяца(Источник.ПериодНачисления)); 
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = "По счету № " + СокрЛП(Выборка.НомерСчетаТекст) + " дата окончания срока по срочной пенсии - " + Формат(Выборка.ДатаОкончанияСрочнойВыплаты, "ДЛФ=D");
#Вставка 
		// +ХМНПФ 24.12.2024 ЛыткинАМ // Разганичить начисление и задолженность
		Если Выборка.ЭтоЗадолдженность Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"По счету № ","По приостановленному счету № ");
		КонецЕсли;
#КонецВставки 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Источник.ЭтотОбъект,,,);
		Источник.ДополнительныеСвойства.МассивСообщений.Добавить(ТекстСообщения);
	КонецЦикла;

КонецПроцедуры

Функция ХМ_ПолучитьРегистрациюВНалоговомОргане(Организация, ДатаДокумента) Экспорт
	СписокПодписантов = Новый Массив;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	РегистрацииВНалоговомОргане.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	               |ГДЕ
	               |	(РегистрацииВНалоговомОргане.Владелец = &Организация
	               |			ИЛИ РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация)
	               |	И РегистрацииВНалоговомОргане.Код = &КодНО
	               |	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления";
				   
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КодНО", Организация.КодНалоговогоОргана);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", РегламентированнаяОтчетность.ГоловнаяОрганизация(Организация));
	
	Если Организация.КПП <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса + " И РегистрацииВНалоговомОргане.КПП = &КПП";
		Запрос.УстановитьПараметр("КПП", Организация.КПП);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Процедура ХМ_ПеренестиТЧпоСтруктуре(Приемник,Источник,СтруктураПоиска,УдалитьПрошлоеЗначение = Ложь) Экспорт
	ИсточникСтроки = Источник.НайтиСтроки(СтруктураПоиска);
	Для каждого ИсточникСтрока Из ИсточникСтроки Цикл
    	ПриемникСтрока = Приемник.Добавить();
		ЗаполнитьЗначенияСвойств(ПриемникСтрока,ИсточникСтрока);
		Если УдалитьПрошлоеЗначение Тогда
	    	Источник.Удалить(ИсточникСтрока);	
		КонецЕсли;
	КонецЦикла;                                               
	Если УдалитьПрошлоеЗначение Тогда
		Для каждого ИсточникСтрока Из ИсточникСтроки Цикл
	    	Источник.Удалить(ИсточникСтрока);	
		КонецЦикла;                                               
	КонецЕсли;
	
КонецПроцедуры

Функция ХМ_ПолучитьРезервыНачислений(МетаданныеЗначения_Имя = Неопределено) Экспорт
	Массив = Новый Массив();
	Если МетаданныеЗначения_Имя = "уп_ЗакрытиеСчетов"  Тогда
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);
	Иначе	
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНаследниковИПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНаследниковСПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковИПС);
		Массив.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковСПС);
	КонецЕсли;

	ВОзврат Массив;
КонецФункции

Процедура ХМ_ПеренестиРезервыНачислений(Приемник,Источник, МетаданныеЗначения_Имя, УдалитьПрошлоеЗначение = Ложь) Экспорт
	Для каждого ТипРезерва Из ХМ_ПолучитьРезервыНачислений(МетаданныеЗначения_Имя) Цикл
		уп_ОбщегоНазначенияСервер.ХМ_ПеренестиТЧпоСтруктуре(Приемник,Источник,Новый Структура("ТипРезерва",ТипРезерва),УдалитьПрошлоеЗначение);
	КонецЦикла;
КонецПроцедуры
