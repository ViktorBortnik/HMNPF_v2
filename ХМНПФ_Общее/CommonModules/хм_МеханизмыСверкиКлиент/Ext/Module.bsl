// +ХМНПФ 27.08.2025 КопыткоАА // http://redmine.npf.com/issues/4564
Функция ОбработатьВыделенное(СтрокаКоманда,ДополнительныеПараметры = Неопределено, Форма, ДанныеРасшифровки, Результат) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура();
	КонецЕсли;
	Если СтрокаКоманда = "ОповеститьОВыборе" 
		ИЛИ СтрокаКоманда = "ДобавитьСчетаВИсключение"
		// +ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625
		ИЛИ СтрокаКоманда = "УточнитьНастройкиМСФО"
		// -ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625
        Тогда
		ДополнительныеПараметры.Вставить("СчитатьВсюСтроку");
	КонецЕсли;

							
	МассивРассшифровки = Новый Массив();
    ПоляДляУточнения = Новый Структура();
	Если ТипЗНЧ(Результат) = Тип("ТабличныйДокумент") Тогда
		ИдетификаторДляХранилища =  Новый УникальныйИдентификатор();
    	АдресРезультата = ПоместитьВоВременноеХранилище(Результат,ИдетификаторДляХранилища);	
		Для Каждого Область Из Результат.ВыделенныеОбласти Цикл
			Если ТипЗнч(Область) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				Верх = Область.Верх;
				Низ = Область.Низ;
				Лево = Область.Лево;
				Право = Область.Право;
				
				Если ДополнительныеПараметры.Свойство("СчитатьВсюСтроку") Тогда
					Лево = 1;
					Право = Результат.ШиринаТаблицы;
				КонецЕсли; 
				Если ДополнительныеПараметры.Свойство("ОбходитьВсе") Тогда
					Лево = 1;
					Право = Результат.ШиринаТаблицы;
					Верх = 1;
					Низ = Результат.ВысотаТаблицы
				КонецЕсли; 
				
				НовыйМассивРассшифровки = хм_МеханизмыСверкиСервер.ОбработатьВыделенноеНаСервере(СтрокаКоманда,Верх,Низ,Лево,Право,ДополнительныеПараметры, ДанныеРасшифровки, АдресРезультата);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивРассшифровки,НовыйМассивРассшифровки);
			КонецЕсли;
			Если ДополнительныеПараметры.Свойство("ОбходитьВсе") Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		УдалитьИзВременногоХранилища(АдресРезультата);	
	ИначеЕсли ТИПЗНЧ(Результат) = ТИП("ТаблицаФормы") Тогда
		МассивВыделенныхСтрок = Новый Массив;		
		Для каждого Область из Результат.ВыделенныеСтроки Цикл
			МассивВыделенныхСтрок.Добавить(Область);
		КонецЦикла;
		Для каждого Область из МассивВыделенныхСтрок Цикл		
			Результат.ТекущаяСтрока = Область;
			ТекущиеДанные = Результат.ТекущиеДанные; 
			НовыйМассивРассшифровки = хм_МеханизмыСверкиСервер.ОбработатьВыделенноеНаСервере(СтрокаКоманда,0,0,0,0,ДополнительныеПараметры, ДанныеРасшифровки, ТекущиеДанные);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивРассшифровки,НовыйМассивРассшифровки);			
		КонецЦикла;		
	КонецЕсли;	
	
	Если СтрокаКоманда = "ОповеститьОВыборе" Тогда 
		ДополнительныеПараметры.Вставить("МассивСверки",МассивРассшифровки);
		ДополнительныеПараметры.Вставить("КлючЗапуска", Новый УникальныйИдентификатор());
		Оповестить(ДополнительныеПараметры.ИмяСобытия, ДополнительныеПараметры, Форма);
	ИначеЕсли СтрокаКоманда = "ДобавитьСчетаВИсключение" Тогда 
		ДобавитьСчетаВИсключение_ОткрытьФорму(МассивРассшифровки, ДополнительныеПараметры, Форма);
	// +ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625
	ИначеЕсли СтрокаКоманда = "УточнитьНастройкиМСФО" Тогда
		УточнитьНастройкиМСФО_ОткрытьФорму(МассивРассшифровки, ДополнительныеПараметры, Форма);
	// -ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625
	КонецЕсли;
	
	Возврат МассивРассшифровки; 
КонецФункции 

Процедура ДобавитьСчетаВИсключение_ОткрытьФорму(МассивРассшифровки, ДополнительныеПараметры, Форма)
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("РежимУточнения",МассивРассшифровки);  
	
	ПараметрыОткрытияФормы.Вставить("ДополнительныеПараметры",ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("МассивРассшифровки",МассивРассшифровки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСчетаВИсключениеНаКлиенте", ЭтотОбъект ,ДополнительныеПараметры);

 	ОткрытьФорму("РегистрСведений.ХМ_Кейсы.Форма.ФормаУстановкиКейсов",ПараметрыОткрытияФормы,Форма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры   

Процедура ДобавитьСчетаВИсключениеНаКлиенте(ТребуемоеУтонение,МассивРассшифровки) Экспорт
	Если ЗначениеЗаполнено(ТребуемоеУтонение) Тогда 
		хм_МеханизмыСверкиСервер.ДобавитьСчетаВИсключениеНаСервере(ТребуемоеУтонение,МассивРассшифровки);
	КонецЕсли;
КонецПроцедуры  

// +ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625
Процедура УточнитьНастройкиМСФО_ОткрытьФорму(МассивРассшифровки, ДополнительныеПараметры, Форма)
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("РежимУточнения",МассивРассшифровки);  
	Префикс = "МСФО.";
	Для Инд = 0 По МассивРассшифровки.Количество() - 1 Цикл
		Попытка
			ТипДокумента = Новый(МассивРассшифровки[Инд].Получить(Префикс + "ТипДокумента"));
			Прервать;
		Исключение
		КонецПопытки; 
		Попытка
			ТипДокумента = Новый(МассивРассшифровки[Инд].Получить("ТипДокумента"));
			Префикс = "";
			Прервать;
		Исключение
		КонецПопытки;	
	КонецЦикла;
	
	ИзмеренияРегистраСведений = Новый Структура();
	Если НЕ МассивРассшифровки[Инд].Получить(Префикс + "Документ") = НЕОПРЕДЕЛЕНО И НЕ МассивРассшифровки[Инд].Получить(Префикс + "Документ").Пустая() Тогда
		ИзмеренияРегистраСведений.Вставить("ТипДокумента",МассивРассшифровки[Инд].Получить(Префикс + "Документ"));
	Иначе
		ИзмеренияРегистраСведений.Вставить("ТипДокумента",Новый(МассивРассшифровки[Инд].Получить(Префикс + "ТипДокумента")));		
	КонецЕсли;
	ИзмеренияРегистраСведений.Вставить("ТипВыплаты",МассивРассшифровки[Инд].Получить(Префикс + "ТипВыплаты"));
	ИзмеренияРегистраСведений.Вставить("ВидРучнойОперации",МассивРассшифровки[Инд].Получить(Префикс + "ВидРучнойОперации"));
	ИзмеренияРегистраСведений.Вставить("ТипРезерва",МассивРассшифровки[Инд].Получить(Префикс + "ТипРезерва"));
	ИзмеренияРегистраСведений.Вставить("ВидДеятельности",МассивРассшифровки[Инд].Получить(Префикс + "ВидДеятельности"));
	Если ДополнительныеПараметры.Свойство("УточнениеПоПустомуРезерву") Тогда
		Если ИзмеренияРегистраСведений.ВидДеятельности = ПредопределенноеЗначение("Перечисление.уп_ВидыДеятельности.ПДС") Тогда	
		    ИзмеренияРегистраСведений.Вставить("ТипРезерва",ПредопределенноеЗначение("Перечисление.пдс_ТипыРезервовПДС.ПустаяСсылка"));
		Иначе 
			ИзмеренияРегистраСведений.Вставить("ТипРезерва",ПредопределенноеЗначение("Перечисление.уп_ТипыРезервовОПС.ПустаяСсылка"));
		КонецЕсли;		
	КонецЕсли;			
	// Формируем массив для ключа записи  
	КлючРегистраСведений = Новый Массив;  
	КлючРегистраСведений.Добавить(ИзмеренияРегистраСведений);  
	// Создаём ключ записи регистра  
	КлючЗаписьРегистраСведений = Новый("РегистрСведенийКлючЗаписи.хм_МСФО_ОПС_МаппингДП", КлючРегистраСведений);  
	// Подготавливаем параметры для открытия формы  
	ПараметрыОткрытияФормы.Вставить("Ключ", КлючЗаписьРегистраСведений);
	ПараметрыОткрытияФормы.Вставить("ДополнительныеПараметры",ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("МассивРассшифровки",МассивРассшифровки);
	ДополнительныеПараметры.Вставить("Префикс",Префикс);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УточнитьНастройкиМСФОНаКлиенте", ЭтотОбъект, ДополнительныеПараметры);
	// Открываем форму записи регистра
	Попытка
	 	ОткрытьФорму("РегистрСведений.хм_МСФО_ОПС_МаппингДП.Форма.ХМ_ФормаЗаписи",ПараметрыОткрытияФормы,Форма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	 	//ОткрытьФорму("ВнешнийОтчет.ПроверкаЦБ_ОПС_ОперацииПоДоговорам_МСФО_Регистр.Форма.ФормаЗаписи",ПараметрыОткрытияФормы,Форма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Исключение                                                         
		Если НЕ ДополнительныеПараметры.Свойство("УточнениеПоПустомуРезерву") Тогда     
			ДополнительныеПараметры.Вставить("УточнениеПоПустомуРезерву");
	   		УточнитьНастройкиМСФО_ОткрытьФорму(МассивРассшифровки, ДополнительныеПараметры, Форма); 
		Иначе
			ПоказатьПредупреждение(,ОписаниеОшибки());
		КонецЕсли;
	КонецПопытки;
КонецПроцедуры  

Процедура УточнитьНастройкиМСФОНаКлиенте(ТребуемоеУтонение,МассивРассшифровки) Экспорт
	Если ЗначениеЗаполнено(ТребуемоеУтонение) Тогда 
		хм_МеханизмыСверкиСервер.УточнитьНастройкиМСФОНаСервере(ТребуемоеУтонение,МассивРассшифровки);
	КонецЕсли;
КонецПроцедуры  

// -ХМНПФ 11.09.2025 КопыткоАА // http://redmine.npf.com/issues/4625

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник, Форма, Объект) Экспорт
	Если ИмяСобытия = "ПередатьНаСверку" Тогда //И Не Источник.ИмяФормы = ЭтаФорма.ИмяФормы Тогда
		Если Источник.УникальныйИдентификатор = Форма.УникальныйИдентификатор Тогда
			Возврат;	
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма,"ГлавнаяФорма") Тогда
			Если Форма.ГлавнаяФорма Тогда
				Возврат;
			КонецЕсли;		
		КонецЕсли; 
			
		Если Параметр.Свойство("ИмяПоля") И Параметр.ИмяПоля = "НомерСчета" Тогда
			сз = Новый СписокЗначений;  
			сз.ЗагрузитьЗначения(Параметр.МассивСверки);  
			ДобавитьПользовательскийОтбор(Объект, Форма,
				Новый ПолеКомпоновкиДанных(Параметр.ИмяПоля),
				сз,
				ВидСравненияКомпоновкиДанных.ВСписке,
				Истина,
				Новый Структура("ВПользовательскиеНастройки, ЗаменятьСуществующий", Истина, Истина));
		КонецЕсли;     
		Если Параметр.Свойство("ПоляДляУточнения") И Параметр.Свойство("ВозвращатьСтроками") Тогда
			Для каждого ПолеСверки Из Параметр.ПоляДляУточнения Цикл
				МассивСверки = Новый Массив();
				Для каждого СтрокаСверки Из Параметр.МассивСверки Цикл
					МассивСверки.Добавить(СтрокаСверки[ПолеСверки.Ключ]);		
				КонецЦикла;                                              
				сз = Новый СписокЗначений;  
				сз.ЗагрузитьЗначения(МассивСверки);  
				ДобавитьПользовательскийОтбор(Объект, Форма,
					Новый ПолеКомпоновкиДанных(ПолеСверки.Ключ),
					сз,
					ВидСравненияКомпоновкиДанных.ВСписке,
					Истина,
					Новый Структура("ВПользовательскиеНастройки, ЗаменятьСуществующий", Истина, Истина));
			КонецЦикла;
		КонецЕсли;     
		Форма.Активизировать(); 
		ФормироватьПриУточнении = Ложь;
		Если Параметр.Свойство("ФормироватьПриУточнении",ФормироватьПриУточнении) Тогда
			Если ФормироватьПриУточнении Тогда
				Форма.СкомпоноватьРезультат(РежимКомпоновкиРезультата.Непосредственно);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;                
КонецПроцедуры  

Процедура ДобавитьПользовательскийОтбор(Объект, Форма, Знач ПолеКомпоновки, Знач ЗначениеОтбора, ВидСравнения, Использование, ДополнительныеПараметры) Экспорт    
	
	Если ТипЗнч(ПолеКомпоновки) = Тип("Строка") Тогда
		ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ПолеКомпоновки);
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеОтбора) = Тип("Массив") Тогда
		сз = Новый СписокЗначений;  
		сз.ЗагрузитьЗначения(ЗначениеОтбора);
		ЗначениеОтбора = сз;
	КонецЕсли;
	
	Настройки = Объект.КомпоновщикНастроек.Настройки;
	ОтборНастройки = Объект.КомпоновщикНастроек.Настройки.Отбор;
	
	Если ОтборНастройки.Элементы.Количество() = 0 И ТипЗнч(Объект) = Тип("ДинамическийСписок") Тогда
		ДобавитьПользовательскийОтборКогдаНетОтбора(Объект, Форма, ПолеКомпоновки, ЗначениеОтбора, ВидСравнения, Использование, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	ЭлементыПользовательскиеНастройки = Объект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;

	ЭлементОтбора = ПолучитьЭлементОтбораПоСтруктуре(ПолеКомпоновки, Настройки);
	
	Если ЭлементОтбора = Неопределено Тогда
	   ЭлементОтбора = ОтборНастройки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	   ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки;
	   ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	   ЭлементОтбора.Использование = Использование;
	   ЭлементОтбора.ВидСравнения = ВидСравнения;
	   ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;	   
		
	   //Создаем идентификатор пользовательской настройки, чтобы отбор добавился в пользовательские настройки
	   ЭлементОтбора.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
	   //работает одинаково и без этого
		//хм_МеханизмыСверкиСервер.СоздатьЭлементыФормыПользовательскихНастроекНаСервере(Форма);
	КонецЕсли;
		
	//Находим отбор в пользовательских настройках по идентификатору
	ОтборПоСкладуПользовательскиеНастройки = ЭлементыПользовательскиеНастройки.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
	ОтборПоСкладуПользовательскиеНастройки.Использование = Использование;
	ОтборПоСкладуПользовательскиеНастройки.ВидСравнения = ВидСравнения;
	ОтборПоСкладуПользовательскиеНастройки.ПравоеЗначение = ЗначениеОтбора;
КонецПроцедуры   

Процедура ДобавитьПользовательскийОтборКогдаНетОтбора(Объект, Форма, ПолеКомпоновки, ЗначениеОтбора, ВидСравнения, Использование, ДополнительныеПараметры)     
	
	ОтборНастройки = Объект.КомпоновщикНастроек.Настройки.Отбор; 
	Настройки = Объект.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	ЭлементыПользовательскиеНастройки = Объект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	ЭлементОтбора = ПолучитьЭлементОтбора(ПолеКомпоновки, Настройки);
	
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = ОтборНастройки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		ЭлементОтбора.Использование = Использование;
		ЭлементОтбора.ВидСравнения = ВидСравнения;
		ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;	   
		//Создаем идентификатор пользовательской настройки, чтобы отбор добавился в пользовательские настройки
		ЭлементОтбора.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор; 
		//работает одинаково и без этого
		// хм_МеханизмыСверкиСервер.СоздатьЭлементыФормыПользовательскихНастроекНаСервере(Форма, Истина);
	КонецЕсли;
	
	//Находим отбор в пользовательских настройках по идентификатору
	//ОтборПоСкладуПользовательскиеНастройки = ЭлементыПользовательскиеНастройки.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
	ЭлементОтбора.Использование = Использование;
	ЭлементОтбора.ВидСравнения = ВидСравнения;
	ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;
КонецПроцедуры

Функция ПолучитьЭлементОтбораПоСтруктуре(ПолеКомпоновки, Настройка)
	ЭлементОтбора = ПолучитьЭлементОтбора(ПолеКомпоновки,Настройка.Отбор); 
	Если Не ЭлементОтбора = Неопределено Тогда
		Возврат ЭлементОтбора; 
	КонецЕсли;
	Для каждого НастройкиСтроки Из Настройка.Структура Цикл
		ЭлементОтбора = ПолучитьЭлементОтбораПоСтруктуре(ПолеКомпоновки,НастройкиСтроки);
		Если Не ЭлементОтбора = Неопределено Тогда
			Возврат ЭлементОтбора; 
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено; 
КонецФункции

Функция ПолучитьЭлементОтбора(ПолеКомпоновки, ГруппаОтбора)
	Попытка
		Для каждого ТекЭлементОтбора Из ГруппаОтбора.Элементы Цикл
			Если ТипЗнч(ТекЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
				и ТекЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
				Возврат ТекЭлементОтбора;
			ИначеЕсли НЕ ТипЗнч(ТекЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")	Тогда   
				ЭлементОтбора  = ПолучитьЭлементОтбора(ПолеКомпоновки, ТекЭлементОтбора);
				Если Не ЭлементОтбора = Неопределено Тогда
					Возврат ЭлементОтбора;	
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла; 
	Исключение
		//на случай если нет элементов у объекта в принципе
	КонецПопытки;		
	Возврат Неопределено;
КонецФункции 

Процедура СКД_ДобавитьВЕЖ(Команда,Форма) Экспорт
КонецПроцедуры
// -ХМНПФ 27.08.2025 КопыткоАА // http://redmine.npf.com/issues/4564


