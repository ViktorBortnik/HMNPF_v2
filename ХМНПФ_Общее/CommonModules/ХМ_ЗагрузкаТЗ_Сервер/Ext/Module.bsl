Функция Загрузка_ЗагрузитьТЗ_ТребуетсяОбработкаПроведения(Знач МетаданныеЗначения_Имя, Знач ПотоковаяЗагрузка, Знач СтрокаТаблицы)
	
	// ХМНПФ+ 22.02.2024 ЛыткинАМ // ОПС Настройка проведения при загрузке // №3792
	Если (СтрокаТаблицы.Свойство("ПроводитьЧерезЗагрузку") И СтрокаТаблицы.ПроводитьЧерезЗагрузку)
		ИЛИ 
		(ПотоковаяЗагрузка = ЛОЖЬ И (МетаданныеЗначения_Имя = "уп_РешениеОЕдиновременнойВыплате" 
		ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОНазначенииСрочнойВыплаты"
		ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОНазначенииНЧТП"
		ИЛИ МетаданныеЗначения_Имя = "уп_РегистрацияПолучателейОПС"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеПравопреемникаОВыплатеОПС"
		ИЛИ МетаданныеЗначения_Имя = "уп_ИндексацияНЧТП"
		ИЛИ МетаданныеЗначения_Имя = "уп_ИндексацияСрочнойВыплаты"
		ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОВыплатеСредствПенсионныхНакоплений"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗакрытиеДоговораОПС"
		ИЛИ МетаданныеЗначения_Имя = "уп_НачислениеПенсийОПС"
		ИЛИ МетаданныеЗначения_Имя = "ХМ_pf_ops_reserv"
		ИЛИ МетаданныеЗначения_Имя = "ХМ_pf_fond_npf_pfr"
		ИЛИ МетаданныеЗначения_Имя = "уп_РаспределениеИДОПС"
		ИЛИ МетаданныеЗначения_Имя = "ХМ_corr_cpn_oper"
		ИЛИ МетаданныеЗначения_Имя = "уп_ОткрытиеДоговораОПС"
		ИЛИ МетаданныеЗначения_Имя = "уп_ВыплатныеРеестры"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеЗЛОЕдиновременнойВыплате"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеЗЛОНазначенииНЧТП"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеЗЛОНазначенииСрочнойВыплаты"
		ИЛИ МетаданныеЗначения_Имя = "уп_ДокументРегистрации"
		// +ХМНПФ 22.06.2016 ШадринАВ // Загрузка_ЗагрузитьТЗ // №386
		ИЛИ МетаданныеЗначения_Имя = "уп_ОткрытиеДоговора"
		// +ХМНПФ 22.08.2016 ШадринАВ // Загрузка_ЗагрузитьТЗ // №484
		ИЛИ МетаданныеЗначения_Имя = "уп_ПоступлениеВзносов"
		// +ХМНПФ 03.11.2016 ШадринАВ // Загрузка_ЗагрузитьТЗ // №631
		ИЛИ МетаданныеЗначения_Имя = "уп_ДополнительноеСоглашение"
		// +ХМНПФ 09.01.2017 ШадринАВ // Загрузка_ЗагрузитьТЗ // №742
		ИЛИ МетаданныеЗначения_Имя = "ХМ_ПеререгистрацияУчастника"
		// +ХМНПФ 27.01.2017 ШадринАВ // Загрузка_ЗагрузитьТЗ // №779
		ИЛИ МетаданныеЗначения_Имя = "уп_НачислениеНДФЛ"
		ИЛИ МетаданныеЗначения_Имя = "уп_НачислениеПенсий"
		ИЛИ МетаданныеЗначения_Имя = "уп_Распоряжение"
		ИЛИ МетаданныеЗначения_Имя = "уп_РаспределениеВзносов"
		ИЛИ МетаданныеЗначения_Имя = "уп_РаспределениеИД"
		ИЛИ МетаданныеЗначения_Имя = "уп_ПеремещениеПенсионныхРезервов"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗакрытиеДоговораВкладчиком"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеНаПредоставлениеВычетов"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеУчастникаОРасторженииНПО"
		ИЛИ МетаданныеЗначения_Имя = "уп_ЗаявлениеПравопреемникаОВыплатеНПО"
		ИЛИ МетаданныеЗначения_Имя = "уп_ИзменениеРазмеровПенсионныхВыплат"
		ИЛИ МетаданныеЗначения_Имя = "ХМ_ДополнительноеСоглашение"
		ИЛИ МетаданныеЗначения_Имя = "ХМ_ПрекращениеВыплатыПенсии"
		// +ХМНПФ 07.06.2017 ШадринАВ // Загрузка_ЗагрузитьТЗ // №1032
		ИЛИ МетаданныеЗначения_Имя = "ХМ_ЗаявленияДСВ"
		ИЛИ МетаданныеЗначения_Имя = "пдс_ОткрытиеДоговораПДС"))
	Тогда                          
		Возврат Истина;
	Иначе
		Возврат Ложь;		
	КонецЕсли;
	
КонецФункции


Функция Загрузка_ЗагрузитьТЗ_ЕстьКолонка(ТЗ) 
	
	ЕстьКолонка = Новый Структура();
	
	//https://redmine.npf.com/issues/4302
	ЕстьКолонка.Вставить("УникальныйИдентификатор",Не ТЗ.Колонки.Найти("GUID_УИ") = НЕОПРЕДЕЛЕНО);

	//https://redmine.npf.com/issues/4303
	ЕстьКолонка.Вставить("НаУдаление",Не ТЗ.Колонки.Найти("УдалитьДокумент") = НЕОПРЕДЕЛЕНО);
	ЕстьКолонка.Вставить("ПометитьНаУдаление",Не ТЗ.Колонки.Найти("ТолькоПометитьНаУдаление") = НЕОПРЕДЕЛЕНО);
	ЕстьКолонка.Вставить("НеПерегружатьДокумент",Не ТЗ.Колонки.Найти("НеПерегружатьДокумент") = НЕОПРЕДЕЛЕНО);

	// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
	ЕстьКолонка.Вставить("УдалитьДокументИДругиеРесурсы",Не ТЗ.Колонки.Найти("УдалитьДокументИДругиеРесурсы") = НЕОПРЕДЕЛЕНО);	
	ЕстьКолонка.Вставить("ОтправлятьДанныеНаПочту",Не ТЗ.Колонки.Найти("ОтправлятьДанныеНаПочту") = НЕОПРЕДЕЛЕНО);	
	// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607

	ЕстьКолонка.Вставить("ПропуститьНовыйДокумент",Не ТЗ.Колонки.Найти("ПропуститьНовыйДокумент") = Неопределено);
	ЕстьКолонка.Вставить("ПочтаДляУведомления",Не ТЗ.Колонки.Найти("ПочтаДляУведомления") = НЕОПРЕДЕЛЕНО);
	ЕстьКолонка.Вставить("ТелоПисьмаДляУведомления",Не ТЗ.Колонки.Найти("ТелоПисьмаДляУведомления") = НЕОПРЕДЕЛЕНО);
	ЕстьКолонка.Вставить("ТемаПисьмаДляУведомления",Не ТЗ.Колонки.Найти("ТемаПисьмаДляУведомления") = НЕОПРЕДЕЛЕНО);
	
	Возврат ЕстьКолонка;
	
КонецФункции	


&Вместо("Загрузка_ЗагрузитьТЗ")
Функция ХМЗагрузка_ЗагрузитьТЗ(ПравилаЗагрузкиКлиент, ПараметрыЗагрузки, ЛогОшибок, ДополнительныеПараметры)
                                         
	Структура_Реквизиты = Неопределено;
	ПравилаЗагрузкиКлиент.Реквизиты.Свойство("Реквизиты",Структура_Реквизиты);
	Если Структура_Реквизиты = Неопределено Тогда Структура_Реквизиты = Новый Структура(); КонецЕсли;

	Структура_ТабличныеЧасти = Неопределено;
	ПравилаЗагрузкиКлиент.Свойство("ТабличныеЧасти",Структура_ТабличныеЧасти);
	Если Структура_ТабличныеЧасти = Неопределено Тогда Структура_ТабличныеЧасти = Новый Структура(); КонецЕсли;


	Структура_Движения = Неопределено;
	ПравилаЗагрузкиКлиент.Свойство("Движения",Структура_Движения);
	Если Структура_Движения = Неопределено Тогда Структура_Движения = Новый Структура(); КонецЕсли;

	Структура_ДвиженияПослеПроведения = Неопределено;
	ПравилаЗагрузкиКлиент.Свойство("ДвиженияПослеПроведения",Структура_ДвиженияПослеПроведения);
	Если Структура_ДвиженияПослеПроведения = Неопределено Тогда Структура_ДвиженияПослеПроведения = Новый Структура(); КонецЕсли;

	Контекст = Новый Структура();
	Контекст.Вставить("Структура_Реквизиты",Структура_Реквизиты);
	Контекст.Вставить("СтруктураТЧ",Структура_ТабличныеЧасти);
	Контекст.Вставить("СтруктураДвижения",Структура_Движения);
	Контекст.Вставить("СтруктураДвиженияПослеПроведения",Структура_ДвиженияПослеПроведения);

	#Область Инициализация
	ПотоковаяЗагрузка = Ложь;
	ФиксироватьЗаписьДокумента = Истина;
	Если Не ДополнительныеПараметры = Неопределено Тогда
		ПотоковаяЗагрузка = ДополнительныеПараметры.Свойство("ПотоковаяЗагрузка");	
	КонецЕсли;

	Реквизиты_УЗЕЛ = Структура_Реквизиты.Узел;
	Реквизиты_поУмолчанию = Структура_Реквизиты.Реквизиты_поУмолчанию;
	Реквизиты_ИзОбъекта = Структура_Реквизиты.Реквизиты_ИзОбъекта;
	МетаданныеЗначения_Имя = ДополнительныеПараметры.СтрокаТаблицы.ИмяТаблицы;
	Если ПараметрыЗагрузки.ЗагрузкаНеСвязанных Тогда
		Реквизиты_поУмолчанию.Вставить("ТипДокумента",МетаданныеЗначения_Имя);
		МетаданныеЗначения_Имя = "ХМ_НеПривязанныеРешения";
	КонецЕсли;	

	ТЗ = Структура_Реквизиты.ТЗ;
	ТипОбъекта = ДополнительныеПараметры.СтрокаТаблицы.ТипОбъекта;


	Результат = Истина;
	Каспер_Поле = Реквизиты_УЗЕЛ.Каспер_поле;
	ИмяРегистраСведений = Реквизиты_УЗЕЛ.Тип;
	НЗ = РегистрыСведений[ИмяРегистраСведений].СоздатьНаборЗаписей();
	// +ХМНПФ 20.10.2023 ХлопцевБА // Ежедневная загрузка НПО в тонком клиенте // №3697
	нзКасперЗагрузка = РегистрыСведений.хм_КасперЗагрузка.СоздатьМенеджерЗаписи();  
	// -ХМНПФ 20.10.2023 ХлопцевБА // Ежедневная загрузка НПО в тонком клиенте // №3697
	МетаданныеРС = Метаданные.РегистрыСведений[Реквизиты_УЗЕЛ.Тип];
	Поле1С = ХМ_ЗагрузкаТЗ_Сервер.ПолучитьПоле1С(Реквизиты_УЗЕЛ.Тип,ПолучитьМетаТип_по_ТипОбъекта(ТипОбъекта)+МетаданныеЗначения_Имя);
	ФильтрПоПолю1С = Ложь;
	ЗначениеОтбораПоПолю1С = "";
	Если МетаданныеРС.Измерения.Количество() = 1 Тогда
		ИзмерениеКаспер_Поле = МетаданныеРС.Измерения[0].Имя;	
	ИначеЕсли МетаданныеРС.Измерения.Количество() = 2 и МетаданныеРС.Измерения[0].Имя = "ИмяПоля1С" Тогда
		ИзмерениеКаспер_Поле = МетаданныеРС.Измерения[1].Имя;
		ФильтрПоПолю1С = Истина;
		ЗначениеОтбораПоПолю1С = Реквизиты_УЗЕЛ.Каспер_Условие;
	Иначе
		ВызватьИсключение "Что-то не то с регистром сведений";
	КонецЕсли;

	Если ТипОбъекта = 0 Тогда
		МенеджерЗначения = Справочники[МетаданныеЗначения_Имя];
	ИначеЕсли ТипОбъекта = 1 Тогда
		МенеджерЗначения = Документы[МетаданныеЗначения_Имя];
	ИначеЕсли ТипОбъекта = 2 Тогда
		МенеджерЗначения = Перечисления[МетаданныеЗначения_Имя];
	Иначе
		ВызватьИсключение "Не найден менеджер объекта";
	КонецЕсли;
	МетаданныеЗначения = Метаданные.НайтиПоПолномуИмени(ПолучитьМетаТип_по_ТипОбъекта(ТипОбъекта)+МетаданныеЗначения_Имя);
	Если Не МетаданныеЗначения.Реквизиты.Найти(Каспер_Поле) = Неопределено Тогда
		ВызватьИсключение("Соответствие полей");	
	КонецЕсли;
	Если Не МетаданныеЗначения.Реквизиты.Найти(Поле1С) = Неопределено Тогда
		ВызватьИсключение("Соответствие полей");	
	КонецЕсли;

	ТекЗапись=0;
	КоличествоЗаписей = ТЗ.Количество();
	ТекДата = ТекущаяДата()-1;
	ТипСсылкиНаОбъект = ТипЗнч(МенеджерЗначения.ПустаяСсылка());
	СоответствиеЗаменыТипов = ПолучитьСоответствиеЗаменыТипов(); 
	СоответствиеМетаданныеДляПоискаСсылок = ПолучитьМетаданныеДляПоискаСсылок();
	#КонецОбласти
	
	ЕстьКолонка = Загрузка_ЗагрузитьТЗ_ЕстьКолонка(ТЗ);

	Для каждого СтрокаТЗ Из ТЗ Цикл

		ТекЗапись = ТекЗапись + 1;
		Закончится = ТекущаяДата() + (КоличествоЗаписей - ТекЗапись)*(ТекущаяДата()-ТекДата)/ТекЗапись;
		ВыйтиИзЦикла = Ложь;
		Если ВыйтиИзЦикла = Истина Тогда
			Результат = Ложь;
			ЛогОшибок.Добавить("Обработка прервана пользователем"); 
			Прервать;
		КонецЕсли;
		Попытка

			#Область Объект_Инициализация
			НачатьТранзакцию();

			Значение1С = Неопределено;
			Значение1СОбъект = Неопределено;
			Если ЗначениеЗаполнено(СтрокаТЗ[Поле1С]) Тогда
				Значение1СОбъект = СтрокаТЗ[Поле1С].ПолучитьОбъект();
				Если Не Значение1СОбъект = Неопределено Тогда
					Значение1С = Значение1СОбъект.Ссылка;
				КонецЕсли;
			КонецЕсли;

			Если Реквизиты_поУмолчанию.Свойство("МаппингПоРеквизитам") И ЗначениеЗаполнено(СтрокаТЗ[Реквизиты_поУмолчанию.МаппингПоРеквизитам]) Тогда
				Значение1С_ПоРеквизитам = СтрокаТЗ[Реквизиты_поУмолчанию.МаппингПоРеквизитам];
				Если Не ЗначениеЗаполнено(Значение1С) Тогда
					СтрокаТЗ[Поле1С] = Значение1С_ПоРеквизитам;
					Значение1СОбъект = СтрокаТЗ[Поле1С].ПолучитьОбъект();
					Если Не Значение1СОбъект = Неопределено Тогда
						Значение1С = Значение1СОбъект.Ссылка;
					КонецЕсли;

					НЗ.Очистить();
					НЗ.Отбор[ИзмерениеКаспер_Поле].Установить(СтрокаТЗ[Каспер_Поле]);
					Если ФильтрПоПолю1С = Истина Тогда
						НЗ.Отбор["ИмяПоля1С"].Установить(ЗначениеОтбораПоПолю1С);
					КонецЕсли;

					МЗ = НЗ.Добавить();
					Если ФильтрПоПолю1С = Истина Тогда
						МЗ["ИмяПоля1С"] = ЗначениеОтбораПоПолю1С;
					КонецЕсли;
					МЗ[ИзмерениеКаспер_Поле] = СтрокаТЗ[Каспер_Поле];
					МЗ[Поле1С] = Значение1С;
					ЗаписатьОбъект(НЗ); 
					ЗафиксироватьТранзакцию();
					НачатьТранзакцию();	

				ИначеЕсли Не Значение1С_ПоРеквизитам =  Значение1С Тогда
					ЛогОшибок.Добавить("Не соответствие текущего маппинг по ID " + СтрокаТЗ[Каспер_Поле]); 
					ЗафиксироватьТранзакцию();
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			//https://redmine.npf.com/issues/4302
			ОбновитьУИ = Ложь;
			УникальныйИдентификатор = Неопределено;	
			Если ЕстьКолонка.УникальныйИдентификатор Тогда
				УникальныйИдентификатор = Новый УникальныйИдентификатор(СтрокаТЗ.GUID_УИ);
				Если Не Значение1СОбъект = Неопределено Тогда
					ОбновитьУИ = Не Значение1С.УникальныйИдентификатор() = УникальныйИдентификатор;						
				Иначе
					ОбновитьУИ = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Не Значение1СОбъект = Неопределено И (
				НЕ ТипСсылкиНаОбъект = ТипЗнч(Значение1С) 
				ИЛИ ОбновитьУИ
				ИЛИ (ЕстьКолонка.НаУдаление И СтрокаТЗ.УдалитьДокумент = Истина)
				ИЛИ (ЕстьКолонка.ПометитьНаУдаление И СтрокаТЗ.ТолькоПометитьНаУдаление = Истина) 
				// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				ИЛИ (ЕстьКолонка.УдалитьДокументИДругиеРесурсы И СтрокаТЗ.УдалитьДокументИДругиеРесурсы = ИСТИНА)
				// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607	
				) Тогда
				ДействиеЗамены = Новый Структура("ОшибкаОпределенияОбъекта");

				Если ОбновитьУИ = Истина 
					ИЛИ (ЕстьКолонка.НаУдаление И СтрокаТЗ.УдалитьДокумент = Истина) 
					Тогда
					ДействиеЗамены = Новый Структура("Удалить");	

					Если ТипОбъекта = 0 Тогда
						ДействиеЗамены.Вставить("ПроверитьСсылки");
					ИначеЕсли ТипОбъекта = 1 Тогда
						ДействиеЗамены.Вставить("ОтменитьПроведение");
						ДействиеЗамены.Вставить("ОчиститьДвижения");
					ИначеЕсли ТипОбъекта = 2 Тогда
						ОтменитьТранзакцию();	
						ЛогОшибок.Добавить("ОшибкаОпределенияОбъекта, "+СтрокаТЗ[Каспер_Поле]+" по причине: не соответствие типа найденного по ИД объекта: "+Значение1С);
						Отказ = Истина;
						Результат = Ложь;
						Продолжить;
					КонецЕсли;
				ИначеЕсли
					(ЕстьКолонка.ПометитьНаУдаление И СтрокаТЗ.ТолькоПометитьНаУдаление = Истина) 
					Тогда
					Если Значение1СОбъект.ПометкаУдаления = Истина Тогда
						ЗафиксироватьТранзакцию();	
						Продолжить;
					КонецЕсли;  

					ДействиеЗамены = Новый Структура("ПометитьНаУдаление");	

					Если ТипОбъекта = 0 Тогда
						ДействиеЗамены.Вставить("ДобавитьНеВыбирать");
					ИначеЕсли ТипОбъекта = 1 Тогда
						ДействиеЗамены.Вставить("ОтменитьПроведение");
						ДействиеЗамены.Вставить("ОчиститьДвижения");
					ИначеЕсли ТипОбъекта = 2 Тогда
						ОтменитьТранзакцию();	
						ЛогОшибок.Добавить("ОшибкаОпределенияОбъекта, "+СтрокаТЗ[Каспер_Поле]+" по причине: не соответствие типа найденного по ИД объекта: "+Значение1С);
						Отказ = Истина;
						Результат = Ложь;
						Продолжить;
					КонецЕсли; 
					// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				ИначеЕсли
					(ЕстьКолонка.УдалитьДокументИДругиеРесурсы И СтрокаТЗ.УдалитьДокументИДругиеРесурсы = Истина) 
					Тогда
					ДействиеЗамены = Новый Структура("УдалитьДоговорОПС");

					Если ТипОбъекта = 0 Тогда 					
						ДействиеЗамены = Новый Структура("ПометитьНаУдаление");	
					ИначеЕсли ТипОбъекта = 1 Тогда
						ДействиеЗамены.Вставить("ОтменитьПроведение");
						ДействиеЗамены.Вставить("ОчиститьДвижения"); 
					ИначеЕсли ТипОбъекта = 2 Тогда
						ОтменитьТранзакцию();	
						ЛогОшибок.Добавить("ОшибкаОпределенияОбъекта, "+СтрокаТЗ[Каспер_Поле]+" по причине: не соответствие типа найденного по ИД объекта: "+Значение1С);
						Отказ = Истина;
						Результат = Ложь;
						Продолжить;
					КонецЕсли;	
					// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				Иначе
					Если Ложь Тогда СоответствиеЗаменыТипов = Новый Соответствие(); КонецЕсли;
					СоответствиеТипа = СоответствиеЗаменыТипов.Получить(ТипСсылкиНаОбъект);
					Если Не СоответствиеТипа = Неопределено Тогда
						ДействиеЗамены = СоответствиеТипа.Получить(ТипЗнч(Значение1С));		
					КонецЕсли; 
				КонецЕсли;

				Если ДействиеЗамены = Неопределено ИЛИ ДействиеЗамены.Свойство("ОшибкаОпределенияОбъекта") Тогда
					ОтменитьТранзакцию();	
					ЛогОшибок.Добавить("ОшибкаОпределенияОбъекта, "+СтрокаТЗ[Каспер_Поле]+" по причине: не соответствие типа найденного по ИД объекта: "+Значение1С);
					Отказ = Истина;
					Результат = Ложь;
					Продолжить;
				КонецЕсли;
				Если ДействиеЗамены.Свойство("Очистить") Тогда
					Значение1С = Неопределено;
					Значение1СОбъект = Неопределено;
					СтрокаТЗ[Поле1С] = Неопределено;
				КонецЕсли;
				Если ДействиеЗамены.Свойство("ОтменитьПроведение") Тогда
					Если Значение1СОбъект.Проведен Тогда
						Значение1СОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
					КонецЕсли;
				КонецЕсли;
				Если ДействиеЗамены.Свойство("ОчиститьДвижения") Тогда
					// Заполнение реквизитов регистров Период и Организация
					Для каждого НаборЗаписей Из Значение1СОбъект.Движения Цикл

						НаборЗаписей.Прочитать();
						Если НаборЗаписей.Количество() = 0 Тогда
							Продолжить;
						КонецЕсли;
						НаборЗаписей.Очистить();
						НаборЗаписей.Записать();
					КонецЦикла;
				КонецЕсли;
				Если ДействиеЗамены.Свойство("ПроверитьСсылки") Тогда
					Если Ложь Тогда СоответствиеМетаданныеДляПоискаСсылок = Новый Соответствие(); КонецЕсли;
					МетаданныеДляПоискаСсылок = СоответствиеМетаданныеДляПоискаСсылок.Получить(ТипСсылкиНаОбъект);
					Если Не МетаданныеДляПоискаСсылок = Неопределено Тогда
						ОбластьПоиска = Новый Массив();
					Иначе
						ОбластьПоиска = Неопределено;
					КонецЕсли; 

					СписокСсылок = Новый Массив();
					СписокСсылок.Добавить(Значение1С);	
					НайденныеСсылки = НайтиПоСсылкам(СписокСсылок,ОбластьПоиска,МетаданныеДляПоискаСсылок);
					Если НайденныеСсылки.Количество() > 0 Тогда
						ЛогОшибок.Добавить("ОшибкаУдаленияОбъекта, "+СтрокаТЗ[Каспер_Поле]+" по причине: по объекту присутствуют ссылки: "+Значение1С);
						Отказ = Истина;
						Результат = Ложь;
						ОтменитьТранзакцию();	
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				Если ДействиеЗамены.Свойство("ДобавитьНеВыбирать") Тогда
					Значение1СОбъект.Наименование = "(НЕ ВЫБИРАТЬ) " + СТРЗАМЕНИТЬ(Значение1СОбъект.Наименование,"(НЕ ВЫБИРАТЬ) ","");
					Значение1СОбъект.Записать();
				КонецЕсли;
				Если ДействиеЗамены.Свойство("ПометитьНаУдаление") Тогда
					Значение1СОбъект.ПометкаУдаления = Истина;
					Значение1СОбъект.Записать();
					СтрокаТЗ[Поле1С] = Неопределено;
					Значение1СОбъект = Неопределено;
				КонецЕсли;  
				// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				Если ДействиеЗамены.Свойство("УдалитьДоговорОПС") Тогда					
					МЗ=РегистрыСведений[ИмяРегистраСведений].СоздатьМенеджерЗаписи();
					МЗ[ИзмерениеКаспер_Поле] = СтрокаТЗ[Каспер_Поле];
					МЗ.Прочитать();
					МассивРесурсов = ПолучитьМассивЗначенийРесурсов(МЗ,МетаданныеРС);

					Для Каждого УдаляемыйРесурс Из МассивРесурсов Цикл
						УдаляемыйОбъект = УдаляемыйРесурс.ПолучитьОбъект();
						УдаляемыйОбъект.Удалить();					
					КонецЦикла;	
					МЗ.Удалить();					
				КонецЕсли;
				// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				Если ДействиеЗамены.Свойство("Удалить") Тогда
					Значение1СОбъект.Удалить();
					СтрокаТЗ[Поле1С] = Неопределено;
					Значение1СОбъект = Неопределено;
				КонецЕсли;
				// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
			ИначеЕсли Не Значение1СОбъект = Неопределено И 
				(ЕстьКолонка.ОтправлятьДанныеНаПочту И СтрокаТЗ.ОтправлятьДанныеНаПочту = ИСТИНА) Тогда	
				//отправлять нужно, смотрим дальше куда и что
				Если ЕстьКолонка.ПочтаДляУведомления и ЗначениеЗаполнено(СтрокаТЗ.ПочтаДляУведомления) = ИСТИНА Тогда
					Если ЕстьКолонка.ТелоПисьмаДляУведомления и ЗначениеЗаполнено(СтрокаТЗ.ТелоПисьмаДляУведомления) = ИСТИНА Тогда
						ТелоПисьма = СтрокаТЗ.ТелоПисьмаДляУведомления;
					Иначе
						ТелоПисьма = "Уведомление о загрузке объекта " + Строка(Значение1СОбъект);
					КонецЕсли; 
					Если ЕстьКолонка.ТемаПисьмаДляУведомления и ЗначениеЗаполнено(СтрокаТЗ.ТемаПисьмаДляУведомления) = ИСТИНА Тогда
						ТемаПисьма = СтрокаТЗ.ТемаПисьмаДляУведомления;
					Иначе
						ТемаПисьма = "Уведомление о загрузке объекта " + Строка(Значение1СОбъект);
					КонецЕсли;
					ХМ_ПрочиеПроцедурыВызовСервера.ОтправитьСистемноеСообщениеНаПочту(ТемаПисьма,ТелоПисьма,СтрокаТЗ.ПочтаДляУведомления);						
				КонецЕсли;				
				// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607			
			КонецЕсли;
			//https://redmine.npf.com/issues/4302
			Если 
				(ЕстьКолонка.НаУдаление И СтрокаТЗ.УдалитьДокумент = Истина) 
				ИЛИ	(ЕстьКолонка.ПометитьНаУдаление И СтрокаТЗ.ТолькоПометитьНаУдаление = Истина) 
				// ++ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				ИЛИ (ЕстьКолонка.УдалитьДокументИДругиеРесурсы И СтрокаТЗ.УдалитьДокументИДругиеРесурсы = ИСТИНА)
				// --ХМНПФ 05.09.2025 КопыткоАА http://redmine.npf.com/issues/4607
				Тогда
				ЗафиксироватьТранзакцию();	
				Продолжить;
			КонецЕсли;

			Если Значение1СОбъект = Неопределено Тогда
				Если ЕстьКолонка.ПропуститьНовыйДокумент И СтрокаТЗ["ПропуститьНовыйДокумент"] = Истина Тогда
					ЗафиксироватьТранзакцию();	
					Продолжить;
				КонецЕсли;
				НовыйЭлемент = Истина;
				Значение1СОбъект = ИнициализироватьНовыйОбъект(МенеджерЗначения,ТипОбъекта,УникальныйИдентификатор);
				Значение1С = Значение1СОбъект.ПолучитьСсылкуНового();
			Иначе

				Если ПараметрыЗагрузки.НеПерезагружатьНайденные ИЛИ ПараметрыЗагрузки.ЗагрузкаНеСвязанных Тогда
					ЗафиксироватьТранзакцию();	
					Продолжить;
				КонецЕсли;
				Если ЕстьКолонка.НеПерегружатьДокумент И СтрокаТЗ.НеПерегружатьДокумент = Истина Тогда
					ЗафиксироватьТранзакцию();	
					Продолжить;
				КонецЕсли;

				НовыйЭлемент = Ложь;

			КонецЕсли;

			Значение1СОбъект.ДополнительныеСвойства.Вставить("КасперЗагрузка",Новый Структура());	
			Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Вставить("СтрокаТЗ",СтрокаТЗ);	
			Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Вставить("Контекст",Контекст);	
			#КонецОбласти

			#Область Объект_Заполнение

			ЗаполнитьЗначенияСвойств(Значение1СОбъект,Реквизиты_поУмолчанию);
			ЗаполнитьЗначенияСвойств(Значение1СОбъект,СтрокаТЗ);
			//		Если ТипОбъекта = 1 Тогда
			//			Значение1СОбъект.Дата = Значение1СОбъект.Дата+Число(Прав(Строка(СтрокаТЗ.DOC_ID),3));
			//		КонецЕсли;


			Для каждого Реквизит_ИзОбъекта Из Реквизиты_ИзОбъекта Цикл
				Попытка
					Значение1СОбъект[Реквизит_ИзОбъекта.Ключ]  = Значение1СОбъект[Реквизит_ИзОбъекта.Значение];
				Исключение
				КонецПопытки;
			КонецЦикла;

			Для каждого ОписаниеТЧ Из Структура_ТабличныеЧасти Цикл
				Если ПараметрыЗагрузки.ЗагрузкаНеСвязанных Тогда
					Если ОписаниеТЧ.Значение.МетаданныеЗначения_Имя = "ДоговорыОПС" Тогда
						СтрокиНаДобавление = ОписаниеТЧ.Значение.ТЗ.НайтиСтроки(Новый Структура(ОписаниеТЧ.Значение.Узел.Каспер_поле,СтрокаТЗ[ОписаниеТЧ.Значение.Узел.Каспер_поле]));
						Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
							ЗаполнитьЗначенияСвойств(Значение1СОбъект,СтрокаНаДобавление);
						КонецЦикла;
					КонецЕсли;
					Продолжить;	
				КонецЕсли;	


				Реквизиты_ИзОбъекта = ОписаниеТЧ.Значение.Реквизиты_ИзОбъекта;
				Попытка
					ТЧ = Значение1СОбъект[ОписаниеТЧ.Значение.МетаданныеЗначения_Имя];
				Исключение
					Продолжить;
				КонецПопытки;
				
				СтрокаПоиска = Новый Структура(ОписаниеТЧ.Значение.Узел.Каспер_поле,СтрокаТЗ[ОписаниеТЧ.Значение.Узел.Каспер_поле]);
				Если ОписаниеТЧ.Значение.С1_МаппингРеквизитов.Свойство("УдалитьСтрокуТЧ") Тогда
					СтрокаПоиска.Вставить("УдалитьСтрокуТЧ",Ложь);
				КонецЕсли;
					
				СтрокиНаДобавление = ОписаниеТЧ.Значение.ТЗ.НайтиСтроки(СтрокаПоиска);
				ТЧ.Очистить();
				Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
					СтрокаТЧ = ТЧ.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,ОписаниеТЧ.Значение.Реквизиты_поУмолчанию);
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,СтрокаНаДобавление);
					Для каждого Реквизит_ИзОбъекта Из Реквизиты_ИзОбъекта Цикл
						СтрокаТЧ[Реквизит_ИзОбъекта.Ключ]  = Значение1СОбъект[Реквизит_ИзОбъекта.Значение];
					КонецЦикла;
				КонецЦикла;
				//Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
				//	ОписаниеТЧ.Значение.ТЗ.Удалить(СтрокаНаДобавление);
				//КонецЦикла;
			КонецЦикла;

			НЗ.Очистить();
			НЗ.Отбор[ИзмерениеКаспер_Поле].Установить(СтрокаТЗ[Каспер_Поле]);
			Если ФильтрПоПолю1С = Истина Тогда
				НЗ.Отбор["ИмяПоля1С"].Установить(ЗначениеОтбораПоПолю1С);
			КонецЕсли;

			МЗ = НЗ.Добавить();
			Если ФильтрПоПолю1С = Истина Тогда
				МЗ["ИмяПоля1С"] = ЗначениеОтбораПоПолю1С;
			КонецЕсли;
			МЗ[ИзмерениеКаспер_Поле] = СтрокаТЗ[Каспер_Поле];
			МЗ[Поле1С] = Значение1С;

			#КонецОбласти

			#Область Объект_Заполение_Кастомизация
			Если Реквизиты_поУмолчанию.Свойство("СоздатьЗаполнитьБанковскиеСчета") И Не ЗначениеЗаполнено(Значение1СОбъект[Реквизиты_поУмолчанию.СоздатьЗаполнитьБанковскиеСчета]) Тогда
				БС = Справочники.БанковскиеСчета.СоздатьЭлемент();
				БС.Банк = СтрокаТЗ.Банк;
				БС.ВалютаДенежныхСредств = Реквизиты_поУмолчанию.ВалютаДенежныхСредств;
				БС.ВидСчета = "Расчетный";
				БС.Владелец = СтрокаТЗ.Получатель;
				БС.УстановитьНовыйКод();
				БС.Наименование =  СтрокаТЗ.НомерЛицевогоСчета + БС.Банк.Наименование;
				БС.НомерСчета = СтрокаТЗ.НомерЛицевогоСчета; 
				БС.Записать();
				Значение1СОбъект[Реквизиты_поУмолчанию.СоздатьЗаполнитьБанковскиеСчета] = БС.Ссылка;
			КонецЕсли;

			Если МетаданныеЗначения_Имя = "уп_ОткрытиеДоговораОПС" Тогда
				уп_ОткрытиеДоговораОПС_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,Значение1С,СтрокаТЗ,МЗ)
			КонецЕсли;

			Если МетаданныеЗначения_Имя = "Банки" Тогда
				Банки_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,Значение1С,СтрокаТЗ,МЗ)
			КонецЕсли;

			Если МетаданныеЗначения_Имя = "уп_РешениеОбОтказеВЕдиновременнойВыплате"
				ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОбОтказеСВ"
				ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОбОтказеНЧТП"
				Тогда
				уп_РешениеОбОтказеВыплаты_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,Значение1С,СтрокаТЗ,МЗ,НовыйЭлемент)
			КонецЕсли;                             

			Если МетаданныеЗначения_Имя = "уп_РешениеОЕдиновременнойВыплате"
				ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОНазначенииСрочнойВыплаты"
				ИЛИ МетаданныеЗначения_Имя = "уп_РешениеОНазначенииНЧТП"
				Тогда
				уп_РешениеОВыплате_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,Значение1С,СтрокаТЗ,МЗ,НовыйЭлемент)
			КонецЕсли;

			Если МетаданныеЗначения_Имя = "уп_РешениеОЕдиновременнойВыплате"
				Тогда
				уп_РешениеОЕдиновременнойВыплате_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,Значение1С,СтрокаТЗ,МЗ,НовыйЭлемент)
			КонецЕсли;
			Если МетаданныеЗначения_Имя = "уп_ПакетЭД"
				Тогда
				уп_ПакетЭД_ОбработкаПроведения_ДоЗаписи(Значение1СОбъект,СтрокаТЗ,Структура_ТабличныеЧасти);
			КонецЕсли;

			#КонецОбласти

			#Область Объект_ЗаписьИПроведение                  
			НужнаОтменаПроведения = Ложь;
			Если Загрузка_ЗагрузитьТЗ_ТребуетсяОбработкаПроведения(МетаданныеЗначения_Имя, ПотоковаяЗагрузка, ДополнительныеПараметры.СтрокаТаблицы) 
				ИЛИ ( ДополнительныеПараметры.СтрокаТаблицы.Свойство("Проводить") И  ДополнительныеПараметры.СтрокаТаблицы.Проводить) Тогда
				Попытка
					БылПроведен = Значение1СОбъект.Проведен;
					Значение1СОбъект.Проведен = Ложь;
					НужнаОтменаПроведения = Значение1СОбъект.Ссылка.Проведен = Истина;
					Если БылПроведен = Истина Тогда
						Значение1СОбъект.ДополнительныеСвойства.Вставить("Проведен");
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЕсли;


			Если НужнаОтменаПроведения = Истина Тогда
				Если НужнаОтменаПроведения = Истина Тогда
					Для каждого НаборЗаписей Из Значение1СОбъект.Движения Цикл

						НаборЗаписей.Прочитать();
						Если НаборЗаписей.Количество() = 0 Тогда
							Продолжить;
						КонецЕсли;
						НаборЗаписей.Очистить();
						НаборЗаписей.Записать();
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;


			Попытка

				ЗаписатьОбъект(Значение1СОбъект);

			Исключение

				ОтменитьТранзакцию();	
				ЛогОшибок.Добавить("ОшибкаЗаписиОбъекта, ID Каспер: "+СтрокаТЗ[Каспер_Поле]+" по причине: "+ ОписаниеОшибки());
				Отказ = Истина;
				Результат = Ложь;
				Продолжить;
			КонецПопытки;

			Если Загрузка_ЗагрузитьТЗ_ТребуетсяОбработкаПроведения(МетаданныеЗначения_Имя, ПотоковаяЗагрузка, ДополнительныеПараметры.СтрокаТаблицы) 
				ИЛИ ( ДополнительныеПараметры.СтрокаТаблицы.Свойство("Проводить") И  ДополнительныеПараметры.СтрокаТаблицы.Проводить) Тогда
				Значение1СОбъект.Проведен = БылПроведен;
			КонецЕсли;      

			ЗаписатьОбъект(НЗ); 

			// ХМНПФ 10.11.2017 ШадринАВ // Загрузка_ЗагрузитьТЗ // №1179
			//Если МетаданныеЗначения_Имя = "уп_ОткрытиеДоговораОПС" Тогда
			//	// И НовыйЭлемент = Истина Тогда
			//	ПередатьГУИДдоговораВКаспер(СтрокаТЗ, ЛогОшибок);
			//КонецЕсли;

			Если ФиксироватьЗаписьДокумента = Истина Тогда
				ЗафиксироватьТранзакцию();
				НачатьТранзакцию();	
			КонецЕсли;

			Отказ = Ложь; Режим = Неопределено;
			Если (ДополнительныеПараметры.СтрокаТаблицы.Свойство("Проводить") И ДополнительныеПараметры.СтрокаТаблицы.Проводить) Тогда
				Попытка
					Значение1СОбъект.ОбменДанными.Загрузка = Ложь;
					Если Значение1СОбъект.Проведен Тогда 
						Значение1СОбъект.Записать(РежимЗаписиДокумента.Проведение);
					Иначе
						Значение1СОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					КонецЕсли;
					Значение1СОбъект.ОбменДанными.Загрузка = Истина;
					Если ФиксироватьЗаписьДокумента = Истина Тогда
						ЗафиксироватьТранзакцию();
						НачатьТранзакцию();	
					КонецЕсли;
				Исключение
					Если ФиксироватьЗаписьДокумента = Истина Тогда
						ОтменитьТранзакцию();
						НачатьТранзакцию();	
					КонецЕсли;
					ЛогОшибок.Добавить("ОшибкаПроведения"+Значение1С+", по причине: "+ОписаниеОшибки());
					Отказ = Истина;
					Результат = Ложь;
				КонецПопытки;
			КонецЕсли;                   

			#КонецОбласти

			// +ХМНПФ 20.10.2023 ХлопцевБА // Ежедневная загрузка НПО в тонком клиенте // №3697   
			Если ТипОбъекта = 1 Тогда  
				Если ТипЗнч(Значение1СОбъект) <> тип("ДокументОбъект.уп_ОперацияНПО") и 
					ТипЗнч(Значение1СОбъект) <> тип("ДокументОбъект.уп_ОперацияОПС") Тогда  
					нзКасперЗагрузка.Документ = Значение1СОбъект.Ссылка;   
					нзКасперЗагрузка.Прочитать();   
					Если Не нзКасперЗагрузка.Выбран() Тогда
						нзКасперЗагрузка.Документ = Значение1СОбъект.Ссылка;   
						нзКасперЗагрузка.Записать(); 
					КонецЕсли;
				КонецЕсли;  
			КонецЕсли;
			// -ХМНПФ 20.10.2023 ХлопцевБА // Ежедневная загрузка НПО в тонком клиенте // №3697

			#Область Объект_КасперДвижение
			Для каждого ОписаниеТЧ Из Структура_Движения Цикл
				Реквизиты_ИзОбъекта = ОписаниеТЧ.Значение.Реквизиты_ИзОбъекта;
				ТЧ = Значение1СОбъект.Движения[ОписаниеТЧ.Значение.МетаданныеЗначения_Имя];

				Если Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Свойство("ПрошлыеДвижения") = Ложь Тогда
					Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Вставить("ПрошлыеДвижения",Новый Структура());
				КонецЕсли;

				Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.ПрошлыеДвижения.Вставить(ОписаниеТЧ.Значение.МетаданныеЗначения_Имя,ТЧ.Выгрузить());
				ТЧ.Очистить();

				Значение1СОбъект.ДополнительныеСвойства.Вставить("ТекДвжиения",);
				СтрокиНаДобавление = ОписаниеТЧ.Значение.ТЗ.НайтиСтроки(Новый Структура(ОписаниеТЧ.Значение.Узел.Каспер_поле,СтрокаТЗ[ОписаниеТЧ.Значение.Узел.Каспер_поле]));
				Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
					СтрокаТЧ = ТЧ.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,ОписаниеТЧ.Значение.Реквизиты_поУмолчанию);
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,СтрокаНаДобавление);

					Для каждого Реквизит_ИзОбъекта Из Реквизиты_ИзОбъекта Цикл
						СтрокаТЧ[Реквизит_ИзОбъекта.Ключ]  = Значение1СОбъект[Реквизит_ИзОбъекта.Значение];
					КонецЦикла;

				КонецЦикла;
				//Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
				//	ОписаниеТЧ.Значение.ТЗ.Удалить(СтрокаНаДобавление);
				//КонецЦикла;
			КонецЦикла;

			Для каждого ОписаниеТЧ Из Структура_Движения Цикл
				ТЧ = Значение1СОбъект.Движения[ОписаниеТЧ.Значение.МетаданныеЗначения_Имя];
				ЗаписатьОбъект(ТЧ);
			КонецЦикла;

			#КонецОбласти

			#Область ХМ_ЗАГРУЗКА_ОбработкаПроведения
			// ХМНПФ+ 22.02.2024 ЛыткинАМ // ОПС Настройка проведения при загрузке // №3792
			Если Загрузка_ЗагрузитьТЗ_ТребуетсяОбработкаПроведения(МетаданныеЗначения_Имя, ПотоковаяЗагрузка, ДополнительныеПараметры.СтрокаТаблицы) Тогда
				Попытка
					Если Отказ = Ложь Тогда
						Режим = Неопределено;
						Значение1СОбъект.ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, Структура_ТабличныеЧасти);
						Если Отказ = Истина Тогда 
							ЛогОшибок.Добавить("ОшибкаЗагрузкиПослеПроведения"+Значение1С); 
							Результат = Ложь;
							Если Значение1СОбъект.Проведен = Истина Тогда
								Значение1СОбъект.Проведен = Ложь;	
								ЗаписатьОбъект(Значение1СОбъект);
							КонецЕсли;
							Если Значение1СОбъект.Проведен = Ложь Тогда
								Значение1СОбъект.Проведен = Истина;	
								ЗаписатьОбъект(Значение1СОбъект);
							КонецЕсли;
						Иначе
							ЗаписатьОбъект(Значение1СОбъект);
						КонецЕсли;
					КонецЕсли;
				Исключение
					Если ФиксироватьЗаписьДокумента = Истина Тогда
						ЗафиксироватьТранзакцию();
						НачатьТранзакцию();	
					КонецЕсли;
					Если Значение1СОбъект.Ссылка.Проведен = Истина Тогда
						Значение1СОбъект.Проведен = Ложь;	
						ЗаписатьОбъект(Значение1СОбъект);
					КонецЕсли;
					ЛогОшибок.Добавить("ОшибкаЗагрузкиПослеПроведения"+Значение1С+", по причине: "+ОписаниеОшибки()); 
					Результат = Ложь;
				КонецПопытки;
			КонецЕсли;

			#КонецОбласти

			Если Отказ = Истина Тогда
				Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка = Неопределено;
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли; 

			#Область Объект_КасперДвижениеПослеПроведения
			Для каждого ОписаниеТЧ Из Структура_ДвиженияПослеПроведения Цикл
				Реквизиты_ИзОбъекта = ОписаниеТЧ.Значение.Реквизиты_ИзОбъекта;
				ТЧ = Значение1СОбъект.Движения[ОписаниеТЧ.Значение.МетаданныеЗначения_Имя];

				Если Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Свойство("ПрошлыеДвиженияПослеПроведения") = Ложь Тогда
					Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.Вставить("ПрошлыеДвиженияПослеПроведения",Новый Структура());
				КонецЕсли;

				Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка.ПрошлыеДвиженияПослеПроведения.Вставить(ОписаниеТЧ.Значение.МетаданныеЗначения_Имя,ТЧ.Выгрузить());
				ТЧ.Очистить();


				СтрокиНаДобавление = ОписаниеТЧ.Значение.ТЗ.НайтиСтроки(Новый Структура(ОписаниеТЧ.Значение.Узел.Каспер_поле,СтрокаТЗ[ОписаниеТЧ.Значение.Узел.Каспер_поле]));
				Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
					СтрокаТЧ = ТЧ.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,ОписаниеТЧ.Значение.Реквизиты_поУмолчанию);
					ЗаполнитьЗначенияСвойств(СтрокаТЧ,СтрокаНаДобавление);

					Для каждого Реквизит_ИзОбъекта Из Реквизиты_ИзОбъекта Цикл
						СтрокаТЧ[Реквизит_ИзОбъекта.Ключ]  = Значение1СОбъект[Реквизит_ИзОбъекта.Значение];
					КонецЦикла;

				КонецЦикла;
				//Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
				//	ОписаниеТЧ.Значение.ТЗ.Удалить(СтрокаНаДобавление);
				//КонецЦикла;
			КонецЦикла;

			Для каждого ОписаниеТЧ Из Структура_ДвиженияПослеПроведения Цикл
				ТЧ = Значение1СОбъект.Движения[ОписаниеТЧ.Значение.МетаданныеЗначения_Имя];
				ЗаписатьОбъект(ТЧ);
			КонецЦикла;
			#КонецОбласти

			Значение1СОбъект.ДополнительныеСвойства.КасперЗагрузка = Неопределено;
			ЗафиксироватьТранзакцию();	
		Исключение
			ОтменитьТранзакцию();
			Сообщить(ОписаниеОшибки());
			ВызватьИсключение "Не удалось загрузить документ";
		КонецПопытки;
	КонецЦикла;
	Возврат Результат; 
КонецФункции

&Вместо("Загрузка_ЗагрузитьТЗ_ФизическиеЛица")
Процедура ХМЗагрузка_ЗагрузитьТЗ_ФизическиеЛица(ТЗ,ТЗ_Адреса, НеПерезагружатьНайденные)
	
	ЕстьКолонка = Загрузка_ЗагрузитьТЗ_ЕстьКолонка(ТЗ);

	п_ПериодРС = '19000101000000';
	БессрочнаяПоИнвалидности = Перечисления.уп_ПенсионныеОснования.БессрочнаяПоИнвалидности;
	СрочнаяПоИнвалидности = Перечисления.уп_ПенсионныеОснования.СрочнаяПоИнвалидности;
	Для каждого СтрокаДаннных Из ТЗ Цикл
		Если СтрокаДаннных.уп_ПенсионныеОснования = БессрочнаяПоИнвалидности и ЗначениеЗаполнено(СтрокаДаннных.уп_ДатаОкончанияГосПенсии) Тогда
			СтрокаДаннных.уп_ПенсионныеОснования = СрочнаяПоИнвалидности; 	
		КонецЕсли;
	КонецЦикла;
	
	СоответствиеТипВидКИ = Новый Соответствие;	
	МенеджерКонтрагенты = Справочники.Контрагенты;
	МенеджерФизЛица = Справочники.ФизическиеЛица;
	ГруппаФизЛицо = МенеджерКонтрагенты.ФизическиеЛица;
	ПеречислениеФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
#Область ШаблонКонтактнойИнформации

	ШаблонКонтактнойИнформации = Справочники.ФизическиеЛица.СоздатьЭлемент().КонтактнаяИнформация;
	СтрокаТелефон = ШаблонКонтактнойИнформации.Добавить();
	СтрокаТелефон.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
	СтрокаТелефон.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица;
	СтрокаТелефон.Представление = "";
	СтрокаТелефон.ЗначенияПолей = "<КонтактнаяИнформация xmlns=""http://www.v8.1c.ru/ssl/contactinfo""" 
	+" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance""" 
	+" Представление=""***""><Комментарий/><Состав xsi:type=""НомерТелефона"" КодСтраны="""" КодГорода=""""" 
	+" Номер=""***"" Добавочный=""""/></КонтактнаяИнформация>";
	СтрокаТелефон.НомерТелефона = "";
	СтрокаТелефон.НомерТелефонаБезКодов = "";
	                                
	СтрокаРабочий = ШаблонКонтактнойИнформации.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаРабочий,СтрокаТелефон,"Тип, Представление, ЗначенияПолей, НомерТелефона, НомерТелефонаБезКодов");
	СтрокаРабочий.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица;
	
	СтрокаМобильный = ШаблонКонтактнойИнформации.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаМобильный,СтрокаТелефон,"Тип, Представление, ЗначенияПолей, НомерТелефона, НомерТелефонаБезКодов");
	СтрокаМобильный.Вид = ПолучитьВидКонтактнойИнформации("Телефон мобильный",Перечисления.ТипыКонтактнойИнформации.Телефон, Справочники.ВидыКонтактнойИнформации.СправочникФизическиеЛица);
	
	СтрокаКонтактный = ШаблонКонтактнойИнформации.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаКонтактный,СтрокаТелефон,"Тип, Представление, ЗначенияПолей, НомерТелефона, НомерТелефонаБезКодов");
	СтрокаКонтактный.Вид = ПолучитьВидКонтактнойИнформации("Телефон контактный",Перечисления.ТипыКонтактнойИнформации.Телефон, Справочники.ВидыКонтактнойИнформации.СправочникФизическиеЛица);
		
	СтрокаДополнительный = ШаблонКонтактнойИнформации.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаДополнительный,СтрокаТелефон,"Тип, Представление, ЗначенияПолей, НомерТелефона, НомерТелефонаБезКодов");
	СтрокаДополнительный.Вид = ПолучитьВидКонтактнойИнформации("Телефон дополнительный",Перечисления.ТипыКонтактнойИнформации.Телефон, Справочники.ВидыКонтактнойИнформации.СправочникФизическиеЛица);
	
	СтрокаEmail = ШаблонКонтактнойИнформации.Добавить();
	СтрокаEmail.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	СтрокаEmail.Вид = ПолучитьВидКонтактнойИнформации("Email",Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, Справочники.ВидыКонтактнойИнформации.СправочникФизическиеЛица);
	СтрокаEmail.Представление = "";
	СтрокаEmail.ЗначенияПолей = "<КонтактнаяИнформация xmlns=""http://www.v8.1c.ru/ssl/contactinfo""" 
	+" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance""" 
	+" Представление=""***""><Комментарий/><Состав xsi:type=""ЭлектроннаяПочта"" Значение=""***""/></КонтактнаяИнформация>";
	
	СтруктураПолей = Неопределено;
#КонецОбласти
	
	ТекЗапись=0;
	КоличествоЗаписей = ТЗ.Количество();
	ТекДата = ТекущаяДата()-1;
	
	Для каждого СтрокаТЗ Из ТЗ Цикл
		
		ТекЗапись = ТекЗапись + 1;               
		Закончится = ТекущаяДата() + (КоличествоЗаписей - ТекЗапись)*(ТекущаяДата()-ТекДата)/ТекЗапись;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Контрагент) Тогда
			Если ЕстьКолонка.ПропуститьНовыйДокумент И СтрокаТЗ["ПропуститьНовыйДокумент"] = Истина Тогда
				Продолжить;
			КонецЕсли;
			НовыйКонтрагент = Истина;
			
			КонтрагентОбъект = ИнициализироватьНовыйОбъект(МенеджерКонтрагенты);
			КонтрагентСсылка = КонтрагентОбъект.ПолучитьСсылкуНового();
			
			ФизическоеЛицоОбъект = ИнициализироватьНовыйОбъект(МенеджерФизЛица);
			ФизическоеЛицоСсылка = ФизическоеЛицоОбъект.ПолучитьСсылкуНового();
		Иначе
			Если НеПерезагружатьНайденные Тогда
				Продолжить;
			КонецЕсли;
			Если ЕстьКолонка.НеПерегружатьДокумент И СтрокаТЗ.НеПерегружатьДокумент = Истина Тогда
				Продолжить;
			КонецЕсли;
			НовыйКонтрагент = Ложь;
			
			КонтрагентОбъект = СтрокаТЗ.Контрагент.ПолучитьОбъект();
			КонтрагентСсылка = КонтрагентОбъект.Ссылка;
			
			ФизическоеЛицоСсылка = КонтрагентОбъект.уп_ФизЛицо.Ссылка;
			ФизическоеЛицоОбъект = ФизическоеЛицоСсылка.ПолучитьОбъект();
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(ФизическоеЛицоОбъект,СтрокаТЗ,
		"Наименование,ДатаРождения,Пол,ИНН,СтраховойНомерПФР,МестоРождения,ФИО,уп_ДатаСмерти,ХМ_ОтказОтСМС"); // ХМНПФ 18.12.2018 ШадринАВ // заполнение реквизита ХМ_ОтказОтСМС // №1949
		
		ЗаполнитьЗначенияСвойств(КонтрагентОбъект,СтрокаТЗ,
		"Наименование,НаименованиеПолное,ИНН,уп_ПенсионныеОснования,уп_ДатаНазначенияГосПенсии,уп_НомерПенсионногоУдостоверения,уп_ДатаПенсионногоУдостоверения,уп_ФИОПриРождении,СтранаРегистрации");
		КонтрагентОбъект.уп_СтраховойНомерПФР = ФизическоеЛицоОбъект.СтраховойНомерПФР;
		
		ФизическоеЛицоОбъект.уп_Контрагент = КонтрагентСсылка;
		КонтрагентОбъект.уп_ФизЛицо = ФизическоеЛицоСсылка;
		КонтрагентОбъект.Родитель = ГруппаФизЛицо;
		КонтрагентОбъект.ЮридическоеФизическоеЛицо = ПеречислениеФизЛицо;
		КонтрагентОбъект.уп_Участник = Истина;
#Область КонтактнаяИнформация
		// +ХМНПФ 25.03.2021 ШадринАВ // Загружать адреса ФИАС // №3137
		ЗагружатьАдрес = НадоЗагружатьАдрес(СтрокаТЗ, ТЗ_Адреса, ФизическоеЛицоСсылка.ХМ_АдресФИАС);
		// -ХМНПФ 25.03.2021 ШадринАВ // Загружать адреса ФИАС // №3137                                 
		
		// +ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
		Если НЕ ЗагружатьАдрес Тогда
			ТЗ_ТолькоАдресКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Выгрузить(Новый Структура("Тип",Перечисления.ТипыКонтактнойИнформации.Адрес));
		КонецЕсли;
		// -ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
		ФизическоеЛицоОбъект.КонтактнаяИнформация.Очистить();
		КонтрагентОбъект.КонтактнаяИнформация.Очистить();
		// +ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
		Если НЕ ЗагружатьАдрес Тогда
			ФизическоеЛицоОбъект.КонтактнаяИнформация.Загрузить(ТЗ_ТолькоАдресКИ);
		КонецЕсли;
		// -ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
		Если ЗначениеЗаполнено(СтрокаТЗ.PHONE) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаТелефон,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.PHONE;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаТелефон.ЗначенияПолей,"***",СтрокаТЗ.PHONE);
			СтрокаКИ.НомерТелефона = СтрокаТЗ.PHONE;
			СтрокаКИ.НомерТелефонаБезКодов = СтрокаТЗ.PHONE;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.PHONE_MOBIL) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаМобильный,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.PHONE_MOBIL;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаМобильный.ЗначенияПолей,"***",СтрокаТЗ.PHONE_MOBIL);
			СтрокаКИ.НомерТелефона = СтрокаТЗ.PHONE_MOBIL;
			СтрокаКИ.НомерТелефонаБезКодов = СтрокаТЗ.PHONE_MOBIL;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.PHONE_CONTACT) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаКонтактный,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.PHONE_CONTACT;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаКонтактный.ЗначенияПолей,"***",СтрокаТЗ.PHONE_CONTACT);
			СтрокаКИ.НомерТелефона = СтрокаТЗ.PHONE_CONTACT;
			СтрокаКИ.НомерТелефонаБезКодов = СтрокаТЗ.PHONE_CONTACT;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.PHONE_OFFICE) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаРабочий,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.PHONE_OFFICE;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаРабочий.ЗначенияПолей,"***",СтрокаТЗ.PHONE_OFFICE);
			СтрокаКИ.НомерТелефона = СтрокаТЗ.PHONE_OFFICE;
			СтрокаКИ.НомерТелефонаБезКодов = СтрокаТЗ.PHONE_OFFICE;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.PHONE_ADD) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаДополнительный,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.PHONE_ADD;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаДополнительный.ЗначенияПолей,"***",СтрокаТЗ.PHONE_ADD);
			СтрокаКИ.НомерТелефона = СтрокаТЗ.PHONE_ADD;
			СтрокаКИ.НомерТелефонаБезКодов = СтрокаТЗ.PHONE_ADD;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЗ.EMAIL_FL) Тогда
			СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаEmail,"Тип, Вид");
			СтрокаКИ.Представление = СтрокаТЗ.EMAIL_FL;
			СтрокаКИ.ЗначенияПолей = СтрЗаменить(СтрокаEmail.ЗначенияПолей,"***",СтрокаТЗ.EMAIL_FL);
		КонецЕсли;
		
#КонецОбласти

#Область КонтактнаяИнформацияАдреса
		СтрокиНаДобавление = ТЗ_Адреса.НайтиСтроки(Новый Структура("Actor_id",СтрокаТЗ["Actor_id"]));
		Если ЗагружатьАдрес Тогда // ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
			Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
				ТипКИ = СоответствиеТипВидКИ.Получить(СтрокаНаДобавление.Вид);
				Если ТипКИ = Неопределено Тогда
					СоответствиеТипВидКИ.Вставить(СтрокаНаДобавление.Вид,СтрокаНаДобавление.Вид.Тип);
					ТипКИ = СтрокаНаДобавление.Вид.Тип; 
				КонецЕсли;
				// +ХМНПФ 28.10.2019 ШадринАВ // Использовать ГУИДы улицы и дома от ФИАС при загрузке адреса // №2958
				Если ЗначениеЗаполнено(СтрокаНаДобавление.AOGUID) И ЗначениеЗаполнено(СтрокаНаДобавление.HOUSEGUID) Тогда
					//+ХМНПФ 26.10.2023 ХлопцевБА // Некорректно загружаются адреса физ.лиц , добавил ЗагруженныеДанные = Истина// №3713 
					СведенияОбАдресномОбъекте = Новый Структура("Идентификатор,Муниципальный,ЗагруженныеДанные,Родитель,Представление,Адрес", СтрокаНаДобавление.AOGUID, Ложь,Истина,"","","");  
					//-ХМНПФ 26.10.2023 ХлопцевБА // Некорректно загружаются адреса физ.лиц , добавил ЗагруженныеДанные = Истина// №3713 
					СтруктураРезультата = АдресныйКлассификаторСлужебный.АктуальныеАдресныеСведения(СведенияОбАдресномОбъекте);
					ДанныеАдресногоОбъекта = СтруктураРезультата.Данные;
					ДанныеАдресногоОбъекта.country = "РОССИЯ";
					ДанныеАдресногоОбъекта.countryCode = "643";
					
					СписокДомов = АдресныйКлассификаторСлужебный.СписокДомов(Новый УникальныйИдентификатор(СтрокаНаДобавление.AOGUID), "%");
					ДомПоGUID = СписокДомов.Скопировать(Новый Структура("Идентификатор", Новый УникальныйИдентификатор(СтрокаНаДобавление.HOUSEGUID)));
					Если ДомПоGUID.Количество()>0 Тогда
						ЗаполнитьПоляДомовСтроенийЗемельныхУчастков(ДомПоGUID[0].Значение, ДанныеАдресногоОбъекта);
					Иначе
						ДанныеАдресногоОбъекта.houseNumber = СтрокаНаДобавление.ADDR_HOUSE;
						ДанныеАдресногоОбъекта.houseType = "Дом";
						Если ЗначениеЗаполнено(СтрокаНаДобавление.ADDR_BUILD) Тогда
							ДанныеАдресногоОбъекта.buildings.Добавить(Новый Структура("number,type",СтрокаНаДобавление.ADDR_BUILD,"Корпус"));
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаНаДобавление.ADDR_FLAT) Тогда
						ИнформацияОПомещении = Новый Структура("type, number", "Квартира", СтрокаНаДобавление.ADDR_FLAT);
						ДанныеАдресногоОбъекта.Apartments.Добавить(ИнформацияОПомещении);
					КонецЕсли;
					
					СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
					СтрокаКИ.Тип = ТипКИ;
					СтрокаКИ.Вид = СтрокаНаДобавление.Вид;
					СтрокаКИ.ВидДляСписка = СтрокаНаДобавление.Вид;
					СтрокаКИ.Страна = "РОССИЯ";
					СтрокаКИ.Регион = ДанныеАдресногоОбъекта.area+" "+ДанныеАдресногоОбъекта.areaType;
					СтрокаКИ.Город = ДанныеАдресногоОбъекта.city+" "+ДанныеАдресногоОбъекта.cityType;
					
					ДанныеАдресногоОбъекта.Value = РаботаСАдресамиКлиентСервер.ПредставлениеАдреса(ДанныеАдресногоОбъекта, Истина);
					
					СтрокаКИ.Представление = ДанныеАдресногоОбъекта.Value;
					АдресВJSON = УправлениеКонтактнойИнформациейСлужебный.СтруктураВСтрокуJSON(ДанныеАдресногоОбъекта);
					
					КонтактнаяИнформацияВXML = Неопределено;
					УправлениеКонтактнойИнформациейЛокализация.ПриКонвертированииКонтактнуюИнформациюИзJSONВXML(АдресВJSON, КонтактнаяИнформацияВXML);
					СтрокаКИ.ЗначенияПолей = КонтактнаяИнформацияВXML;
				Иначе
				// -ХМНПФ 28.10.2019 ШадринАВ // Использовать ГУИДы улицы и дома от ФИАС при загрузке адреса // №2958
					СтрокаКИ = ФизическоеЛицоОбъект.КонтактнаяИнформация.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаКИ,СтрокаНаДобавление);
					СтрокаКИ.Тип = ТипКИ;
					СтрокаКИ.ВидДляСписка = СтрокаНаДобавление.Вид;
					СтрокаКИ.Представление = СтрокаНаДобавление.ADDR_FULL;
					СтрокаКИ.ЗначенияПолей = ПолучитьXMLАдреса(СтрокаНаДобавление, СтруктураПолей);
					СтрокаКИ.Страна = "РОССИЯ";
					СтрокаКИ.Регион = СтрокаНаДобавление.Регион;  
					СтрокаКИ.Город = СтрокаНаДобавление.Город;  
				КонецЕсли; // ХМНПФ 28.10.2019 ШадринАВ // Использовать ГУИДы улицы и дома от ФИАС при загрузке адреса // №2958
			КонецЦикла;
		КонецЕсли; // ХМНПФ 08.10.2019 ШадринАВ // Адрес приведен к ФИАС // №2482
		Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
			ТЗ_Адреса.Удалить(СтрокаНаДобавление);
		КонецЦикла;
#КонецОбласти
		НачатьТранзакцию();
		ЗаписатьОбъект(ФизическоеЛицоОбъект);
		ЗаписатьОбъект(КонтрагентОбъект);
		
		НЗ_ФИОФизическихЛиц = РегистрыСведений.ФИОФизическихЛиц.СоздатьНаборЗаписей();
		НЗ_ФИОФизическихЛиц.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицоСсылка);
		НЗ_ФИОФизическихЛиц.Отбор.Период.Установить(п_ПериодРС);
		МЗ_ФИОФизическихЛиц = НЗ_ФИОФизическихЛиц.Добавить();
		МЗ_ФИОФизическихЛиц.ФизическоеЛицо = ФизическоеЛицоСсылка;
		МЗ_ФИОФизическихЛиц.Период = п_ПериодРС;
		ЗаполнитьЗначенияСвойств(МЗ_ФИОФизическихЛиц, СтрокаТЗ, "Фамилия,Имя,Отчество");
		ЗаписатьОбъект(НЗ_ФИОФизическихЛиц);
		
		НЗ_ГражданствоФизическихЛиц = РегистрыСведений.ГражданствоФизическихЛиц.СоздатьНаборЗаписей();
		НЗ_ГражданствоФизическихЛиц.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицоСсылка);
		НЗ_ГражданствоФизическихЛиц.Отбор.Период.Установить(п_ПериодРС);
		МЗ_ГражданствоФизическихЛиц = НЗ_ГражданствоФизическихЛиц.Добавить();
		МЗ_ГражданствоФизическихЛиц.ФизическоеЛицо = ФизическоеЛицоСсылка;
		МЗ_ГражданствоФизическихЛиц.Период = п_ПериодРС;
		ЗаполнитьЗначенияСвойств(МЗ_ГражданствоФизическихЛиц, СтрокаТЗ, "Страна");
		ЗаписатьОбъект(НЗ_ГражданствоФизическихЛиц);
		
		Если НовыйКонтрагент Тогда
			НЗ_ХМ_АкторИДПоКонтрагентам = РегистрыСведений.ХМ_АкторИДПоКонтрагентам.СоздатьНаборЗаписей();
			НЗ_ХМ_АкторИДПоКонтрагентам.Отбор.ACTOR_ID.Установить(СтрокаТЗ.ACTOR_ID);
			МЗ_ХМ_АкторИДПоКонтрагентам = НЗ_ХМ_АкторИДПоКонтрагентам.Добавить();
			МЗ_ХМ_АкторИДПоКонтрагентам.ACTOR_ID = СтрокаТЗ.ACTOR_ID;
			МЗ_ХМ_АкторИДПоКонтрагентам.Контрагент = КонтрагентСсылка;
			МЗ_ХМ_АкторИДПоКонтрагентам.ФизическоеЛицо = ФизическоеЛицоСсылка; 
			ЗаписатьОбъект(НЗ_ХМ_АкторИДПоКонтрагентам);
		КонецЕсли;
		ЗафиксироватьТранзакцию();
		ФизическоеЛицоОбъект = Неопределено;
		КонтрагентОбъект = Неопределено;
		НЗ_ФИОФизическихЛиц = Неопределено;
		НЗ_ГражданствоФизическихЛиц = Неопределено;
		НЗ_ХМ_АкторИДПоКонтрагентам = Неопределено;
	КонецЦикла;
КонецПроцедуры
&Вместо("ЗагрузитьПоТаблицамИзКлиента")
Функция ХМЗагрузитьПоТаблицамИзКлиента(СтрокаТаблицы, ПравилаЗагрузкиКлиент, ПараметрыЗагрузки, ЛогОшибок, ДополнительныеПараметры)

	Результат = Истина;
	УстановитьПривилегированныйРежим(Истина); 

	ТЗ = ПравилаЗагрузкиКлиент.Реквизиты.Реквизиты.ТЗ;
	Если СтрокаТаблицы.ИмяТаблицы = "Контрагенты" И СтрокаТаблицы.ПредставлениеТаблицы = СтрокаТаблицы.ИмяТаблицы Тогда
		Загрузка_ЗагрузитьТЗ_Контрагенты(ТЗ, ПараметрыЗагрузки.НеПерезагружатьНайденные);
	ИначеЕсли СтрокаТаблицы.ИмяТаблицы = "ФизическиеЛица" Тогда//И СтрокаТаблицы.ПредставлениеТаблицы = СтрокаТаблицы.ИмяТаблицы Тогда
		ТЗ_ТЧ = ПравилаЗагрузкиКлиент.ТабличныеЧасти["СПР01изv01ADDR_Запрос"].ТЗ;
		Загрузка_ЗагрузитьТЗ_ФизическиеЛица(ТЗ,ТЗ_ТЧ,ПараметрыЗагрузки.НеПерезагружатьНайденные);
		// +ХМНПФ 13.06.2019 ГригорьевМА // ЗагрузитьПоТаблицамИзКлиента // №2309
		ТЗ_ТЧ = ПравилаЗагрузкиКлиент.ТабличныеЧасти["СПР01изv01ДопИнф_Запрос"].ТЗ;
		ЗагрузитьРС_ХМ_КасперКонтрагентДопИнф(ТЗ_ТЧ); 
		// -ХМНПФ 13.06.2019 ГригорьевМА // ЗагрузитьПоТаблицамИзКлиента // №2309
	ИначеЕсли СтрокаТаблицы.ИмяТаблицы = "ДокументыФизическихЛиц" И СтрокаТаблицы.ТипОбъекта = 4 Тогда
		Загрузка_ЗагрузитьТЗ_ФизическиеЛица_ДокументыФизическихЛиц(ТЗ);
		// +ХМНПФ 07.08.2018 ШадринАВ // ЗагрузитьПоТаблицамИзКлиента // №1763
	ИначеЕсли СтрокаТаблицы.ИмяТаблицы = "ХМ_КасперКонтрагентДопИнф" И СтрокаТаблицы.ТипОбъекта = 4 Тогда
		Загрузка_ЗагрузитьТЗ_ДополнительнаяИнформация_ФизическиеЛица(ТЗ);
		// -ХМНПФ 07.08.2018 ШадринАВ // ЗагрузитьПоТаблицамИзКлиента // №1763
	Иначе	

		Результат = Загрузка_ЗагрузитьТЗ(ПравилаЗагрузкиКлиент, ПараметрыЗагрузки, ЛогОшибок, ДополнительныеПараметры);

		ЛогОшибок.Добавить("Загружено");
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Результат;
КонецФункции

