
&ИзменениеИКонтроль("ВыполнитьФоновоеЗадание")
Функция ХМВыполнитьФоновоеЗадание(ПараметрыВыполнения, ИмяМетода, Параметры, Ключ, Наименование)
	#Вставка
	//https://redmine.npf.com/issues/4192	
	Если Не ЗначениеЗаполнено(Параметры[0].ПараметрыКлиентаНаСервере.Получить("ИмяПККлиента")) Тогда
		Попытка
			ИмяПККлиента = ПараметрыСеанса.ИмяПККлиента;
			ПараметрыКлиента = Новый Соответствие(Параметры[0].ПараметрыКлиентаНаСервере);
			ПараметрыКлиента.Вставить("ИмяПККлиента", ИмяПККлиента);
			Параметры[0].ПараметрыКлиентаНаСервере = Новый ФиксированноеСоответствие(ПараметрыКлиента);
		Исключение
		КонецПопытки;
	КонецЕсли;
	#КонецВставки
	
	Если ТекущийРежимЗапуска() = Неопределено
		И ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
		Сеанс = ПолучитьТекущийСеансИнформационнойБазы();
		Если ПараметрыВыполнения.ОжидатьЗавершение = Неопределено И Сеанс.ИмяПриложения = "BackgroundJob" Тогда
			ВызватьИсключение(НСтр("ru = 'В файловой информационной базе невозможно одновременно выполнять более одного фонового задания.'"), 
				КатегорияОшибки.ОшибкаКонфигурации);
		ИначеЕсли Сеанс.ИмяПриложения = "COMConnection" Тогда
			ВызватьИсключение(НСтр("ru = 'В файловой информационной базе можно запустить фоновое задание только из клиентского приложения.'"),
				КатегорияОшибки.ОшибкаКонфигурации);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыВыполнения.БезРасширений Тогда
		Возврат РасширенияКонфигурации.ВыполнитьФоновоеЗаданиеБезРасширений(ИмяМетода, Параметры, Ключ, Наименование);
	
	ИначеЕсли ПараметрыВыполнения.СРасширениямиБазыДанных Тогда
		Возврат РасширенияКонфигурации.ВыполнитьФоновоеЗаданиеСРасширениямиБазыДанных(ИмяМетода, Параметры, 
			Ключ, Наименование);
	Иначе
		Возврат ФоновыеЗадания.Выполнить(ИмяМетода, Параметры, Ключ, Наименование);
	КонецЕсли;
	
КонецФункции

&После("ВыполнитьСКонтекстомКлиента")
Процедура ХМВыполнитьСКонтекстомКлиента(ВсеПараметры)
	//https://redmine.npf.com/issues/4192
	ПараметрыКлиента = ВсеПараметры.ПараметрыКлиентаНаСервере;
	Если ЗначениеЗаполнено(ПараметрыКлиента.Получить("ИмяПККлиента")) Тогда
    	ПараметрыСеанса.ИмяПККлиента = ПараметрыКлиента.Получить("ИмяПККлиента");
	КонецЕсли;
	// Вставить содержимое метода.
КонецПроцедуры
