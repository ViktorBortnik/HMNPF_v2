
&НаСервере
&ИзменениеИКонтроль("СформироватьТЗДокумента")
Процедура ХМСформироватьТЗДокумента(Объект, АдресОбъекта)
	#Удаление
	НачатьТранзакцию();
	#КонецУдаления
	#Вставка
	// +ХМНПФ 17.01.2020 ШадринАВ // СформироватьТЗДокумента() // ОПС_Начисления_прош_пер
	#КонецВставки
	Периодичность = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
	ДатаФормированияДокументов = Объект.ДатаФормированияДокументов;
	ПериодНачисления = Объект.ПериодНачисления;
	ОтборПенсионныйДоговор = Объект.ОтборПенсионныйДоговор;
	ОтборГруппаДоговоров = Объект.ОтборГруппаДоговоров;
	Организация = Объект.Организация;
	Пожизненная = Объект.Пожизненная;
	ОтборПолучатель = Объект.ОтборПолучатель;
	ИспользоватьОтборОчередностьВыплаты = Объект.ИспользоватьОтборОчередностьВыплаты;
	НачислятьЗаПрошлыеПериоды = Объект.НачислятьЗаПрошлыеПериоды;
	Ежемесячно = Объект.Ежемесячно;
	ОтборОчередностьВыплаты = Объект.ОтборОчередностьВыплаты;
	Ежеквартально = Объект.Ежеквартально;
	Полугодие = Объект.Полугодие;
	Срочная = Объект.Срочная;
	
	ТЗДокумента = Новый ТаблицаЗначений();
	ТЗДокумента.Колонки.Добавить("ДоговорОПС");
	ТЗДокумента.Колонки.Добавить("ТипПенсии");
	ТЗДокумента.Колонки.Добавить("ПериодНачисления", Новый ОписаниеТипов("Дата"));
	ТЗДокумента.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТЗДокумента.Колонки.Добавить("Пусто");
	ТЗДокумента.Колонки.Добавить("Получатель");
	ТЗДокумента.Колонки.Добавить("ГруппаДоговора");
	ТЗДокумента.Колонки.Добавить("ОчередностьВыплаты");
	ТЗДокумента.Колонки.Добавить("Периодичность");
	ТЗДокумента.Колонки.Добавить("ПодразделениеБУ");
	ТЗДокумента.Колонки.Добавить("ЭтоДоначисление");
	ТЗДокумента.Колонки.Добавить("ПризнакПоследнейВыплаты");
	
	ДатаВПериоде = уп_НПФ.ОпределитьДатуВПериоде(ДатаФормированияДокументов,Периодичность,ПериодНачисления);
	ДатаКонцаПериода = уп_НПФ.ОпределитьДатуКонцаПериода(ПериодНачисления,Периодичность);
	
	Если (ЗначениеЗаполнено(ОтборПенсионныйДоговор)) 
		или (ЗначениеЗаполнено(ОтборГруппаДоговоров)) Тогда
		ОтборДоговор = Истина;
	Иначе
		ОтборДоговор = Ложь;
	КонецЕсли;
	
	#Вставка
	// +ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
	Если Объект.ХМ_ТолькоНовые И ЗначениеЗаполнено(Объект.ХМ_НачалоПериода) И ЗначениеЗаполнено(Объект.ХМ_КонецПериода) Тогда
		ОтборДоговор = Истина;
		НачислятьЗаПрошлыеПериоды = Ложь;
	КонецЕсли;
	// -ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
	#КонецВставки
	ПенсионныеПравила = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(ДатаВПериоде, Новый Структура("Организация", Организация));
	ЛицевыеСчетаПоТипамВыплат = ПенсионныеПравила.ЛицевыеСчетаПоТипамВыплат;
	
	Если Пожизненная Тогда
		
		Запрос = Новый Запрос;
		
		Текст = "ВЫБРАТЬ
		|	СостоянияПССрезПоследних.ДоговорОПС,
		|	СостоянияПССрезПоследних.ДоговорОПС.Участник КАК Участник,
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДатаУстановкиСостояния КАК Период
		|ПОМЕСТИТЬ ТаблицаСчетов
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
		|			&ДатаВПериоде,
		|			ДоговорОПС.Организация = &Организация
		|		    "+?(ОтборДоговор," И ДоговорОПС В ИЕРАРХИИ (&ПенсионныйДоговор)","")+"
		|				) КАК СостоянияПССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ПериодичностьВыплатСрезПоследних.ДоговорОПС КАК ДоговорОПС
		|		ИЗ
		|			РегистрСведений.уп_ПериодичностьВыплатОПС.СрезПоследних(&ДатаВПериоде, ) КАК ПериодичностьВыплатСрезПоследних
		|		ГДЕ
		|			ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат = &Периодичность) КАК Периодичность
		|		ПО СостоянияПССрезПоследних.ДоговорОПС = Периодичность.ДоговорОПС";
		Если ЗначениеЗаполнено(ОтборПолучатель) Тогда
			Текст = Текст + "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ЛицевыеСчетаУчастниковСрезПоследних.ДоговорОПС КАК ДоговорОПС
			|		ИЗ
			|			РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(&ДатаДок, Организация = &Организация "+?(ЛицевыеСчетаПоТипамВыплат, " И ТипВыплат = &НП", "")+"
			|			) КАК ЛицевыеСчетаУчастниковСрезПоследних
			|		ГДЕ
			|			ЛицевыеСчетаУчастниковСрезПоследних.Банк В ИЕРАРХИИ(&Банк)) КАК Получатели
			|		ПО СостоянияПССрезПоследних.ДоговорОПС = Получатели.ДоговорОПС";
		КонецЕсли;
		Если ИспользоватьОтборОчередностьВыплаты Тогда
			Текст = Текст + "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			НомераОчередейСрезПоследних.ДоговорОПС КАК ДоговорОПС
			|		ИЗ
			|			РегистрСведений.уп_НомераОчередейОПС.СрезПоследних(&ДатаДок,  ТипПенсии = &Пожизненная) КАК НомераОчередейСрезПоследних
			|		ГДЕ
			|			НомераОчередейСрезПоследних.НомерОчереди = &НомерОчереди) КАК Очередность
			|		ПО СостоянияПССрезПоследних.ДоговорОПС = Очередность.ДоговорОПС";
		КонецЕсли;
		Текст = Текст + "
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|				&ДатаВПериоде,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В ИЕРАРХИИ (&ПенсионныйДоговор)) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|		ПО СостоянияПССрезПоследних.ДоговорОПС = уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|ГДЕ
		|	СостоянияПССрезПоследних.Состояние В(&СписокВыплатныхСостояний)
		|	И уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &НЧТП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСчетов.ДоговорОПС,
		|	ТаблицаСчетов.Участник,
		|	ТаблицаСчетов.Период
		|ИЗ
		|	ТаблицаСчетов КАК ТаблицаСчетов  "+ ?(НачислятьЗаПрошлыеПериоды,"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСчетов.ДоговорОПС,
		|	ТаблицаСчетов.Участник,
		|	ТаблицаСчетов.Период
		|ПОМЕСТИТЬ СписокСчетовБезНачислений
		|ИЗ
		|	ТаблицаСчетов КАК ТаблицаСчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачисленийОПС.Обороты(
		|				&ДатаНачалаВремен,
		|				&ДатаКонцаПрошлогоПериода,
		|				Период,
		|				ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ТаблицаСчетов.ДоговорОПС
		|						ИЗ
		|							ТаблицаСчетов КАК ТаблицаСчетов)
		|					И ТипПенсии = &Пожизненная) КАК уп_СчетчикНачисленийОПСОбороты
		|		ПО ТаблицаСчетов.ДоговорОПС = уп_СчетчикНачисленийОПСОбороты.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПоследних(
		|				&ДатаДок,
		|				ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ТаблицаСчетов.ДоговорОПС
		|						ИЗ
		|							ТаблицаСчетов КАК ТаблицаСчетов)
		|					И ТипПенсии = &Пожизненная) КАК уп_РазмерПенсииОПССрезПоследних
		|		ПО ТаблицаСчетов.ДоговорОПС = уп_РазмерПенсииОПССрезПоследних.ДоговорОПС
		|ГДЕ
		|	уп_СчетчикНачисленийОПСОбороты.ДоговорОПС ЕСТЬ NULL 
		|	И уп_РазмерПенсииОПССрезПоследних.ДоговорОПС ЕСТЬ НЕ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокСчетовБезНачислений.ДоговорОПС,
		|	СписокСчетовБезНачислений.Участник,
		|	НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, МЕСЯЦ) КАК Период
		|ИЗ
		|	СписокСчетовБезНачислений КАК СписокСчетовБезНачислений
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, МЕСЯЦ) <> &НачТекПериода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ ПО
		|	Период ПЕРИОДАМИ(МЕСЯЦ, , &ДатаКонцаПрошлогоПериода)","");
		
		Запрос.УстановитьПараметр("ДатаВПериоде", ДатаКонцаПериода);
		Запрос.УстановитьПараметр("Периодичность", Периодичность);
		Запрос.УстановитьПараметр("НачТекПериода", уп_НПФ.ОпределитьДатуНачалаПериода(ПериодНачисления,Периодичность));
		СписокВыплатныхСостояний = Новый СписокЗначений;
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
		
		Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ДатаДок", ДатаФормированияДокументов);
		Запрос.УстановитьПараметр("Банк", ОтборПолучатель);
		Запрос.УстановитьПараметр("НП", Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
		Если ЗначениеЗаполнено(ОтборГруппаДоговоров) Тогда
			ЗначениеОтбораДоговора = ОтборГруппаДоговоров;
		Иначе
			ЗначениеОтбораДоговора = ОтборПенсионныйДоговор;
		КонецЕсли;
		Запрос.УстановитьПараметр("ПенсионныйДоговор", ЗначениеОтбораДоговора);
		Запрос.УстановитьПараметр("НомерОчереди", ОтборОчередностьВыплаты);
		//Запрос.УстановитьПараметр("Срочная", Перечисления.уп_ТипыПенсийОПС.Срочная);
		Запрос.УстановитьПараметр("Пожизненная", Перечисления.уп_ТипыПенсийОПС.Пожизненная);
		Запрос.УстановитьПараметр("НЧТП", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП);
		
		Если НачислятьЗаПрошлыеПериоды Тогда
			ДатаНачалаПериода 			= уп_НПФ.ОпределитьДатуНачалаПериода(ПериодНачисления,Периодичность);
			Коэффициент 				= уп_НПФ.ЧисловойКоэффициентПериода(Периодичность);
			ДатаНачалаПрошлогоПериода   = ДобавитьМесяц(ДатаНачалаПериода, -Коэффициент);
			ДатаКонцаПрошлогоПериода    = ДобавитьМесяц(ДатаКонцаПериода, -Коэффициент);
			
			ПериодичностьИмя = "";
			
			Если Ежемесячно  Тогда
				ПериодичностьИмя = "МЕСЯЦ";
			ИначеЕсли Ежеквартально Тогда
				ПериодичностьИмя = "Квартал";
			ИначеЕсли Полугодие Тогда
				ПериодичностьИмя = "Полугодие"; 
			Иначе
				ПериодичностьИмя = "Год";		
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ДатаНачалаПрошлогоПериода", ДатаНачалаПрошлогоПериода);
			Запрос.УстановитьПараметр("ДатаКонцаПрошлогоПериода", ДатаКонцаПрошлогоПериода);
			Запрос.УстановитьПараметр("ДатаНачалаВремен", '20100101');
			
			Текст = СтрЗаменить(Текст, "&ПериодичностьИмя", ПериодичностьИмя); 
			
		КонецЕсли;
		
		Запрос.Текст = Текст;
		
		#Вставка
		// +ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
		Если Объект.ХМ_ТолькоНовые И ЗначениеЗаполнено(Объект.ХМ_НачалоПериода) И ЗначениеЗаполнено(Объект.ХМ_КонецПериода) Тогда
			СписокДоговоров = ХМ_ПолучитьСписокДоговоров(Объект, "Пожизненная");
			Запрос.УстановитьПараметр("ПенсионныйДоговор", СписокДоговоров);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорОПС В ИЕРАРХИИ", "ДоговорОПС В");
		КонецЕсли;
		// -ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
		#КонецВставки
		ВыборкаПакет = Запрос.ВыполнитьПакет();
		//ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
		ТаблицаСчетов = ВыборкаПакет[1].Выгрузить();
		
		ТаблицаСчетов.Колонки.Добавить("НомерНачисления", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
		ТаблицаСчетов.Колонки.Добавить("НачисленоРаннее", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
		
		Если НачислятьЗаПрошлыеПериоды Тогда
			
			ВыборкаПоПрошлымПериодам = ВыборкаПакет[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Период" ,"Все");
			ТаблицаСчетовПрошлыхПериодов = ТаблицаСчетов.Скопировать();
			
			ТаблицаСчетовПрошлыхПериодов.Очистить();
			
			Пока ВыборкаПоПрошлымПериодам.Следующий() Цикл
				
				ВыборкаПоСчетам = ВыборкаПоПрошлымПериодам.Выбрать();
				
				Пока ВыборкаПоСчетам.Следующий() Цикл 
					
					НайденнаяСтрока = ТаблицаСчетовПрошлыхПериодов.Найти(ВыборкаПоСчетам.ДоговорОПС, "ДоговорОПС");
					
					Если НайденнаяСтрока = Неопределено Тогда
						НоваяСтрока = ТаблицаСчетовПрошлыхПериодов.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСчетам);
					КонецЕсли;
					
				КонецЦикла;
				
				уп_ОПС.СформироватьТЗНачисленияОПС(ТЗДокумента, ТаблицаСчетовПрошлыхПериодов, ДатаФормированияДокументов, ВыборкаПоПрошлымПериодам.Период, Периодичность, Объект, Пожизненная, ЛОЖЬ, Истина, Истина);
				
			КонецЦикла;
			
		КонецЕсли;
		
		уп_ОПС.СформироватьТЗНачисленияОПС(ТЗДокумента, ТаблицаСчетов, ДатаФормированияДокументов, ПериодНачисления, Периодичность, Объект, Пожизненная, ЛОЖЬ, Истина, Ложь);
		
	КонецЕсли;
	
	Если Срочная Тогда
		
		Запрос = Новый Запрос;
		
		Текст = "ВЫБРАТЬ
		|	СостоянияПССрезПоследних.ДоговорОПС,
		|	СостоянияПССрезПоследних.ДоговорОПС.Участник КАК Участник,
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДатаУстановкиСостояния КАК Период
		|ПОМЕСТИТЬ ТаблицаСчетов
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
		|			&ДатаВПериоде,
		|			ДоговорОПС.Организация = &Организация
		|		    "+?(ОтборДоговор," И ДоговорОПС В ИЕРАРХИИ (&ПенсионныйДоговор)","")+"
		|				) КАК СостоянияПССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ПериодичностьВыплатСрезПоследних.ДоговорОПС КАК ДоговорОПС
		|		ИЗ
		|			РегистрСведений.уп_ПериодичностьВыплатОПС.СрезПоследних(&ДатаВПериоде, ) КАК ПериодичностьВыплатСрезПоследних
		|		ГДЕ
		|			ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат = &Периодичность) КАК Периодичность
		|		ПО СостоянияПССрезПоследних.ДоговорОПС = Периодичность.ДоговорОПС";
		Если ЗначениеЗаполнено(ОтборПолучатель) Тогда
			Текст = Текст + "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ЛицевыеСчетаУчастниковСрезПоследних.ДоговорОПС КАК ДоговорОПС
			|		ИЗ
			|			РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(&ДатаДок, Организация = &Организация  "+?(ЛицевыеСчетаПоТипамВыплат, " И ТипВыплат = &СВ", "")+"
			|			) КАК ЛицевыеСчетаУчастниковСрезПоследних
			|		ГДЕ
			|			ЛицевыеСчетаУчастниковСрезПоследних.Банк В ИЕРАРХИИ(&Банк)) КАК Получатели
			|		ПО СостоянияПССрезПоследних.ДоговорОПС = Получатели.ДоговорОПС";
		КонецЕсли;
		Если ИспользоватьОтборОчередностьВыплаты Тогда
			Текст = Текст + "
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			НомераОчередейСрезПоследних.ДоговорОПС КАК ДоговорОПС
			|		ИЗ
			|			РегистрСведений.уп_НомераОчередейОПС.СрезПоследних(&ДатаДок,  ТипПенсии = &Срочная) КАК НомераОчередейСрезПоследних
			|		ГДЕ
			|			НомераОчередейСрезПоследних.НомерОчереди = &НомерОчереди) КАК Очередность
			|		ПО СостоянияПССрезПоследних.ДоговорОПС = Очередность.ДоговорОПС";
		КонецЕсли;
		Текст = Текст + "
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|				&ДатаВПериоде,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В ИЕРАРХИИ (&ПенсионныйДоговор)) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|		ПО СостоянияПССрезПоследних.ДоговорОПС = уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|ГДЕ
		|	СостоянияПССрезПоследних.Состояние В(&СписокВыплатныхСостояний)
		|	И уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСчетов.ДоговорОПС,
		|	ТаблицаСчетов.Участник,
		|	ТаблицаСчетов.Период
		|ИЗ
		|	ТаблицаСчетов КАК ТаблицаСчетов   "+ ?(НачислятьЗаПрошлыеПериоды,"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСчетов.ДоговорОПС,
		|	ТаблицаСчетов.Участник,
		|	ТаблицаСчетов.Период
		|ПОМЕСТИТЬ СписокСчетовБезНачислений
		|ИЗ
		|	ТаблицаСчетов КАК ТаблицаСчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачисленийОПС.Обороты(
		|				&ДатаНачалаВремен,
		|				&ДатаКонцаПрошлогоПериода,
		|				Период,
		|				ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ТаблицаСчетов.ДоговорОПС
		|						ИЗ
		|							ТаблицаСчетов КАК ТаблицаСчетов)
		|					И ТипПенсии = &Срочная) КАК уп_СчетчикНачисленийОПСОбороты
		|		ПО ТаблицаСчетов.ДоговорОПС = уп_СчетчикНачисленийОПСОбороты.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПоследних(
		|				&ДатаДок,
		|				ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ТаблицаСчетов.ДоговорОПС
		|						ИЗ
		|							ТаблицаСчетов КАК ТаблицаСчетов)
		|					И ТипПенсии = &Срочная) КАК уп_РазмерПенсииОПССрезПоследних
		|		ПО ТаблицаСчетов.ДоговорОПС = уп_РазмерПенсииОПССрезПоследних.ДоговорОПС
		|ГДЕ
		|	уп_СчетчикНачисленийОПСОбороты.ДоговорОПС ЕСТЬ NULL 
		|	И уп_РазмерПенсииОПССрезПоследних.ДоговорОПС ЕСТЬ НЕ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокСчетовБезНачислений.ДоговорОПС,
		|	СписокСчетовБезНачислений.Участник,
		|	НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, МЕСЯЦ) КАК Период
		|ИЗ
		|	СписокСчетовБезНачислений КАК СписокСчетовБезНачислений
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СписокСчетовБезНачислений.Период, МЕСЯЦ) <> &НачТекПериода
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ ПО
		|	Период ПЕРИОДАМИ(МЕСЯЦ, , &ДатаКонцаПрошлогоПериода)","");
		
		Запрос.УстановитьПараметр("ДатаВПериоде", ДатаКонцаПериода);
		Запрос.УстановитьПараметр("Периодичность", Периодичность);
		Запрос.УстановитьПараметр("НачТекПериода", уп_НПФ.ОпределитьДатуНачалаПериода(ПериодНачисления,Периодичность));
		СписокВыплатныхСостояний = Новый СписокЗначений;
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены);
		
		
		Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ДатаДок", ДатаФормированияДокументов);
		Запрос.УстановитьПараметр("Банк", ОтборПолучатель);
		Запрос.УстановитьПараметр("СВ", Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
		Если ЗначениеЗаполнено(ОтборГруппаДоговоров) Тогда
			ЗначениеОтбораДоговора = ОтборГруппаДоговоров;
		Иначе
			ЗначениеОтбораДоговора = ОтборПенсионныйДоговор;
		КонецЕсли;
		Запрос.УстановитьПараметр("ПенсионныйДоговор", ЗначениеОтбораДоговора);
		Запрос.УстановитьПараметр("НомерОчереди", ОтборОчередностьВыплаты);
		Запрос.УстановитьПараметр("Срочная", Перечисления.уп_ТипыПенсийОПС.Срочная);
		Запрос.УстановитьПараметр("СрочнаяВыплата", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.СрочнаяВыплата);
		//Запрос.УстановитьПараметр("Пожизненная", Перечисления.уп_ТипыПенсийОПС.Пожизненная);
		
		Если НачислятьЗаПрошлыеПериоды Тогда
			ДатаНачалаПериода 			= уп_НПФ.ОпределитьДатуНачалаПериода(ПериодНачисления,Периодичность);
			Коэффициент 				= уп_НПФ.ЧисловойКоэффициентПериода(Периодичность);
			ДатаНачалаПрошлогоПериода   = ДобавитьМесяц(ДатаНачалаПериода, -Коэффициент);
			ДатаКонцаПрошлогоПериода    = ДобавитьМесяц(ДатаКонцаПериода, -Коэффициент);
			
			ПериодичностьИмя = "";
			
			Если Ежемесячно  Тогда
				ПериодичностьИмя = "МЕСЯЦ";
			ИначеЕсли Ежеквартально Тогда
				ПериодичностьИмя = "Квартал";
			ИначеЕсли Полугодие Тогда
				ПериодичностьИмя = "Полугодие"; 
			Иначе
				ПериодичностьИмя = "Год";		
			КонецЕсли;
			
			Запрос.УстановитьПараметр("ДатаНачалаПрошлогоПериода", ДатаНачалаПрошлогоПериода);
			Запрос.УстановитьПараметр("ДатаКонцаПрошлогоПериода", ДатаКонцаПрошлогоПериода);
			Запрос.УстановитьПараметр("ДатаНачалаВремен", '20100101');
			
			Текст = СтрЗаменить(Текст, "&ПериодичностьИмя", ПериодичностьИмя); 
			
		КонецЕсли;
		
		Запрос.Текст = Текст;
		
		#Вставка
		// +ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
		Если Объект.ХМ_ТолькоНовые И ЗначениеЗаполнено(Объект.ХМ_НачалоПериода) И ЗначениеЗаполнено(Объект.ХМ_КонецПериода) Тогда
			СписокДоговоров = ХМ_ПолучитьСписокДоговоров(Объект, "Срочная");
			Запрос.УстановитьПараметр("ПенсионныйДоговор", СписокДоговоров);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорОПС В ИЕРАРХИИ", "ДоговорОПС В");
		КонецЕсли;
		// -ХМНПФ 25.05.2022 МилевскийАЮ // ХМСформироватьТЗДокумента() // №3473
		#КонецВставки
		ВыборкаПакет = Запрос.ВыполнитьПакет();
		//ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
		ТаблицаСчетов = ВыборкаПакет[1].Выгрузить();
		
		ТаблицаСчетов.Колонки.Добавить("НомерНачисления", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
		ТаблицаСчетов.Колонки.Добавить("НачисленоРаннее", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15,2));
		
		Если НачислятьЗаПрошлыеПериоды Тогда
			
			ВыборкаПоПрошлымПериодам = ВыборкаПакет[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Период" ,"Все");
			ТаблицаСчетовПрошлыхПериодов = ТаблицаСчетов.Скопировать();
			
			ТаблицаСчетовПрошлыхПериодов.Очистить();
			
			Пока ВыборкаПоПрошлымПериодам.Следующий() Цикл
				
				ВыборкаПоСчетам = ВыборкаПоПрошлымПериодам.Выбрать();
				
				Пока ВыборкаПоСчетам.Следующий() Цикл 
					
					НайденнаяСтрока = ТаблицаСчетовПрошлыхПериодов.Найти(ВыборкаПоСчетам.ДоговорОПС, "ДоговорОПС");
					
					Если НайденнаяСтрока = Неопределено Тогда
						НоваяСтрока = ТаблицаСчетовПрошлыхПериодов.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСчетам);
					КонецЕсли;
					
				КонецЦикла;
				
				уп_ОПС.СформироватьТЗНачисленияОПС(ТЗДокумента, ТаблицаСчетовПрошлыхПериодов, ДатаФормированияДокументов, ВыборкаПоПрошлымПериодам.Период, Периодичность, Объект, Ложь, Срочная, Истина, Истина);
				
			КонецЦикла;
			
		КонецЕсли;
		
		уп_ОПС.СформироватьТЗНачисленияОПС(ТЗДокумента, ТаблицаСчетов, ДатаФормированияДокументов, ПериодНачисления, Периодичность, Объект, Ложь, Срочная, Истина, Ложь);
		
	КонецЕсли;
	#Вставка
	// +ХМНПФ 17.01.2020 ШадринАВ // СформироватьТЗДокумента() // ОПС_Начисления_прош_пер
	#КонецВставки
	#Удаление
	ПоместитьВоВременноеХранилище(Объект, АдресОбъекта);
	
	ЗафиксироватьТранзакцию();
	#КонецУдаления
КонецПроцедуры

&НаСервере
Функция ХМ_ПолучитьСписокДоговоров(Объект, Тип)
	
	СписокДоговоров = Новый Массив;	 
	
	Запрос = Новый Запрос;
	
	Если Тип = "Пожизненная" Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_РешениеОНазначенииНЧТП.ДоговорОПС КАК ДоговорОПС
		|ИЗ
		|	Документ.уп_РешениеОНазначенииНЧТП КАК уп_РешениеОНазначенииНЧТП
		|ГДЕ
		|	уп_РешениеОНазначенииНЧТП.Дата МЕЖДУ &ХМ_НачалоПериода И &ХМ_КонецПериода";
		
		Запрос.УстановитьПараметр("ХМ_КонецПериода", Объект.ХМ_КонецПериода);
		Запрос.УстановитьПараметр("ХМ_НачалоПериода", Объект.ХМ_НачалоПериода);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СписокДоговоров.Добавить(Выборка.ДоговорОПС);	
		КонецЦикла;
	КонецЕсли;
	
	Если Тип = "Срочная" Тогда	
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_РешениеОНазначенииСрочнойВыплаты.ДоговорОПС КАК ДоговорОПС
		|ИЗ
		|	Документ.уп_РешениеОНазначенииСрочнойВыплаты КАК уп_РешениеОНазначенииСрочнойВыплаты
		|ГДЕ
		|	уп_РешениеОНазначенииСрочнойВыплаты.Дата МЕЖДУ &ХМ_НачалоПериода И &ХМ_КонецПериода";
		
		Запрос.УстановитьПараметр("ХМ_КонецПериода", Объект.ХМ_КонецПериода);
		Запрос.УстановитьПараметр("ХМ_НачалоПериода", Объект.ХМ_НачалоПериода);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СписокДоговоров.Добавить(Выборка.ДоговорОПС);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат СписокДоговоров;
	
КонецФункции

// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_СформироватьТЗДокумента() // ОПС_Начисления_прош_пер
&НаСервере
Процедура ХМ_СформироватьТЗДокумента(Объект, АдресОбъекта, ИмяПК = Неопределено) Экспорт
	
	Если Не ИмяПК = Неопределено Тогда
		хм_РегистрацияСобытийПривелигирован.УстановитьЗначениеИмяПККлиента(ИмяПК);
	КонецЕсли;

	НачатьТранзакцию();
	
	Если Объект.НачислятьЗаПрошлыеПериоды  Тогда
		ИсхПериодНачисления = Объект.ПериодНачисления;
		Объект.НачислятьЗаПрошлыеПериоды = Ложь;
		Для к = 0 ПО Объект.ХМ_КоличествоПрошлыхПериодов Цикл
			Объект.ПериодНачисления = ДобавитьМесяц(ИсхПериодНачисления, -к);
			СформироватьТЗДокумента(Объект, АдресОбъекта);
		КонецЦикла;
		Объект.НачислятьЗаПрошлыеПериоды = Истина;
		Объект.ПериодНачисления = ИсхПериодНачисления;
	Иначе
		СформироватьТЗДокумента(Объект, АдресОбъекта);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Объект, АдресОбъекта);
	ЗафиксироватьТранзакцию();
КонецПроцедуры
	
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 28.10.2015 ШадринАВ // ЗаписатьДокументы() // №4
	// ХМНПФ 13.10.2016 ШадринАВ // Новая ХМ_ПолучитьДатуНачалаДействия() // №582
	// ХМНПФ 13.10.2016 ШадринАВ // ДатаНачалаДействия() // №582
	// ХМНПФ 22.02.2017 ШадринАВ // СформироватьТаблицуИДПоСпискуДоговоров() // №829
	// ХМНПФ 01.03.2017 ШадринАВ // Новая ХМ_ПолучитьТабИзбранных() // №844
	// ХМНПФ 02.03.2017 ШадринАВ // СформироватьТЗДокументаПоИД() // для отладки по одному договору
	// ХМНПФ 02.03.2017 ШадринАВ // СформироватьТЗДокументаПоИД() // №845
	// ХМНПФ 22.03.2017 ШадринАВ // ДатаНачалаДействия() // №869
	// ХМНПФ 31.03.2017 ШадринАВ // ЗаписатьДокументыНачисленияИД() // №895
	// ХМНПФ 22.03.2018 ШадринАВ // РазобратьТаблицу() // №1448
	// ХМНПФ 19.04.2018 КлименкоМИ // ОпределитьВидОперации() // №1530
	// ХМНПФ 15.08.2019 ШадринАВ // ЗаписатьДокументы() // №2400
	// ХМНПФ 17.01.2020 ШадринАВ // СформироватьТЗДокумента() // ОПС_Начисления_прош_пер
	// ХМНПФ 17.01.2020 ШадринАВ // Новая ХМ_СформироватьТЗДокумента() // ОПС_Начисления_прош_пер
	
	// ХМНПФ 31.03.2017 ШадринАВ // СформироватьТЗДокументаПоИД() // №895 // не актуально ШадринАВ
КонецПроцедуры

// ХМНПФ 01.03.2017 ШадринАВ // Новая ХМ_ПолучитьТабИзбранных() // №844
Функция ХМ_ПолучитьТабИзбранных(ТекОбъект,ИмяОбласти)
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ДоговорОПС", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(13,ДопустимаяДлина.Переменная)));
	
	Если Год(ТекОбъект.КонецПериода) = 2016 И ТекОбъект.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений Тогда
		Макет = ТекОбъект.ПолучитьМакет("ХМ_ИД2016");
		ОблВст = Макет.ПолучитьОбласть(ИмяОбласти);
	    ВысотаТаб = ОблВст.ВысотаТаблицы;
		Для НСтр = 1 По ВысотаТаб Цикл
			НовСтр = ТЗ.Добавить();
			НовСтр.ДоговорОПС = СокрЛП(ОблВст.Область(НСтр, 1).Текст);
		КонецЦикла;
	КонецЕсли;
	ТЗ.Свернуть("ДоговорОПС");
	
	Возврат ТЗ
КонецФункции // ХМ_ПолучитьТабИзбранных()



