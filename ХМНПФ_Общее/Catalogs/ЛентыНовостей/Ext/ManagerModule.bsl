
Процедура ХМ_НеТиповыеИзменения()
	//	ХМНПФ 28.04.2018 КлименкоМИ// ЗагрузитьСтандартныеЗначения() №1548
КонецПроцедуры

&ИзменениеИКонтроль("ЗагрузитьСтандартныеЗначения")
Процедура ХМЗагрузитьСтандартныеЗначения(КонтекстВыполнения)

	#Область ИнициализацияМетода

	ОбъектМетаданных     = Справочники.ЛентыНовостей; // Переопределение
	ИмяОбъектаМетаданных = "Справочники.ЛентыНовостей"; // Переопределение
	ИмяСвойства          = "CatalogObject_ЛентыНовостей"; // Переопределение

	НаименованиеМетода = ИмяОбъектаМетаданных + ".ЗагрузитьСтандартныеЗначения"; // АПК:1297 Это идентификатор.
	Если КонтекстВыполнения = Неопределено Тогда
		КонтекстВыполнения = НовыйКонтекстВыполнения(НаименованиеМетода, Истина); // Этот контекст.
	КонецЕсли;

	НачатьШагВыполнения(КонтекстВыполнения, "Запуск:" + НаименованиеМетода); // АПК:1297 Это идентификатор.

	ТипСоответствие = Тип("Соответствие");
	ТипСтрока       = Тип("Строка");

	#КонецОбласти

	#Область Проверки

	Если НЕ ОбработкаНовостейСлужебный.Проверка_ДоступныНеразделенныеДанные(КонтекстВыполнения) Тогда
		Возврат;
	КонецЕсли;

	#КонецОбласти

	#Область Обработка

	Шаг = НаименованиеМетода + "/ЗагрузкаДанныхИзМакета";
	НачатьШагВыполнения(КонтекстВыполнения, Шаг);
	ЛогШаговВыполнения = Новый Массив;

	СодержимоеМакета = ОбъектМетаданных.ПолучитьМакет("СтандартныеЗначения").ПолучитьТекст();
	ПоставляемыеДанныеОбъекта = Новый Соответствие;
	ПоставляемыеДанныеОбъекта.Вставить(
	"" + ИмяОбъектаМетаданных + ":СтандартныеЗначения", // АПК:1297 Это идентификатор.
	СодержимоеМакета);
	ОбработкаНовостейСлужебный.ПолучитьДополнительныеСтандартныеЗначенияКлассификаторов(
	ИмяОбъектаМетаданных,
	ПоставляемыеДанныеОбъекта);
	Если ТипЗнч(ПоставляемыеДанныеОбъекта) = ТипСоответствие Тогда
		Для Каждого СтрокаСтандартныхЗначений Из ПоставляемыеДанныеОбъекта Цикл
			Если (ТипЗнч(СтрокаСтандартныхЗначений.Значение) = ТипСтрока)
				И (НЕ ПустаяСтрока(СтрокаСтандартныхЗначений.Значение)) Тогда
				ЕстьОшибки = Ложь;
				Попытка
					ЛогШаговВыполнения.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Попытка загрузки стандартных значений из строки (первые 2000 символов):
					|%1'"),
					Лев(СтрокаСтандартныхЗначений.Значение, 2000)));
					ЧтениеХМЛ = Новый ЧтениеXML;
					ЧтениеХМЛ.УстановитьСтроку(СтрокаСтандартныхЗначений.Значение);
					ЧтениеХМЛ.Прочитать();
				Исключение
					ИнформацияОбОшибке = ИнформацияОбОшибке();
					ЛогШаговВыполнения.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Ошибка загрузки стандартных значений из строки по причине:
					|%1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке)));
					ЕстьОшибки = Истина;
				КонецПопытки;
				Если ЕстьОшибки <> Истина Тогда
					ХМЛТип = ПолучитьXMLТип(ЧтениеХМЛ);
					#Удаление 
					Если (НРег(ХМЛТип.ИмяТипа) = НРег("DefaultData")) Тогда	
					// ... И (ВРег(ХМЛТип.URIПространстваИмен)=ВРег("http://v8.1c.ru/8.1/data/enterprise/current-config"))
					#КонецУдаления 
					#Вставка 
					//+	ХМНПФ 28.04.2018 КлименкоМИ// ЗагрузитьСтандартныеЗначения() №1548
					Если (НРег(ХМЛТип.ИмяТипа) = НРег("DefaultData")) И (ВРег(ХМЛТип.URIПространстваИмен)=ВРег("http://v8.1c.ru/8.1/data/enterprise/current-config")) Тогда
					//-	ХМНПФ 28.04.2018 КлименкоМИ// ЗагрузитьСтандартныеЗначения() №1548
	              	#КонецВставки
						ОбъектХДТО = ФабрикаXDTO.ПрочитатьXML(ЧтениеХМЛ);
						СвойствоОбъект = ОбъектХДТО.Свойства().Получить(ИмяСвойства);
						Если ТипЗнч(СвойствоОбъект) = Тип("СвойствоXDTO") Тогда
							Если (СвойствоОбъект.ВерхняяГраница = -1) ИЛИ (СвойствоОбъект.ВерхняяГраница > 1) Тогда
								СписокХДТО = ОбъектХДТО.ПолучитьСписок(СвойствоОбъект);
								Для каждого ЛокальныйТекущийОбъект Из СписокХДТО Цикл
									ЗагрузитьСтандартноеЗначение(
									ЛокальныйТекущийОбъект,
									ОбъектМетаданных,
									ИмяСвойства,
									ЛогШаговВыполнения);
								КонецЦикла;
							ИначеЕсли (СвойствоОбъект.НижняяГраница = 1) И (СвойствоОбъект.ВерхняяГраница = 1) Тогда
								ЗагрузитьСтандартноеЗначение(
								ОбъектХДТО.Получить(СвойствоОбъект),
								ОбъектМетаданных,
								ИмяСвойства,
								ЛогШаговВыполнения);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ЗавершитьШагВыполнения(КонтекстВыполнения, "", СтрСоединить(ЛогШаговВыполнения, Символы.ПС));

	#КонецОбласти

	#Область Завершение

	ОбработкаНовостейСлужебный.ЗаписатьКонтекстВыполненияВЖурналРегистрации(КонтекстВыполнения);

	#КонецОбласти

КонецПроцедуры
