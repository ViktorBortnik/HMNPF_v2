
&НаСервере
&ИзменениеИКонтроль("ОбновитьСсылки")
Процедура ХМОбновитьСсылки()
	
	лДатаПолученияОстатков = КонецДня(ДатаПолученияОстатков);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СостоянияПССрезПоследних.Состояние КАК Состояние,
	|	уп_СостоянияПССрезПоследних.ДатаУстановкиСостояния КАК ДатаУстановкиСостояния,
	|	уп_СостоянияПССрезПоследних.Основание КАК Основание,
	|	уп_СостоянияПССрезПоследних.ДатаОкончанияОснований КАК ДатаОкончанияОснований,
	|	уп_СостоянияПССрезПоследних.Расшифровка КАК Расшифровка,
	|	ВЫБОР
	|		КОГДА уп_СостоянияПССрезПоследних.Регистратор ССЫЛКА Документ.уп_ПриостановкаВыплат
	|				И уп_СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.НачисленияПриостановлены)
	|			ТОГДА уп_СостоянияПССрезПоследних.Регистратор.ПричинаПриостановки
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ПричинаПриостановки
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаПолученияОстатков, НомерСчета = &НомерСчета) КАК уп_СостоянияПССрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаПолученияОстатков", лДатаПолученияОстатков);
	Запрос.УстановитьПараметр("НомерСчета", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ЗаписьСостояний = РезультатЗапроса.Выбрать();
	
	Если ЗаписьСостояний.Следующий() Тогда
		ТекСостояние = ЗаписьСостояний.Состояние;
		ТекРасшифровка = ЗаписьСостояний.Расшифровка;
		ТекПричинаПриостановки =?(ЗначениеЗаполнено(ЗаписьСостояний.ПричинаПриостановки), "("+ЗаписьСостояний.ПричинаПриостановки+")","");
		ПенсионныеОснования = ЗаписьСостояний.Основание;
		ДатаОкончанияОснований = ЗаписьСостояний.ДатаОкончанияОснований;  
		
	Иначе
		ТекСостояние = "-";
		ТекРасшифровка = "";
		ПенсионныеОснования = "";
		ДатаОкончанияОснований = "";
		ТекПричинаПриостановки = "";
	КонецЕсли;
	
	СрезСхем = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка));                                                                                               
	ТекСхема = СрезСхем.Схема;
	Если ЗначениеЗаполнено(ТекСхема) Тогда
		Если ТекСхема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.Смешанная Тогда
			ТекТипПенсии = СрезСхем.ТипПенсии;
			Если ЗначениеЗаполнено(ТекТипПенсии) Тогда
				ТипПенсии = "("+ТекТипПенсии+")";
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	Значение = РегистрыСведений.уп_ВидПенсионногоСчета.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)).ВидПенсионногоСчета;                                                                                               
	ТекВид = Значение;
	
	Значение = РегистрыСведений.уп_ПричиныЗакрытияПС.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)).ПричинаЗакрытия;
	ТекПричинаЗакрытия = Значение;
	
	Значение = РегистрыСведений.уп_РазмерПенсии.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)).Размер;                                                                                               
	ТекРазмер = Значение;
	
	Значение = РегистрыСведений.уп_ПериодичностьВыплат.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)).ПериодичностьВыплат;                                                                                               
	ТекПериодичность = Значение;
	
	Значение = РегистрыСведений.уп_КоличествоНазначенныхВыплат.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)); 
	Если Значение<>Неопределено Тогда
		КолНазначено = Значение.КоличествоВыплат;
	Иначе
		КолНазначено = 0;
	КонецЕсли;
	ТекКоличество = КолНазначено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_СчетчикНачисленийОбороты.КоличествоОборот
	|ИЗ
#Вставка
//++https://redmine.npf.com/issues/4274
	|		(ВЫБРАТЬ уп_СчетчикНачислений.НомерСчета, Максимум(естьNUll(хм_СрезСчетчик.Количество,0)) + СУММА(ВЫБОР КОГДА хм_СрезСчетчик.ДатаУстановкиСостояния есть NUll Тогда естьNUll(уп_СчетчикНачислений.Количество,0) ИНАЧЕ 0 КОНЕЦ) КАК КоличествоОборот
	|		ИЗ РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаКон,НомерСчета = &Ссылка) КАК хм_СрезСчетчик
	|		ПО (уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета)
	|			И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния 
	|			И хм_СрезСчетчик.ДатаУстановкиСостояния  >= &ДатаНач	
	|		ГДЕ уп_СчетчикНачислений.Период <= &ДатаКон 
	|			И уп_СчетчикНачислений.Период >= &ДатаНач
	|			И уп_СчетчикНачислений.НомерСчета = &Ссылка
	|		СГРУППИРОВАТЬ ПО уп_СчетчикНачислений.НомерСчета
	|		) КАК уп_СчетчикНачисленийОбороты";
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|	РегистрНакопления.уп_СчетчикНачислений.Обороты(&ДатаНач, &ДатаКон, Период, НомерСчета = &Ссылка) КАК уп_СчетчикНачисленийОбороты";
#КонецУдаления
	
	Запрос.УстановитьПараметр("ДатаНач", '19900101');
	Запрос.УстановитьПараметр("ДатаКон", лДатаПолученияОстатков);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		КолНачислено = Выборка.КоличествоОборот;
	Иначе
		КолНачислено = 0;
	КонецЕсли;
	КоличествоНачислено = КолНачислено;
	КолОсталось = Макс(КолНазначено - КолНачислено,0);
	КоличествоОсталось = КолОсталось;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СрокиПеререгистрацииСрезПоследних.Период КАК ДатаПеререгистрации,
	|	уп_СрокиПеререгистрацииСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_СрокиПеререгистрацииСрезПоследних.ДатаСледующейПеререгистрации КАК ДатаСледующейПеререгистрации
	|ИЗ
	|	РегистрСведений.уп_СрокиПеререгистрации.СрезПоследних(&ДатаПроверкиОстатков, НомерСчета = &НомерСчета) КАК уп_СрокиПеререгистрацииСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаПроверкиОстатков", лДатаПолученияОстатков);
	Запрос.УстановитьПараметр("НомерСчета", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ДатаПеререгистрации = ВыборкаДетальныеЗаписи.ДатаПеререгистрации;
		ДатаСледующейПеререгистрации = ВыборкаДетальныеЗаписи.ДатаСледующейПеререгистрации;
	Иначе
		ДатаПеререгистрации = "";
		ДатаСледующейПеререгистрации = "";
	КонецЕсли;
	
	ПенсПравила = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(лДатаПолученияОстатков,Новый Структура("Организация", Объект.Ссылка.Владелец.Организация));      
	Если ПенсПравила.ВестиУчетПСпоТипамДоговоровЕПС Тогда
		СвойствоТипЕПС = ПланыВидовХарактеристик.уп_ДополнительныеСведенияСРегистратором.ТипДоговораНПО_ЕПС; 
		ТипДоговораДляЕПС = РегистрыСведений.уп_ДопСведенияДляТрансляции.ПолучитьПоследнее(ДатаПолученияОстатков,Новый Структура("Объект, Свойство", Объект.Ссылка, СвойствоТипЕПС)).Значение;
		Элементы.ГруппаТипДоговораЕПС.Видимость = Истина;
	Иначе
		Элементы.ГруппаТипДоговораЕПС.Видимость = Ложь;
	КонецЕсли;	
	
	Значение = РегистрыСведений.уп_ПериодыГарантийногоВосполненияНПО.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка));
	ПериодГарантийногоВосполнения = Значение.ПериодГарантийногоВосполнения; 
	
	Значение = РегистрыСведений.ИФРС17_ГруппыСчетовНПО.ПолучитьПоследнее(лДатаПолученияОстатков, Новый Структура("НомерСчета", Объект.Ссылка)); 
	Если Значение<>Неопределено Тогда
		ифрс17_ГруппаДоговоров = Значение.ГруппаДоговоровНПО; 
		ифрс17_Подгруппа = Значение.Подгруппа;
	Иначе
		ифрс17_ГруппаДоговоров = ""; 
		ифрс17_Подгруппа = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
&После("ОбновитьСсылки")
Процедура ХМОбновитьСсылки_После()
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХМ_ДПОСчетаЮЛ.ПенсионныйДоговор КАК ПенсионныйДоговор
		|ИЗ
		|	РегистрСведений.ХМ_ДПОСчетаЮЛ КАК ХМ_ДПОСчетаЮЛ
		|ГДЕ
		|	ХМ_ДПОСчетаЮЛ.ПенсионныйСчетЮЛ = &ПенсионныйСчетЮЛ";
	
	Запрос.УстановитьПараметр("ПенсионныйСчетЮЛ", ЭтаФорма.Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ТекДоговорДПОсФЛ = ВыборкаДетальныеЗаписи.ПенсионныйДоговор;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|  уп_ПенсионныеСчета.Ссылка КАК НомерСчета,
	|	уп_ПенсионныеСчета.Владелец КАК ПенсионныйДоговор,
	|	уп_ПенсионныеСчета.Участник КАК Участник
	|ПОМЕСТИТЬ втТЧ
	|ИЗ
	|	Справочник.уп_ПенсионныеСчета КАК уп_ПенсионныеСчета
	|ГДЕ
	|	уп_ПенсионныеСчета.Ссылка = &ПенсионныйСчетЮЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ПенсионныйДоговорПаритетный КАК ПенсионныйДоговорПаритетный,
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ПроцентВзноса КАК ПроцентВзноса,
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ВариантРасчета.ТипРасчета КАК ВариантРасчетаТипРасчета
	|ПОМЕСТИТЬ втРазмерПаритета
	|ИЗ
	|	РегистрСведений.ХМ_РазмерПаритетногоВзноса.СрезПоследних(
	|			&ДатаСреза,
	|			ПенсионныйДоговор В
	|				(ВЫБРАТЬ
	|					втТЧ.ПенсионныйДоговор
	|				ИЗ
	|					втТЧ КАК втТЧ)) КАК ХМ_РазмерПаритетногоВзносаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета КАК НомерСчета,
	|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета.Владелец КАК НомерСчетаВладелец
	|ПОМЕСТИТЬ втСчетаСПС
	|ИЗ
	|	РегистрСведений.уп_ВидПенсионногоСчета.СрезПоследних(
	|			&ДатаСреза,
	|			НомерСчета.Владелец В
	|					(ВЫБРАТЬ
	|						втРазмерПаритета.ПенсионныйДоговорПаритетный
	|					ИЗ
	|						втРазмерПаритета КАК втРазмерПаритета)
	|				И ВидПенсионногоСчета = ЗНАЧЕНИЕ(Перечисление.уп_ВидыПенсионныхСчетов.СПС)) КАК уп_ВидПенсионногоСчетаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТЧ.НомерСчета КАК НомерСчета,
	|	втТЧ.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	втТЧ.Участник КАК Участник,
	|	втСчетаСПС.НомерСчета КАК НомерСчетаСПС,
	|	втРазмерПаритета.ПроцентВзноса КАК ПроцентВзноса,
	|	втРазмерПаритета.ВариантРасчетаТипРасчета
	|ПОМЕСТИТЬ втОбъединение
	|ИЗ
	|	втТЧ КАК втТЧ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРазмерПаритета КАК втРазмерПаритета
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаСПС КАК втСчетаСПС
	|			ПО втРазмерПаритета.ПенсионныйДоговорПаритетный = втСчетаСПС.НомерСчетаВладелец
	|		ПО втТЧ.ПенсионныйДоговор = втРазмерПаритета.ПенсионныйДоговор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъединение.НомерСчета КАК НомерСчета,
	|	втОбъединение.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	втОбъединение.Участник КАК Участник,
	|	втОбъединение.НомерСчетаСПС КАК НомерСчетаСПС,
	|	ВЫБОР
	|		КОГДА ХМ_ДПОСчетаЮЛ_Состояние.НомерСчета ЕСТЬ NULL
	|			ТОГДА втОбъединение.НомерСчета
	|		ИНАЧЕ ХМ_ДПОСчетаЮЛ_Состояние.НомерСчета
	|	КОНЕЦ КАК НомерСчетаИПСфл
	|ИЗ
	|	втОбъединение КАК втОбъединение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_ДПОСчетаЮЛ КАК ХМ_ДПОСчетаЮЛ
	|		ПО втОбъединение.ПенсионныйДоговор = ХМ_ДПОСчетаЮЛ.ПенсионныйДоговор
	|			И втОбъединение.НомерСчетаСПС.Владелец = ХМ_ДПОСчетаЮЛ.ПенсионныйСчетЮЛ.Владелец
	|			И втОбъединение.Участник = ХМ_ДПОСчетаЮЛ.ПенсионныйСчетЮЛ.Участник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаСреза, ) КАК ХМ_ДПОСчетаЮЛ_Состояние
	|		ПО (ХМ_ДПОСчетаЮЛ.ПенсионныйСчетЮЛ = ХМ_ДПОСчетаЮЛ_Состояние.НомерСчета)";

	Запрос.УстановитьПараметр("ПенсионныйСчетЮЛ", ЭтаФорма.Объект.Ссылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаПолученияОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ТекНПОсФЛ = ВыборкаДетальныеЗаписи.НомерСчетаИПСфл;
	Иначе
		ТекНПОсФЛ = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПечатьСостоянияСчета")
Процедура ХМПечатьСостоянияСчета(Команда)
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Организация", ОрганизацияДоговора(Объект.Владелец));
	ПараметрыОткрытия.Вставить("НомерСчета", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("ОтборАгент", Ложь);
	ПараметрыОткрытия.Вставить("ОтборМестоРаботы", Ложь);
	ПараметрыОткрытия.Вставить("ОтборРегион", Ложь);
	ПараметрыОткрытия.Вставить("ПоСпискуДоговоров", Ложь);
	ПараметрыОткрытия.Вставить("ПоСпискуСчетов", Ложь);
	ПараметрыОткрытия.Вставить("ФормироватьФайлыДляПечати", Ложь);  
	#Удаление 
	ОткрытьФорму("Отчет.уп_СостояниеСчета.Форма.ФормаОтчета",ПараметрыОткрытия);
	#КонецУдаления  
	#Вставка 
	ОткрытьФорму("Отчет.хм_уп_СостояниеСчета.Форма.ФормаОтчета",ПараметрыОткрытия);
	#КонецВставки
КонецПроцедуры
