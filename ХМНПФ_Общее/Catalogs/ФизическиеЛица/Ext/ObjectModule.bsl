
&После("ПриЗаписи")
Процедура ХМПриЗаписи(Отказ)
	ХМ_ЗаписатьРегионФизЛицаПриЗаписи(ЭтотОбъект, Отказ);
КонецПроцедуры

// ХМНПФ+ 04.08.2021 МилевскийАЮ // подписка на события ХМ_ЗаписатьРегионФизЛица // №3288
Процедура ХМ_ЗаписатьРегионФизЛицаПриЗаписи(Источник, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХМ_РегионыФизическихЛицСрезПоследних.Регион КАК Регион
		|ИЗ
		|	РегистрСведений.ХМ_РегионыФизическихЛиц.СрезПоследних(, ФизическоеЛицо = &ФизическоеЛицо) КАК ХМ_РегионыФизическихЛицСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка,
		|	ФизическиеЛицаКонтактнаяИнформация.Вид КАК Вид,
		|	ФизическиеЛицаКонтактнаяИнформация.Регион КАК Регион
		|ПОМЕСТИТЬ ВсеРегионы
		|ИЗ
		|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
		|ГДЕ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка = &ФизическоеЛицо
		|	И ФизическиеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
		|	И ФизическиеЛицаКонтактнаяИнформация.Вид В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФизЛица.ФизЛицо КАК ФизЛицо,
		|	АдресПоПрописке.Регион КАК РегионАдресПоПрописке,
		|	АдресМестаПроживания.Регион КАК РегионАдресМестаПроживания
		|ПОМЕСТИТЬ ФизЛицоСРегионами
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВсеРегионы.Ссылка КАК ФизЛицо
		|	ИЗ
		|		ВсеРегионы КАК ВсеРегионы
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВсеРегионы.Ссылка) КАК ФизЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеРегионы КАК АдресПоПрописке
		|		ПО (ВЫБОР
		|				КОГДА АдресПоПрописке.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица)
		|						И АдресПоПрописке.Регион <> """"
		|					ТОГДА ФизЛица.ФизЛицо = АдресПоПрописке.Ссылка
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеРегионы КАК АдресМестаПроживания
		|		ПО (ВЫБОР
		|				КОГДА АдресМестаПроживания.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица)
		|						И АдресМестаПроживания.Регион <> """"
		|					ТОГДА ФизЛица.ФизЛицо = АдресМестаПроживания.Ссылка
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФизЛицоСРегионами.ФизЛицо КАК ФизЛицо,
		|	ЕСТЬNULL(ФизЛицоСРегионами.РегионАдресПоПрописке, ФизЛицоСРегионами.РегионАдресМестаПроживания) КАК Регион
		|ИЗ
		|	ФизЛицоСРегионами КАК ФизЛицоСРегионами
		|ГДЕ
		|	НЕ(ФизЛицоСРегионами.РегионАдресПоПрописке ЕСТЬ NULL
		|				И ФизЛицоСРегионами.РегионАдресМестаПроживания ЕСТЬ NULL)";
	
	
	Запрос.УстановитьПараметр("ФизическоеЛицо", Источник.Ссылка);
	
	МассивПакетов = Запрос.ВыполнитьПакет();
	
	//Получаем текущее значение региона из контактной информации физ лица
	РезультатЗапросаРегионИзСправочника = МассивПакетов[МассивПакетов.Количество()-1];
	Если РезультатЗапросаРегионИзСправочника.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ВыборкаИзСправочника = РезультатЗапросаРегионИзСправочника.Выбрать();
	ВыборкаИзСправочника.Следующий();
	//Проверяем наличие записей в регистре сведений ХМ_РегионыФизическихЛиц
	РезультатЗапросаРегионИзРегистра = МассивПакетов[0];	
	Если РезультатЗапросаРегионИзРегистра.Пустой() Тогда
		ЗаписатьРегионФизЛицаВРегистр(Источник.Ссылка, ВыборкаИзСправочника.Регион);
	Иначе
		ВыборкаИзРегистра = РезультатЗапросаРегионИзРегистра.Выбрать();
		ВыборкаИзРегистра.Следующий();
		Если ВыборкаИзРегистра.Регион <> ВыборкаИзСправочника.Регион Тогда 
			ЗаписатьРегионФизЛицаВРегистр(Источник.Ссылка, ВыборкаИзСправочника.Регион);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаписатьРегионФизЛицаВРегистр(ФизическоеЛицо, Регион)
	МенеджерЗаписи = РегистрыСведений.ХМ_РегионыФизическихЛиц.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;
	МенеджерЗаписи.Регион = Регион;
	МенеджерЗаписи.Записать();
КонецПроцедуры
