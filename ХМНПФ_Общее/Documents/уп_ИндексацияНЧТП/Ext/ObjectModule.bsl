
Процедура ХМ_НеТиповыеИзменения()
	//ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
	//ЛыткинАМ //2016-07-28 //№204 //ОбработкаПроведения
	//ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	// ХМНПФ 08.05.2020 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2807
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьТЧДоговорыОПС")
Процедура РасшОбщ_ЗаполнитьТЧДоговорыОПС(ВариантРасчета) Экспорт
	//ищем текущие состояния НЧТП
	//дополняем состояниями доп части (НЧТП)
	//смортим остатки по осн части счета
	//смотрим остатки по доп. части счета
	//смотрим дату назначения НЧТП по справочнику договоры ОПС
	//если есть остатки, считаем период выплат и дельту

	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ ДоговораОПС
		|ИЗ
		|	РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
		|	ВЫРАЗИТЬ(РАЗНОСТЬДАТ(уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения, &ДатаРасчета, ДЕНЬ) / 365.25 - 0.5 КАК ЧИСЛО(10, 0)) КАК Возраст,
		|	ВЫРАЗИТЬ(РАЗНОСТЬДАТ(уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС.ДатаНазначенияНЧТП, &ДатаРасчета, ДЕНЬ) / 365.25 - 0.5 КАК ЧИСЛО(10, 0)) КАК ПолныхЛетСДатыНазначения,
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС.СрокНЧТППриНазначении КАК СрокНЧТППриНазначении
		|ПОМЕСТИТЬ СписокЗЛ
		|ИЗ
		|	РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаСумм, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК ЕстьДопСчета,
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ ДоговорыВДопСчетами
		|ИЗ
		|	РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаСумм, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		#Удаление
		|ВЫБРАТЬ
		|	уп_СчетчикНачисленийОПСОбороты.ДоговорОПС КАК ДоговорОПС,
		|	уп_СчетчикНачисленийОПСОбороты.КоличествоОборот КАК КоличествоСделанныхВыплат
		|ПОМЕСТИТЬ КолНачислений
		|ИЗ
		|	РегистрНакопления.уп_СчетчикНачисленийОПС.Обороты(
		|			&ДатаНачалаВремен,
		|			&ДатаРасчета,
		|			Период,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокЗЛ.ДоговорОПС
		|					ИЗ
		|						СписокЗЛ)
		|				И ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Пожизненная)) КАК уп_СчетчикНачисленийОПСОбороты
		#КонецУдаления
		#Вставка
		// +ХМНПФ 08.05.2020 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2807
		|ВЫБРАТЬ
		|	НачисленияПенсийОПС.ДоговорОПС КАК ДоговорОПС,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НачисленияПенсийОПС.НачПериода) КАК КоличествоСделанныхВыплат
		|ПОМЕСТИТЬ КолНачислений
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсийОПС.Обороты(
		|			&ДатаНачалаВремен,
		|			&ДатаРасчета,
		|			Регистратор,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокЗЛ.ДоговорОПС
		|					ИЗ
		|						СписокЗЛ)
		|				И ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Пожизненная)) КАК НачисленияПенсийОПС
		|ГДЕ 
		|	НачисленияПенсийОПС.Регистратор ССЫЛКА Документ.уп_НачислениеПенсийОПС
		|СГРУППИРОВАТЬ ПО
		|	НачисленияПенсийОПС.ДоговорОПС
		// -ХМНПФ 08.05.2020 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2807
		#КонецВставки
    	|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДоговорыВДопСчетами.ЕстьДопСчета, ЛОЖЬ) КАК ЕстьДопСчет,
		|	ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф * ЕСТЬNULL(ДоговорыВДопСчетами.ЕстьДопСчета, 0) КАК СуммаДопЧастиСчета,
		|	ЕСТЬNULL(уп_РезервыОПСОстаткиОснЧасть.СуммаОстаток, 0) * &Коэф КАК СуммаОснЧастиСчета,
		|	уп_РазмерПенсииОПССрезПоследних.Размер КАК ТекРазмер,
		|	ВЫБОР
		|		КОГДА уп_РазмерПенсииОПССрезПервых.Регистратор.Дата <= &КонецПрошлогоГода
		|			ТОГДА уп_РазмерПенсииОПССрезПоследних.Размер * &КоэфИндексации
		|		ИНАЧЕ уп_РазмерПенсииОПССрезПоследних.Размер
		|	КОНЕЦ КАК ИндексацияПроцент,
		|	ВЫБОР
		|		КОГДА СписокЗЛ.СрокНЧТППриНазначении - СписокЗЛ.ПолныхЛетСДатыНазначения * 12 > &МинСрокМесяц
		|			ТОГДА СписокЗЛ.СрокНЧТППриНазначении - СписокЗЛ.ПолныхЛетСДатыНазначения * 12
		|		ИНАЧЕ &МинСрокМесяц
		|	КОНЕЦ КАК ПериодВыплатДляРасчета,
		|	(ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф * ЕСТЬNULL(ДоговорыВДопСчетами.ЕстьДопСчета, 0) + ЕСТЬNULL(уп_РезервыОПСОстаткиОснЧасть.СуммаОстаток, 0) * &Коэф) / ВЫБОР
		|		КОГДА СписокЗЛ.СрокНЧТППриНазначении - СписокЗЛ.ПолныхЛетСДатыНазначения * 12 > &МинСрокМесяц
		|			ТОГДА СписокЗЛ.СрокНЧТППриНазначении - СписокЗЛ.ПолныхЛетСДатыНазначения * 12
		|		ИНАЧЕ &МинСрокМесяц
		|	КОНЕЦ КАК ИндексацияДельта,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СписокЗЛ.Возраст >= уп_ПенсионныйВозрастСрезПоследних.Возраст
		|					ТОГДА ВЫБОР
		|							КОГДА СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст < &РазницаГод
		|								ТОГДА &НомСрокМесяц - (СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст) * 12
		|							ИНАЧЕ &МинСрокМесяц
		|						КОНЕЦ
		|				ИНАЧЕ &НомСрокМесяц + 12
		|			КОНЕЦ - СписокЗЛ.ПолныхЛетСДатыНазначения * 12 < &МинСрокМесяц
		|			ТОГДА &МинСрокМесяц
		|		ИНАЧЕ ВЫБОР
		|				КОГДА СписокЗЛ.Возраст >= уп_ПенсионныйВозрастСрезПоследних.Возраст
		|					ТОГДА ВЫБОР
		|							КОГДА СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст < &РазницаГод
		|								ТОГДА &НомСрокМесяц - (СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст) * 12
		|							ИНАЧЕ &МинСрокМесяц
		|						КОНЕЦ
		|				ИНАЧЕ &НомСрокМесяц + 12
		|			КОНЕЦ - СписокЗЛ.ПолныхЛетСДатыНазначения * 12
		|	КОНЕЦ КАК ПериодВыплатДляРасчетаСт,
		|	(ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф * ЕСТЬNULL(ДоговорыВДопСчетами.ЕстьДопСчета, 0) + ЕСТЬNULL(уп_РезервыОПСОстаткиОснЧасть.СуммаОстаток, 0) * &Коэф) / ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СписокЗЛ.Возраст >= уп_ПенсионныйВозрастСрезПоследних.Возраст
		|					ТОГДА ВЫБОР
		|							КОГДА СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст < &РазницаГод
		|								ТОГДА &НомСрокМесяц - (СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст) * 12
		|							ИНАЧЕ &МинСрокМесяц
		|						КОНЕЦ
		|				ИНАЧЕ &НомСрокМесяц + 12
		|			КОНЕЦ - СписокЗЛ.ПолныхЛетСДатыНазначения * 12 < &МинСрокМесяц
		|			ТОГДА &МинСрокМесяц
		|		ИНАЧЕ ВЫБОР
		|				КОГДА СписокЗЛ.Возраст >= уп_ПенсионныйВозрастСрезПоследних.Возраст
		|					ТОГДА ВЫБОР
		|							КОГДА СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст < &РазницаГод
		|								ТОГДА &НомСрокМесяц - (СписокЗЛ.Возраст - уп_ПенсионныйВозрастСрезПоследних.Возраст) * 12
		|							ИНАЧЕ &МинСрокМесяц
		|						КОНЕЦ
		|				ИНАЧЕ &НомСрокМесяц + 12
		|			КОНЕЦ - СписокЗЛ.ПолныхЛетСДатыНазначения * 12
		|	КОНЕЦ КАК ИндексацияДельтаСт,
		|	СписокЗЛ.ДоговорОПС,
		|	СписокЗЛ.ДоговорОПС.Участник КАК ЗастрахованноеЛицо,
		|	СписокЗЛ.Возраст,
		|	уп_РазмерПенсииОПССрезПервых.Регистратор.Дата КАК ДатаРешения,
		|	СписокЗЛ.ПолныхЛетСДатыНазначения,
		|	СписокЗЛ.СрокНЧТППриНазначении,
		|	КолНачислений.КоличествоСделанныхВыплат
		|ИЗ
		|	СписокЗЛ КАК СписокЗЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыВДопСчетами КАК ДоговорыВДопСчетами
		|		ПО СписокЗЛ.ДоговорОПС = ДоговорыВДопСчетами.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_РезервыОПС.Остатки(
		|				&ДатаСумм,
		|				Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговораОПС.ДоговорОПС
		|						ИЗ
		|							ДоговораОПС КАК ДоговораОПС)
		|					И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений)
		|					И ТипСуммы В (ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз), ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))) КАК уп_РезервыОПСОстаткиОснЧасть
		|		ПО СписокЗЛ.ДоговорОПС = уп_РезервыОПСОстаткиОснЧасть.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_РезервыОПС.Остатки(
		|				&ДатаСумм,
		|				Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговораОПС.ДоговорОПС
		|						ИЗ
		|							ДоговораОПС КАК ДоговораОПС)
		|					И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений)
		|					И НЕ ТипСуммы В (ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз), ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))) КАК уп_РезервыОПСОстаткиДопЧасть
		|		ПО СписокЗЛ.ДоговорОПС = уп_РезервыОПСОстаткиДопЧасть.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПоследних(
		|				&ДатаРазмера,
		|				ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Пожизненная)
		|					И ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговораОПС.ДоговорОПС
		|						ИЗ
		|							ДоговораОПС КАК ДоговораОПС)) КАК уп_РазмерПенсииОПССрезПоследних
		|		ПО СписокЗЛ.ДоговорОПС = уп_РазмерПенсииОПССрезПоследних.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПенсионныйВозраст.СрезПоследних(&ДатаРасчета, ) КАК уп_ПенсионныйВозрастСрезПоследних
		|		ПО СписокЗЛ.ДоговорОПС.Участник.уп_ФизЛицо.Пол = уп_ПенсионныйВозрастСрезПоследних.Пол
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПервых(
		|				&ДатаНачалаВремен,
		|				ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Пожизненная)
		|					И ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							ДоговораОПС.ДоговорОПС
		|						ИЗ
		|							ДоговораОПС КАК ДоговораОПС)) КАК уп_РазмерПенсииОПССрезПервых
		|		ПО СписокЗЛ.ДоговорОПС = уп_РазмерПенсииОПССрезПервых.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ КолНачислений КАК КолНачислений
		|		ПО СписокЗЛ.ДоговорОПС = КолНачислений.ДоговорОПС
		|ГДЕ
		|	уп_РазмерПенсииОПССрезПервых.Регистратор.Дата < &ДатаСумм
		|
		|УПОРЯДОЧИТЬ ПО
		|	СписокЗЛ.ДоговорОПС.Участник.Наименование";		

	Запрос.УстановитьПараметр("ДатаРасчета", НачалоДня(Дата)-1);
	Запрос.УстановитьПараметр("ДатаРазмера", КонецДня(Дата));
	Если КорректироватьНаКоэффициент Тогда
		Запрос.УстановитьПараметр("КоэфИндексации", КорректировочныйКоэффициент);
	Иначе
		Запрос.УстановитьПараметр("КоэфИндексации", 1);
	КонецЕсли;
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("НЧТП", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Пожизненная", Перечисления.уп_ТипыПенсийОПС.Пожизненная);
	Запрос.УстановитьПараметр("ДатаСумм", НачалоДня(ДатаСумм));
	Запрос.УстановитьПараметр("ДатаНачалаВремен", '20050101');
	Запрос.УстановитьПараметр("КонецПрошлогоГода", КонецДня(НачалоГода(Дата)-1));
	Если КорректироватьПоОстаткам Тогда
		Запрос.УстановитьПараметр("Коэф", 1);
	Иначе
		Запрос.УстановитьПараметр("Коэф", 0);
	КонецЕсли;
	
	СписокОснЧасть = Новый СписокЗначений;
	СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыФиз);
	СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ИДФиз);
	Запрос.УстановитьПараметр("СписокОснЧасть", СписокОснЧасть);
	
	МинПериодДожития = РегистрыСведений.уп_ПериодыДожития.ПолучитьПоследнее(Дата, Новый Структура("ТипПериодаДожития", Перечисления.уп_ТипыПериодовДожития.Минимальный)).ЗначениеПериодаДожития;
	//МинСрок = ?(МинПериодДожития = 0, 14*12, МинПериодДожития*12);
	МинСрок = ?(МинПериодДожития = 0, 14*12, МинПериодДожития);
	Запрос.УстановитьПараметр("МинСрокМесяц", МинСрок);
	
	НомПериодДожития = РегистрыСведений.уп_ПериодыДожития.ПолучитьПоследнее(Дата, Новый Структура("ТипПериодаДожития", Перечисления.уп_ТипыПериодовДожития.Номинальный)).ЗначениеПериодаДожития; 
	//Запрос.УстановитьПараметр("НомСрокГод", НомПериодДожития);
	Запрос.УстановитьПараметр("НомСрокГод", НомПериодДожития/12);
	Запрос.УстановитьПараметр("НомСрокМесяц", НомПериодДожития);
	
	РазницаГод =  НомПериодДожития-МинПериодДожития;
	Запрос.УстановитьПараметр("РазницаГод", РазницаГод);
	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	ТекущийГод = СтрЗаменить(Строка(Год(Дата)),Символы.НПП, "");
	ДатаРасчета = Дата(ТекущийГод + "0801");
	//МинПериодДожития = МинПериодДожития*12;
	#Вставка
	//+ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	ХМ_ДатаДок = НачалоДня(?(ЗначениеЗаполнено(ДатаНазначения),ДатаНазначения,ДатаСумм));
	МинСрок = ?(МинПериодДожития = 0, 14*12, МинПериодДожития);
	//-ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	#КонецВставки
	
	ТабДоговоровДляРасшифровки = Новый ТаблицаЗначений;
	ТабДоговоровДляРасшифровки.Колонки.Добавить("ДоговорОПС", Новый ОписаниеТипов("СправочникСсылка.уп_ДоговорыОПС"));
	ТабДоговоровДляРасшифровки.Колонки.Добавить("ЕстьДопСчет", Новый ОписаниеТипов("Число"));

	ВариантРасчетаПериодаТ = 0;
	Если ВариантРасчета = 2 Тогда
		ВариантРасчетаПериодаТ = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация)).ВариантРасчетаЛетДосрочности;	
	КонецЕсли;	
		
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если (ВыборкаДетальныеЗаписи.СуммаОснЧастиСчета+ВыборкаДетальныеЗаписи.СуммаДопЧастиСчета)<>0 или ВыборкаДетальныеЗаписи.ДатаРешения<=КонецДня(НачалоГода(Дата)-1) Тогда
			НовСтр = ДоговорыОПС.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
			
			Если ВыборкаДетальныеЗаписи.СуммаОснЧастиСчета<>0 ИЛИ ВыборкаДетальныеЗаписи.СуммаДопЧастиСчета<>0 Тогда
				НовСтрТабРасшифровки = ТабДоговоровДляРасшифровки.Добавить();
				НовСтрТабРасшифровки.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				НовСтрТабРасшифровки.ЕстьДопСчет = ?(ВыборкаДетальныеЗаписи.ЕстьДопСчет,1,0);
			КонецЕсли;
			#Удаление
			Если ВариантРасчета=2 Тогда
				ПериодВыплатДляРасчета = уп_ОПС.СрокПожизненнойПенсии(НовСтр.ЗастрахованноеЛицо, ДатаРасчета,,ВариантРасчетаПериодаТ);
				КоличествоПолныхЛетСДатыНазначения = уп_ОПС.РазобратьРазностьДат(ДатаРасчета,НовСтр.ДоговорОПС.ДатаНазначенияНЧТП);
				ПериодВыплатДляРасчета=ПериодВыплатДляРасчета-КоличествоПолныхЛетСДатыНазначения*12;
				ПериодВыплатДляРасчета = ?(ПериодВыплатДляРасчета<МинПериодДожития,МинПериодДожития, ПериодВыплатДляРасчета);				
				НовСтр.ПериодВыплатДляРасчета=ПериодВыплатДляРасчета;
				НовСтр.ИндексацияДельта = (НовСтр.СуммаОснЧастиСчета+НовСтр.СуммаДопЧастиСчета)/НовСтр.ПериодВыплатДляРасчета;
			КонецЕсли;			
			#КонецУдаления
			#Вставка
			РазницаВМесяцах = ХМ_ПолучитьРазницуВМесяцах(НовСтр.ДоговорОПС.ДатаНазначенияНЧТП,ХМ_ДатаДок);
			НовСтр.ПериодВыплатДляРасчета = ?(НовСтр.ДоговорОПС.СрокНЧТППриНазначении - РазницаВМесяцах > МинСрок, НовСтр.ДоговорОПС.СрокНЧТППриНазначении - РазницаВМесяцах, МинСрок);
			НовСтр.ИндексацияДельта = (НовСтр.СуммаОснЧастиСчета+НовСтр.СуммаДопЧастиСчета)/НовСтр.ПериодВыплатДляРасчета;
			//-ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
			#КонецВставки
			Если НеУменьшатьПенсиюПриКорректировке И НовСтр.ИндексацияДельта<0 Тогда
				НовСтр.ИндексацияДельта = 0;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	//заполнить таблицу расшифровки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабДоговоров.ДоговорОПС КАК ДоговорОПС,
		|	ТабДоговоров.ЕстьДопСчет
		|ПОМЕСТИТЬ ТабИсходная
		|ИЗ
		|	&ТабДоговоров КАК ТабДоговоров
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОПСОстатки.ДоговорОПС КАК ДоговорОПС,
		|	уп_РезервыОПСОстатки.ТипСуммы,
		|	уп_РезервыОПСОстатки.СуммаОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.уп_РезервыОПС.Остатки(
		|			&ДатаСумм,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						ТабИсходная.ДоговорОПС
		|					ИЗ
		|						ТабИсходная)
		|				И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений)) КАК уп_РезервыОПСОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.ДоговорОПС,
		|	Остатки.ТипСуммы,
		|	ВЫБОР
		|		КОГДА Остатки.ТипСуммы В (ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз), ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))
		|			ТОГДА Остатки.СуммаОстаток
		|		ИНАЧЕ ТабИсходная.ЕстьДопСчет * Остатки.СуммаОстаток
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	Остатки КАК Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТабИсходная КАК ТабИсходная
		|		ПО Остатки.ДоговорОПС = ТабИсходная.ДоговорОПС
		|ГДЕ
		|	ВЫБОР
		|			КОГДА Остатки.ТипСуммы В (ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ВзносыФиз), ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДФиз))
		|				ТОГДА Остатки.СуммаОстаток
		|			ИНАЧЕ ТабИсходная.ЕстьДопСчет * Остатки.СуммаОстаток
		|		КОНЕЦ <> 0";
	
	Запрос.УстановитьПараметр("ДатаСумм", НачалоДня(ДатаСумм));
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("СписокОснЧасть", СписокОснЧасть);
	Запрос.УстановитьПараметр("ТабДоговоров", ТабДоговоровДляРасшифровки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		//Если ВыборкаДетальныеЗаписи.Сумма>0 Тогда
			НовСтр = РасшифровкаСуммы.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
		//ИначеЕсли ВыборкаДетальныеЗаписи.Сумма<0 Тогда
		//	Если НеУменьшатьПенсиюПриКорректировке Тогда
		//		НовСтр = РасшифровкаСуммы.Добавить();
		//		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
		//	КонецЕсли;
		//КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

&ИзменениеИКонтроль("ОбработкаПроведения")	
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	
	СтарыйАлгоритм = ?(РасшифровкаСуммы.Количество()=0 , ИСТИНА, ЛОЖЬ);
    //проверка остатков по счетам
	ПроверкаОстатков(Отказ, СтарыйАлгоритм);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	//движения по регистру Резервы ОПС
	//движения по регистру Хозрасчетный
	Движения.уп_РезервыОПС.Записывать = Истина;
	Движения.уп_РезервыОПС.Очистить();
	
	Движения.уп_ТекущаяСуммаГарантирования.Записывать = Истина;
	Движения.уп_ТекущаяСуммаГарантирования.Очистить();
	
	//параметры зачисления
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоДоговорам,
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоТипамСумм
	|ИЗ
	|	РегистрСведений.уп_ПенсионныеПравила.СрезПоследних(&Дата, Организация = &Организация) КАК уп_ПенсионныеПравилаСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	    ПоДоговорам = Выборка.ВестиРезервВыплатОПСПоДоговорам;
		ПоТипамСумм = Выборка.ВестиРезервВыплатОПСПоТипамСумм;
	Иначе
		ПоДоговорам = Ложь;
		ПоТипамСумм = Ложь;
	КонецЕсли;
	
	Если СтарыйАлгоритм Тогда
		Если КорректироватьПоОстаткам Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_ИндексацияНЧТПДоговорыОПС.ДоговорОПС,
			|	уп_ИндексацияНЧТПДоговорыОПС.СуммаОснЧастиСчета,
			|	уп_ИндексацияНЧТПДоговорыОПС.СуммаДопЧастиСчета,
			|	уп_ИндексацияНЧТПДоговорыОПС.НомерСтроки
			|ПОМЕСТИТЬ ОснТаблица
			|ИЗ
			|	Документ.уп_ИндексацияНЧТП.ДоговорыОПС КАК уп_ИндексацияНЧТПДоговорыОПС
			|ГДЕ
			|	уп_ИндексацияНЧТПДоговорыОПС.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_РезервыОПСОстатки.ТипСуммы,
			|	уп_РезервыОПСОстатки.СуммаОстаток КАК СуммаОстаток,
			|	уп_РезервыОПСОстатки.ТипСуммы.Родитель КАК ТипСуммыРодитель,
			|	уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток КАК СуммаГарантирования,
			|	уп_РезервыОПСОстатки.ДоговорОПС
			|ПОМЕСТИТЬ Остатки
			|ИЗ
			|	РегистрНакопления.уп_РезервыОПС.Остатки(
			|			&ДатаРасчета,
			|			Организация = &Организация
			|				И ДоговорОПС В
			|					(ВЫБРАТЬ
			|						уп_ИндексацияНЧТПДоговорыОПС.ДоговорОПС
			|					ИЗ
			|						Документ.уп_ИндексацияНЧТП.ДоговорыОПС КАК уп_ИндексацияНЧТПДоговорыОПС
			|					ГДЕ
			|						уп_ИндексацияНЧТПДоговорыОПС.Ссылка = &Ссылка)
			|				И ТипРезерва = &Накопительный) КАК уп_РезервыОПСОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(
			|				&ДатаРасчета,
			|				Организация = &Организация
			|					И ДоговорОПС В
			|						(ВЫБРАТЬ
			|							уп_ИндексацияНЧТПДоговорыОПС.ДоговорОПС
			|						ИЗ
			|							Документ.уп_ИндексацияНЧТП.ДоговорыОПС КАК уп_ИндексацияНЧТПДоговорыОПС
			|						ГДЕ
			|							уп_ИндексацияНЧТПДоговорыОПС.Ссылка = &Ссылка)) КАК уп_ТекущаяСуммаГарантированияОстатки
			|		ПО уп_РезервыОПСОстатки.ДоговорОПС = уп_ТекущаяСуммаГарантированияОстатки.ДоговорОПС
			|			И уп_РезервыОПСОстатки.ТипСуммы = уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОснТаблица.ДоговорОПС КАК ДоговорОПС,
			|	ОснТаблица.СуммаОснЧастиСчета КАК СуммаОснЧастиСчета,
			|	ОснТаблица.СуммаДопЧастиСчета КАК СуммаДопЧастиСчета,
			|	ОснТаблица.НомерСтроки КАК НомерСтроки,
			|	Остатки.ТипСуммы,
			|	ОснТаблица.ДоговорОПС.Участник КАК ЗастрахованноеЛицо,
			|	Остатки.ТипСуммы.Родитель КАК ТипСуммыРодитель,
			|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
			|	ЕСТЬNULL(Остатки.СуммаГарантирования, 0) КАК СуммаГарантирования
			|ИЗ
			|	ОснТаблица КАК ОснТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
			|		ПО ОснТаблица.ДоговорОПС = Остатки.ДоговорОПС
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки,
			|	ТипСуммыРодитель
			|ИТОГИ
			|	МАКСИМУМ(СуммаОснЧастиСчета),
			|	МАКСИМУМ(СуммаДопЧастиСчета),
			|	СУММА(СуммаОстаток),
			|	СУММА(СуммаГарантирования)
			|ПО
			|	ДоговорОПС";
			
			Запрос.УстановитьПараметр("ДатаРасчета", ЭтотОбъект.МоментВремени());
			Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Результат = Запрос.Выполнить();
			
			ВыборкаИтогов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаИтогов.Следующий() Цикл
				Если ВыборкаИтогов.СуммаОснЧастиСчета<>0 или ВыборкаИтогов.СуммаДопЧастиСчета<>0 Тогда
					Если НеУменьшатьПенсиюПриКорректировке и (ВыборкаИтогов.СуммаОснЧастиСчета+ВыборкаИтогов.СуммаДопЧастиСчета)<0 Тогда
						//ничего не делаем
					Иначе
						ВыборкаДетальныеЗаписи = ВыборкаИтогов.Выбрать();
						//фиксируем
						ОсталосьОснЧастиСчета = ВыборкаИтогов.СуммаОснЧастиСчета;
						ОсталосьДопЧастиСчета = ВыборкаИтогов.СуммаДопЧастиСчета;
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
							Если  ВыборкаДетальныеЗаписи.ТипСуммы = Справочники.уп_ТипыСуммОПС.ВзносыФиз или 
								ВыборкаДетальныеЗаписи.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДФиз Тогда
								//списываем осн часть
								СуммаКСписанию = Мин(ВыборкаДетальныеЗаписи.СуммаОстаток, ОсталосьОснЧастиСчета);
								ОсталосьОснЧастиСчета = ОсталосьОснЧастиСчета-СуммаКСписанию;
							Иначе
								//списываем доп часть
								СуммаКСписанию = Мин(ВыборкаДетальныеЗаписи.СуммаОстаток, ОсталосьДопЧастиСчета);
								ОсталосьДопЧастиСчета = ОсталосьДопЧастиСчета-СуммаКСписанию;
							КонецЕсли; 							
							#Удаление
							Если СуммаКСписанию>0 Тогда
							#КонецУдаления
							#Вставка
							// +ХМНПФ 10.02.2016 ЛыткинАМ //ОбработкаПроведения //№204
							Если СуммаКСписанию<>0 Тогда
							// -ХМНПФ 10.02.2016 ЛыткинАМ //ОбработкаПроведения //№204
							#КонецВставки
								//списываем
								Движение = Движения.уп_РезервыОПС.Добавить();
								Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
								Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
								Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
								Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
								Движение.Сумма = СуммаКСписанию;
								
								Движение = Движения.уп_РезервыОПС.Добавить();
								Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
								Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
								Если ПоДоговорам Тогда
									Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
								КонецЕсли;
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
								Если ПоТипамСумм Тогда
									//Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
									Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
								КонецЕсли;
								Движение.Сумма = СуммаКСписанию;
								
								Если ВыборкаДетальныеЗаписи.СуммаГарантирования<>0 Тогда
									Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
									Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
									Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
									Движение.Организация = Организация;
									Движение.Период = Дата;
									Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
									Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаГарантирования;
								КонецЕсли;	
								
								Если ОтражатьВБухгалтерскомУчете Тогда
									ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
									уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ВыборкаДетальныеЗаписи.ЗастрахованноеЛицо,ВыборкаИтогов.ДоговорОПС.ДоговорКонтрагента,
									Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.ЗастрахованноеЛицо, ВыборкаИтогов.ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаКСписанию);
								КонецЕсли;
							КонецЕсли;
							Если ОсталосьОснЧастиСчета = 0 И ОсталосьДопЧастиСчета = 0 Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	Иначе
		Если КорректироватьПоОстаткам Тогда

			//текущая сумма гарантирования
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.ДоговорОПС,
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.ТипСуммы,
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.Сумма
			|ПОМЕСТИТЬ ТабДокумента
			|ИЗ
			|	Документ.уп_ИндексацияНЧТП.РасшифровкаСуммы КАК уп_ИндексацияНЧТПРасшифровкаСуммы
			|ГДЕ
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ТекущаяСуммаГарантированияОстатки.ДоговорОПС,
			|	уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы,
			|	уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток
			|ПОМЕСТИТЬ Гарантирование
			|ИЗ
			|	РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(
			|			&ДатаДок,
			|			ДоговорОПС В
			|				(ВЫБРАТЬ
			|					ТабДокумента.ДоговорОПС
			|				ИЗ
			|					ТабДокумента)) КАК уп_ТекущаяСуммаГарантированияОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_РезервыОПСОстатки.ДоговорОПС,
			|	уп_РезервыОПСОстатки.ТипСуммы,
			|	уп_РезервыОПСОстатки.СуммаОстаток
			|ПОМЕСТИТЬ Выплатной
			|ИЗ
			|	РегистрНакопления.уп_РезервыОПС.Остатки(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ТабДокумента.ДоговорОПС
			|					ИЗ
			|						ТабДокумента)
			|				И ТипРезерва = &Выплатной) КАК уп_РезервыОПСОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТабДокумента.ДоговорОПС КАК ДоговорОПС,
			|	ТабДокумента.ТипСуммы,
			|	ТабДокумента.Сумма КАК Сумма,
			|	ЕСТЬNULL(Гарантирование.СуммаОстаток, 0) КАК СуммаОстаток,
			|	ЕСТЬNULL(Выплатной.СуммаОстаток, 0) КАК СуммаВыплатной
			|ИЗ
			|	ТабДокумента КАК ТабДокумента
			|		ЛЕВОЕ СОЕДИНЕНИЕ Гарантирование КАК Гарантирование
			|		ПО ТабДокумента.ДоговорОПС = Гарантирование.ДоговорОПС
			|			И ТабДокумента.ТипСуммы = Гарантирование.ТипСуммы
			|		ЛЕВОЕ СОЕДИНЕНИЕ Выплатной КАК Выплатной
			|		ПО ТабДокумента.ТипСуммы = Выплатной.ТипСуммы
			|			И ТабДокумента.ДоговорОПС = Выплатной.ДоговорОПС
			|ИТОГИ
			|	СУММА(Сумма)
			|ПО
			|	ДоговорОПС";
			
			Запрос.УстановитьПараметр("ДатаДок", ЭтотОбъект.МоментВремени());
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("Выплатной", Перечисления.уп_ТипыРезервовОПС.Выплатной);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока Выборка.Следующий() Цикл
				Если НеУменьшатьПенсиюПриКорректировке И Выборка.Сумма<0 Тогда
					//ничего не делаем
				Иначе
					ВыборкаДетальныеЗаписи = Выборка.Выбрать();
					
					Движения.уп_ТекущаяСуммаГарантирования.Записывать = Истина;
					Движения.уп_ТекущаяСуммаГарантирования.Очистить();
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						Если ВыборкаДетальныеЗаписи.СуммаОстаток<>0 Тогда
							Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
							Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
							Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
							Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток;
						КонецЕсли;
						
						Если ВыборкаДетальныеЗаписи.Сумма<0 Тогда
							Если  ВыборкаДетальныеЗаписи.СуммаВыплатной<ВыборкаДетальныеЗаписи.Сумма*(-1) Тогда
								Отказ = Истина;
								Сообщить("По договору ОПС "+ВыборкаДетальныеЗаписи.ДоговорОПС+" по типу суммы "+ВыборкаДетальныеЗаписи.ТипСуммы+" недостаточно средств в выплатном резерве для покрытия отрицательного остатка РПН. Наличие отрицательного остатка по типам сумм в выплатном резерве недопустимо.");
							КонецЕсли;	
						КонецЕсли;	
						//еще по резервам
						Движение = Движения.уп_РезервыОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
						Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
						Движение.Период = Дата;
						Движение.Организация = Организация;
						Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
						Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
						Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
						Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
						
						Движение = Движения.уп_РезервыОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.Период = Дата;
						Движение.Организация = Организация;
						Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
						Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
						Если ПоДоговорам Тогда
							Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
						КонецЕсли;
						Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
						Если ПоТипамСумм Тогда
							//Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
							Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
						КонецЕсли;
						Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
						
						Если ОтражатьВБухгалтерскомУчете Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
							уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ВыборкаДетальныеЗаписи.ДоговорОПС.Участник,ВыборкаДетальныеЗаписи.ДоговорОПС.ДоговорКонтрагента,
							Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.ДоговорОПС.Участник, ВыборкаДетальныеЗаписи.ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.Сумма);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// регистр уп_РазмерПенсииОПС
	Движения.уп_РазмерПенсииОПС.Записывать = Истина;
	Движения.уп_РазмерПенсииОПС.Очистить();
	
	Движения.уп_ДатыКорректировокПенсий.Записывать = Истина;
	Движения.уп_ДатыКорректировокПенсий.Очистить();
	
	Для Каждого стр из ДоговорыОПС Цикл
		Если (стр.ИндексацияПроцент+стр.ИндексацияДельта)<>стр.ТекРазмер Тогда  
			Движение = Движения.уп_РазмерПенсииОПС.Добавить();
			#Удаление
			Движение.Период = Дата;
			#КонецУдаления
			#Вставка
			//+ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
			Движение.Период = ?(ЗначениеЗаполнено(ДатаНазначения),ДатаНазначения,ДатаСумм);
			//-ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
			#КонецВставки
			Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.Размер = стр.ИндексацияПроцент+стр.ИндексацияДельта;
		КонецЕсли;
		Если КорректироватьНаКоэффициент или КорректироватьПоОстаткам Тогда
			Движение = Движения.уп_ДатыКорректировокПенсий.Добавить();
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
			Движение.Период = Дата;
			Движение.КорректировкаНаКоэффициент = КорректироватьНаКоэффициент;
			Движение.КорректировкаНаСумму = КорректироватьПоОстаткам;
		КонецЕсли;
	КонецЦикла;
	
	Движения.уп_ПроизведенныеДоначисления.Записывать = Истина;
	Движения.уп_ПроизведенныеДоначисления.Очистить();
	Если ДоначислениеПоПереданномуВзносуИзПФР Тогда
		//надо найти основания
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_ИндексацияНЧТПДоговорыОПС.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ СписокДоговоров
		|ИЗ
		|	Документ.уп_ИндексацияНЧТП.ДоговорыОПС КАК уп_ИндексацияНЧТПДоговорыОПС
		|ГДЕ
		|	уп_ИндексацияНЧТПДоговорыОПС.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Регистратор,
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ РешенияОНазначении
		|ИЗ
		|	РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|			&ДатаДок,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоров.ДоговорОПС
		|					ИЗ
		|						СписокДоговоров)
		|				И Состояние = &НЧТП) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокДоговоров.ДоговорОПС,
		|	РешенияОНазначении.Регистратор
		|ИЗ
		|	СписокДоговоров КАК СписокДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РешенияОНазначении КАК РешенияОНазначении
		|		ПО СписокДоговоров.ДоговорОПС = РешенияОНазначении.ДоговорОПС";
		
		Запрос.УстановитьПараметр("ДатаДок", Дата);
		Запрос.УстановитьПараметр("НЧТП", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			//фиксируем, что доначисление произведено
			Движение = Движения.уп_ПроизведенныеДоначисления.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
			Движение.Решение = ВыборкаДетальныеЗаписи.Регистратор;
			Движение.ПроизведеноДоначисление = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.Хозрасчетный.Очистить();
	Если ОтражатьВБухгалтерскомУчете Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
КонецПроцедуры

// ХМНПФ 26.07.2016 //ЛыткинАМ Новая ХМ_ПолучитьРазницуВМесяцах() // №194
Функция ХМ_ПолучитьРазницуВМесяцах(НачДата,КонДата)
	КолМес = 0;
	МинДата = НачалоМесяца(НачДата);
	МаксДата = НачалоМесяца(КонДата);
	Пока МинДата < МаксДата Цикл
		КолМес = КолМес + 1;
		МинДата = ДобавитьМесяц(МинДата, 1);
	КонецЦикла;
	Возврат КолМес;
КонецФункции

Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	Если НЕ ПРОВЕДЕН Тогда
		#Если Сервер Тогда
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
		#КонецЕсли
		Возврат;	
	КонецЕсли;
	
	СтрокиНаДобавление = СтруктураТЧ.уп_РезервыОПС.ТЗ.НайтиСтроки(Новый Структура(СтруктураТЧ.уп_РезервыОПС.Узел.Каспер_поле,СтрокаТЗ[СтруктураТЧ.уп_РезервыОПС.Узел.Каспер_поле]));
	Для каждого ВыборкаДетальныеЗаписи Из СтрокиНаДобавление Цикл
		ВыборкаИтогов = ВыборкаДетальныеЗаписи;
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
		Движение.Период = ВыборкаДетальныеЗаписи.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
		Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
		Движение.Сумма = ВыборкаИтогов.СуммаКСписанию;
		
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = ВыборкаДетальныеЗаписи.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
		Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
//		Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
		Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
		Движение.Сумма = ВыборкаИтогов.СуммаКСписанию;
		ВыборкаИтогов = Неопределено;
	КонецЦикла;
	Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
		СтруктураТЧ.уп_РезервыОПС.ТЗ.Удалить(СтрокаНаДобавление);
	КонецЦикла;
	
	Для Каждого стр из ДоговорыОПС Цикл
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = стр.ДатаРешения;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
		Движение.ДоговорОПС = стр.ДоговорОПС;
		Движение.Размер = стр.ИндексацияПроцент+стр.ИндексацияДельта;
	КонецЦикла;
	
	#Если Сервер Тогда
		ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
		Если СтрокаТЗ.ОбновитьРазмер = Истина Тогда
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);	
КонецПроцедуры

