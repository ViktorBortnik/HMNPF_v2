
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 27.01.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
КонецПроцедуры

// ХМНПФ 27.01.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	Если Проведен Тогда
		ЗаменитьПроводкиНаСервере_Напрямую(СтрокаТЗ.ved_id);
		РучнаяКорректировка = Истина;
		Записать();
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Функция ТекстЗапросаСACC_id_Каспер(ID_VED_ID) Экспорт

    Результат = Документы.уп_НачислениеНДФЛ.ПолучитьМакет("ТекстЗапросаСACC_id_Каспер").ПолучитьТекст();
	Результат = СтрЗаменить(Результат,":VED_ID",ID_VED_ID);
	Результат = СтрЗаменить(Результат,":МесяцНачисления","to_date('"+Формат(МесяцНачисления, "ДФ=""дд.ММ.гггг""")+"')");
	
	Возврат Результат;
 	
КонецФункции    

Функция ТекстЗапросаПроводок1С() 
 	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТACC_ID.VED_ID КАК VED_ID,
	|	ВТACC_ID.oper_date_pp КАК oper_date_pp,
	|	ВТACC_ID.ACC_ID КАК ACC_ID,
	|	ПОДСТРОКА(ВТACC_ID.ved_list, 1, 50) КАК ved_list,
	|	ПОДСТРОКА(ВТACC_ID.opergroup_sid, 1, 50) КАК opergroup_sid,
	|	ПОДСТРОКА(ВТACC_ID.pens_sub_map, 1, 50) КАК pens_sub_map,
	|	ВТACC_ID.oper_sum КАК oper_sum
	|ПОМЕСТИТЬ ВТ_ACC_ID
	|ИЗ
	|	&ВТACC_ID КАК ВТACC_ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ACC_ID.VED_ID КАК VED_ID,
	|	ВТ_ACC_ID.oper_date_pp КАК oper_date_pp,
	|	ВТ_ACC_ID.ACC_ID КАК ACC_ID,
	|	ВТ_ACC_ID.ved_list КАК ved_list,
	|	ВТ_ACC_ID.opergroup_sid КАК opergroup_sid,
	|	ВТ_ACC_ID.pens_sub_map КАК pens_sub_map,
	|	ХМ_КасперСчета.Счет КАК Счет,
	|	ВЫРАЗИТЬ(ХМ_КасперPROP_SID.Справочник КАК Справочник.уп_ПенсионныеСхемы).Ссылка КАК Схема,
	|	ВЫРАЗИТЬ(ХМ_КасперPROP_SID1.Справочник КАК Справочник.уп_ТипыСумм).Ссылка КАК ТипСумм,
	|	ВЫБОР
	|		КОГДА ВТ_ACC_ID.ved_list = ""СПС""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.СПС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.ИПС)
	|	КОНЕЦ КАК ТипРезерваСписания,
	|	ВЫБОР
	|		КОГДА ВТ_ACC_ID.ved_list = ""СПС""
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.РезервНачисленийПенсийСПС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.РезервНачисленийПенсийИПС)
	|	КОНЕЦ КАК ТипРезерваНачисления,
	|	ВТ_ACC_ID.oper_sum КАК oper_sum
	|ИЗ
	|	ВТ_ACC_ID КАК ВТ_ACC_ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_КасперPROP_SID КАК ХМ_КасперPROP_SID
	|		ПО ВТ_ACC_ID.pens_sub_map = ХМ_КасперPROP_SID.PROP_SID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_КасперPROP_SID КАК ХМ_КасперPROP_SID1
	|		ПО ВТ_ACC_ID.opergroup_sid = ХМ_КасперPROP_SID1.PROP_SID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_КасперСчета КАК ХМ_КасперСчета
	|		ПО ВТ_ACC_ID.ACC_ID = ХМ_КасперСчета.ACC_ID";       

	
КонецФункции     

Функция ТекстЗапросаСACC_id_1С() Экспорт
	
	Результат = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТACC_ID.VED_ID КАК VED_ID,
	|	ВТACC_ID.ACTOR_ID КАК ACTOR_ID,
	|	ВТACC_ID.acc_id КАК acc_id,
	|	ВТACC_ID.VED_PERIOD КАК VED_PERIOD,
	|	ВТACC_ID.oper_date_pp КАК oper_date_pp,
	|	ВТACC_ID.OPER_ndfl КАК OPER_ndfl,
	|	ВЫРАЗИТЬ(ВТACC_ID.OPER_NAME КАК СТРОКА(50)) КАК OPER_NAME
	|ПОМЕСТИТЬ ВТ_ACC_ID
	|ИЗ
	|	&ВТACC_ID КАК ВТACC_ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ACC_ID.VED_ID КАК VED_ID,
	|	ВТ_ACC_ID.ACTOR_ID КАК ACTOR_ID,
	|	ВТ_ACC_ID.acc_id КАК acc_id,
	|	ВТ_ACC_ID.VED_PERIOD КАК VED_PERIOD,
	|	ВТ_ACC_ID.oper_date_pp КАК oper_date_pp,
	|	ВТ_ACC_ID.OPER_ndfl КАК OPER_ndfl,
	|	ХМ_Каспер_НачисленияНДФЛ_VED_ID.Документ КАК Документ,
	|	ХМ_КасперСчета.Счет КАК Счет,
	|	ХМ_АкторИДПоКонтрагентам.Контрагент КАК Контрагент,
	|	ВЫБОР КОГДА oper_name = ""Начисление пенсии"" ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.уп_Пенсия)
	| 	КОГДА oper_name = ""Начисление ВС"" ТОГДА ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцам)
	|	ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КОНЕЦ КАК ВидРасчета
	|ПОМЕСТИТЬ ВТНДФЛПерсонифицированно
	|ИЗ
	|	ВТ_ACC_ID КАК ВТ_ACC_ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_Каспер_НачисленияНДФЛ_VED_ID КАК ХМ_Каспер_НачисленияНДФЛ_VED_ID
	|		ПО ВТ_ACC_ID.VED_ID = ХМ_Каспер_НачисленияНДФЛ_VED_ID.Ved_ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_КасперСчета КАК ХМ_КасперСчета
	|		ПО ВТ_ACC_ID.acc_id = ХМ_КасперСчета.ACC_ID
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_АкторИДПоКонтрагентам КАК ХМ_АкторИДПоКонтрагентам
	|		ПО ВТ_ACC_ID.ACTOR_ID = ХМ_АкторИДПоКонтрагентам.ACTOR_ID
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Активность,
	|	&ВидДвижения КАК ВидДвижения,
	|	ВТНДФЛПерсонифицированно.oper_date_pp КАК Период,
	|	ВТНДФЛПерсонифицированно.Документ КАК Регистратор,
	|	ВТНДФЛПерсонифицированно.Контрагент КАК Участник,
    |	ВТНДФЛПерсонифицированно.Контрагент.уп_ФизЛицо КАК ФизическоеЛицо,
	|	&Организация КАК Организация,
	|	&Организация КАК ГоловнаяОрганизация,
	|	&СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ВТНДФЛПерсонифицированно.oper_date_pp КАК МесяцНалоговогоПериода,
	|	ВТНДФЛПерсонифицированно.Счет КАК НомерСчета,
	|	ВТНДФЛПерсонифицированно.ВидРасчета КАК ВидРасчета,
	|	ВТНДФЛПерсонифицированно.OPER_ndfl КАК Налог,
	|	ВТНДФЛПерсонифицированно.OPER_ndfl КАК Сумма,
    |	ВТНДФЛПерсонифицированно.ВидРасчета.КатегорияДохода КАК КатегорияДохода,
    |	&РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	&ВидСтроки КАК ВидСтроки
	|ИЗ
	|	ВТНДФЛПерсонифицированно КАК ВТНДФЛПерсонифицированно";

	Возврат Результат;
	
КонецФункции    

&НаСервере
Процедура ЗаменитьПроводкиНаСервере_Напрямую(Знач ved_id)   
	
	Сообщить("Начало загрузки из Каспер: "+ ТекущаяДата());

	Число = Формат(ved_id, "ЧГ=");
	ПроизвольныйЗапрос =  ТекстЗапросаСACC_id_Каспер(Строка(Число));  
	ТЧЗапроса = ХМ_ЗагрузкаТЗ_Клиент.ИмпортИзКасперМодульОбъекта(ПроизвольныйЗапрос);  

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСACC_id_1С();
	Запрос.УстановитьПараметр("ВТACC_ID", ТЧЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтавкаНалогообложенияРезидента", Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
	Запрос.УстановитьПараметр("ВидСтроки", Перечисления.УдалитьНДФЛРасчетыСБюджетомВидСтроки.Начисление);
	Запрос.УстановитьПараметр("ВидДвижения", ВидДвиженияНакопления.Приход);

	РегистрацияВНалоговомОргане = ЗарплатаКадры.РегистрацияВНалоговомОргане(Организация, МесяцНачисления); 

	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	НаборЗаписей = Движения.уп_НДФЛУчастниковРасчетыСБюджетом;
	НаборЗаписей.Очистить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),ВыборкаДетальныеЗаписи);
	КонецЦикла;

	НаборЗаписей.Записать();

	НаборЗаписей = Движения.РасчетыНалогоплательщиковСБюджетомПоНДФЛ;
	НаборЗаписей.Очистить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),ВыборкаДетальныеЗаписи);
	КонецЦикла;

	НаборЗаписей.Записать();

КонецПроцедуры

&ИзменениеИКонтроль("СформироватьНДФЛПоДоговорам")
Функция ХМСформироватьНДФЛПоДоговорам(НаборЗаписей, Отказ)
	СрезПенсионныхПравил = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	ВестиУчетПСпоТипамДоговоровЕПС = СрезПенсионныхПравил.ВестиУчетПСпоТипамДоговоровЕПС;
	
	//для бух учета нужна таблица получателей и схем
	//сформируем таблицу получателей для проводок по бухгалтерскому учету 
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета КАК НомерСчета,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Банк КАК Банк,
		|	ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении КАК ПолучательВПлатежномПоручении,
		|	СостоянияПССрезПоследних.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&КонецДня, НомерСчета В (&СписокСчетов)) КАК СостоянияПССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(&Дата, НомерСчета В (&СписокСчетов)) КАК ЛицевыеСчетаУчастниковСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК ПолучателиПоДоговорамОбслуживания
		|			ПО ЛицевыеСчетаУчастниковСрезПоследних.Банк = ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении
		|		ПО СостоянияПССрезПоследних.НомерСчета = ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	пдс_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета,
		|	пдс_ЛицевыеСчетаУчастниковСрезПоследних.Банк,
		|	ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении,
		|	пдс_СостоянияПенсионныхСчетовПДССрезПоследних.Состояние
		|ИЗ
		|	РегистрСведений.пдс_СостоянияПенсионныхСчетовПДС.СрезПоследних(&КонецДня, НомерСчета В (&СписокСчетов)) КАК пдс_СостоянияПенсионныхСчетовПДССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.пдс_ЛицевыеСчетаУчастников.СрезПоследних(&Дата, НомерСчета В (&СписокСчетов)) КАК пдс_ЛицевыеСчетаУчастниковСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК ПолучателиПоДоговорамОбслуживания
		|			ПО пдс_ЛицевыеСчетаУчастниковСрезПоследних.Банк = ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении
		|		ПО пдс_СостоянияПенсионныхСчетовПДССрезПоследних.НомерСчета = пдс_ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета";
		
		Запрос.УстановитьПараметр("СписокСчетов", Начисления.ВыгрузитьКолонку("НомерСчета"));
		Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
		Запрос.УстановитьПараметр("КонецДня", КонецДня(Дата));
		
		ТабПолучателей = Запрос.Выполнить().Выгрузить();
		
		//и отдельный запрос по наследникам - у них не открыты пенсионные счета
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Банк,
		|	ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Участник
		|ИЗ
		|	РегистрСведений.пдс_ЛицевыеСчетаУчастников.СрезПоследних(
		|			&Дата,
		|			Участник В (&СписокУчастников)
		|				И НомерСчета = &ПустойСчет) КАК ЛицевыеСчетаУчастниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК ПолучателиПоДоговорамОбслуживания
		|		ПО ЛицевыеСчетаУчастниковСрезПоследних.Банк = ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении";
		
		Запрос.УстановитьПараметр("СписокУчастников", Начисления.ВыгрузитьКолонку("Участник"));
		Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
		Запрос.УстановитьПараметр("ПустойСчет", Справочники.пдс_ПенсионныеСчетаПДС.ПустаяСсылка());
		
		ТабНаследниковПДС = Запрос.Выполнить().Выгрузить(); 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Банк,
		|	ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Участник
		|ИЗ
		|	РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(
		|			&Дата,
		|			Участник В (&СписокУчастников)
		|				И НомерСчета = &ПустойСчет) КАК ЛицевыеСчетаУчастниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК ПолучателиПоДоговорамОбслуживания
		|		ПО ЛицевыеСчетаУчастниковСрезПоследних.Банк = ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении";
		
		Запрос.УстановитьПараметр("СписокУчастников", Начисления.ВыгрузитьКолонку("Участник"));
		Запрос.УстановитьПараметр("Дата", КонецДня(Дата));
		Запрос.УстановитьПараметр("ПустойСчет", Справочники.уп_ПенсионныеСчета.ПустаяСсылка());
		
		ТабНаследников = Запрос.Выполнить().Выгрузить(); 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НачислениеНДФЛНаДоходыУчастниковНачисления.НомерСчета КАК НомерСчета,
	               |	НачислениеНДФЛНаДоходыУчастниковНачисления.Участник КАК Участник,
	               |	НачислениеНДФЛНаДоходыУчастниковНачисления.Результат КАК Результат,
#Вставка	
	|	НачислениеНДФЛНаДоходыУчастниковНачисления.Участник.уп_ФизЛицо КАК УчастникУп_ФизЛицо,
#КонецВставки	
	               |	НачислениеНДФЛНаДоходыУчастниковНачисления.Начисление КАК ВидРасчета
	               |ИЗ
	               |	Документ.уп_НачислениеНДФЛ.Начисления КАК НачислениеНДФЛНаДоходыУчастниковНачисления
	               |ГДЕ
	               |	НачислениеНДФЛНаДоходыУчастниковНачисления.Ссылка = &Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Участник,
	               |	НомерСчета
#Удаление
	               |ИТОГИ
	               |	СУММА(Результат)
	               |ПО
	               |	Участник";
#КонецУдаления
#Вставка	
	|";
#КонецВставки	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
#Удаление
	ДеревоСчетов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
#КонецУдаления
#Вставка	
	ДеревоСчетов = Запрос.Выполнить().Выгрузить();
    ДеревоСчетов.Индексы.Добавить("Участник");
#КонецВставки	
	
	Для Каждого стр из НДФЛ Цикл
#Удаление
			ИтоговаяСтрока = ДеревоСчетов.Строки.Найти(стр.ФизическоеЛицо.уп_Контрагент, "Участник");
#КонецУдаления
#Вставка	
		ИтоговаяСтрока = Новый Структура("Строки",ДеревоСчетов.НайтиСтроки(Новый Структура("УчастникУп_ФизЛицо",стр.ФизическоеЛицо)));
		Если ИтоговаяСтрока.Строки.Количество() = 0 Тогда
			ИтоговаяСтрока = Неопределено;
		Иначе
			ИтогоНБ = 0;
			Для каждого ТекСтр Из ИтоговаяСтрока.Строки Цикл
				ИтогоНБ = ИтогоНБ + ТекСтр.Результат;
			КонецЦикла;
			ИтоговаяСтрока.Вставить("Результат",ИтогоНБ);
			ИтоговаяСтрока.Вставить("Участник",ИтоговаяСтрока.Строки[0].Участник);
		КонецЕсли;
#КонецВставки	
			ИтогоНДФЛ = стр.Налог;
			НДФЛОсталось = ИтогоНДФЛ; 
			
			ИтогоНДФЛсПревышения = стр.НалогСПревышения;
			НДФЛсПревышенияОсталось = ИтогоНДФЛсПревышения; 
			
			ИтогоНДФЛсПревышения18 = стр.НалогСПревышенияПоСтавке18;
			НДФЛсПревышенияОсталось18 = ИтогоНДФЛсПревышения18; 
			
			ИтогоНДФЛсПревышения20 = стр.НалогСПревышенияПоСтавке20;
			НДФЛсПревышенияОсталось20 = ИтогоНДФЛсПревышения20;
			
			ИтогоНДФЛсПревышения22 = стр.НалогСПревышенияПоСтавке22;
			НДФЛсПревышенияОсталось22 = ИтогоНДФЛсПревышения22; 
			Если ИтоговаяСтрока<>Неопределено Тогда
				ИтогоНБ = ИтоговаяСтрока.Результат;
				Для а = 0 по ИтоговаяСтрока.Строки.Количество()-1  Цикл
					ТекСтр = ИтоговаяСтрока.Строки.Получить(а);
					
					Если а <> (ИтоговаяСтрока.Строки.Количество()-1) Тогда
						Если ИтогоНБ<>0 Тогда
							СуммаНДФЛНаСчет = Окр(ИтогоНДФЛ/ИтогоНБ*ТекСтр.Результат,0,2); 
							СуммаНДФЛНаСчетсПревышения = Окр(ИтогоНДФЛсПревышения/ИтогоНБ*ТекСтр.Результат,0,2); 
							СуммаНДФЛНаСчетсПревышения18 = Окр(ИтогоНДФЛсПревышения18/ИтогоНБ*ТекСтр.Результат,0,2); 
							СуммаНДФЛНаСчетсПревышения20 = Окр(ИтогоНДФЛсПревышения20/ИтогоНБ*ТекСтр.Результат,0,2); 
							СуммаНДФЛНаСчетсПревышения22 = Окр(ИтогоНДФЛсПревышения22/ИтогоНБ*ТекСтр.Результат,0,2); 
						ИначеЕсли ИтогоНДФЛ = 0 Тогда
							СуммаНДФЛНаСчет = 0;
						Иначе
							Отказ = ИСТИНА;
							Сообщить("Нет возможности распределить НДФЛ по налоговой базе, т.к. отсутствует налоговая база по "+стр.Участник);
						КонецЕсли;	
					Иначе
						СуммаНДФЛНаСчет = НДФЛОсталось; 
						СуммаНДФЛНаСчетсПревышения = НДФЛсПревышенияОсталось;
						СуммаНДФЛНаСчетсПревышения18 = НДФЛсПревышенияОсталось18;
						СуммаНДФЛНаСчетсПревышения20 = НДФЛсПревышенияОсталось20;
						СуммаНДФЛНаСчетсПревышения22 = НДФЛсПревышенияОсталось22;
					КонецЕсли;
					НДФЛОсталось = НДФЛОсталось-СуммаНДФЛНаСчет; 
					НДФЛсПревышенияОсталось = НДФЛсПревышенияОсталось-СуммаНДФЛНаСчетсПревышения; 
					НДФЛсПревышенияОсталось18 = НДФЛсПревышенияОсталось18-СуммаНДФЛНаСчетсПревышения18; 
					НДФЛсПревышенияОсталось20 = НДФЛсПревышенияОсталось20-СуммаНДФЛНаСчетсПревышения20; 
					НДФЛсПревышенияОсталось22 = НДФЛсПревышенияОсталось22-СуммаНДФЛНаСчетсПревышения22; 
					
					Движение = НаборЗаписей.Добавить();
					
					// свойства
					Движение.Период 				= Дата;
					
					// измерения 
					Движение.Организация 			= Организация;
					Движение.Участник 				= ИтоговаяСтрока.Участник;
					Движение.МесяцНалоговогоПериода	= Стр.МесяцНалоговогоПериода;
					Движение.НомерСчета      		= ТекСтр.НомерСчета;
					Движение.СтавкаНалогообложенияРезидента	= Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13;
					Движение.ВидРасчета 			= ТекСтр.ВидРасчета; 
					
					// ресурсы
					Движение.Налог 			        	= СуммаНДФЛНаСчет; 
					Движение.СуммаСПревышения 			= СуммаНДФЛНаСчетсПревышения; 
					Движение.СуммаСПревышенияПоСтавке18 = СуммаНДФЛНаСчетсПревышения18; 
					Движение.СуммаСПревышенияПоСтавке20 = СуммаНДФЛНаСчетсПревышения20; 
					Движение.СуммаСПревышенияПоСтавке22 = СуммаНДФЛНаСчетсПревышения22; 
					Движение.ВидСтроки              	= Перечисления.УдалитьНДФЛРасчетыСБюджетомВидСтроки.Начисление;
					
					//ОПА НПО 2018.04.28 +
					лВидДвиженияПоТипамЕПС = Неопределено;
					Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
						ТипПС = РегистрыСведений.уп_ТипыПенсионныхСчетовДляЕПС.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", ТекСтр.НомерСчета)).ТипДоговораЕПС;
						//ТипПС = РегистрыСведений.уп_ТипыПенсионныхСчетовДляЕПС.ПолучитьПоследнее(Дата, Новый Структура("Объект, Свойство", ТекСтр.НомерСчета, ПланыВидовХарактеристик.уп_ДополнительныеСведенияСРегистратором.ТипДоговораНПО_ЕПС)).Значение;
						Если ТипПС = Перечисления.нфо_ТипДоговораНПФ.Инвестиционный Тогда
							Если  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_Пенсия Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеПенсии_Инвестиционный;
							ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцам 
								или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцамВзносыФЛ Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеВыкупной_Инвестиционный;
							ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаНаследникам Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеНаследникам_Инвестиционный;		
							КонецЕсли;	
						ИначеЕсли ТипПС = Перечисления.нфо_ТипДоговораНПФ.Страховой Тогда
							Если  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_Пенсия Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеПенсии_Страховой;
							ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцам 
								или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцамВзносыФЛ Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеВыкупной_Страховой;
							ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаНаследникам Тогда
								лВидДвиженияПоТипамЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.НачислениеНаследникам_Страховой;		
							КонецЕсли;	
						КонецЕсли;
						Движение.ВидДвиженияПоТипамЕПС = лВидДвиженияПоТипамЕПС;
					КонецЕсли;
					//ОПА НПО 2018.04.28 -
					
					Если ОтражатьВБухгалтерскомУчете Тогда
						//здесь должна быть проводка по бух учету 
						Если ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_Пенсия 
							или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцам 
							или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцамВзносыФЛ Тогда
							СтрПолуч = ТабПолучателей.Найти(ТекСтр.НомерСчета, "НомерСчета"); 
						ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_Пенсия   
							или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыплатыПоОсобымОбстоятельствам
							или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыкупнаяСуммаРасторженцам 
							или ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыкупнаяСуммаРасторженцамВзносыФЛ Тогда
							СтрПолуч = ТабПолучателей.Найти(ТекСтр.НомерСчета, "НомерСчета");
						ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаНаследникам Тогда
							СтрПолуч = ТабНаследников.Найти(ТекСтр.Участник, "Участник"); 
						ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыкупнаяСуммаПравопреемникам Тогда
							СтрПолуч = ТабНаследниковПДС.Найти(ТекСтр.Участник, "Участник"); 
						КонецЕсли;
						Попытка
							Если СтрПолуч.ПолучательВПлатежномПоручении = Null Тогда
								Получатель = СтрПолуч.Банк;
							Иначе
								Получатель = СтрПолуч.ПолучательВПлатежномПоручении;
							КонецЕсли;
						Исключение   //получатель не оформлен
							Если ЭтотОбъект.уп_ВидДеятельности.ВидДеятельности = Перечисления.уп_ВидыДеятельности.ПР
								или ЭтотОбъект.уп_ВидДеятельности.ВидДеятельности = Перечисления.уп_ВидыДеятельности.ПДС Тогда
								Сообщить("По участнику "+ТекСтр.Участник+" не оформлен получатель.");
								МассивСообщений.Добавить("По участнику "+ТекСтр.Участник+" не оформлен получатель.");
							КонецЕсли;
							Получатель = Справочники.Контрагенты.ПустаяСсылка();
						Конецпопытки;
						Если НЕ ЗначениеЗаполнено(Получатель) Тогда //через кассу
							Получ = ТекСтр.Участник;
							ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
						Иначе
							Получ = Получатель;
							ДоговорКонтрагента = Получатель.ОсновнойДоговорКонтрагента;
						КонецЕсли;
						//здесь еще надо разобраться что это за НДФЛ - это видно по виду начисления
						Если ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаНаследникам Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛНаследников;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_Пенсия Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛПенсионеров;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ  
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцам Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛРасторженцев;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ 
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаРасторженцамВзносыФЛ Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛРасторженцев;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ 
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.уп_ВыкупнаяСуммаПравопреемникамОПС Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛПравопреемниковОПС;
							ПодразделениеБУ = ТекСтр.НомерСчета.ПодразделениеБУ 
						ИначеЕсли ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыкупнаяСуммаПравопреемникам Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛНаследниковПДС;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_Пенсия Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛПенсионеровПДС;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ
						ИначеЕсли  ТекСтр.ВидРасчета = ПланыВидовРасчета.Начисления.пдс_ВыплатыПоОсобымОбстоятельствам Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛОсобыеОбстоятельстваПДС;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ
						Иначе
							ВидДвижения = Перечисления.уп_ВидыДвижений.НДФЛРасторженцевПДС;
							ПодразделениеБУ = ТекСтр.НомерСчета.Владелец.ПодразделениеБУ
						КонецЕсли;
						
						уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, Получ, ДоговорКонтрагента,
						Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, Справочники.Контрагенты.ПустаяСсылка(), Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаНДФЛНаСчет, ПодразделениеБУ, ПодразделениеБУ);
					КонецЕсли;
				КонецЦикла;	
			Иначе
				Отказ = Истина;
				уп_ОсновнаяПоставкаСервер.ОшибкаПриПроведении("У Участника по строке таблицы НДФЛ "+стр.НомерСтроки+" не задана база распределения НДФЛ по договорам", Отказ);
			КонецЕсли;	
	КонецЦикла;	
	
КонецФункции

&После("ПередЗаписью")
Процедура ХМПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = НачалоДня(Дата)+60*60*6+60*Месяц(МесяцНачисления);
КонецПроцедуры

