
Процедура ХМ_НеТиповыеИзменения()
	//ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
	//ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	// ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
	
	// ХМНПФ 16.02.2016 ШадринАВ // ОбработкаПроведения() // №207 Не актуально
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьТЧДоговорыОПС")
Процедура РасшОбщ_ЗаполнитьТЧДоговорыОПС() Экспорт
	//ищем текущие состояния НЧТП
	//дополняем состояниями доп части (НЧТП)
	//смортим остатки по осн части счета
	//смотрим остатки по доп. части счета
	//смотрим дату назначения НЧТП по справочнику договоры ОПС
	//если есть остатки, считаем период выплат и дельту

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|ПОМЕСТИТЬ СписокЗЛ
		|ИЗ
		|	РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаСумм, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
 		#Удаление
		|ВЫБРАТЬ
		|	уп_СчетчикНачисленийОПСОбороты.ДоговорОПС,
		|	уп_СчетчикНачисленийОПСОбороты.КоличествоОборот КАК КоличествоСделанныхВыплат
		|ПОМЕСТИТЬ КолНачислений
		|ИЗ
		|	РегистрНакопления.уп_СчетчикНачисленийОПС.Обороты(
		|			&ДатаНачалаВремен,
		|			&ДатаРасчета,
		|			Период,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокЗЛ.ДоговорОПС
		|					ИЗ
		|						СписокЗЛ)
		|				И ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Срочная)) КАК уп_СчетчикНачисленийОПСОбороты
		#КонецУдаления
		#Вставка
		// +ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
		|ВЫБРАТЬ
		|	НачисленияПенсийОПС.ДоговорОПС КАК ДоговорОПС,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НачисленияПенсийОПС.НачПериода) КАК КоличествоСделанныхВыплат
		|ПОМЕСТИТЬ КолНачислений
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсийОПС КАК НачисленияПенсийОПС
		|ГДЕ
		|	НачисленияПенсийОПС.ДоговорОПС В
		|			(ВЫБРАТЬ
		|				СписокЗЛ.ДоговорОПС
		|			ИЗ
		|				СписокЗЛ)
		|	И НачисленияПенсийОПС.Регистратор ССЫЛКА Документ.уп_НачислениеПенсийОПС
		|	И НачисленияПенсийОПС.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсийОПС.Срочная)
		|	И НачисленияПенсийОПС.НачПериода <= &ДатаСумм
		|СГРУППИРОВАТЬ ПО
		|	НачисленияПенсийОПС.ДоговорОПС
		// -ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
		#КонецВставки
    	|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокЗЛ.ДоговорОПС,
		|	уп_РазмерПенсииОПССрезПоследних.Размер КАК ТекРазмер,
		|	СписокЗЛ.ДоговорОПС.Участник КАК ЗастрахованноеЛицо,
		|	ВЫБОР
		|		КОГДА уп_РазмерПенсииОПССрезПервых.Регистратор.Дата <= &КонецПрошлогоГода
		|			ТОГДА уп_РазмерПенсииОПССрезПоследних.Размер * &КоэфИндексации
		|		ИНАЧЕ уп_РазмерПенсииОПССрезПоследних.Размер
		|	КОНЕЦ КАК ИндексацияПроцент,
		|	ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф КАК СуммаДопЧастиСчета,
		|	ВЫБОР
		#Удаление
		|		КОГДА ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОПСОбороты.КоличествоОборот, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф / (ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОПСОбороты.КоличествоОборот, 0))
		|	КОНЕЦ КАК ИндексацияДельта,
		|	ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОПСОбороты.КоличествоОборот, 0) КАК ПериодВыплатДляРасчета,
		#КонецУдаления
		#Вставка
		// +ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
		|		КОГДА ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(КолНачислений.КоличествоСделанныхВыплат, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) * &Коэф / (ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(КолНачислений.КоличествоСделанныхВыплат, 0))
		|	КОНЕЦ КАК ИндексацияДельта,
		|	ЕСТЬNULL(уп_КоличествоВыплатОПССрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(КолНачислений.КоличествоСделанныхВыплат, 0) КАК ПериодВыплатДляРасчета,
		// -ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
		#КонецВставки
 		|	уп_РазмерПенсииОПССрезПервых.Регистратор.Дата КАК ДатаРешения,
		|	КолНачислений.КоличествоСделанныхВыплат,
		|	ЕСТЬNULL(уп_РезервыОПСОстаткиДопЧасть.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	СписокЗЛ КАК СписокЗЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_РезервыОПС.Остатки(
		|				&ДатаСумм,
		|				Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|						ИЗ
		|							РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|						ГДЕ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата)
		|					И ТипРезерва = &Накопительный
		|					И НЕ ТипСуммы В (&СписокОснЧасть)) КАК уп_РезервыОПСОстаткиДопЧасть
		|		ПО СписокЗЛ.ДоговорОПС = уп_РезервыОПСОстаткиДопЧасть.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПоследних(
		|				&ДатаРазмера,
		|				ТипПенсии = &Срочная
		|					И ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|						ИЗ
		|							РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|						ГДЕ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата)) КАК уп_РазмерПенсииОПССрезПоследних
		|		ПО СписокЗЛ.ДоговорОПС = уп_РазмерПенсииОПССрезПоследних.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоВыплатОПС.СрезПоследних(
		|				&ДатаРасчета,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|						ИЗ
		|							РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|						ГДЕ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата)) КАК уп_КоличествоВыплатОПССрезПоследних
		|		ПО СписокЗЛ.ДоговорОПС = уп_КоличествоВыплатОПССрезПоследних.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачисленийОПС.Обороты(
		|				&НачалоВремен,
		|				&ДатаРасчета,
		|				Период,
		|				ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|						ИЗ
		|							РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|						ГДЕ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата)
		|					И ТипПенсии = &Срочная) КАК уп_СчетчикНачисленийОПСОбороты
		|		ПО СписокЗЛ.ДоговорОПС = уп_СчетчикНачисленийОПСОбороты.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсииОПС.СрезПервых(
		|				&ДатаНачалаВремен,
		|				ТипПенсии = &Срочная
		|					И ДоговорОПС.Организация = &Организация
		|					И ДоговорОПС В
		|						(ВЫБРАТЬ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|						ИЗ
		|							РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ДатаРасчета, ДоговорОПС.Организация = &Организация) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|						ГДЕ
		|							уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &СрочнаяВыплата)) КАК уп_РазмерПенсииОПССрезПервых
		|		ПО СписокЗЛ.ДоговорОПС = уп_РазмерПенсииОПССрезПервых.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ КолНачислений КАК КолНачислений
		|		ПО СписокЗЛ.ДоговорОПС = КолНачислений.ДоговорОПС
		|ГДЕ
		|	уп_РазмерПенсииОПССрезПервых.Регистратор.Дата < &ДатаСумм
		|
		|УПОРЯДОЧИТЬ ПО
		|	СписокЗЛ.ДоговорОПС.Участник.Наименование";

	Запрос.УстановитьПараметр("ДатаРасчета", НачалоДня(Дата)-1);
	Запрос.УстановитьПараметр("ДатаРазмера", КонецДня(Дата));
	Запрос.УстановитьПараметр("НачалоВремен", '20090101');
	Если КорректироватьНаКоэффициент Тогда
		Запрос.УстановитьПараметр("КоэфИндексации", КорректировочныйКоэффициент);
	Иначе
		Запрос.УстановитьПараметр("КоэфИндексации", 1);
	КонецЕсли;
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("СрочнаяВыплата", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.СрочнаяВыплата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Срочная", Перечисления.уп_ТипыПенсийОПС.Срочная);
	Запрос.УстановитьПараметр("ДатаСумм", ДатаСумм);
	Запрос.УстановитьПараметр("ДатаНачалаВремен", '20050101');
	Запрос.УстановитьПараметр("КонецПрошлогоГода", КонецДня(НачалоГода(Дата)-1));
	Если КорректироватьПоОстаткам Тогда
		Запрос.УстановитьПараметр("Коэф", 1);
	Иначе
		Запрос.УстановитьПараметр("Коэф", 0);
	КонецЕсли;
	
	СписокОснЧасть = Новый СписокЗначений;
	СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыФиз);
	СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ИДФиз);
	Запрос.УстановитьПараметр("СписокОснЧасть", СписокОснЧасть);	
	
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	ТабДоговоровДляРасшифровки = Новый ТаблицаЗначений;
	ТабДоговоровДляРасшифровки.Колонки.Добавить("ДоговорОПС", Новый ОписаниеТипов("СправочникСсылка.уп_ДоговорыОПС"));

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.СуммаДопЧастиСчета<>0 или ВыборкаДетальныеЗаписи.ДатаРешения<=КонецДня(НачалоГода(Дата)-1) Тогда
			НовСтр = ДоговорыОПС.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
			Если НеУменьшатьПенсиюПриКорректировке И НовСтр.ИндексацияДельта<0 Тогда
				НовСтр.ИндексацияДельта = 0;
			КонецЕсли;
			Если ВыборкаДетальныеЗаписи.СуммаДопЧастиСчета<>0 Тогда
				НовСтрТабРасшифровки = ТабДоговоровДляРасшифровки.Добавить();
				НовСтрТабРасшифровки.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				//НовСтрТабРасшифровки.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	#Вставка
	// +ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
	////+ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	//ХМ_ДатаДок = НачалоДня(?(ЗначениеЗаполнено(ДатаНазначения),ДатаНазначения,ДатаСумм));
	//Для Каждого	НовСтр Из ДоговорыОПС Цикл
	//	РазницаВМесяцах = ХМ_ПолучитьРазницуВМесяцах(НовСтр.ДоговорОПС.ДатаНазначенияСрочнойВыплаты,ХМ_ДатаДок);
	//	НовСтр.ПериодВыплатДляРасчета = ?(120 - РазницаВМесяцах > 0, 120 - РазницаВМесяцах, 1);
	//	НовСтр.ИндексацияДельта = (НовСтр.СуммаДопЧастиСчета)/НовСтр.ПериодВыплатДляРасчета;
	//	Если НеУменьшатьПенсиюПриКорректировке И НовСтр.ИндексацияДельта<0 Тогда
	//		НовСтр.ИндексацияДельта = 0;
	//	КонецЕсли;	
	//КонецЦикла;
	////-ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ЗаполнитьТЧДоговорыОПС
	// -ХМНПФ 06.08.2019 ШадринАВ // ЗаполнитьТЧДоговорыОПС() // №2380
	#КонецВставки
	
	//заполнить таблицу расшифровки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТабДоговоров.ДоговорОПС
		|ПОМЕСТИТЬ ТабИсходная
		|ИЗ
		|	&ТабДоговоров КАК ТабДоговоров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОПСОстатки.ДоговорОПС,
		|	уп_РезервыОПСОстатки.ТипСуммы,
		|	уп_РезервыОПСОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.уп_РезервыОПС.Остатки(
		|			&ДатаСумм,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						ТабИсходная.ДоговорОПС
		|					ИЗ
		|						ТабИсходная)
		|				И ТипРезерва = &Накопительный
		|				И НЕ ТипСуммы В (&СписокОснЧасть)) КАК уп_РезервыОПСОстатки";
	
	Запрос.УстановитьПараметр("ДатаСумм", НачалоДня(ДатаСумм));
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("СписокОснЧасть", СписокОснЧасть);
	Запрос.УстановитьПараметр("ТабДоговоров", ТабДоговоровДляРасшифровки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = РасшифровкаСуммы.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры	

&ИзменениеИКонтроль("ОбработкаПроведения")	
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	
	СтарыйАлгоритм = ?(РасшифровкаСуммы.Количество()=0 , ИСТИНА, ЛОЖЬ);
    //проверка остатков по счетам
	ПроверкаОстатков(Отказ, СтарыйАлгоритм);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	//движения по регистру Резервы ОПС
	//движения по регистру Хозрасчетный
	Движения.уп_РезервыОПС.Записывать = Истина;
	Движения.уп_РезервыОПС.Очистить();
	
	Движения.уп_ТекущаяСуммаГарантирования.Записывать = Истина;
	Движения.уп_ТекущаяСуммаГарантирования.Очистить();
	
	Если  СтарыйАлгоритм Тогда
		Если КорректироватьПоОстаткам Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.ДоговорОПС,
			|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.СуммаДопЧастиСчета,
			|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.НомерСтроки
			|ПОМЕСТИТЬ ОснТаблица
			|ИЗ
			|	Документ.уп_ИндексацияСрочнойВыплаты.ДоговорыОПС КАК уп_ИндексацияСрочнойВыплатыДоговорыОПС
			|ГДЕ
			|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_РезервыОПСОстатки.ТипСуммы,
			|	уп_РезервыОПСОстатки.СуммаОстаток КАК СуммаОстаток,
			|	уп_РезервыОПСОстатки.ТипСуммы.Родитель КАК ТипСуммыРодитель,
			|	уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток КАК СуммаГарантирования,
			|	уп_РезервыОПСОстатки.ДоговорОПС
			|ПОМЕСТИТЬ Остатки
			|ИЗ
			|	РегистрНакопления.уп_РезервыОПС.Остатки(
			|			&ДатаРасчета,
			|			Организация = &Организация
			|				И ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			|				И ТипРезерва = &Накопительный
			|				И НЕ ТипСуммы В (&СписокОсн)) КАК уп_РезервыОПСОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(
			|				&ДатаРасчета,
			|				Организация = &Организация
			|					И НЕ ТипСуммы В (&СписокОсн)
			|					И ДоговорОПС В
			|						(ВЫБРАТЬ
			|							ОснТаблица.ДоговорОПС
			|						ИЗ
			|							ОснТаблица)) КАК уп_ТекущаяСуммаГарантированияОстатки
			|		ПО уп_РезервыОПСОстатки.ДоговорОПС = уп_ТекущаяСуммаГарантированияОстатки.ДоговорОПС
			|			И уп_РезервыОПСОстатки.ТипСуммы = уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОснТаблица.ДоговорОПС КАК ДоговорОПС,
			|	ОснТаблица.СуммаДопЧастиСчета КАК СуммаДопЧастиСчета,
			|	ОснТаблица.НомерСтроки КАК НомерСтроки,
			|	Остатки.ТипСуммы,
			|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
			|	ОснТаблица.ДоговорОПС.Участник КАК ЗастрахованноеЛицо,
			|	Остатки.ТипСуммы.Родитель КАК ТипСуммыРодитель,
			|	ЕСТЬNULL(Остатки.СуммаГарантирования, 0) КАК СуммаГарантирования
			|ИЗ
			|	ОснТаблица КАК ОснТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
			|		ПО ОснТаблица.ДоговорОПС = Остатки.ДоговорОПС
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки,
			|	ТипСуммыРодитель
			|ИТОГИ
			|	МАКСИМУМ(СуммаДопЧастиСчета),
			|	СУММА(СуммаОстаток),
			|	СУММА(СуммаГарантирования)
			|ПО
			|	ДоговорОПС";
			
			Запрос.УстановитьПараметр("ДатаРасчета", ЭтотОбъект.МоментВремени());
			Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			СписокОснЧасть = Новый СписокЗначений;
			СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыФиз);
			СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ИДФиз);
			Запрос.УстановитьПараметр("СписокОсн", СписокОснЧасть);
			
			Результат = Запрос.Выполнить();
			
			ВыборкаИтогов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаИтогов.Следующий() Цикл
				Если ВыборкаИтогов.СуммаДопЧастиСчета<>0 Тогда
					Если НеУменьшатьПенсиюПриКорректировке и ВыборкаИтогов.СуммаДопЧастиСчета<0 Тогда
						//ничего не делаем
					Иначе
						ВыборкаДетальныеЗаписи = ВыборкаИтогов.Выбрать();
						//фиксируем
						ОсталосьДопЧастиСчета = ВыборкаИтогов.СуммаДопЧастиСчета;
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
							//списываем доп часть
							СуммаКСписанию = Мин(ВыборкаДетальныеЗаписи.СуммаОстаток, ОсталосьДопЧастиСчета);
							ОсталосьДопЧастиСчета = ОсталосьДопЧастиСчета-СуммаКСписанию; 							
							#Удаление
							Если СуммаКСписанию>0 Тогда
							#КонецУдаления
							#Вставка
							// +ХМНПФ 10.02.2016 Лыткин А.М. //ОбработкаПроведения //№204
							Если СуммаКСписанию<>0 Тогда
							// -ХМНПФ 10.02.2016 Лыткин А.М. //ОбработкаПроведения //№204
							#КонецВставки
								//списываем
								Движение = Движения.уп_РезервыОПС.Добавить();
								Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
								Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
								Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
								Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
								Движение.Сумма = СуммаКСписанию;
								
								Движение = Движения.уп_РезервыОПС.Добавить();
								Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
								Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
								Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
								//Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
								Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
								Движение.Сумма = СуммаКСписанию;
								
								Если ВыборкаДетальныеЗаписи.СуммаГарантирования<>0 Тогда
									Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
									Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
									Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
									Движение.Организация = Организация;
									Движение.Период = Дата;
									Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
									Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаГарантирования;
								КонецЕсли;	
								
								Если ОтражатьВБухгалтерскомУчете Тогда
									ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
									уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ВыборкаДетальныеЗаписи.ЗастрахованноеЛицо,ВыборкаИтогов.ДоговорОПС.ДоговорКонтрагента,
									Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.ЗастрахованноеЛицо, ВыборкаИтогов.ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаКСписанию);
								КонецЕсли;
							КонецЕсли;
							Если ОсталосьДопЧастиСчета = 0 Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Если КорректироватьПоОстаткам Тогда

			//текущая сумма гарантирования
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.ДоговорОПС,
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.ТипСуммы,
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.Сумма
			|ПОМЕСТИТЬ ТабДокумента
			|ИЗ
			|	Документ.уп_ИндексацияСрочнойВыплаты.РасшифровкаСуммы КАК уп_ИндексацияНЧТПРасшифровкаСуммы
			|ГДЕ
			|	уп_ИндексацияНЧТПРасшифровкаСуммы.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ТекущаяСуммаГарантированияОстатки.ДоговорОПС,
			|	уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы,
			|	уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток
			|ПОМЕСТИТЬ Гарантирование
			|ИЗ
			|	РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ТабДокумента.ДоговорОПС
			|					ИЗ
			|						ТабДокумента)
			|				И НЕ ТипСуммы В (&СписокОсн)) КАК уп_ТекущаяСуммаГарантированияОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_РезервыОПСОстатки.ДоговорОПС,
			|	уп_РезервыОПСОстатки.ТипСуммы,
			|	уп_РезервыОПСОстатки.СуммаОстаток
			|ПОМЕСТИТЬ Выплатной
			|ИЗ
			|	РегистрНакопления.уп_РезервыОПС.Остатки(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ТабДокумента.ДоговорОПС
			|					ИЗ
			|						ТабДокумента)
			|				И ТипРезерва = &Выплатной) КАК уп_РезервыОПСОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТабДокумента.ДоговорОПС КАК ДоговорОПС,
			|	ТабДокумента.ТипСуммы,
			|	ТабДокумента.Сумма КАК Сумма,
			|	ЕСТЬNULL(Гарантирование.СуммаОстаток, 0) КАК СуммаОстаток,
			|	ЕСТЬNULL(Выплатной.СуммаОстаток, 0) КАК СуммаВыплатной
			|ИЗ
			|	ТабДокумента КАК ТабДокумента
			|		ЛЕВОЕ СОЕДИНЕНИЕ Гарантирование КАК Гарантирование
			|		ПО ТабДокумента.ДоговорОПС = Гарантирование.ДоговорОПС
			|			И ТабДокумента.ТипСуммы = Гарантирование.ТипСуммы
			|		ЛЕВОЕ СОЕДИНЕНИЕ Выплатной КАК Выплатной
			|		ПО ТабДокумента.ТипСуммы = Выплатной.ТипСуммы
			|			И ТабДокумента.ДоговорОПС = Выплатной.ДоговорОПС
			|ИТОГИ
			|	СУММА(Сумма)
			|ПО
			|	ДоговорОПС";
			
			Запрос.УстановитьПараметр("ДатаДок", ЭтотОбъект.МоментВремени());
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("Выплатной", Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты);
			
			СписокОснЧасть = Новый СписокЗначений;
			СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыФиз);
			СписокОснЧасть.Добавить(Справочники.уп_ТипыСуммОПС.ИДФиз);
			Запрос.УстановитьПараметр("СписокОсн", СписокОснЧасть);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока Выборка.Следующий() Цикл
				Если НеУменьшатьПенсиюПриКорректировке И Выборка.Сумма<0 Тогда
					//ничего не делаем
				Иначе
					ВыборкаДетальныеЗаписи = Выборка.Выбрать();
					
					Движения.уп_ТекущаяСуммаГарантирования.Записывать = Истина;
					Движения.уп_ТекущаяСуммаГарантирования.Очистить();
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						Если ВыборкаДетальныеЗаписи.СуммаОстаток<>0 Тогда
							Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
							Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
							Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
							Движение.Организация = Организация;
							Движение.Период = Дата;
							Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
							Движение.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток;
						КонецЕсли;
						
						Если ВыборкаДетальныеЗаписи.Сумма<0 Тогда
							Если  ВыборкаДетальныеЗаписи.СуммаВыплатной<ВыборкаДетальныеЗаписи.Сумма*(-1) Тогда
								Отказ = Истина;
								Сообщить("По договору ОПС "+ВыборкаДетальныеЗаписи.ДоговорОПС+" по типу суммы "+ВыборкаДетальныеЗаписи.ТипСуммы+" недостаточно средств в выплатном резерве для покрытия отрицательного остатка РПН.");
							КонецЕсли;	
						КонецЕсли;	
						Движение = Движения.уп_РезервыОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
						Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
						Движение.Период = Дата;
						Движение.Организация = Организация;
						Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
						Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
						Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
						Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
						
						Движение = Движения.уп_РезервыОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.Период = Дата;
						Движение.Организация = Организация;
						Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
						Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
						Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
						Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
						//Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
						Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
						Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
						
						Если ОтражатьВБухгалтерскомУчете Тогда
							ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
							уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ВыборкаДетальныеЗаписи.ДоговорОПС.Участник,ВыборкаДетальныеЗаписи.ДоговорОПС.ДоговорКонтрагента,
							Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.ДоговорОПС.Участник, ВыборкаДетальныеЗаписи.ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ВыборкаДетальныеЗаписи.Сумма);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
	// регистр уп_РазмерПенсииОПС
	Движения.уп_РазмерПенсииОПС.Записывать = Истина;
	Движения.уп_РазмерПенсииОПС.Очистить();
	
	Движения.уп_ДатыКорректировокПенсий.Записывать = Истина;
	Движения.уп_ДатыКорректировокПенсий.Очистить();
	
	Для Каждого стр из ДоговорыОПС Цикл
		Если (стр.ИндексацияПроцент+стр.ИндексацияДельта)<>стр.ТекРазмер Тогда  
			Движение = Движения.уп_РазмерПенсииОПС.Добавить();
			#Удаление
			Движение.Период = Дата;
			#КонецУдаления
			#Вставка
			//+ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
			Движение.Период = ?(ЗначениеЗаполнено(ДатаНазначения),ДатаНазначения,ДатаСумм);
			//-ХМНПФ ЛыткинАМ //2016-07-28 //№455 //ОбработкаПроведения
			#КонецВставки
			Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.Размер = стр.ИндексацияПроцент+стр.ИндексацияДельта;
		КонецЕсли;
		Если КорректироватьНаКоэффициент или КорректироватьПоОстаткам Тогда
			Движение = Движения.уп_ДатыКорректировокПенсий.Добавить();
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
			Движение.Период = Дата;
			Движение.КорректировкаНаКоэффициент = КорректироватьНаКоэффициент;
			Движение.КорректировкаНаСумму = КорректироватьПоОстаткам;
		КонецЕсли;
	КонецЦикла;
	
	Движения.уп_ПроизведенныеДоначисления.Записывать = Истина;
	Движения.уп_ПроизведенныеДоначисления.Очистить();
	Если ДоначислениеПоПереданномуВзносуИзПФР Тогда
		//надо найти основания
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ СписокДоговоров
		|ИЗ
		|	Документ.уп_ИндексацияСрочнойВыплаты.ДоговорыОПС КАК уп_ИндексацияСрочнойВыплатыДоговорыОПС
		|ГДЕ
		|	уп_ИндексацияСрочнойВыплатыДоговорыОПС.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Регистратор,
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ РешенияОНазначении
		|ИЗ
		|	РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|			&ДатаДок,
		|			ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоров.ДоговорОПС
		|					ИЗ
		|						СписокДоговоров)
		|				И Состояние = &СрочнаяВыплата) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокДоговоров.ДоговорОПС,
		|	РешенияОНазначении.Регистратор
		|ИЗ
		|	СписокДоговоров КАК СписокДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РешенияОНазначении КАК РешенияОНазначении
		|		ПО СписокДоговоров.ДоговорОПС = РешенияОНазначении.ДоговорОПС";
		
		Запрос.УстановитьПараметр("ДатаДок", Дата);
		Запрос.УстановитьПараметр("СрочнаяВыплата", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.СрочнаяВыплата);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			//фиксируем, что доначисление произведено
			Движение = Движения.уп_ПроизведенныеДоначисления.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
			Движение.Решение = ВыборкаДетальныеЗаписи.Регистратор;
			Движение.ПроизведеноДоначисление = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.Хозрасчетный.Очистить();
	Если ОтражатьВБухгалтерскомУчете Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
КонецПроцедуры

// ХМНПФ 10.02.2016 ШадринАВ // Новая ХМ_ПолучитьРазницуВМесяцах() // №194
Функция ХМ_ПолучитьРазницуВМесяцах(НачДата,КонДата)
	КолМес = 0;
	МинДата = НачалоМесяца(НачДата);
	МаксДата = НачалоМесяца(КонДата);
	Пока МинДата < МаксДата Цикл
		КолМес = КолМес + 1;
		МинДата = ДобавитьМесяц(МинДата, 1);
	КонецЦикла;
	Возврат КолМес;
КонецФункции

Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	Если НЕ ПРОВЕДЕН Тогда
		#Если Сервер Тогда
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
		#КонецЕсли
		Возврат;	
	КонецЕсли;
	
	СтрокиНаДобавление = СтруктураТЧ.уп_РезервыОПС.ТЗ.НайтиСтроки(Новый Структура(СтруктураТЧ.уп_РезервыОПС.Узел.Каспер_поле,СтрокаТЗ[СтруктураТЧ.уп_РезервыОПС.Узел.Каспер_поле]));
	Для каждого ВыборкаДетальныеЗаписи Из СтрокиНаДобавление Цикл
		ВыборкаИтогов = ВыборкаДетальныеЗаписи;
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
		Движение.Период = ВыборкаДетальныеЗаписи.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
		Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
		Движение.Сумма = ВыборкаИтогов.СуммаКСписанию;
		
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = ВыборкаДетальныеЗаписи.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
		Движение.ДоговорОПС = ВыборкаИтогов.ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
//		Движение.ТипСуммы = ПреобразоватьТипСуммы(ВыборкаДетальныеЗаписи.ТипСуммы);
		Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
		Движение.Сумма = ВыборкаИтогов.СуммаКСписанию;
		ВыборкаИтогов = Неопределено;
	КонецЦикла;
	Для каждого СтрокаНаДобавление Из СтрокиНаДобавление Цикл
		СтруктураТЧ.уп_РезервыОПС.ТЗ.Удалить(СтрокаНаДобавление);
	КонецЦикла;
	
	Для Каждого стр из ДоговорыОПС Цикл
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = стр.ДатаРешения;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
		Движение.ДоговорОПС = стр.ДоговорОПС;
		Движение.Размер = стр.ИндексацияПроцент+стр.ИндексацияДельта;
	КонецЦикла;
	
	#Если Сервер Тогда
		ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
		ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
	#КонецЕсли
КонецПроцедуры

&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);	
КонецПроцедуры

