
&НаСервере
&ИзменениеИКонтроль("ЗаполнитьНаСервере")
Процедура ХМЗаполнитьНаСервере()
	Объект.СписокСчетов.Очистить(); 

	ТекстУсловия = " ";
	Если ЗначениеЗаполнено(Объект.ПенсионныйДоговор) Тогда
		ТекстУсловия = ТекстУсловия+" И ВТ_Поступления.ПенсионныйДоговор = &ПенсионныйДоговор ";
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ПодразделениеОрганизации) Тогда
		ТекстУсловия = ТекстУсловия+" И ВТ_Поступления.Подразделение В ИЕРАРХИИ (&Подразделение) ";
	КонецЕсли; 
	Если ЗначениеЗаполнено(Объект.Схема) Тогда
		ТекстУсловия = ТекстУсловия+" И уп_СхемаСчетаСрезПоследних.Схема = &Схема ";
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_ПоступленияКПерерасчетуОстатки.НомерСчета КАК НомерСчета,
	|	уп_ПоступленияКПерерасчетуОстатки.СуммаОстаток КАК СуммаОстаток,
	|	уп_ПоступленияКПерерасчетуОстатки.НомерСчета.Владелец КАК ПенсионныйДоговор,
	|	уп_ПоступленияКПерерасчетуОстатки.НомерСчета.Владелец.ПодразделениеБУ КАК Подразделение
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	РегистрНакопления.уп_ПоступленияКПерерасчету.Остатки(&ДатаДок, Организация = &Организация) КАК уп_ПоступленияКПерерасчетуОстатки
	|ГДЕ
	|	уп_ПоступленияКПерерасчетуОстатки.ТипПоступления = &ТипПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
#Вставка
//++https://redmine.npf.com/issues/4274
	|ВЫБРАТЬ
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	МАКСИМУМ(ЕСТЬNULL(хм_СрезСчетчик.Количество, 0)) + СУММА(ВЫБОР
	|			КОГДА хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|				ТОГДА ЕСТЬNULL(уп_СчетчикНачислений.Количество, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаДок,НомерСчета В (ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК хм_СрезСчетчик
	|		ПО (уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета)
	|			И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния 
	|			И хм_СрезСчетчик.ДатаУстановкиСостояния  >= &ДатаНачВремен	
	|		ГДЕ уп_СчетчикНачислений.Период <= &ДатаДок 
	|			И уп_СчетчикНачислений.Период >= &ДатаНачВремен
	|			И уп_СчетчикНачислений.НомерСчета В (ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)
	|		СГРУППИРОВАТЬ ПО уп_СчетчикНачислений.НомерСчета
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
//--https://redmine.npf.com/issues/4274
#КонецВставки
	|ВЫБРАТЬ
	|	ВТ_Поступления.НомерСчета КАК НомерСчета,
	|	ВТ_Поступления.СуммаОстаток КАК СуммаВзноса,
	|	уп_ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат КАК ПериодичностьВыплат,
	|	уп_СостоянияПССрезПоследних.Состояние КАК Состояние,
	|	уп_СхемаСчетаСрезПоследних.Схема КАК Схема,
	|	уп_СхемаСчетаСрезПоследних.ТипПенсии КАК ТипПенсии,
	|	ВЫБОР
	|		КОГДА уп_СхемаСчетаСрезПоследних.Схема.ТипПенсионнойСхемы = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|				ИЛИ уп_СхемаСчетаСрезПоследних.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.СрочныхВыплат)
	|			ТОГДА ЕСТЬNULL(уп_КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) - ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоОсталось,
	|	ЕСТЬNULL(уп_РазмерПенсииСрезПоследних.Размер, 0) КАК РазмерВыплаты,
	|	ВТ_Поступления.ПенсионныйДоговор КАК ПенсионныйДоговор,
	|	ВТ_Поступления.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
	|			&ДатаДок,
	|			НомерСчета В
	|				(ВЫБРАТЬ
	|					ВТ_Поступления.НомерСчета
	|				ИЗ
	|					ВТ_Поступления)) КАК уп_СостоянияПССрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Поступления КАК ВТ_Поступления
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = ВТ_Поступления.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(
	|				&ДатаДок,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК уп_СхемаСчетаСрезПоследних
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = уп_СхемаСчетаСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоНазначенныхВыплат.СрезПоследних(
	|				&ДатаДок,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК уп_КоличествоНазначенныхВыплатСрезПоследних
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = уп_КоличествоНазначенныхВыплатСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(
	|				&ДатаДок,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК уп_ПериодичностьВыплатСрезПоследних
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = уп_ПериодичностьВыплатСрезПоследних.НомерСчета
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(
	|				&ДатаНачВремен,
	|				&ДатаДок,
	|				Период,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсии.СрезПоследних(
	|				&ДатаДок,
	|				НомерСчета В
	|					(ВЫБРАТЬ
	|						ВТ_Поступления.НомерСчета
	|					ИЗ
	|						ВТ_Поступления)) КАК уп_РазмерПенсииСрезПоследних
	|		ПО уп_СостоянияПССрезПоследних.НомерСчета = уп_РазмерПенсииСрезПоследних.НомерСчета
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.Состояние В(&СписокВыплатныхСостояний)"+ТекстУсловия+" ";

	Запрос.УстановитьПараметр("ДатаДок", Объект.Дата);
	Запрос.УстановитьПараметр("ДатаНачВремен", '19800101');
	Запрос.УстановитьПараметр("Организация", объект.Организация); 
	СписокВыплатныхСостояний = Новый СписокЗначений;
	СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод); 
	СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены); 
	СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены); 
	Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
	Запрос.УстановитьПараметр("ПенсионныйДоговор", Объект.ПенсионныйДоговор);
	Запрос.УстановитьПараметр("Подразделение", Объект.ПодразделениеОрганизации); 
	Запрос.УстановитьПараметр("Схема", Объект.Схема);
	Запрос.УстановитьПараметр("ТипПоступления", Объект.ВидОперации);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовСтр = Объект.СписокСчетов.Добавить();   
		ЗаполнитьЗначенияСвойств(НовСтр, ВыборкаДетальныеЗаписи); 
		Если Не ЗначениеЗаполнено(НовСтр.ТипПенсии) Тогда
			НовСтр.ТипПенсии = ВыборкаДетальныеЗаписи.Схема.ТипПенсионнойСхемы;	 
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры
