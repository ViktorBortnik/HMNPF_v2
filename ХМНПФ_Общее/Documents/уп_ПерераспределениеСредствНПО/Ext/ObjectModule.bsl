
&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура НПФ_ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	
	ПроверкаКорректностиЗаполнения(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивСообщений = Новый СписокЗначений;   
	ПроверитьНаличиеЗакрытыхПС(МассивСообщений, Отказ);
	
	Движения.уп_Резервы.Записывать = Истина;
	Движения.уп_Резервы.Очистить();
	Движения.уп_Резервы.Записывать = Истина;
	Движения.уп_Резервы.Очистить();
	Движения.уп_АналитикаРПАР.Записывать = Истина;
	Движения.уп_АналитикаРПАР.Очистить(); 
	Движения.уп_АналитикаСтраховогоРезерва.Записывать = Истина;
	Движения.уп_АналитикаСтраховогоРезерва.Очистить(); 
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
		Движения.Хозрасчетный.Записывать=Истина;
	КонецЕсли;
	
	мПеремещениеИзСтрахового=Перечисления.уп_ВидыДвижений.ПеремещениеИзСтраховогоРезерва;
	мСтраховой=Перечисления.уп_ТипыРезерва.Страховой;
	мПеремещениеВСтраховой=Перечисления.уп_ВидыДвижений.ПеремещениеВСтраховойРезерв;
	
	ДатаНачалаУчетаСРСхемы = Константы.уп_ДатаНачалаВеденияСтраховогоРезерваПоСхемам.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаУчетаСРСхемы) И ДатаНачалаУчетаСРСхемы<=Дата Тогда
		УчетСРПоСхемам = Истина;
	Иначе
		УчетСРПоСхемам = Ложь;
	КонецЕсли;
	ДатаНачалаВеденияУчетаПоСхемам = Константы.уп_УчетРезерваНПОвРазрезеСхем.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаВеденияУчетаПоСхемам) И ДатаНачалаВеденияУчетаПоСхемам<=Дата Тогда
		ВестиУчетПоСхемам = Истина;
	Иначе
		ВестиУчетПоСхемам = Ложь;
	КонецЕсли;
	
	Если ТипРезерваИ = Перечисления.уп_ТипыРезерва.Страховой И ТипРезерваП = Перечисления.уп_ТипыРезерва.Страховой Тогда
		#Область Оба_типа_резервов_страховые
		// Не имеет смысла
		Сообщить("Такое перераспраспределение не производится.");
		Возврат;
		#КонецОбласти
	ИначеЕсли ТипРезерваИ = Перечисления.уп_ТипыРезерва.РПАР И ТипРезерваП = Перечисления.уп_ТипыРезерва.РПАР Тогда
		Сообщить("Такое перераспраспределение не производится.");
		Возврат;

	ИначеЕсли ТипРезерваИ = Перечисления.уп_ТипыРезерва.Страховой 
		Или ТипРезерваИ = Перечисления.уп_ТипыРезерва.РПАР Тогда
		#Область Тип_резерва_источника_страховой
		
				
		#Область Текст_запроса_и_параметры
		Запрос=Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	уп_ПерераспределениеСредствНПОПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОПриемник.Схема КАК Схема,
		|	уп_ПерераспределениеСредствНПОПриемник.ТипСуммы КАК ТипСуммы,
		|	СУММА(уп_ПерераспределениеСредствНПОПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПриемник
		|ИЗ
		|	Документ.уп_ПерераспределениеСредствНПО.Приемник КАК уп_ПерераспределениеСредствНПОПриемник
		|ГДЕ
		|	уп_ПерераспределениеСредствНПОПриемник.Ссылка = &Ссылка
		|	И уп_ПерераспределениеСредствНПОПриемник.Сумма > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПерераспределениеСредствНПОПриемник.ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОПриемник.Схема,
		|	уп_ПерераспределениеСредствНПОПриемник.ТипСуммы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&Момент,
		|			Организация = &Организация
		|				И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.Страховой)) КАК уп_РезервыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
		|	СостоянияПССрезПоследних.Состояние КАК Состояние,
		|	ЕСТЬNULL(СтартовыеСуммыСрезПоследних.Сумма, 0) КАК СтартоваяСумма,
		|	СхемаСчетаСрезПоследних.Схема КАК Схема
		|ПОМЕСТИТЬ мСостояния
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					мПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПриемник)) КАК СостоянияПССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СтартовыеСуммы.СрезПоследних(
		|				&ДатаСтартовойСуммы,
		|				НомерСчета В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						мПриемник.ПенсионныйСчет
		|					ИЗ
		|						мПриемник)) КАК СтартовыеСуммыСрезПоследних
		|		ПО СостоянияПССрезПоследних.НомерСчета = СтартовыеСуммыСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(
		|				&Момент,
		|				НомерСчета В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						мПриемник.ПенсионныйСчет
		|					ИЗ
		|						мПриемник)) КАК СхемаСчетаСрезПоследних
		|		ПО СостоянияПССрезПоследних.НомерСчета = СхемаСчетаСрезПоследних.НомерСчета
		|ГДЕ
		|	СостоянияПССрезПоследних.Состояние В(&СписокВыплатныхСостояний)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПриемник.Схема КАК Схема,
		|	мПриемник.ТипСуммы КАК ТипСуммы,
		|	мПриемник.Сумма КАК Сумма,
		|	мСостояния.Состояние КАК Состояние,
		|	мСостояния.СтартоваяСумма КАК СтартоваяСумма,
		|	мСостояния.Схема КАК СхемаСостояние,
		|	мПриемник.ПенсионныйСчет.Владелец КАК ПенсионныйСчетВладелец
		|ИЗ
		|	мПриемник КАК мПриемник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостояния КАК мСостояния
		|		ПО мПриемник.ПенсионныйСчет = мСостояния.НомерСчета
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПриемник.ПенсионныйСчет.Владелец КАК ПенсионныйСчетВладелец,
		|	СУММА(мПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПенсионныеПриемник
		|ИЗ
		|	мПриемник КАК мПриемник
		|
		|СГРУППИРОВАТЬ ПО
		|	мПриемник.ПенсионныйСчет,
		|	мПриемник.ПенсионныйСчет.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостоянияПССрезПоследних.Состояние КАК Состояние,
		|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета
		|ПОМЕСТИТЬ мСостоянияП
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					мПенсионныеПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПенсионныеПриемник)) КАК уп_СостоянияПССрезПоследних
		|ГДЕ
		|	уп_СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ОжиданиеВзносов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета КАК НомерСчета,
		|	уп_ВидПенсионногоСчетаСрезПоследних.ВидПенсионногоСчета КАК ВидПенсионногоСчета
		|ПОМЕСТИТЬ мВидПенсионногоСчета
		|ИЗ
		|	РегистрСведений.уп_ВидПенсионногоСчета.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					мПенсионныеПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПенсионныеПриемник)) КАК уп_ВидПенсионногоСчетаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПенсионныеПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПенсионныеПриемник.Сумма КАК Сумма,
		|	ЕСТЬNULL(мСостояния.СтартоваяСумма, 0) КАК СтартоваяСумма,
		|	мСостоянияП.Состояние КАК Состояние,
		|	мВидПенсионногоСчета.ВидПенсионногоСчета КАК ВидПенсионногоСчета,
		|	мПенсионныеПриемник.ПенсионныйСчет.Участник.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостояния КАК мСостояния
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мСостояния.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостоянияП КАК мСостоянияП
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мСостоянияП.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ мВидПенсионногоСчета КАК мВидПенсионногоСчета
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мВидПенсионногоСчета.НомерСчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец КАК ПДП,
		|	СУММА(мПенсионныеПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПДП
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник
		|
		|СГРУППИРОВАТЬ ПО
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостоянияДоговораСрезПоследних.Состояние КАК Состояние,
		|	мПДП.Сумма КАК Сумма,
		|	мПДП.ПДП КАК ПДП
		|ИЗ
		|	мПДП КАК мПДП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговора.СрезПоследних(
		|				&Момент,
		|				ПенсионныйДоговор В
		|					(ВЫБРАТЬ
		|						мПДП.ПДП
		|					ИЗ
		|						мПДП КАК мПДП)) КАК уп_СостоянияДоговораСрезПоследних
		|		ПО мПДП.ПДП = уп_СостоянияДоговораСрезПоследних.ПенсионныйДоговор
		|ГДЕ
		|	уп_СостоянияДоговораСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговора.Открыт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.ТипСуммы КАК ТипСуммы,
		|	уп_РезервыОстатки.СуммаОстаток КАК СуммаПоТипу,
		|	ВЫБОР
		|		КОГДА уп_РезервыОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.Резерв)
		|			ТОГДА 1
		|		КОГДА уп_РезервыОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДНАРезерв)
		|			ТОГДА 2
		|		ИНАЧЕ уп_РезервыОстатки.ТипСуммы.Код
		|	КОНЕЦ КАК Порядок,
		|	уп_РезервыОстатки.ПенсионнаяСхема КАК ПенсионнаяСхема
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&Момент,
		|			Организация = &Организация
		|				И ТипРезерва = &ТипРезерваИ) КАК уп_РезервыОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок
		|ИТОГИ
		|	СУММА(СуммаПоТипу)
		|ПО
		|	ПенсионнаяСхема
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.ПенсионнаяСхема КАК ПенсионнаяСхема,
		|	уп_РезервыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&Момент,
		|			Организация = &Организация
		|				И ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезерва.Страховой)) КАК уп_РезервыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	мПенсионныеПриемник.ПенсионныйСчет КАК НомерСчета,
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец КАК ПенсионныйДоговор
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник";
		
		Запрос.УстановитьПараметр("ТипРезерваИ",ТипРезерваИ);
		Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("Момент",МоментВремени()); 
		#Вставка
 		//https://redmine.npf.com/issues/4156
 		Если ДополнительныеСвойства.Свойство("КасперЗагрузка") Тогда
			Если Не ДополнительныеСвойства.КасперЗагрузка.Контекст.Структура_Реквизиты.ТЗ.Колонки.Найти("ДатаОснования") = Неопределено Тогда
				ДатаОснования = ДополнительныеСвойства.КасперЗагрузка.СтрокаТЗ.ДатаОснования;	
				Если Не НачалоДня(ДатаОснования) = НачалоДня(Дата) Тогда
					Запрос.УстановитьПараметр("Момент",Новый МоментВремени(ДатаОснования));
					Запрос.УстановитьПараметр("ДатаСтартовойСуммы", ДатаОснования-1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецВставки
		Запрос.УстановитьПараметр("ДатаСтартовойСуммы", Дата-1);
		СписокВыплатныхСостояний = Новый СписокЗначений;
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены);
		Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
		
		#КонецОбласти
		
		Результат=Запрос.ВыполнитьПакет();
		ВыборкаПриемникИтог=Результат[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
		#Область Проверяем_есть_ли_данные_в_табличных_частях_документа
		Если НЕ ВыборкаПриемникИтог.Следующий() Тогда
			Сообщить("Не заполнен приемник");
			Отказ=Истина;
		КонецЕсли; 
		
		Если ТипРезерваИ = Перечисления.уп_ТипыРезерва.РПАР Тогда
			
			СоотвПенсСхемы = Новый Соответствие;
			
			ВыборкаИсточникПоСхемам = Результат[10].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаИсточникПоСхемам.Следующий() Цикл
				ДанныеДляСхем = Новый Структура;
				ДанныеДляСхем.Вставить("Сумма", ВыборкаИсточникПоСхемам.СуммаПоТипу);
				ИсточникПоТипамСуммВыборка = ВыборкаИсточникПоСхемам.Выбрать();
				ИсточникПоТипамСумм = Новый ТаблицаЗначений;
				ИсточникПоТипамСумм.Колонки.Добавить("ТипСуммы");
				ИсточникПоТипамСумм.Колонки.Добавить("СуммаПоТипу");
				ИсточникПоТипамСумм.Колонки.Добавить("Порядок");
				ИсточникПоТипамСумм.Колонки.Добавить("ПенсионнаяСхема");
				Пока ИсточникПоТипамСуммВыборка.Следующий() Цикл
					СтрокаТЗ = ИсточникПоТипамСумм.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТЗ, ИсточникПоТипамСуммВыборка);
				КонецЦикла;
				ДанныеДляСхем.Вставить("ИсточникПоТипамСумм", ИсточникПоТипамСумм);
				СоотвПенсСхемы.Вставить(ВыборкаИсточникПоСхемам.ПенсионнаяСхема, ДанныеДляСхем);
			КонецЦикла;	
			
			ПриемникТЗ = Приемник.Выгрузить();
			ПриемникТЗ.Свернуть("Схема", "Сумма");
			
			Для Каждого СтрокаТЗ Из ПриемникТЗ Цикл
				
				ЭлСоотв = СоотвПенсСхемы[СтрокаТЗ.Схема];
				
				Если ЭлСоотв = Неопределено Или ЭлСоотв.Сумма < СтрокаТЗ.Сумма Тогда
					Отказ = Истина;
					Сообщить("В источнике РПАР недостаточно средств по схеме " + СтрокаТЗ.Схема + ". Доступно " + ?(ЭлСоотв = Неопределено, "0", Строка(ЭлСоотв.Сумма)) + ". Указано для списания " + СтрокаТЗ.Сумма);
				КонецЕсли;	
			КонецЦикла;
			
		ИначеЕсли ТипРезерваИ = Перечисления.уп_ТипыРезерва.Страховой Тогда
			
			Если УчетСРПоСхемам Тогда
				
				СоотвПенсСхемыСР = Новый Соответствие;
				
				ВыборкаИсточникПоСхемамСР = Результат[11].Выбрать();
				
				Пока ВыборкаИсточникПоСхемамСР.Следующий() Цикл
					
					СоотвПенсСхемыСР.Вставить(ВыборкаИсточникПоСхемамСР.ПенсионнаяСхема, ВыборкаИсточникПоСхемамСР.Сумма); 
					
				КонецЦикла;
				
				ПриемникТЗ = Приемник.Выгрузить();
				ПриемникТЗ.Свернуть("Схема", "Сумма");
				
				Для Каждого СтрокаТЗ Из ПриемникТЗ Цикл
					
					ЭлСоотв = СоотвПенсСхемыСР[СтрокаТЗ.Схема];
					
					Если ЭлСоотв = Неопределено Или ЭлСоотв < СтрокаТЗ.Сумма Тогда
						Отказ = Истина;
						Сообщить("В источнике Страховой недостаточно средств по схеме " + СтрокаТЗ.Схема + ". Доступно " + ?(ЭлСоотв = Неопределено, "0", Строка(ЭлСоотв)) + ". Указано для списания " + СтрокаТЗ.Сумма);
					КонецЕсли;	
				КонецЦикла;
				
			Иначе
				ВыборкаИсточник = Результат[1].Выбрать();
				Если ВыборкаИсточник.Следующий() Тогда
					НужноСписать = ВыборкаИсточник.Сумма;
					ВыборкаПриемникИтог.Сбросить();
					Если ВыборкаПриемникИтог.Следующий() Тогда
						Если НужноСписать < ВыборкаПриемникИтог.Сумма Тогда
							Отказ = Истина;
							Сообщить("В источнике Страховой недостаточно средств. Доступно всего " + НужноСписать + ". Указано для списания всего " + ВыборкаПриемникИтог.Сумма);
						КонецЕсли;	
					КонецЕсли;
				Иначе
					Сообщить("В источнике нет остатков");
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
		
		Если ВыборкаПриемникИтог.Сумма=0 Тогда
			Отказ=Истина;
			Сообщить("Суммы в приемнике не заполнены");
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		#КонецОбласти
		
		ВыборкаПриемник=ВыборкаПриемникИтог.Выбрать();
		
		Если ТипРезерваИ=Перечисления.уп_ТипыРезерва.РПАР Тогда
			мВидДвижения = Перечисления.уп_ВидыДвижений.ПеремещениеИзРПАР;
		Иначе
			мВидДвижения = Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва;
		КонецЕсли;
		
		Если ТипРезерваИ = Перечисления.уп_ТипыРезерва.РПАР Тогда
			ВидДвиженияЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияЕПС(ТипРезерваИ, ТипРезерваП);
			
		#Вставка
 		//https://redmine.npf.com/issues/4156
			Для Каждого СтрокаТЗ Из Приемник Цикл // цикл по схемам
		#КонецВставки
		#Удаление
			Для Каждого СтрокаТЗ Из ПриемникТЗ Цикл // цикл по схемам
		#КонецУдаления
				НужноСписать = СтрокаТЗ.Сумма;
				
				ЭлСоотв = СоотвПенсСхемы[СтрокаТЗ.Схема];
				
				Для Каждого ВыборкаИсточникПоТипамСумм Из ЭлСоотв.ИсточникПоТипамСумм Цикл // цикл по типам сумм 
					// алгоритм списания
					Движение = Движения.уп_Резервы.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.НомерСчета = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
					#Вставка
			 		//https://redmine.npf.com/issues/4156
					Движение.ПенсионныйСчетИсточник = СтрокаТЗ.ПенсионныйСчет;
					#КонецВставки
					Движение.ТипРезерва = ТипРезерваИ;
					Движение.ТипСуммы = ВыборкаИсточникПоТипамСумм.ТипСуммы;
					Движение.Движение = мВидДвижения;
					Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
					Движение.ДокументРегистрации = ДокОсн;
					Движение.Схема = СтрокаТЗ.Схема;
					Движение.ПенсионнаяСхема = СтрокаТЗ.Схема;
					
					Если ВыборкаИсточникПоТипамСумм.СуммаПоТипу >= НужноСписать Тогда
						Движение.Сумма = НужноСписать;
						Прервать;
					Иначе
						Движение.Сумма = ВыборкаИсточникПоТипамСумм.СуммаПоТипу;
						НужноСписать = НужноСписать - ВыборкаИсточникПоТипамСумм.СуммаПоТипу;
					КонецЕсли;
					Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
					
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		// Расписываем суммы из приемника по указанным в источнике суммам 
		Пока ВыборкаПриемник.Следующий() Цикл
			#Область Формируем_реквизиты_движений_регистра
			Списать=ВыборкаПриемник.Сумма;
						
			НомерСчетаП=ВыборкаПриемник.ПенсионныйСчет;
			ТипСуммыП_Ит = ВыборкаПриемник.ТипСуммы;
			Если ВыборкаПриемник.СхемаСостояние<>NULL Тогда
				КонТипРезерва = уп_ОбщегоНазначенияУчетРезервов.ОпределитьКонечныйТипРезерва(ВыборкаПриемник.СхемаСостояние,ТипРезерваП);
				Если (КонТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат) Тогда
					ПорядокВеденияПожизненногоРезерва = уп_НПФ.ОпределитьПорядокВеденияПожизненногоРезерва(ВыборкаПриемник.СхемаСостояние, Дата);
					Если ПорядокВеденияПожизненногоРезерва = Перечисления.уп_РезервПожизненныхВыплат.ПоДоговорам Тогда
						НомерСчетаП = уп_ОбщегоНазначенияУчетРезервов.ОпределитьНомерПожизненногоСчетаДоговора(ВыборкаПриемник.ПенсионныйСчетВладелец, ЭтотОбъект
						, Истина, ВыборкаПриемник.СхемаСостояние);
					ИначеЕсли ПорядокВеденияПожизненногоРезерва = Перечисления.уп_РезервПожизненныхВыплат.БезАналитики Тогда
						НомерСчетаП = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
						ТипСуммыП_Ит = Справочники.уп_ТипыСумм.ПустаяСсылка();
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				КонТипРезерва = ТипРезерваП;
			КонецЕсли;
			#КонецОбласти
			
			#Область Формируем_движения
			
			Если ТипРезерваИ = Перечисления.уп_ТипыРезерва.РПАР Тогда
				Движение = Движения.уп_АналитикаРПАР.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = НомерСчетаП;
				Движение.ТипРезерва = КонТипРезерва;
				Движение.ТипСуммы = ТипСуммыП_Ит;
				Движение.Сумма = Списать;
				Движение.ПенсионнаяСхема = ВыборкаПриемник.Схема;
			Иначе
				
				ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(ТипРезерваИ, КонТипРезерва);
				Движение = Движения.уп_Резервы.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
				#Вставка
		 		//https://redmine.npf.com/issues/4156
				Движение.ПенсионныйСчетИсточник = ВыборкаПриемник.ПенсионныйСчет;
				#КонецВставки
				Движение.ТипРезерва = ТипРезерваИ;
				Движение.ТипСуммы = Справочники.уп_ТипыСумм.ПустаяСсылка();
				Движение.Сумма = Списать;
				Движение.Движение = мВидДвижения;
				Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
				Движение.ДокументРегистрации = ДокОсн;
				Движение.Схема = ВыборкаПриемник.Схема;
				Если (ТипРезерваИ = Перечисления.уп_ТипыРезерва.Страховой И УчетСРПоСхемам) Тогда
					Движение.ПенсионнаяСхема = ВыборкаПриемник.Схема;
				КонецЕсли; 
				Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
				
				Если ТипРезерваИ = Перечисления.уп_ТипыРезерва.Страховой Тогда
					Движение = Движения.уп_АналитикаСтраховогоРезерва.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Организация = Организация; 
					Движение.Сумма = Списать;
					Движение.Расшифровка = АналитикаСРРасход; 
					Движение.ВидДвиженияСР = АналитикаСРРасход.ВидДвижения; 
					Движение.НомерСчета = НомерСчетаП;
                 КонецЕсли;
			КонецЕсли; 
			
			#Область Приход
			Движение = Движения.уп_Резервы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.НомерСчета = НомерСчетаП;
			Движение.ТипРезерва = КонТипРезерва;
			Движение.ТипСуммы = ТипСуммыП_Ит;
			Движение.Сумма = Списать;
			Движение.Движение = мВидДвижения;
			Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
			Движение.ДокументРегистрации = ДокОсн;
			Движение.Схема = ВыборкаПриемник.Схема;
			Если  ВестиУчетПоСхемам Тогда
				Движение.ПенсионнаяСхема = ВыборкаПриемник.Схема;
			КонецЕсли; 
			Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
			#КонецОбласти
			
			#КонецОбласти
			
#Вставка
		КонецЦикла;
#КонецВставки
					
		#Область Регистры_сведений 
		Движения.уп_СтартовыеСуммы.Записывать=Истина;
		Движения.уп_СостоянияПС.Записывать=Истина;
		Движения.уп_ВидПенсионногоСчета.Записывать=Истина;
		
		Выборка=Результат[7].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.СтартоваяСумма<>0 Тогда
				Движение = Движения.уп_СтартовыеСуммы.Добавить();
				Движение.Период = Дата;
				Движение.НомерСчета = Выборка.ПенсионныйСчет;
				Движение.Сумма = Выборка.СтартоваяСумма+Выборка.Сумма;
			КонецЕсли;
			Если Выборка.Состояние=Перечисления.уп_СостоянияПС.ОжиданиеВзносов Тогда
				СостояниеПС=Движения.уп_СостоянияПС.Добавить();
				СостояниеПС.Период=Дата;
				СостояниеПС.НомерСчета=Выборка.ПенсионныйСчет;
				СостояниеПС.Состояние=Перечисления.уп_СостоянияПС.НакопительныйПериод;
			КонецЕсли;
			Если ТипРезерваП=Перечисления.уп_ТипыРезерва.ИПС И (Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.БУС 
			ИЛИ Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.УК) Тогда
				ВидПенсионногоСчета=Движения.уп_ВидПенсионногоСчета.Добавить();
				ВидПенсионногоСчета.Период=Дата;
				ВидПенсионногоСчета.НомерСчета=Выборка.ПенсионныйСчет;
				ВидПенсионногоСчета.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.ИПС;
			КонецЕсли;
			Если ТипРезерваП=Перечисления.уп_ТипыРезерва.СПС И (Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.БУС 
			И Выборка.ЮридическоеФизическоеЛицо=Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо) Тогда
				ВидПенсионногоСчета=Движения.уп_ВидПенсионногоСчета.Добавить();
				ВидПенсионногоСчета.Период=Дата;
				ВидПенсионногоСчета.НомерСчета=Выборка.ПенсионныйСчет;
				ВидПенсионногоСчета.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.УК;
			КонецЕсли;
		КонецЦикла;
		
		Движения.уп_СостоянияДоговора.Записывать=Истина;
		Выборка=Результат[9].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Состояние=Перечисления.уп_СостоянияДоговора.Открыт Тогда
				СостояниеДоговора=Движения.уп_СостоянияДоговора.Добавить();
				СостояниеДоговора.Период=Дата;
				СостояниеДоговора.ПенсионныйДоговор=Выборка.ПДП;
				СостояниеДоговора.Состояние=Перечисления.уп_СостоянияДоговора.Действует;
			КонецЕсли;
		КонецЦикла; 
		
		ТаблицаДокумента = Результат[12].Выгрузить();
		Движения.уп_ДатаФиксацииОбязательствНПО.Записывать=Истина;
		Движения.уп_ПериодыГарантийногоВосполненияНПО.Записывать = Истина;
		уп_СистемаГарантированияНПО.ПолучитьДатыФиксацииОбязательствПоТЧ(ЭтотОбъект, ТаблицаДокумента);
		#КонецОбласти

		#Область Движения_БухУчет
		Если ОтражатьВБухгалтерскомУчете Тогда
					
			//СхемаСтарая = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", Справочники.уп_ПенсионныеСчета.ПустаяСсылка())).Схема;
			
			//мВидОперацииБух=?(ТипРезерваИ=мСтраховой,мПеремещениеИзСтрахового,?(КонТипРезерва=мСтраховой,мПеремещениеВСтраховой,мВидДвижения));
			//мВидОперацииБух=мВидДвижения;
			
			//ДобавитьстрокуВТабБухУчет(ТЗ, Дата, Организация, ВидОперации, КонтрагентДт, ДоговорДт, СхемаДт, ПризнакДт, КонтрагентКт
			//, ДоговорКт, СхемаКт, ПризнакКт, Сумма)
			
			уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация
			,мПеремещениеИзСтрахового
			,Справочники.ДоговорыКонтрагентов.ПустаяСсылка()
			, Справочники.ДоговорыКонтрагентов.ПустаяСсылка()
			, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка()
			,(ТипРезерваИ=Перечисления.уп_ТипыРезерва.ПожизненныхВыплат)
			, НомерСчетаП.Владелец.Вкладчик
			,НомерСчетаП.Владелец.ДоговорКонтрагента
			, НомерСчетаП.Владелец.Схема
			,(КонТипРезерва=Перечисления.уп_ТипыРезерва.ПожизненныхВыплат)
			,Списать);
		КонецЕсли;
		#КонецОбласти
		
#Удаление
		КонецЦикла;
#КонецУдаления
				
		#КонецОбласти
	ИначеЕсли ТипРезерваП = Перечисления.уп_ТипыРезерва.Страховой 
		ИЛИ ТипРезерваП = Перечисления.уп_ТипыРезерва.РПАР Тогда
		
		#Область Тип_резерва_приемника_страховой
		ПроверитьИсточникНаСПТ(Отказ);
		#Область Текст_запроса_и_параметры
		Запрос=Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОИсточник.Схема,
		|	СУММА(уп_ПерераспределениеСредствНПОИсточник.Сумма) КАК Сумма,
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет.Представление КАК ПФНазвание
		|ПОМЕСТИТЬ мИсточник
		|ИЗ
		|	Документ.уп_ПерераспределениеСредствНПО.Источник КАК уп_ПерераспределениеСредствНПОИсточник
		|ГДЕ
		|	уп_ПерераспределениеСредствНПОИсточник.Ссылка = &Ссылка
		|	И уп_ПерераспределениеСредствНПОИсточник.Сумма > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОИсточник.Схема,
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет.Представление
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.НомерСчета,
		|	уп_РезервыОстатки.ТипСуммы,
		|	уп_РезервыОстатки.СуммаОстаток
		|ПОМЕСТИТЬ мОстатки
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&Момент,
		|			Организация = &Организация
		|				И ТипРезерва = &ТипРезерва
		|				И НомерСчета В
		|					(ВЫБРАТЬ
		|						мИсточник.ПенсионныйСчет
		|					ИЗ
		|						мИсточник)) КАК уп_РезервыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мИсточник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мИсточник.Схема,
		|	ЕСТЬNULL(мОстатки.ТипСуммы, ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ПустаяСсылка)) КАК ТипСуммы,
		|	ЕСТЬNULL(мОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	мИсточник.Сумма КАК Сумма,
		|	мИсточник.ПФНазвание КАК ПФНазвание
		|ИЗ
		|	мИсточник КАК мИсточник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мОстатки КАК мОстатки
		|		ПО мИсточник.ПенсионныйСчет = мОстатки.НомерСчета
		|ИТОГИ
		|	СУММА(СуммаОстаток),
		|	МАКСИМУМ(Сумма),
		|	МАКСИМУМ(ПФНазвание)
		|ПО
		|	ОБЩИЕ,
		|	ПенсионныйСчет";
		
		Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("Момент",МоментВремени());
		Запрос.УстановитьПараметр("ТипРезерва",ТипРезерваИ);
		
		#Вставка
 		//https://redmine.npf.com/issues/4156
 		Если ДополнительныеСвойства.Свойство("КасперЗагрузка") Тогда
			Если Не ДополнительныеСвойства.КасперЗагрузка.Контекст.Структура_Реквизиты.ТЗ.Колонки.Найти("ДатаОснования") = Неопределено Тогда
				ДатаОснования = ДополнительныеСвойства.КасперЗагрузка.СтрокаТЗ.ДатаОснования;
				Если Не НачалоДня(ДатаОснования) = НачалоДня(Дата) Тогда
					Запрос.УстановитьПараметр("Момент",Новый МоментВремени(ДатаОснования));
					Запрос.УстановитьПараметр("ДатаСтартовойСуммы", ДатаОснования-1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецВставки
		#КонецОбласти
		
		ВыборкаИсточникИтог=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		#Область Проверяем_есть_ли_данные_в_табличных_частях_документа
		Если Не ВыборкаИсточникИтог.Следующий() Тогда
			Отказ=Истина;
			Сообщить("В источнике нет данных");
			Возврат;
		КонецЕсли;
		
		Если ВыборкаИсточникИтог.Сумма=0 Тогда
			Отказ=Истина;
			Сообщить("В источнике нет сумм отличных от нуля");
			Возврат;
		КонецЕсли;
		
		#КонецОбласти
				
		ВыборкаИсточник=ВыборкаИсточникИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		#Область Суммы_указанные_в_источнике_должны_быть_не_больше_чем_есть_на_остатках
		Пока ВыборкаИсточник.Следующий() Цикл
			Если ВыборкаИсточник.Сумма>ВыборкаИсточник.СуммаОстаток Тогда
				Сообщить("Для пенсионного счета "+ВыборкаИсточник.ПФНазвание+" указана сумма к распределению большая чем есть на остатках.");
				Отказ=Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		#КонецОбласти
		
		ВыборкаИсточник.Сбросить();
		
		Если ТипРезерваП=Перечисления.уп_ТипыРезерва.РПАР Тогда
			мВидДвижения = Перечисления.уп_ВидыДвижений.ПеремещениеВРПАР;
		Иначе
			мВидДвижения = Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва;
		КонецЕсли;
		
		// Расписываем суммы из приемника по указанным в источнике суммам 
		Пока ВыборкаИсточник.Следующий() Цикл
			ВыборкаПФ=ВыборкаИсточник.Выбрать();
			СуммаПоСтроке=ВыборкаИсточник.Сумма;
			Пока ВыборкаПФ.Следующий() И СуммаПоСтроке>0  Цикл
				
				Списать=Мин(ВыборкаПФ.СуммаОстаток,СуммаПоСтроке);
				СуммаПоСтроке=СуммаПоСтроке-Списать;
				
				#Область Формируем_движения
				
				Если ТипРезерваП=Перечисления.уп_ТипыРезерва.РПАР Тогда
					ВидДвиженияЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияЕПС(ТипРезерваИ, ТипРезерваП);
				Иначе
					ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(ТипРезерваИ, ТипРезерваП);
				КонецЕсли;
				#Область Расход
				Движение = Движения.уп_Резервы.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = ВыборкаПФ.ПенсионныйСчет;
				#Вставка
				//https://redmine.npf.com/issues/4156
				Движение.ПенсионныйСчетИсточник = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
				#КонецВставки
				Движение.ТипРезерва = ТипРезерваИ;
				Движение.ТипСуммы = ВыборкаПФ.ТипСуммы;
				Движение.Сумма = Списать;
				Движение.Движение = мВидДвижения;
				Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
				Движение.ДокументРегистрации = ДокОсн;
				Движение.Схема = ВыборкаПФ.Схема;
				Если  ВестиУчетПоСхемам Тогда
					Движение.ПенсионнаяСхема = ВыборкаПФ.Схема;
				КонецЕсли; 
				Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
				#КонецОбласти
				
				#Область Приход
				
				Движение = Движения.уп_Резервы.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();   
				#Вставка
				//https://redmine.npf.com/issues/4156
				Движение.ПенсионныйСчетИсточник = ВыборкаПФ.ПенсионныйСчет;
				#КонецВставки
				Движение.ТипРезерва = ТипРезерваП;
				Если ТипРезерваП = Перечисления.уп_ТипыРезерва.РПАР Тогда
					Движение.ТипСуммы = Справочники.уп_ТипыСумм.Резерв;
				Иначе
					Движение.ТипСуммы = Справочники.уп_ТипыСумм.ПустаяСсылка();
				КонецЕсли;
				Движение.Сумма = Списать;   
				Движение.Движение = мВидДвижения;    
				Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;  
				Движение.ДокументРегистрации = ДокОсн;
				Если (ТипРезерваП = Перечисления.уп_ТипыРезерва.Страховой И УчетСРПоСхемам) 
					ИЛИ ТипРезерваП = Перечисления.уп_ТипыРезерва.РПАР Тогда
					Движение.ПенсионнаяСхема = ВыборкаПФ.Схема;
				КонецЕсли;
				Движение.Схема = ВыборкаПФ.Схема;
				Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
				
				Если ТипРезерваП = Перечисления.уп_ТипыРезерва.РПАР Тогда
					Движение = Движения.уп_АналитикаРПАР.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.НомерСчета = ВыборкаПФ.ПенсионныйСчет;
					Движение.ТипРезерва = ТипРезерваИ;
					Движение.ТипСуммы = ВыборкаПФ.ТипСуммы;
					Движение.Сумма = Списать;
					Движение.ПенсионнаяСхема = ВыборкаПФ.Схема;
				ИначеЕсли ТипРезерваП = Перечисления.уп_ТипыРезерва.Страховой Тогда
					Движение = Движения.уп_АналитикаСтраховогоРезерва.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Период = Дата;
					Движение.Организация = Организация; 
					Движение.Сумма = Списать;
					Движение.Расшифровка = АналитикаСРПриход; 
					Движение.ВидДвиженияСР = АналитикаСРПриход.ВидДвижения; 
					Движение.НомерСчета = ВыборкаПФ.ПенсионныйСчет;
				КонецЕсли;
				
				#КонецОбласти
				
				Если ОтражатьВБухгалтерскомУчете Тогда
					
						СхемаСтарая = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", ВыборкаПФ.ПенсионныйСчет)).Схема;
						
						//мВидОперацииБух=?(ТипРезерваИ=мСтраховой,мПеремещениеИзСтрахового,?(КонТипРезерва=мСтраховой,мПеремещениеВСтраховой,мВидДвижения));
						//мВидОперацииБух=мВидДвижения;
						
						//ДобавитьстрокуВТабБухУчет(ТЗ, Дата, Организация, ВидОперации, КонтрагентДт, ДоговорДт, СхемаДт, ПризнакДт, КонтрагентКт
						//, ДоговорКт, СхемаКт, ПризнакКт, Сумма)
						
						уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация
						,мПеремещениеВСтраховой
						,ВыборкаПФ.ПенсионныйСчет.Владелец.Вкладчик
						, ВыборкаПФ.ПенсионныйСчет.Владелец.ДоговорКонтрагента
						, СхемаСтарая
						,(ТипРезерваИ=Перечисления.уп_ТипыРезерва.ПожизненныхВыплат)
						, Справочники.Контрагенты.ПустаяСсылка()
						,Справочники.ДоговорыКонтрагентов.ПустаяСсылка()
						, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка()
						,Ложь
						,Списать);
					КонецЕсли;
				
				#КонецОбласти
				
			КонецЦикла;
		КонецЦикла;
		#КонецОбласти
		
	Иначе
		#Область Оба_типа_резервов_не_страховые
		
		ПроверитьИсточникНаСПТ(Отказ);
		#Область Текст_запроса_и_параметры
		Запрос=Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	уп_ПерераспределениеСредствНПОПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОПриемник.Схема КАК Схема,
		|	уп_ПерераспределениеСредствНПОПриемник.ТипСуммы КАК ТипСуммы,
		|	СУММА(уп_ПерераспределениеСредствНПОПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПриемник
		|ИЗ
		|	Документ.уп_ПерераспределениеСредствНПО.Приемник КАК уп_ПерераспределениеСредствНПОПриемник
		|ГДЕ
		|	уп_ПерераспределениеСредствНПОПриемник.Ссылка = &Ссылка
		|	И уп_ПерераспределениеСредствНПОПриемник.Сумма > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПерераспределениеСредствНПОПриемник.ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОПриемник.Схема,
		|	уп_ПерераспределениеСредствНПОПриемник.ТипСуммы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОИсточник.Схема КАК Схема,
		|	СУММА(уп_ПерераспределениеСредствНПОИсточник.Сумма) КАК Сумма,
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет.Представление КАК ПФНазвание
		|ПОМЕСТИТЬ мИсточник
		|ИЗ
		|	Документ.уп_ПерераспределениеСредствНПО.Источник КАК уп_ПерераспределениеСредствНПОИсточник
		|ГДЕ
		|	уп_ПерераспределениеСредствНПОИсточник.Ссылка = &Ссылка
		|	И уп_ПерераспределениеСредствНПОИсточник.Сумма > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет,
		|	уп_ПерераспределениеСредствНПОИсточник.Схема,
		|	уп_ПерераспределениеСредствНПОИсточник.ПенсионныйСчет.Представление
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
		|	уп_РезервыОстатки.ТипСуммы КАК ТипСуммы,
		|	уп_РезервыОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ мОстатки
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(
		|			&Момент,
		|			Организация = &Организация
		|				И ТипРезерва = &ТипРезерва
		|				И НомерСчета В
		|					(ВЫБРАТЬ
		|						мИсточник.ПенсионныйСчет
		|					ИЗ
		|						мИсточник)) КАК уп_РезервыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мИсточник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мИсточник.Схема КАК Схема,
		|	ЕСТЬNULL(мОстатки.ТипСуммы, ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ПустаяСсылка)) КАК ТипСуммы,
		|	ЕСТЬNULL(мОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	мИсточник.Сумма КАК Сумма,
		|	мИсточник.ПФНазвание КАК ПФНазвание
		|ИЗ
		|	мИсточник КАК мИсточник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мОстатки КАК мОстатки
		|		ПО мИсточник.ПенсионныйСчет = мОстатки.НомерСчета
		|ИТОГИ
		|	СУММА(СуммаОстаток),
		|	МАКСИМУМ(Сумма),
		|	МАКСИМУМ(ПФНазвание)
		|ПО
		|	ОБЩИЕ,
		|	ПенсионныйСчет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
		|	СостоянияПССрезПоследних.Состояние КАК Состояние,
		|	ЕСТЬNULL(СтартовыеСуммыСрезПоследних.Сумма, 0) КАК СтартоваяСумма,
		|	СхемаСчетаСрезПоследних.Схема КАК Схема
		|ПОМЕСТИТЬ мСостояния
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					мПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПриемник)) КАК СостоянияПССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СтартовыеСуммы.СрезПоследних(
		|				&ДатаСтартовойСуммы,
		|				НомерСчета В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						мПриемник.ПенсионныйСчет
		|					ИЗ
		|						мПриемник)) КАК СтартовыеСуммыСрезПоследних
		|		ПО СостоянияПССрезПоследних.НомерСчета = СтартовыеСуммыСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(
		|				&Момент,
		|				НомерСчета В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						мПриемник.ПенсионныйСчет
		|					ИЗ
		|						мПриемник)) КАК СхемаСчетаСрезПоследних
		|		ПО СостоянияПССрезПоследних.НомерСчета = СхемаСчетаСрезПоследних.НомерСчета
		|ГДЕ
		|	СостоянияПССрезПоследних.Состояние В(&СписокВыплатныхСостояний)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПриемник.Схема КАК Схема,
		|	мПриемник.ТипСуммы КАК ТипСуммы,
		|	мПриемник.Сумма КАК Сумма,
		|	мСостояния.Состояние КАК Состояние,
		|	мСостояния.СтартоваяСумма КАК СтартоваяСумма,
		|	мСостояния.Схема КАК СхемаСостояние,
		|	мПриемник.ПенсионныйСчет.Владелец КАК ПенсионныйСчетВладелец
		|ИЗ
		|	мПриемник КАК мПриемник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостояния КАК мСостояния
		|		ПО мПриемник.ПенсионныйСчет = мСостояния.НомерСчета
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(мИсточник.Сумма) КАК Сумма
		|ИЗ
		|	мИсточник КАК мИсточник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПриемник.ПенсионныйСчет.Владелец КАК ПенсионныйСчетВладелец,
		|	СУММА(мПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПенсионныеПриемник
		|ИЗ
		|	мПриемник КАК мПриемник
		|
		|СГРУППИРОВАТЬ ПО
		|	мПриемник.ПенсионныйСчет,
		|	мПриемник.ПенсионныйСчет.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостоянияПССрезПоследних.Состояние КАК Состояние,
		|	уп_СостоянияПССрезПоследних.НомерСчета КАК НомерСчета
		|ПОМЕСТИТЬ мСостоянияП
		|ИЗ
		|	РегистрСведений.уп_СостоянияПС.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					мПенсионныеПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПенсионныеПриемник)) КАК уп_СостоянияПССрезПоследних
		|ГДЕ
		|	уп_СостоянияПССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияПС.ОжиданиеВзносов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета КАК НомерСчета,
		|	уп_ВидПенсионногоСчетаСрезПоследних.ВидПенсионногоСчета КАК ВидПенсионногоСчета
		|ПОМЕСТИТЬ мВидПенсионногоСчета
		|ИЗ
		|	РегистрСведений.уп_ВидПенсионногоСчета.СрезПоследних(
		|			&Момент,
		|			НомерСчета В
		|				(ВЫБРАТЬ
		|					мПенсионныеПриемник.ПенсионныйСчет
		|				ИЗ
		|					мПенсионныеПриемник)) КАК уп_ВидПенсионногоСчетаСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	мПенсионныеПриемник.ПенсионныйСчет КАК ПенсионныйСчет,
		|	мПенсионныеПриемник.Сумма КАК Сумма,
		|	ЕСТЬNULL(мСостояния.СтартоваяСумма, 0) КАК СтартоваяСумма,
		|	мСостоянияП.Состояние КАК Состояние,
		|	мВидПенсионногоСчета.ВидПенсионногоСчета КАК ВидПенсионногоСчета,
		|	мПенсионныеПриемник.ПенсионныйСчет.Участник.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостояния КАК мСостояния
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мСостояния.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ мСостоянияП КАК мСостоянияП
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мСостоянияП.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ мВидПенсионногоСчета КАК мВидПенсионногоСчета
		|		ПО мПенсионныеПриемник.ПенсионныйСчет = мВидПенсионногоСчета.НомерСчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец КАК ПДП,
		|	СУММА(мПенсионныеПриемник.Сумма) КАК Сумма
		|ПОМЕСТИТЬ мПДП
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник
		|
		|СГРУППИРОВАТЬ ПО
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостоянияДоговораСрезПоследних.Состояние КАК Состояние,
		|	мПДП.Сумма КАК Сумма,
		|	мПДП.ПДП КАК ПДП
		|ИЗ
		|	мПДП КАК мПДП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговора.СрезПоследних(
		|				&Момент,
		|				ПенсионныйДоговор В
		|					(ВЫБРАТЬ
		|						мПДП.ПДП
		|					ИЗ
		|						мПДП КАК мПДП)) КАК уп_СостоянияДоговораСрезПоследних
		|		ПО мПДП.ПДП = уп_СостоянияДоговораСрезПоследних.ПенсионныйДоговор
		|ГДЕ
		|	уп_СостоянияДоговораСрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговора.Открыт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	мПенсионныеПриемник.ПенсионныйСчет КАК НомерСчета,
		|	мПенсионныеПриемник.ПенсионныйСчетВладелец КАК ПенсионныйДоговор
		|ИЗ
		|	мПенсионныеПриемник КАК мПенсионныеПриемник";
		
		Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("Момент",МоментВремени());
		Запрос.УстановитьПараметр("ТипРезерва",ТипРезерваИ);
		
		Запрос.УстановитьПараметр("ДатаСтартовойСуммы", Дата-1);
		#Вставка
 		//https://redmine.npf.com/issues/4156
 		Если ДополнительныеСвойства.Свойство("КасперЗагрузка") Тогда
			Если Не ДополнительныеСвойства.КасперЗагрузка.Контекст.Структура_Реквизиты.ТЗ.Колонки.Найти("ДатаОснования") = Неопределено Тогда
				ДатаОснования = ДополнительныеСвойства.КасперЗагрузка.СтрокаТЗ.ДатаОснования;	
				Если Не НачалоДня(ДатаОснования) = НачалоДня(Дата) Тогда
					Запрос.УстановитьПараметр("Момент",Новый МоментВремени(ДатаОснования));
					Запрос.УстановитьПараметр("ДатаСтартовойСуммы", ДатаОснования-1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		#КонецВставки
		СписокВыплатныхСостояний = Новый СписокЗначений;
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатнойПериод);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.НачисленияПриостановлены);
		СписокВыплатныхСостояний.Добавить(Перечисления.уп_СостоянияПС.ВыплатыПриостановлены);
		Запрос.УстановитьПараметр("СписокВыплатныхСостояний", СписокВыплатныхСостояний);
		
		#КонецОбласти
		
		Результат=Запрос.ВыполнитьПакет();
		ВыборкаИсточникИтог=Результат[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаПриемникИтог=Результат[5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ИсточникИтог=Результат[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		#Область Проверяем_есть_ли_данные_в_табличных_частях_документа
		Если Не ВыборкаИсточникИтог.Следующий() Тогда
			Отказ=Истина;
			Сообщить("Источник не заполнен");
		КонецЕсли;
		
		Если НЕ ВыборкаПриемникИтог.Следующий() Тогда
			Отказ=Истина;
			Сообщить("Приемник не заполнен");
		КонецЕсли;
		
		Если НЕ ИсточникИтог.Следующий() Тогда
			Отказ=Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		#КонецОбласти
		
		НужноСписать=ИсточникИтог.Сумма;
		Если ВыборкаИсточникИтог.Сумма=0 Тогда
			Отказ=Истина;
			Сообщить("Суммы в источнике не заполнены");
		КонецЕсли;
		
		Если ВыборкаПриемникИтог.Сумма=0 Тогда
			Отказ=Истина;
			Сообщить("Суммы в приемнике не заполнены");
		КонецЕсли;
		
		Если НужноСписать<ВыборкаПриемникИтог.Сумма Тогда
			Отказ=Истина;
			Сообщить("Сумма в источнике меньше чем в приемнике");
		КонецЕсли;
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
				
		ВыборкаИсточник=ВыборкаИсточникИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаПриемник=ВыборкаПриемникИтог.Выбрать();
		
		#Область Суммы_указанные_в_источнике_должны_быть_не_больше_чем_есть_на_остатках
		Пока ВыборкаИсточник.Следующий() Цикл
			Если ВыборкаИсточник.Сумма>ВыборкаИсточник.СуммаОстаток Тогда
				Сообщить("Для пенсионного счета "+ВыборкаИсточник.ПФНазвание+" указана сумма к распределению большая чем есть на остатках.");
				Отказ=Истина;
			КонецЕсли;
		КонецЦикла;
		#КонецОбласти
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ВыборкаИсточник.Сбросить();
		
		мВидДвижения = Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва;
		
		// Расписываем суммы из приемника по указанным в источнике суммам 
		ОстатокПриемник=0;
		
		Пока ВыборкаИсточник.Следующий() И НужноСписать>0 Цикл
			ВыборкаПФ=ВыборкаИсточник.Выбрать();
			СуммаПоСтроке=ВыборкаИсточник.Сумма;
			Пока ВыборкаПФ.Следующий() И НужноСписать>0 И СуммаПоСтроке>0 Цикл
				МожноСписать=Мин(?(ВыборкаПФ.СуммаОстаток < 0,0,ВыборкаПФ.СуммаОстаток),СуммаПоСтроке);
				СуммаПоСтроке=СуммаПоСтроке-МожноСписать;
				Пока МожноСписать>0 Цикл
					Если ОстатокПриемник=0 Тогда
						Если ВыборкаПриемник.Следующий() Тогда
							ОстатокПриемник=ВыборкаПриемник.Сумма;
						Иначе
							Прервать;
						КонецЕсли;
					КонецЕсли;
				//Пока ОстатокПриемник=0 И ВыборкаПриемник.Следующий() И НужноСписать>0 И МожноСписать>0 Цикл
					#Область Формируем_реквизиты_движений_регистра
					Если МожноСписать>=ОстатокПриемник Тогда
						Списать=ОстатокПриемник;
						ОстатокПриемник=0;
						МожноСписать=МожноСписать-Списать;
					Иначе
						Списать=МожноСписать;
						ОстатокПриемник=ОстатокПриемник-Списать;
						МожноСписать=0;
					КонецЕсли;
					НужноСписать=НужноСписать-Списать;
					
					НомерСчетаП=ВыборкаПриемник.ПенсионныйСчет;
					ТипСуммыП_Ит = ВыборкаПриемник.ТипСуммы;
					Если ВыборкаПриемник.СхемаСостояние<>NULL Тогда
						КонТипРезерва = уп_ОбщегоНазначенияУчетРезервов.ОпределитьКонечныйТипРезерва(ВыборкаПриемник.СхемаСостояние,ТипРезерваП);
						Если (КонТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат) Тогда
							ПорядокВеденияПожизненногоРезерва = уп_НПФ.ОпределитьПорядокВеденияПожизненногоРезерва(ВыборкаПриемник.СхемаСостояние, Дата);
							Если ПорядокВеденияПожизненногоРезерва = Перечисления.уп_РезервПожизненныхВыплат.ПоДоговорам Тогда
								НомерСчетаП = уп_ОбщегоНазначенияУчетРезервов.ОпределитьНомерПожизненногоСчетаДоговора(ВыборкаПриемник.ПенсионныйСчетВладелец, ЭтотОбъект
								, Истина, ВыборкаПриемник.СхемаСостояние);
							ИначеЕсли ПорядокВеденияПожизненногоРезерва = Перечисления.уп_РезервПожизненныхВыплат.БезАналитики Тогда
								НомерСчетаП = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
								ТипСуммыП_Ит = Справочники.уп_ТипыСумм.ПустаяСсылка();
							КонецЕсли;
						КонецЕсли;
						
					Иначе
						КонТипРезерва = ТипРезерваП;
					КонецЕсли;
					#КонецОбласти
					
					#Область Формируем_движения
					
					ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(ТипРезерваИ, КонТипРезерва);
					#Область Расход
					Движение = Движения.уп_Резервы.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.НомерСчета = ВыборкаПФ.ПенсионныйСчет;
					#Вставка
					//https://redmine.npf.com/issues/4156
					Движение.ПенсионныйСчетИсточник = НомерСчетаП;
					#КонецВставки
					Движение.ТипРезерва = ТипРезерваИ;
					Движение.ТипСуммы = ВыборкаПФ.ТипСуммы;
					Движение.Сумма = Списать;
					Движение.Движение = мВидДвижения;
					Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
					Движение.ДокументРегистрации = ДокОсн;
					Движение.Схема = ВыборкаПФ.Схема;
					Если  ВестиУчетПоСхемам Тогда
						Движение.ПенсионнаяСхема = ВыборкаПФ.Схема;
					КонецЕсли;
					Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
					#КонецОбласти
					
					#Область Приход
					Движение = Движения.уп_Резервы.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.НомерСчета = НомерСчетаП;
					#Вставка
					//https://redmine.npf.com/issues/4156
					Движение.ПенсионныйСчетИсточник = ВыборкаПФ.ПенсионныйСчет;
					#КонецВставки
					Движение.ТипРезерва = КонТипРезерва;
					Движение.ТипСуммы = ТипСуммыП_Ит;
					Движение.Сумма = Списать;
					Движение.Движение = мВидДвижения;
					Движение.ВидДвиженияЕПС = ВидДвиженияЕПС;
					Движение.ДокументРегистрации = ДокОсн;
					Движение.Схема = ВыборкаПриемник.Схема;
					Если  ВестиУчетПоСхемам Тогда
						Движение.ПенсионнаяСхема = ВыборкаПриемник.Схема;
					КонецЕсли; 
					Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
					#КонецОбласти
					
					#КонецОбласти
					
					Если ОтражатьВБухгалтерскомУчете Тогда
					
						СхемаСтарая = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", ВыборкаПФ.ПенсионныйСчет)).Схема;
						
						// Заголовок функции
						//ДобавитьстрокуВТабБухУчет(ТЗ, Дата, Организация, ВидОперации, КонтрагентДт, ДоговорДт, СхемаДт, ПризнакДт, КонтрагентКт
						//, ДоговорКт, СхемаКт, ПризнакКт, Сумма)
						
						уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация
						,мВидДвижения
						,ВыборкаПФ.ПенсионныйСчет.Владелец.Вкладчик
						, ВыборкаПФ.ПенсионныйСчет.Владелец.ДоговорКонтрагента
						, СхемаСтарая
						,(ТипРезерваИ=Перечисления.уп_ТипыРезерва.ПожизненныхВыплат)
						, НомерСчетаП.Владелец.Вкладчик
						,НомерСчетаП.Владелец.ДоговорКонтрагента
						, НомерСчетаП.Владелец.Схема
						,(КонТипРезерва=Перечисления.уп_ТипыРезерва.ПожизненныхВыплат)
						,Списать);
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		#Область Регистры_сведений 
		Движения.уп_СтартовыеСуммы.Записывать=Истина;
		Движения.уп_СостоянияПС.Записывать=Истина;
		Движения.уп_ВидПенсионногоСчета.Записывать=Истина;
		
		Выборка=Результат[10].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.СтартоваяСумма<>0 Тогда
				Движение = Движения.уп_СтартовыеСуммы.Добавить();
				Движение.Период = Дата;
				Движение.НомерСчета = Выборка.ПенсионныйСчет;
				Движение.Сумма = Выборка.СтартоваяСумма+Выборка.Сумма;
			КонецЕсли;
			Если Выборка.Состояние=Перечисления.уп_СостоянияПС.ОжиданиеВзносов Тогда
				СостояниеПС=Движения.уп_СостоянияПС.Добавить();
				СостояниеПС.Период=Дата;
				СостояниеПС.НомерСчета=Выборка.ПенсионныйСчет;
				СостояниеПС.Состояние=Перечисления.уп_СостоянияПС.НакопительныйПериод;
			КонецЕсли;
			Если ТипРезерваП=Перечисления.уп_ТипыРезерва.ИПС И (Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.БУС 
			ИЛИ Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.УК) Тогда
				ВидПенсионногоСчета=Движения.уп_ВидПенсионногоСчета.Добавить();
				ВидПенсионногоСчета.Период=Дата;
				ВидПенсионногоСчета.НомерСчета=Выборка.ПенсионныйСчет;
				ВидПенсионногоСчета.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.ИПС;
			КонецЕсли;
			Если ТипРезерваП=Перечисления.уп_ТипыРезерва.СПС И (Выборка.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.БУС 
			И Выборка.ЮридическоеФизическоеЛицо=Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо) Тогда
				ВидПенсионногоСчета=Движения.уп_ВидПенсионногоСчета.Добавить();
				ВидПенсионногоСчета.Период=Дата;
				ВидПенсионногоСчета.НомерСчета=Выборка.ПенсионныйСчет;
				ВидПенсионногоСчета.ВидПенсионногоСчета=Перечисления.уп_ВидыПенсионныхСчетов.УК;
			КонецЕсли;
		КонецЦикла;
		
		Движения.уп_СостоянияДоговора.Записывать=Истина;
		Выборка=Результат[12].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Состояние=Перечисления.уп_СостоянияДоговора.Открыт Тогда
				СостояниеДоговора=Движения.уп_СостоянияДоговора.Добавить();
				СостояниеДоговора.Период=Дата;
				СостояниеДоговора.ПенсионныйДоговор=Выборка.ПДП;
				СостояниеДоговора.Состояние=Перечисления.уп_СостоянияДоговора.Действует;
			КонецЕсли;
		КонецЦикла;  
		
		ТаблицаДокумента = Результат[13].Выгрузить();
		Движения.уп_ДатаФиксацииОбязательствНПО.Записывать=Истина;  
		Движения.уп_ПериодыГарантийногоВосполненияНПО.Записывать = Истина;
		уп_СистемаГарантированияНПО.ПолучитьДатыФиксацииОбязательствПоТЧ(ЭтотОбъект, ТаблицаДокумента);
		#КонецОбласти
		
		#КонецОбласти
	КонецЕсли;
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("МассивСообщений", МассивСообщений);
КонецПроцедуры
