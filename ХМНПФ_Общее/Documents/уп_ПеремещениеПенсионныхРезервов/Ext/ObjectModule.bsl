// ХМНПФ 05.03.2019 ХлопцевБА // ОбработкаЗаполнения // № 1498
&Перед("ОбработкаЗаполнения")
Процедура НПФ_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.уп_Распоряжение") Тогда
		// Заполнение шапки
		Комментарий = "Фондирование";
		Организация = ДанныеЗаполнения.Организация;
		Ответственный = ДанныеЗаполнения.Ответственный;
		ОтражатьВБухгалтерскомУчете = ДанныеЗаполнения.ОтражатьВБухгалтерскомУчете;
		ДокОсн = ДанныеЗаполнения.Ссылка;
		ПодразделениеОрганизации = ДанныеЗаполнения.ПодразделениеОрганизации;
		
		Для Каждого ТекСтрокаСписокСчетов Из ДанныеЗаполнения.СписокСчетов Цикл
			
			НоваяСтрока = Перемещения.Добавить();
			
			ДоговорВкладчикаИ  =  ТекСтрокаСписокСчетов.ПенсионныйДоговор;
			ВкладчикИ = ТекСтрокаСписокСчетов.ПенсионныйДоговор.Вкладчик;
			ТипРезерваИ = Перечисления.уп_ТипыРезерва.СПС;
			
			ДоговорВкладчикаП  =  ТекСтрокаСписокСчетов.ПенсионныйДоговор;
			ВкладчикП = ТекСтрокаСписокСчетов.ПенсионныйДоговор.Вкладчик;
			ТипРезерваП = Перечисления.уп_ТипыРезерва.ИПС;
			
			НоваяСтрока.ПенсионныйСчетИ = ТекСтрокаСписокСчетов.ПенсионныйДоговор.ПенсионныйСчет;
			НоваяСтрока.УчастникИ = ТекСтрокаСписокСчетов.ПенсионныйДоговор.Вкладчик;
			НоваяСтрока.ПенсионныйДоговорИ = ТекСтрокаСписокСчетов.ПенсионныйДоговор;
			ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", НоваяСтрока.ПенсионныйСчетИ)).Схема;
			НоваяСтрока.СхемаИ = ТекСхема;
			НоваяСтрока.ТипСуммыИ  = Справочники.уп_ТипыСумм.ВзносыЮр;
			
			НоваяСтрока.ПенсионныйСчетП = ТекСтрокаСписокСчетов.НомерСчета;
			НоваяСтрока.УчастникП = ТекСтрокаСписокСчетов.Участник;
			НоваяСтрока.ПенсионныйДоговорП = ТекСтрокаСписокСчетов.ПенсионныйДоговор;
			ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", НоваяСтрока.ПенсионныйСчетП)).Схема;
			НоваяСтрока.СхемаП = ТекСхема;
			НоваяСтрока.ТипСуммыП  = Справочники.уп_ТипыСумм.ВзносыЮр;
			
			ДатаРождения = ТекСтрокаСписокСчетов.Участник.уп_ФизЛицо.ДатаРождения;
			Пол = ТекСтрокаСписокСчетов.Участник.уп_ФизЛицо.Пол;
			
			НоваяСтрока.Сумма =  (ТекСтрокаСписокСчетов.РазмерВыплаты*12) * хм_ПрочиеПроцедуры.ПолучитьКоэффициентДожития(ДатаРождения,Пол, ДанныеЗаполнения.Дата,0);
			
			Сум = Сум + НоваяСтрока.Сумма;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// ХМНПФ 15.02.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	ЭтотОбъект.ОбменДанными.Загрузка = Ложь;
	ЭтотОбъект.Записать(?(ПРОВЕДЕН,РежимЗаписиДокумента.Проведение,РежимЗаписиДокумента.ОтменаПроведения));
	ЭтотОбъект.ОбменДанными.Загрузка = Истина;
КонецПроцедуры

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ+ 22.08.2016 ШадринАВ // длина номера изменена с 9 до 18 // №459
	// ХМНПФ 15.02.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура ХМОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументССылка.уп_ПоступлениеВзносов") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеЗаполнения.СписокСчетов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрока = ДанныеЗаполнения.СписокСчетов[0]; 
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения.СписокСчетов[0].ПенсионныйДоговор) Тогда
		Возврат;
	КонецЕсли;
	
	ТекДоговор = ТекСтрока.ПенсионныйДоговор;
	Если ТекДоговор.ПаритетныйДоговор.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТекДоговор.ПаритетныйДоговор.Паритетный Тогда
		Возврат;
	КонецЕсли;
	
	
	уп_ВидДеятельности = хм_ПрочиеПроцедуры.ПолучитьЗначениеПоИдентификатору("уп_ВидДеятельностиНПО",ДанныеЗаполнения.Организация,ДанныеЗаполнения.Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_МестаРаботыУчастниковСрезПоследних.Период,
	|	уп_МестаРаботыУчастниковСрезПоследних.Регистратор,
	|	уп_МестаРаботыУчастниковСрезПоследних.НомерСтроки,
	|	уп_МестаРаботыУчастниковСрезПоследних.Активность,
	|	уп_МестаРаботыУчастниковСрезПоследних.Участник,
	|	уп_МестаРаботыУчастниковСрезПоследних.МестоРаботы
	|ПОМЕСТИТЬ вт_ЮЛ
	|ИЗ
	|	РегистрСведений.уп_МестаРаботыУчастников.СрезПоследних(&ДатаСреза, Участник = &Участник) КАК уп_МестаРаботыУчастниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	вт_ЮЛ.Участник,
	|	вт_ЮЛ.МестоРаботы,
	|	уп_СостоянияДоговораСрезПоследних.ПенсионныйДоговор
	|ПОМЕСТИТЬ вт_Договор
	|ИЗ
	|	вт_ЮЛ КАК вт_ЮЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияДоговора.СрезПоследних(
	|				&ДатаСреза,
	|				ПенсионныйДоговор.Вкладчик В
	|						(ВЫБРАТЬ
	|							вт_ЮЛ.МестоРаботы
	|						ИЗ
	|							вт_ЮЛ КАК вт_ЮЛ)
	|					И ПенсионныйДоговор.Паритетный
	|					И Состояние = ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговора.Действует)) КАК уп_СостоянияДоговораСрезПоследних
	|		ПО вт_ЮЛ.МестоРаботы = уп_СостоянияДоговораСрезПоследних.ПенсионныйДоговор.Вкладчик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Договор.Участник,
	|	вт_Договор.ПенсионныйДоговор,
	|	вт_Договор.МестоРаботы,
	|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета КАК НомерСчетаСПС
	|ПОМЕСТИТЬ вт_СПС
	|ИЗ
	|	вт_Договор КАК вт_Договор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_ВидПенсионногоСчета.СрезПоследних(
	|				&ДатаСреза,
	|				НомерСчета.Владелец В
	|						(ВЫБРАТЬ
	|							вт_Договор.ПенсионныйДоговор
	|						ИЗ
	|							вт_Договор КАК вт_Договор)
	|					И ВидПенсионногоСчета = ЗНАЧЕНИЕ(Перечисление.уп_ВидыПенсионныхСчетов.СПС)) КАК уп_ВидПенсионногоСчетаСрезПоследних
	|		ПО вт_Договор.ПенсионныйДоговор = уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_СПС.Участник,
	|	вт_СПС.МестоРаботы,
	|	вт_СПС.ПенсионныйДоговор,
	|	вт_СПС.НомерСчетаСПС,
	|	уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета КАК НомерСчетаУК,
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ВариантРасчета,
	|	ХМ_РазмерПаритетногоВзносаСрезПоследних.ПроцентВзноса
	|ИЗ
	|	вт_СПС КАК вт_СПС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ВидПенсионногоСчета.СрезПоследних(
	|				&ДатаСреза,
	|				НомерСчета.Владелец В
	|						(ВЫБРАТЬ
	|							вт_Договор.ПенсионныйДоговор
	|						ИЗ
	|							вт_Договор КАК вт_Договор)
	|					И ВидПенсионногоСчета = ЗНАЧЕНИЕ(Перечисление.уп_ВидыПенсионныхСчетов.УК)) КАК уп_ВидПенсионногоСчетаСрезПоследних
	|		ПО вт_СПС.ПенсионныйДоговор = уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета.Владелец
	|			И вт_СПС.Участник = уп_ВидПенсионногоСчетаСрезПоследних.НомерСчета.Участник,
	|	РегистрСведений.ХМ_РазмерПаритетногоВзноса.СрезПоследних(
	|			&ДатаСреза,
	|			ПенсионныйДоговор В
	|				(ВЫБРАТЬ
	|					вт_СПС.ПенсионныйДоговор
	|				ИЗ
	|					вт_СПС КАК вт_СПС)) КАК ХМ_РазмерПаритетногоВзносаСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДанныеЗаполнения.Дата);
	Запрос.УстановитьПараметр("Участник", ДанныеЗаполнения.Плательщик);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл 
		
		ВкладчикП = РезультатЗапроса.МестоРаботы;
		ДоговорВкладчикаП = РезультатЗапроса.ПенсионныйДоговор;
		Если ВкладчикП.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ТипРезерваП = Перечисления.уп_ТипыРезерва.ИПС;                                                                 
		КонецЕсли;
		
		ВкладчикИ = РезультатЗапроса.МестоРаботы;
		ДоговорВкладчикаИ = РезультатЗапроса.ПенсионныйДоговор;
		Если ВкладчикИ.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
			ТипРезерваИ = Перечисления.уп_ТипыРезерва.СПС;                                                                 
		КонецЕсли;
		
		нСтрока = Перемещения.Добавить();
		
		нСтрока.УчастникИ = РезультатЗапроса.МестоРаботы;
		нСтрока.ПенсионныйСчетИ = РезультатЗапроса.НомерСчетаСПС;
		нСтрока.ПенсионныйДоговорИ = РезультатЗапроса.ПенсионныйДоговор;
		нСтрока.ТипСуммыИ = Справочники.уп_ТипыСумм.ВзносыЮр;                  
		
		нСтрока.УчастникП = РезультатЗапроса.Участник;
		нСтрока.ПенсионныйСчетП = РезультатЗапроса.НомерСчетаУК;
		нСтрока.ПенсионныйДоговорП = РезультатЗапроса.ПенсионныйДоговор;
		нСтрока.ТипСуммыП = Справочники.уп_ТипыСумм.ВзносыЮр;  
		
		Если РезультатЗапроса.ВариантРасчета.ТипРасчета = Перечисления.хм_ТипРасчетаПаритетногоВзноса.Процентом Тогда
			нСтрока.Сумма =  ДанныеЗаполнения.СуммаДокумента/100*РезультатЗапроса.ПроцентВзноса;
			Сум = Сум + нСтрока.Сумма;
		Иначе
			Сообщить("На данный момент реализован только механизм расчета ""процентом""");
		КонецЕсли;
		
		Комментарий = "Выполнено заполнение с учетом паритетных договорных отношений.";
		
		Прервать;                              
		
	КонецЦикла;
	
КонецПроцедуры
// ХМНПФ 05.03.2019 ХлопцевБА // ОбработкаЗаполнения // № 1498
