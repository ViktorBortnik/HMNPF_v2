
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	// ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	// ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	// ХМНПФ 28.07.2016 ШадринАВ // ПроверитьОстатки() // №447
	// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
	//-ХМНПФ 28.0217 Кучеров//убрал директиву на клиенте
	// ХМНПФ 11.09.2017 ШадринАВ // Новая ПриЗаписи() // №1161
	// ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
	// ХМНПФ 15.05.2019 ГригорьевМА // Изменен макет Макет стр.14 текст "О НАЗНАЧЕНИИ НАКОПИТЕЛЬНОЙ ЧАСТИ ТРУДОВОЙ ПЕНСИИ " заменен на "О НАЗНАЧЕНИИ ПЕНСИИ" // №2232
	// ХМНПФ 15.05.2019 ГригорьевМА // Изменен макет Макет2015 стр.14 текст "О НАЗНАЧЕНИИ НАКОПИТЕЛЬНОЙ ЧАСТИ ТРУДОВОЙ ПЕНСИИ " заменен на "О НАЗНАЧЕНИИ ПЕНСИИ" // №2232
	// ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешения() // №2232
    // ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешенияСервер() // №2232
	// ХМНПФ 11.09.2019 ГригорьевМА // Изменен макет Макет2015 стр.7-8 колонка 10 объеденена с колонками 2-9 // №2348
	// ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешения() // №3103
	// ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешенияСервер() // №3103
	// ХМНПФ 01.03.2021 ШадринАВ // ОбработкаЗаполнения() // №3103
КонецПроцедуры

&ИзменениеИКонтроль("ПечатьРешения")
Функция РасшОбщ_ПечатьРешения()

	ТабДок = Новый ТабличныйДокумент;
	Если Дата < Дата("20150101") Тогда
		Макет = ПолучитьМакет("Макет");
	Иначе
		Макет = ПолучитьМакет("Макет2015");
	КонецЕсли;
	// Заголовок
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьОтчет"); // Шапка документа
	
	СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	
	// выводим данные о руководителях организации
	
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерДок = "№ "+СокрЛП(Номер);
	ОбластьМакета.Параметры.НаименованиеФонда = СведенияОбОрг.НаименованиеДляПечатныхФорм;
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;                
	Отчество = ФИОФизЛица.Отчество;
	ОбластьМакета.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
    ОбластьМакета.Параметры.НомерПенсионногоСчета = ДоговорОПС.НомерЛицевогоСчета;
	РазмерПожизненнойВыплаты = РазмерВыплаты;
	ОбластьМакета.Параметры.РазмерПВ = РазмерПожизненнойВыплаты;
	ОбластьМакета.Параметры.РазмерПВПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(РазмерПожизненнойВыплаты, Константы.ВалютаРегламентированногоУчета.Получить());

	#Вставка
	// +ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешения() // №2232
	//ОбластьМакета.Параметры.ДатаНачала = ДокументОснование.ДатаРегистрации;
	// -ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешения() // №2232
	// +ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешения() // №3103
	#КонецВставки
	ОбластьМакета.Параметры.ДатаНачала = НачальнаяДата;
	
	СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОНазначенииНЧТП.ПустаяСсылка(),Организация,ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Решение",Дата);
	Если ЗначениеЗаполнено(СведенияОбОтветственном.Фамилия) Тогда
		ОбластьМакета.Параметры.ДолжностьРуководителя = СведенияОбОтветственном.Должность;
		ОбластьМакета.Параметры.ФИОРуководителя = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
	Иначе
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		ОбластьМакета.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
		ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
	КонецЕсли;	
	
	ТабДок.Вывести(ОбластьМакета); 
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	#Вставка
	// +ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	ХМ_ФункцииОтчетов.ПечатьРешения_ДобавитьРасчет(ЭтотОбъект,ТабДок);
	// -ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	#КонецВставки

    Возврат ТабДок;
КонецФункции // ПечатьПриложения1()

&ИзменениеИКонтроль("ПечатьРешенияСервер")
Функция РасшОбщ_ПечатьРешенияСервер(ТабДок, ВыводитьАдрес = Ложь) Экспорт

	Если ТабДок=Неопределено Тогда
		ТабДок = Новый ТабличныйДокумент;
	Иначе
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Если Дата < Дата("20150101") Тогда
		Макет = ПолучитьМакет("Макет");
	Иначе
		Макет = ПолучитьМакет("Макет2015");
	КонецЕсли;
	// Заголовок
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьОтчет"); // Шапка документа
	
	СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	
	// выводим данные о руководителях организации
	
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерДок = "№ "+СокрЛП(Номер);
	ОбластьМакета.Параметры.НаименованиеФонда = СведенияОбОрг.НаименованиеДляПечатныхФорм;
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;                
	Отчество = ФИОФизЛица.Отчество;
	ОбластьМакета.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
    ОбластьМакета.Параметры.НомерПенсионногоСчета = ДоговорОПС.НомерЛицевогоСчета;
	РазмерПожизненнойВыплаты = РазмерВыплаты;
	ОбластьМакета.Параметры.РазмерПВ = РазмерПожизненнойВыплаты;
	ОбластьМакета.Параметры.РазмерПВПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(РазмерПожизненнойВыплаты, Константы.ВалютаРегламентированногоУчета.Получить());

	#Вставка
	// +ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешенияСервер() // №2232
	//ОбластьМакета.Параметры.ДатаНачала = ДокументОснование.ДатаРегистрации;
	// -ХМНПФ 15.05.2019 ГригорьевМА // ПечатьРешенияСервер() // №2232
	// +ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешенияСервер() // №3103
	#КонецВставки
	ОбластьМакета.Параметры.ДатаНачала = НачальнаяДата;
	
	СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОНазначенииНЧТП.ПустаяСсылка(),Организация,ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Решение",Дата);
	Если ЗначениеЗаполнено(СведенияОбОтветственном.Фамилия) Тогда
		ОбластьМакета.Параметры.ДолжностьРуководителя = СведенияОбОтветственном.Должность;
		ОбластьМакета.Параметры.ФИОРуководителя = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
	Иначе
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		ОбластьМакета.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
		ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
	КонецЕсли;	
	
	
	ТабДок.Вывести(ОбластьМакета);
	Если ВыводитьАдрес Тогда
		ОбластьКудаКому = Макет.ПолучитьОбласть("ОбластьКудаКому");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка,
		|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление
		|ПОМЕСТИТЬ АдресФЛ
		|ИЗ
		|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
		|ГДЕ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка = &ФЛ
		|	И ФизическиеЛицаКонтактнаяИнформация.Тип = &Тип
		|	И ФизическиеЛицаКонтактнаяИнформация.Вид = &Вид
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.ФизическоеЛицо, АдресФЛ.Ссылка) КАК ФизическоеЛицо,
		|	ФИОФизическихЛицСрезПоследних.Фамилия + "" "" + ФИОФизическихЛицСрезПоследних.Имя + "" "" + ФИОФизическихЛицСрезПоследних.Отчество КАК ФИО,
		|	АдресФЛ.Представление КАК Адрес
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаДок, ФизическоеЛицо = &ФЛ) КАК ФИОФизическихЛицСрезПоследних
		|		ПОЛНОЕ СОЕДИНЕНИЕ АдресФЛ КАК АдресФЛ
		|		ПО ФИОФизическихЛицСрезПоследних.ФизическоеЛицо = АдресФЛ.Ссылка";
		
		Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
		Запрос.УстановитьПараметр("ФЛ", ЗастрахованноеЛицо.уп_ФизЛицо);
		Запрос.УстановитьПараметр("ДатаДок", Дата);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Куда = Выборка.Адрес;
			Кому = Выборка.ФИО;
			ОбластьКудаКому.Рисунки.ТекстКудаКому.Текст = "Кому: "+Кому+Символы.ПС+"Куда: "+Куда;
		КонецЕсли;
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		ТабДок.Вывести(ОбластьКудаКому);
	КонецЕсли;	
	
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	#Вставка
	// +ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	ХМ_ФункцииОтчетов.ПечатьРешения_ДобавитьРасчет(ЭтотОбъект,ТабДок);
	// -ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	#КонецВставки

    Возврат ТабДок;
КонецФункции // ПечатьПриложения1()

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура РасшОбщ_ОбработкаЗаполнения(Основание, СтандартнаяОбработка)   Экспорт
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, Основание);
	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание);
		Если Основание.Свойство("Расшифровка") Тогда   //параметры заполнены
			Для а=1 По Основание.Расшифровка.Количество() Цикл
				СтруктураСтроки = Основание.Расшифровка.Получить(а-1);
				НовСтр = Остатки.Добавить();
				//НовСтр.ДоговорОПС = СтруктураСтроки.ДоговорОПС;
				//НовСтр.ТипРезерва = СтруктураСтроки.ТипРезерва;
				НовСтр.ТипСуммы = СтруктураСтроки.ТипСуммы;
				НовСтр.Сумма = СтруктураСтроки.Сумма;
				НовСтр.СуммаГарантированная = СтруктураСтроки.СуммаГарантированная;
				//СуммаНаСчете = СуммаНаСчете+СтруктураСтроки.Сумма;
			КонецЦикла;
		КонецЕсли;
	Иначе
	Если (Основание = Неопределено) ИЛИ Не ЭтотОбъект.Метаданные().Реквизиты.ДокументОснование.Тип.СодержитТип(ТипЗнч(Основание)) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыДокументаОснования = уп_ОсновнаяПоставкаСервер.СформироватьСтруктуруШапкиДокумента(Основание);
	ДокументОснование = ПараметрыДокументаОснования.Ссылка;
	Организация   = ПараметрыДокументаОснования.Организация;
	ЗастрахованноеЛицо = ПараметрыДокументаОснования.ЗастрахованноеЛицо;
	ДоговорОПС = ПараметрыДокументаОснования.ДоговорОПС;
	ПодразделениеОрганизации = ДоговорОПС.ПодразделениеБУ;
	ВыплачиватьПожизненнуюПенсию = Истина;
	ДатаГосПенсии = ?(ЗначениеЗаполнено(ПараметрыДокументаОснования.ПравоНаНакопительнуюПенсию),ПараметрыДокументаОснования.ПравоНаНакопительнуюПенсию,Неопределено); 
	ВариантРасчета = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация)).ВариантРасчетаЛетДосрочности;
//+ АДаниленко 08.08.2019
	Если ВариантРасчета = 0 ИЛИ ВариантРасчета = 1 Тогда
		СрокПожизненнойПенсии = уп_ОПС.СрокПожизненнойПенсии(ПараметрыДокументаОснования.ЗастрахованноеЛицо,ПараметрыДокументаОснования.Дата,ДатаГосПенсии, ВариантРасчета);
	ИначеЕсли ВариантРасчета = 2 ИЛИ ВариантРасчета = 3 Тогда
		СрокПожизненнойПенсии = уп_ОПС.СрокПожизненнойПенсии_2019(ПараметрыДокументаОснования.Ссылка, ПараметрыДокументаОснования.Дата, Ложь, Ложь, Истина);
	Иначе	
		СрокПожизненнойПенсии = 0;
	КонецЕсли;
//- АДаниленко 08.08.2019	
	ФормаВыплаты = ПараметрыДокументаОснования.ФормаВыплаты;
	ТипЛицевогоСчета = ПараметрыДокументаОснования.ТипЛицевогоСчета;
	НомерЛицевогоСчета = ПараметрыДокументаОснования.НомерЛицевогоСчета;
	Получатель =  ПараметрыДокументаОснования.Получатель;
	ДоверенноеЛицо =  ПараметрыДокументаОснования.ДоверенноеЛицо;
	РасчетныйСчет = ПараметрыДокументаОснования.РасчетныйСчет;
	УчитыватьДопВзносы = ПараметрыДокументаОснования.УчитыватьДопВзносы;

	#Вставка
	// +ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	//--НачальнаяДата = ПараметрыДокументаОснования.Дата;
	//НачальнаяДата = НачалоМесяца(ДобавитьМесяц(ПараметрыДокументаОснования.Дата,1)); // №248
	//НачальнаяДата = ПараметрыДокументаОснования.ДатаРегистрации; // ХМНПФ 15.07.2019 ШадринАВ // ОбработкаЗаполнения() // №2355
	// -ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	// +ХМНПФ 01.03.2021 ШадринАВ // ОбработкаЗаполнения() // №3103
	#КонецВставки
	НачальнаяДата = ПараметрыДокументаОснования.Дата;

	ЗаполнитьОстатки();
	
	Если СрокПожизненнойПенсии<>0 Тогда
		РазмерВыплаты = Остатки.Итог("Сумма")/СрокПожизненнойПенсии;
	КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ ВыплачиватьПожизненнуюПенсию Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СрокПожизненнойПенсии) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не заполнен расчетный срок выплаты.",Отказ,Заголовок);
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не выбрано заявление о назначении накопительной пенсии.",Отказ,Заголовок);
		Отказ = Истина;
	ИначеЕсли НЕ ДокументОснование.Проведен Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Заявление о назначении накопительной пенсии не проведено.",Отказ,Заголовок);
		Отказ = Истина;
	ИначеЕсли ДокументОснование.Дата>КонецДня(Дата) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Заявление зарегистрировано после решения.",Отказ,Заголовок);
		Отказ = Истина;
	КонецЕсли;	
	
	МассивСообщений = Новый СписокЗначений;
	Если КонецДня(НачальнаяДата)> КонецДня(Дата) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Обратите внимание: дата начала выплат превышает дату решения",,Заголовок);
		МассивСообщений.Добавить("Обратите внимание: дата начала выплат превышает дату решения");
	КонецЕсли;	
	
	ПроверитьСрочнуюВыплату(Отказ);
	
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	УчетнаяПолитикаНПФ = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	ПерваяНППропорциональноДням = УчетнаяПолитикаНПФ.ПерваяНППропорциональноДням;
	// состояния договора  - здесь надо проверить - если уже выплатной, то не надо сатвить
	Движения.уп_СостоянияДоговораОПС.Записывать = Истина;
	Движения.уп_СостоянияДоговораОПС.Очистить();
	Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
	
	#Удаление
	Если НачальнаяДата<=КонецДня(Дата) Тогда
		Движение.Период = Дата;
	Иначе
		Движение.Период = НачальнаяДата;
	КонецЕсли;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
	Движение.Период = НачальнаяДата;
	// -ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
	#КонецВставки
	Движение.ДатаУстановкиСостояния = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
	
	// размер выплат
	Движения.уп_РазмерПенсииОПС.Записывать = Истина;
	Движения.уп_РазмерПенсииОПС.Очистить();
	
	ДатаНачПериода= НачалоМесяца(НачальнаяДата);
	
	КоличествоВыплатВПервойВыплате = уп_ОПС.ПолучитьКоличествоВыплат(НачальнаяДата, ПлановаяДатаПервойВыплаты);
	
	Если НачальнаяДата = ДатаНачПериода ИЛИ НЕ ПерваяНППропорциональноДням Тогда  // это типовое условие
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
		Движение.Размер = РазмерВыплаты;
		
		РазмерПервойВыплаты = РазмерВыплаты*КоличествоВыплатВПервойВыплате;

	Иначе       // а это мой расчет в слуае неполной выплаты
		ДатаКонПериода = КонецМесяца(НачальнаяДата);
		ДоКонцаПериода = ДатаКонПериода-НачальнаяДата+1;
		СуммаВПервомПериоде = РазмерВыплаты*ДоКонцаПериода/(ДатаКонПериода-ДатаНачПериода+1);
		
		//размер выплаты за 1-й месяц
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
		Движение.Размер = СуммаВПервомПериоде;
		
		//размер выплаты за 2-й и последующий месяц
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = ДатаКонПериода+1;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
		Движение.Размер = РазмерВыплаты;
		
		РазмерПервойВыплаты = СуммаВПервомПериоде+РазмерВыплаты*(КоличествоВыплатВПервойВыплате-1);
	КонецЕсли;
	

	Движения.уп_ДанныеПервойВыплатыОПС.Записывать = Истина;
	Движения.уп_ДанныеПервойВыплатыОПС.Очистить();
	Движение = Движения.уп_ДанныеПервойВыплатыОПС.Добавить();
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Период = Дата;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.СуммаПервойВыплаты = РазмерПервойВыплаты;
	Движение.ПлановаяДатаПервойВыплаты = ПлановаяДатаПервойВыплаты;
	Движение.КоличествоВыплатВПервойВыплате = КоличествоВыплатВПервойВыплате;
	
	
	// периодичность выплат  - здесь надо проверить - если уже стоит, то не надо ставить
	Движения.уп_ПериодичностьВыплатОПС.Записывать = Истина;
	Движения.уп_ПериодичностьВыплатОПС.Очистить();
	Движение = Движения.уп_ПериодичностьВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
	
	//состояние частей
	Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Записывать = Истина;
	Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Очистить();
	Движение = Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Добавить();
	#Удаление
	Если НачальнаяДата<=КонецДня(Дата) Тогда
		Движение.Период = Дата;
	Иначе
		Движение.Период = НачальнаяДата;
	КонецЕсли;
	#КонецУдаления
	#Вставка
	Движение.Период = НачальнаяДата;
	// -ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
	#КонецВставки
	Движение.ДатаУстановкиСостояния = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП;
	
	Если УчитыватьДопВзносы Тогда
		Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Записывать = Истина;
		Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Очистить();
		Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
		#Удаление
		Если НачальнаяДата<=КонецДня(Дата) Тогда
			Движение.Период = Дата;
		Иначе
			Движение.Период = НачальнаяДата;
		КонецЕсли;
		#КонецУдаления
		#Вставка
		// +ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
		Движение.Период = НачальнаяДата;
		// -ХМНПФ 13.03.2019 ШадринАВ // ОбработкаПроведения() // №2103
		#КонецВставки
		Движение.ДатаУстановкиСостояния = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП;
	КонецЕсли;	
	
	
	// регистр уп_ЛицевыеСчетаЗастрахованныхЛиц
	// проверить дату записи и совпадение полей
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Период,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Регистратор,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета,
	               |	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипВыплат
	               |ИЗ
	               |	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
	               |			&ДатаДок,
	               |			ДоговорОПС = &ДоговорОПС
	               |				И ТипВыплат = &ТипВыплат) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДок", Дата);
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("ТипВыплат", Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ЗаписьЕсть = Ложь;
	ЗаписьСовпадает = Ложь;
	
	Если Выборка.Следующий() Тогда
	    ЗаписьЕсть = Истина;
		ДатаЗаписи = Выборка.Период;
		
		Если Выборка.ФормаВыплаты=ФормаВыплаты
			и Выборка.Банк = Получатель
			и Выборка.РасчетныйСчет = РасчетныйСчет
			и Выборка.ТипЛицевогоСчета = ТипЛицевогоСчета
			и Выборка.НомерЛицевогоСчета = НомерЛицевогоСчета Тогда
			ЗаписьСовпадает = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	
	Если НЕ ЗаписьСовпадает  Тогда
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
		Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ЗастрахованноеЛицо = ДоговорОПС.Участник;
		Движение.ФормаВыплаты = ФормаВыплаты;
		Движение.Банк = Получатель;
		Движение.РасчетныйСчет = РасчетныйСчет;
		Движение.ТипЛицевогоСчета = ТипЛицевогоСчета;
		Движение.ТипВыплат = Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС;
		Движение.НомерЛицевогоСчета = НомерЛицевогоСчета;
	КонецЕсли;
	
	// проверим доверенное лицо
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.ДоверенноеЛицо,
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.Период,
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.Регистратор
	               |ИЗ
	               |	РегистрСведений.уп_ДоверенныеЛицаУчастников.СрезПоследних(&ДатаДок, Участник = &Участник) КАК уп_ДоверенныеЛицаУчастниковСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДок", Дата);
	Запрос.УстановитьПараметр("Участник", ЗастрахованноеЛицо);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ЗаписьЕсть = Ложь;
	ЗаписьСовпадает = Ложь;
	ДатаСовпадает = Ложь;
	
	Если Выборка.Следующий() Тогда
	    ЗаписьЕсть = Истина;
		ДатаЗаписи = Выборка.Период;
		Если Выборка.ДоверенноеЛицо=ДоверенноеЛицо Тогда
			ЗаписьСовпадает = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	// доверенные лица
	Если НЕ ЗаписьСовпадает Тогда
		Если ЗаписьЕсть ИЛИ ЗначениеЗаполнено(ДоверенноеЛицо) Тогда
			Движения.уп_ДоверенныеЛицаУчастников.Записывать = Истина;
			Движения.уп_ДоверенныеЛицаУчастников.Очистить();
			Движение = Движения.уп_ДоверенныеЛицаУчастников.Добавить();
			Движение.Период = Дата;
			Движение.Участник = ЗастрахованноеЛицо;
			Движение.ДоверенноеЛицо = ДоверенноеЛицо;
		КонецЕсли;
	КонецЕсли;	
	
	//номер очереди
	Движения.уп_НомераОчередейОПС.Записывать = Истина;
	Движения.уп_НомераОчередейОПС.Очистить();
	Движение = Движения.уп_НомераОчередейОПС.Добавить();
	Движение.Период = Дата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.НомерОчереди = НомерОчереди;
	
	//параметры зачисления
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоДоговорам,
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоТипамСумм
	|ИЗ
	|	РегистрСведений.уп_ПенсионныеПравила.СрезПоследних(&Дата, Организация = &Организация) КАК уп_ПенсионныеПравилаСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	    ПоДоговорам = Выборка.ВестиРезервВыплатОПСПоДоговорам;
		ПоТипамСумм = Выборка.ВестиРезервВыплатОПСПоТипамСумм;
	Иначе
		ПоДоговорам = Ложь;
		ПоТипамСумм = Ложь;
	КонецЕсли;
	
	Если Остатки.Количество() = 0 Тогда //старый алгоритм
		//остатки
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	уп_РезервыОПСОстатки.ТипСуммы,
		|	уп_РезервыОПСОстатки.СуммаОстаток
		|ИЗ
		|	РегистрНакопления.уп_РезервыОПС.Остатки(&Дата, ДоговорОПС = &ДоговорОПС) КАК уп_РезервыОПСОстатки";
		
		Запрос.УстановитьПараметр("Дата", Дата-1);
		Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		
		Пока Выборка.Следующий() Цикл
			
			// регистр уп_РезервыОПС Приход
			Движения.уп_РезервыОПС.Записывать = Истина;
			Движения.уп_РезервыОПС.Очистить();
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ДоговорОПС;
			Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
			Движение.ТипСуммы = ПреобразоватьТипСуммы(Выборка.ТипСуммы);
			Движение.Сумма = Выборка.СуммаОстаток;
			
			// регистр уп_РезервыВыплатОПС Приход

			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
			Если ПоДоговорам Тогда
				Движение.ДоговорОПС = ДоговорОПС;
			КонецЕсли;
			Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
			Если ПоТипамСумм Тогда
				Движение.ТипСуммы = ПреобразоватьТипСуммы(Выборка.ТипСуммы);
			КонецЕсли;
			Движение.Сумма = Выборка.СуммаОстаток;
			
			Если ОтражатьВБухгалтерскомУчете Тогда
				ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
				уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ЗастрахованноеЛицо,ДоговорОПС.ДоговорКонтрагента,
				Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ЗастрахованноеЛицо, ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, Выборка.СуммаОстаток);
			КонецЕсли;
		КонецЦикла;
	Иначе  //по 360 ФЗ
		//проверим остатки
		ПроверитьОстатки(Отказ);
		Движения.уп_РезервыОПС.Записывать = Истина;
		Движения.уп_РезервыОПС.Очистить();
		
		Движения.уп_РезервыВыплатОПС.Записывать = Истина;
		Движения.уп_РезервыВыплатОПС.Очистить();
		
		ДатаВступления = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
		Если НЕ Отказ Тогда
			Для Каждого стр из Остатки Цикл
				//спишем пожизненные
				Движение = Движения.уп_РезервыОПС.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
				Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.ДоговорОПС = ДоговорОПС;
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
				Движение.ТипСуммы = стр.ТипСуммы;
				Движение.Сумма = стр.Сумма;
				Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
				Движение.СуммаВосполнения = стр.СуммаВосполнения;
				Движение.СуммаВозмещения = стр.СуммаВозмещения;
				
				Движение = Движения.уп_РезервыОПС.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РВ;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
				Если ПоДоговорам Тогда
					Движение.ДоговорОПС = ДоговорОПС;
				КонецЕсли;
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
				Если ПоТипамСумм Тогда
					Движение.ТипСуммы = ПреобразоватьТипСуммы(стр.ТипСуммы);
				КонецЕсли;
				Движение.Сумма = стр.Сумма;
				Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
				Движение.СуммаВосполнения = стр.СуммаВосполнения;
				Движение.СуммаВозмещения = стр.СуммаВозмещения;
				
				Если ОтражатьВБухгалтерскомУчете Тогда
					ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
					уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ЗастрахованноеЛицо,ДоговорОПС.ДоговорКонтрагента,
					Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ЗастрахованноеЛицо, ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, стр.Сумма);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
					//еще фиксированные обязательства
					//фиксируем в сумме на дату выплаты (вот не знаю - или как раз стоит вычесть)
					Движение = Движения.уп_РазмерЗафиксированныхОбязательств.Добавить();
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.ТипСуммы = стр.ТипСуммы;
					Движение.Сумма = стр.Сумма;
					
					//еще текущая сумма гарантирования (вот тут точно реальную пишем, т.е. уменьшаем на выплату)
					Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.Организация = Организация;
					Движение.ТипСуммы = стр.ТипСуммы;
					Движение.Сумма = стр.СуммаГарантированная;
					
					Если стр.СуммаКомпенсации<>0 Тогда
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Компенсация;
						Движение.Сумма = стр.СуммаКомпенсации;
					КонецЕсли;
					
					Если стр.СуммаВосполнения<>0 Тогда
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Восполнение;
						Движение.Сумма = стр.СуммаВосполнения;
					КонецЕсли;
					
					Если стр.СуммаВозмещения<>0 Тогда
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Возмещение;
						Движение.Сумма = стр.СуммаВозмещения;
					Конецесли;	
					
				КонецЕсли;	
			КонецЦикла;
			Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
				//еще дата фиксации
				Если  уп_СистемаГарантирования.ПроверитьНеобходимостьФиксации(Ссылка, ДоговорОПС) Тогда
					Движение = Движения.уп_ДатаФиксацииОбязательств.Добавить();
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.ДатаФиксации = Дата;
				КонецЕсли;
			КонецЕсли;	
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	//запишем дату назначения
	ДогОПСОбъект = ДоговорОПС.ПолучитьОбъект();
	ДогОПСОбъект.ДатаНазначенияНЧТП = НачальнаяДата;
	ДогОПСОбъект.СрокНЧТППриНазначении = СрокПожизненнойПенсии;
	ДогОПСОбъект.Записать();
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("МассивСообщений", МассивСообщений);
КонецПроцедуры

&ИзменениеИКонтроль("ПроверитьОстатки")
Процедура РасшОбщ_ПроверитьОстатки(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_ПереводВВыплатнойПериодОПСОстатки.ТипСуммы,
	               |	уп_ПереводВВыплатнойПериодОПСОстатки.Сумма,
	               |	уп_ПереводВВыплатнойПериодОПСОстатки.СуммаГарантированная
	               |ПОМЕСТИТЬ ОстаткиПоДокументу
	               |ИЗ
	               |	Документ.уп_РешениеОНазначенииНЧТП.Остатки КАК уп_ПереводВВыплатнойПериодОПСОстатки
	               |ГДЕ
	               |	уп_ПереводВВыплатнойПериодОПСОстатки.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(уп_РезервыОПСОстатки.СуммаОстаток, 0) КАК СуммаВРегистре,
	               |	ОстаткиПоДокументу.ТипСуммы,
	               |	ОстаткиПоДокументу.Сумма,
	               |	ОстаткиПоДокументу.СуммаГарантированная
	               |ИЗ
	               |	ОстаткиПоДокументу КАК ОстаткиПоДокументу
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_РезервыОПС.Остатки(
	               |				&МомВремени,
	               |				ДоговорОПС = &ДоговорОПС
	               |					И ТипРезерва = &Накопительный) КАК уп_РезервыОПСОстатки
	               |		ПО ОстаткиПоДокументу.ТипСуммы = уп_РезервыОПСОстатки.ТипСуммы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ОстаткиПоДокументу";
	
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДатаВступления = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаВРегистре-Выборка.Сумма>0 Тогда
			//сообщение
			Сообщить("Сумма остатка в резерве по типу суммы "+Выборка.ТипСуммы+" больше указанной в документе. Возможно, документ требуется пересчитать.");
		ИначеЕсли  Выборка.СуммаВРегистре-Выборка.Сумма<0 Тогда
			//отказ
			Сообщить("Сумма остатка в табличной части документа по типу суммы "+Выборка.ТипСуммы+" превышает остаток на счете. Документ требуется пересчитать.");
            Отказ = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
			Если Выборка.Сумма<Выборка.СуммаГарантированная Тогда
				#Удаление
				Отказ = Истина;
				#КонецУдаления
				#Вставка
				// +ХМНПФ 28.07.2016 ШадринАВ // ПроверитьОстатки() // №447
				#КонецВставки
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По договору №"+ДоговорОПС+" по типу суммы "+Выборка.ТипСуммы+" остаток на счете меньше гарантированной суммы. Необходимо оформить документ ""Перевод средств из резерва ОПС"".");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
// для получения печатных форм со стороны Web сервисов
// аналог процедуры печать типового варианта пользовательского интерфейса
Функция ХМ_ПечатьWeb(ИмяМакета) Экспорт
	ТабДокумент = Новый ТабличныйДокумент; // заглушка на случай если не сформируется ни чего 
	Если ИмяМакета = "Решение" тогда
		ТабДокумент = ПечатьРешения();
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции 

Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ = Неопределено, СтруктураТЧ = Неопределено) Экспорт
	Если НЕ ПРОВЕДЕН Тогда
		#Если Сервер Тогда
//			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостоянияДоговораОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ПериодичностьВыплатОПС);
//			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС);
//			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ДоверенныеЛицаУчастников);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НомераОчередейОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
			Сообщить("ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(ДогОПСОбъект)");
		#КонецЕсли
		Возврат;	
	КонецЕсли;
	ПоДоговорам = Истина; ПоТипамСумм = Истина;
	
	Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
	
	Движение = Движения.уп_РазмерПенсииОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.Размер = РазмерВыплаты;
	
	
	Движение = Движения.уп_ПериодичностьВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
	
	Движение = Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП;
	
	Если УчитыватьДопВзносы Тогда
		Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
		Движение.Период = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(НомерЛицевогоСчета) Тогда
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
		Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ЗастрахованноеЛицо = ДоговорОПС.Участник;
		Движение.ФормаВыплаты = ФормаВыплаты;
		Движение.Банк = Получатель;
		Движение.РасчетныйСчет = РасчетныйСчет;
		Движение.ТипЛицевогоСчета = ТипЛицевогоСчета;
		Движение.НомерЛицевогоСчета = НомерЛицевогоСчета;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоверенноеЛицо) Тогда
		Движения.уп_ДоверенныеЛицаУчастников.Записывать = Истина;
		Движения.уп_ДоверенныеЛицаУчастников.Очистить();
		Движение = Движения.уп_ДоверенныеЛицаУчастников.Добавить();
		Движение.Период = Дата;
		Движение.Участник = ЗастрахованноеЛицо;
		Движение.ДоверенноеЛицо = ДоверенноеЛицо;
	КонецЕсли;
	
	Движение = Движения.уп_НомераОчередейОПС.Добавить();
	Движение.Период = Дата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная;
	Движение.НомерОчереди = НомерОчереди;
	
	Для Каждого стр из Остатки Цикл
		//спишем пожизненные
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
		Движение.Период =  стр.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
		Движение.ТипСуммы = стр.ТипСуммы;
		Движение.Сумма = стр.Сумма;
		
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период =  стр.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПС;
		Если ПоДоговорам Тогда
			Движение.ДоговорОПС = ДоговорОПС;
		КонецЕсли;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
		Движение.ТипСуммы = стр.ТипСуммы;
//		Если ПоТипамСумм Тогда
//			Движение.ТипСуммы = ПреобразоватьТипСуммы(стр.ТипСуммы);
//		КонецЕсли;
		Движение.Сумма = стр.Сумма;
	КонецЦикла;
	
	//запишем дату назначения
	ДогОПСОбъект = ДоговорОПС.ПолучитьОбъект();
	ДогОПСОбъект.ДатаНазначенияНЧТП = НачальнаяДата;
	ДогОПСОбъект.СрокНЧТППриНазначении = СрокПожизненнойПенсии;
	ДогОПСОбъект.Записать();
	
#Если Сервер Тогда
//	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостоянияДоговораОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ПериодичностьВыплатОПС);
//	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС);
//	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ДоверенныеЛицаУчастников);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НомераОчередейОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(ДогОПСОбъект);
#КонецЕсли
КонецПроцедуры

&После("ПриЗаписи")
Процедура ХМПриЗаписи(Отказ)
//ЛыткинАМ //[GLPI #0007905] Новая заявка 1 С //Открепить решение из основания при пометке на удаление
	Если Отказ = Ложь Тогда
		Если ПометкаУдаления = Истина И ЗначениеЗаполнено(ДокументОснование) Тогда
			уп_ОбработчикУдаления.ХМ_ОткрепитьРешениеВДокументОснование(ДокументОснование, Ссылка);
		КонецЕсли;
	КонецЕсли;
	// Вставить содержимое метода.
КонецПроцедуры


&После("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, Режим)
	МЗУшедший = РегистрыСведений.ХМ_УшедшиеВДругойПФДоговораОПС.СоздатьМенеджерЗаписи();
	МЗУшедший.ДоговорОПС = ДоговорОПС;
	МЗУшедший.Прочитать();
	Если МЗУшедший.Выбран() Тогда
		Сообщить(Строка(ЭтотОбъект) + ". Проведение решения по договору "+ДоговорОПС+" невозможно! Договор находиться в списке ушедших в другой ПФ!");
		Отказ = Истина;
	КонецЕсли;
    ХМ_ПодпискиНаСобытия_Сервер.ХМ_ОбработкаПоСозданиюОповещений(ЭтотОбъект, Отказ, Режим);
КонецПроцедуры


&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);	
КонецПроцедуры

