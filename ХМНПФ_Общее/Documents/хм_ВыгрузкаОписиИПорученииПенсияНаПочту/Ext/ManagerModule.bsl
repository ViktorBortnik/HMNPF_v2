#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Макет";
	КомандаПечати.Представление = НСтр("ru = 'Выплата пенсии на почту'");
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Макет");
	//Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Макет",
			НСтр("ru = 'Выплата пенсии на почту'"),
			ПечатнаяФормаВыплатеПенсииНаПочту(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.Макет");
	//КонецЕсли;
КонецПроцедуры

Функция ПечатнаяФормаВыплатеПенсииНаПочту(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ключВыплатаПенсииНаПочту";	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.Макет");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Участник КАК Участник,
	|	СУММА(хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Выплата) КАК Выплата,
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.НомерСчетаДоговор КАК НомерСчетаДоговор,
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Участок КАК Участок
	|ПОМЕСТИТЬ втТЧ
	|ИЗ
	|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.спУчастники КАК хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники
	|ГДЕ
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Участок,
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.НомерСчетаДоговор,
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Участник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтутчВыплатныеРеестры.уп_ВыплатныеРеестры КАК уп_ВыплатныеРеестры
	|ПОМЕСТИТЬ втВыплатныеРеестры
	|ИЗ
	|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.тчВыплатныеРеестры КАК хм_ВыгрузкаОписиИПорученииПенсияНаПочтутчВыплатныеРеестры
	|ГДЕ
	|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтутчВыплатныеРеестры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПР) КАК ВидДеятельности,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета КАК НомерСчетаДоговор,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Участник КАК Участник,
	|	СУММА(уп_ВыплатныеРеестрыСписокПенсионеров.Сумма) КАК Сумма,
	|	СУММА(уп_ВыплатныеРеестрыСписокПенсионеров.НДФЛ) КАК НДФЛ,
	|	СУММА(уп_ВыплатныеРеестрыСписокПенсионеров.Выплата) КАК Выплата,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка.ТипВыплаты КАК ТипВыплаты
	|ПОМЕСТИТЬ втТЧВыплатныеРеестры
	|ИЗ
	|	Документ.уп_ВыплатныеРеестры.СписокПенсионеров КАК уп_ВыплатныеРеестрыСписокПенсионеров
	|ГДЕ
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка В(ВЫБРАТЬ втВыплатныеРеестры.уп_ВыплатныеРеестры Из втВыплатныеРеестры)
	|	И НЕ уп_ВыплатныеРеестрыСписокПенсионеров.хм_Удержание
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Участник,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка.ТипВыплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ОПС),
	|	уп_ВыплатныеРеестрыДоговорыОПС.ДоговорОПС,
	|	уп_ВыплатныеРеестрыДоговорыОПС.Участник,
	|	СУММА(уп_ВыплатныеРеестрыДоговорыОПС.Сумма),
	|	0 КАК НДФЛ,
	|	СУММА(уп_ВыплатныеРеестрыДоговорыОПС.Выплата),
	|	уп_ВыплатныеРеестрыДоговорыОПС.Ссылка.ТипВыплаты
	|ИЗ
	|	Документ.уп_ВыплатныеРеестры.ДоговорыОПС КАК уп_ВыплатныеРеестрыДоговорыОПС
	|ГДЕ
	|	уп_ВыплатныеРеестрыДоговорыОПС.Ссылка В(ВЫБРАТЬ втВыплатныеРеестры.уп_ВыплатныеРеестры Из втВыплатныеРеестры)
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатныеРеестрыДоговорыОПС.Участник,
	|	уп_ВыплатныеРеестрыДоговорыОПС.ДоговорОПС,
	|	уп_ВыплатныеРеестрыДоговорыОПС.Ссылка.ТипВыплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТЧ.Участок КАК Территория,
	|	СУММА(втТЧ.Выплата) КАК КВыплате,
	|	СУММА(втТЧВыплатныеРеестры.НДФЛ) КАК СуммаНДФЛ,
	|	СУММА(втТЧВыплатныеРеестры.Сумма) КАК СуммаНачислено
	|ПОМЕСТИТЬ втГруппировка
	|ИЗ
	|	втТЧ КАК втТЧ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТЧВыплатныеРеестры КАК втТЧВыплатныеРеестры
	|		ПО втТЧ.Участник = втТЧВыплатныеРеестры.Участник
	|			И втТЧ.НомерСчетаДоговор = втТЧВыплатныеРеестры.НомерСчетаДоговор
	|
	|СГРУППИРОВАТЬ ПО
	|	втТЧ.Участок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГруппировка.Территория КАК Территория,
	|	СУММА(втГруппировка.КВыплате) КАК КВыплате,
	|	СУММА(втГруппировка.СуммаНДФЛ) КАК СуммаНДФЛ,
	|	СУММА(втГруппировка.СуммаНачислено) КАК СуммаНачислено
	|ИЗ
	|	втГруппировка КАК втГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	втГруппировка.Территория
	|
	|УПОРЯДОЧИТЬ ПО
	|	Территория
	|ИТОГИ
	|	СУММА(КВыплате),
	|	СУММА(СуммаНДФЛ),
	|	СУММА(СуммаНачислено)
	|ПО
	|	Территория";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов[0]);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаТерритория = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	Неоплата = "Требуется ручное заполнение";
	Пока ВыборкаТерритория.Следующий() Цикл
		ОбластьСтрока.Параметры.Неоплата = Неоплата;
		ОбластьСтрока.Параметры.Итого = Неоплата;
       	Неоплата = "";

		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, ВыборкаТерритория);	
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли

Функция ПолучитьВыплатныеРеестрыЗаПоПериодуНаСервере(НачалоПериода, КонецПериода) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_ВыплатныеРеестры.Ссылка КАК уп_ВыплатныеРеестры,
	|	уп_ВыплатныеРеестры.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.уп_ВыплатныеРеестры КАК уп_ВыплатныеРеестры
	|ГДЕ
	|	уп_ВыплатныеРеестры.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И уп_ВыплатныеРеестры.Получатель = &Получатель
	|	И уп_ВыплатныеРеестры.Проведен = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	уп_ВыплатныеРеестры.ТипВыплаты";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Получатель", Константы.уп_КонтрагентВыплатЧерезПочту.Получить());

	ТЗ = Запрос.Выполнить().Выгрузить();
	Возврат ТЗ;
	
КонецФункции

Функция ПолучитьУчастниковНаСервере(мРегистраторов, ВидДеятельности, Территория, локВыводитьУчастниковБезУчастков = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПР) КАК ВидДеятельности,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета КАК НомерСчетаДоговор,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Участник КАК Участник,
	|	СУММА(уп_ВыплатныеРеестрыСписокПенсионеров.Сумма) КАК Сумма,
	|	СУММА(уп_ВыплатныеРеестрыСписокПенсионеров.Выплата) КАК Выплата,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка.ТипВыплаты КАК ТипВыплаты
	|ПОМЕСТИТЬ втСчетаДокумента
	|ИЗ
	|	Документ.уп_ВыплатныеРеестры.СписокПенсионеров КАК уп_ВыплатныеРеестрыСписокПенсионеров
	|ГДЕ
	|	(ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПР) В (&ВидДеятельности)
	|			ИЛИ ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПустаяСсылка) = &ВидДеятельности)
	|	И уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка В(&мРегистраторов)
	|	И НЕ уп_ВыплатныеРеестрыСписокПенсионеров.хм_Удержание
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Участник,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета,
	|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка.ТипВыплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ОПС),
	|	уп_ВыплатныеРеестрыДоговорыОПС.ДоговорОПС,
	|	уп_ВыплатныеРеестрыДоговорыОПС.Участник,
	|	СУММА(уп_ВыплатныеРеестрыДоговорыОПС.Сумма),
	|	СУММА(уп_ВыплатныеРеестрыДоговорыОПС.Выплата),
	|	уп_ВыплатныеРеестрыДоговорыОПС.Ссылка.ТипВыплаты
	|ИЗ
	|	Документ.уп_ВыплатныеРеестры.ДоговорыОПС КАК уп_ВыплатныеРеестрыДоговорыОПС
	|ГДЕ
	|	(ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ОПС) В (&ВидДеятельности)
	|			ИЛИ ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПустаяСсылка) = &ВидДеятельности)
	|	И уп_ВыплатныеРеестрыДоговорыОПС.Ссылка В(&мРегистраторов)
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_ВыплатныеРеестрыДоговорыОПС.Участник,
	|	уп_ВыплатныеРеестрыДоговорыОПС.ДоговорОПС,
	|	уп_ВыплатныеРеестрыДоговорыОПС.Ссылка.ТипВыплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСчетаДокумента.ВидДеятельности КАК ВидДеятельности,
	|	втСчетаДокумента.НомерСчетаДоговор КАК НомерСчетаДоговор,
	|	втСчетаДокумента.Участник КАК Участник,
	|	втСчетаДокумента.Сумма КАК Сумма,
	|	втСчетаДокумента.Выплата КАК Выплата,
	|	хм_ДоставочныеУчасткиУчастников.Участок КАК Участок,
	|	хм_ДоставочныеУчасткиУчастников.Участок.ДоставочныйДень КАК УчастокДоставочныйДень,
	|	хм_ДоставочныеУчасткиУчастников.Участок.Владелец КАК УчастокВладелец,
	|	хм_ДоставочныеУчасткиУчастников.Участок.Территория КАК УчастокТерритория,
	|	втСчетаДокумента.ТипВыплаты КАК ТипВыплаты
	|ПОМЕСТИТЬ втУчастникиПоУчасткам
	|ИЗ
	|	втСчетаДокумента КАК втСчетаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_ДоставочныеУчасткиУчастников КАК хм_ДоставочныеУчасткиУчастников
	|		ПО втСчетаДокумента.Участник = хм_ДоставочныеУчасткиУчастников.Участник
	|ГДЕ
	|	(ЕСТЬNULL(хм_ДоставочныеУчасткиУчастников.Участок.Территория, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) = &Территория
	|			ИЛИ &ВыводитьУчастниковБезУчастков = ИСТИНА
	|				И хм_ДоставочныеУчасткиУчастников.Участок ЕСТЬ NULL
	|			ИЛИ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) = &Территория)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	епс_ДополнительныеСведенияСрезПоследних.Значение КАК Значение,
	|	епс_ДополнительныеСведенияСрезПоследних.Объект КАК Объект
	|ПОМЕСТИТЬ втИндекс
	|ИЗ
	|	РегистрСведений.епс_ДополнительныеСведения.СрезПоследних(
	|			,
	|			Свойство = &Индекс
	|				И Объект В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						втУчастникиПоУчасткам.УчастокВладелец КАК УчастокВладелец
	|					ИЗ
	|						втУчастникиПоУчасткам КАК втУчастникиПоУчасткам)) КАК епс_ДополнительныеСведенияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУчастникиПоУчасткам.ВидДеятельности КАК ВидДеятельности,
	|	втУчастникиПоУчасткам.НомерСчетаДоговор КАК НомерСчетаДоговор,
	|	втУчастникиПоУчасткам.Участник КАК Участник,
	|	втУчастникиПоУчасткам.Выплата КАК Выплата,
	|	втУчастникиПоУчасткам.Участок КАК Участок,
	|	втУчастникиПоУчасткам.УчастокДоставочныйДень КАК ДоставочныйДень,
	|	втУчастникиПоУчасткам.УчастокТерритория КАК Территория,
	|	втИндекс.Значение КАК Индекс,
	|	втУчастникиПоУчасткам.ТипВыплаты КАК ТипВыплаты
	|ИЗ
	|	втУчастникиПоУчасткам КАК втУчастникиПоУчасткам
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИндекс КАК втИндекс
	|		ПО втУчастникиПоУчасткам.УчастокВладелец = втИндекс.Объект";
	
	Запрос.УстановитьПараметр("ВыводитьУчастниковБезУчастков", локВыводитьУчастниковБезУчастков);
	Запрос.УстановитьПараметр("мРегистраторов", мРегистраторов);
	Запрос.УстановитьПараметр("ВидДеятельности", ВидДеятельности);
	Запрос.УстановитьПараметр("Территория", Территория);
	Запрос.УстановитьПараметр("Индекс", ПланыВидовХарактеристик.епс_ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","index_sup_num"));
	
	ТЗ = Запрос.Выполнить().Выгрузить();

	Возврат ТЗ;
	
КонецФункции

///////////////////////////////ПолучитьПапкуНаСервере///////////////////////////////

Функция ПолучитьПапкуНаСервере(Документ, ПараметрыВыводаНаПочту) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ.Организация) Тогда
		ВызватьИсключение "Для документа не заполнена организация, выгрузка не возможна";
	КонецЕсли;
	
	Результат = Новый Массив();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Территория КАК Территория,
		|	СУММА(хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Выплата) КАК Выплата,
		|	СУММА(1) КАК КоличествоПоручений
		|ИЗ
		|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.спУчастники КАК хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники
		|ГДЕ
		|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Ссылка = &Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	хм_ВыгрузкаОписиИПорученииПенсияНаПочтуспУчастники.Территория";     
	
	Запрос.УстановитьПараметр("Документ",Документ);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Номер = Число(Документ.Номер);

    МассивВыплатБезКБК = Новый Массив();

	Пока Выборка.Следующий() Цикл
		
		ПараметрыВыгрузки = ПолучитьПараметрыВыгрузкиФайлов(Документ, Выборка, Номер);
		ПараметрыВыгрузки.Вставить("МассивВыплатБезКБК",МассивВыплатБезКБК);

		ПараметрыВыгрузки.Вставить("ЗаполнятьАдресРайонаРегионом",ПараметрыВыводаНаПочту.ЗаполнятьАдресРайонаРегионом);
		ПараметрыВыгрузки.Вставить("ЗаполнятьНаселенныйПунктГородом",ПараметрыВыводаНаПочту.ЗаполнятьНаселенныйПунктГородом);

		Если ПараметрыВыводаНаПочту.НеВыводитьКППОрганизации = Истина Тогда
			ПараметрыВыгрузки.КПП = "";
		КонецЕсли;
		
		АдресВХПоручение = СформироватьИПоместитьФайлВАдресВХ(Документ, ПараметрыВыгрузки, "ТекстПоручений");
		Поручение = Новый Структура("ПодКаталог,АдресВХ,ИмяФайла",Документ.Номер,АдресВХПоручение,ПараметрыВыгрузки.ИмяФайлаПоручения);
	    Результат.Добавить(Поручение);
		
		АдресВХОпись = СформироватьИПоместитьФайлВАдресВХ(Документ, ПараметрыВыгрузки, "ТекстОписи");
		Опись = Новый Структура("ПодКаталог,АдресВХ,ИмяФайла",Документ.Номер,АдресВХОпись,ПараметрыВыгрузки.ИмяФайлаОписи);
	    Результат.Добавить(Опись);
		
		Номер = Номер + 1;
	КонецЦикла;

	ДокОбъект = Документ.ПолучитьОбъект();
	ДокОбъект.НомерПО = Номер - 1;
	ДокОбъект.Записать();
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьПараметрыВыгрузкиФайлов(Документ, ПараметрыТерритории, Номер) 
	
	ПараметрыВыгрузки = Новый Структура();
//ПараметрПоДокументу
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Документ.Организация,Документ.Дата);
	ПараметрыВыгрузки.Вставить("Должность",ОтветственныеЛица.РуководительДолжность); 
	ПараметрыВыгрузки.Вставить("Руководитель",ОтветственныеЛица.Руководитель);
	ПараметрыВыгрузки.Вставить("ДатаВыдачиДокумента",Формат(Документ.Дата,"ДФ=dd.MM.yyyy"));	

	ПараметрыВыгрузки.Вставить("ДатаСоставления",Формат(Документ.Дата,"ДФ=dd.MM.yyyy"));
    ПараметрыВыгрузки.Вставить("Месяц",Месяц(Документ.ДатаОкончания));
	ПараметрыВыгрузки.Вставить("Год",хм_ПрочиеПроцедуры.УбратьПробелыВСтроке(Год(Документ.ДатаОкончания)));  
	//Параметры Составителя
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ.Организация,"ИНН,КПП,НаименованиеСокращенное");
	ПараметрыВыгрузки.Вставить("ИНН",РеквизитыОрганизации.ИНН);
	ПараметрыВыгрузки.Вставить("КПП",РеквизитыОрганизации.КПП);
	ПараметрыВыгрузки.Вставить("НаименованиеОрганизации",РеквизитыОрганизации.НаименованиеСокращенное);
	ПараметрыВыгрузки.Вставить("РегистрационныйНомер","");
 	ПараметрыВыгрузки.Вставить("ТеррОрганНаименованиеОрганизации",РеквизитыОрганизации.НаименованиеСокращенное);

//ПараметрПоТерритории                   
	ПараметрыВыгрузки.Вставить("Территория", ?(ЗначениеЗаполнено(ПараметрыТерритории.Территория),ПараметрыТерритории.Территория,Документ.Территория));
	ПараметрыВыгрузки.Вставить("ПоПодразделению",ПолучитьIdПодразделения(ПараметрыВыгрузки.Территория));	
	ПараметрыВыгрузки.Вставить("СуммаПоМассиву",ПараметрыТерритории.Выплата);
	ПараметрыВыгрузки.Вставить("НомерОписиПоручений",ДобавитьЛидирующиеНули(Номер,5));	
	ШаблонСтрокиИмениФайла = "%1-700-Y-%2-ORG-001-086-000002-DCK-%3-DPT-%4-DCK-%5.XML";
	ИмяФайлаПоручения 	= СтрШаблон(ШаблонСтрокиИмениФайла,"SZN",Формат(Документ.ДатаОкончания, "ДФ=гггг"),"00000",ПараметрыВыгрузки.ПоПодразделению,ПараметрыВыгрузки.НомерОписиПоручений);
    ИмяФайлаОписи 		= СтрШаблон(ШаблонСтрокиИмениФайла,"SZN",Формат(Документ.ДатаОкончания, "ДФ=гггг"),ПараметрыВыгрузки.ПоПодразделению,ПараметрыВыгрузки.ПоПодразделению,ПараметрыВыгрузки.НомерОписиПоручений);
	ПараметрыВыгрузки.Вставить("ИмяФайлаПоручения",ИмяФайлаПоручения);
   	ПараметрыВыгрузки.Вставить("ИмяФайлаОписи",ИмяФайлаОписи);
	ПараметрыВыгрузки.Вставить("КоличествоДокументовПоручений", ПараметрыТерритории.КоличествоПоручений);

	

//КОНСТАНТЫ
	//Параметры заголовка
	Заголовок = Новый Структура();
	Заголовок.Вставить("ВерсияФормата","07.00");	
	Заголовок.Вставить("ТипФайла","ВНЕШНИЙ");	
	Заголовок.Вставить("НазваниеПрограммы","Каспер-Ю");	
	Заголовок.Вставить("Версия","4.61");	
	Заголовок.Вставить("ИсточникДанных","ОСЗН");	
   	ПараметрыВыгрузки.Вставить("Заголовок",Заголовок);
	
	ПараметрыВыгрузки.Вставить("ДоставочнаяОрганизация","ПОЧТА");
    ПараметрыВыгрузки.Вставить("НомерВпачке",1);
	ПараметрыВыгрузки.Вставить("ТипВходящейОписи","ОПИСЬ ПАЧКИ");
   	ПараметрыВыгрузки.Вставить("ТипМассиваПоручений","ОСНОВНОЙ");

	//параметры СоставДокументов
	ПараметрыВыгрузки.Вставить("Количество",1);	
	ПараметрыВыгрузки.Вставить("ТипДокументаОписи","СОПРОВОДИТЕЛЬНАЯ_ОПИСЬ_ПОРУЧЕНИЙ");	
	ПараметрыВыгрузки.Вставить("ТипДокументаПоручения","ПОРУЧЕНИЕ_НА_ДОСТАВКУ_ПОСОБИЙ");	
   	ПараметрыВыгрузки.Вставить("КоличествоДокументовОписи",1);


    //Параметры почта
	ПараметрыВыгрузки.Вставить("ПочтаНаименованиеОрганизации","ИВЦ_УФПС");	
    ПараметрыВыгрузки.Вставить("ПочтаРегистрационныйНомер","");	
	//Параметры ТерриториальныйОрган
	ПараметрыВыгрузки.Вставить("ТеррОрганРегистрационныйНомер","");
	//Параметры Сопоставительная опись
	ПараметрыВыгрузки.Вставить("СопостОписьНомерВпачке",2);	
	
	Возврат ПараметрыВыгрузки;
   	    	
КонецФункции


//ВыгрузитьВПапку-->
Функция СформироватьИПоместитьФайлВАдресВХ(Документ, ПараметрыВыгрузки, ВидТекста)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьXML = НачатьЗаписьXML("ФайлОСЗН",ИмяВременногоФайла);
	Если ВидТекста = "ТекстПоручений" Тогда
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"ИмяФайла",ПараметрыВыгрузки.ИмяФайлаПоручения);
	Иначе
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"ИмяФайла",ПараметрыВыгрузки.ИмяФайлаОписи);
	КонецЕсли;                 
	ВывестиЗаголовок(ЗаписьXML, ПараметрыВыгрузки);
	ЗаписьXML.ЗаписатьНачалоЭлемента("ПачкаВходящихДокументов");
	ЗаписатьАтрибут(ЗаписьXML,"ДоставочнаяОрганизация",ПараметрыВыгрузки.ДоставочнаяОрганизация);
	ЗаписьXML.ЗаписатьНачалоЭлемента("ВХОДЯЩАЯ_ОПИСЬ");	
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НомерВпачке" ,ПараметрыВыгрузки.НомерВпачке);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ТипВходящейОписи",ПараметрыВыгрузки.ТипВходящейОписи);
	ВывестиСоставителя(ЗаписьXML, ПараметрыВыгрузки);
	ВывестиНомерПачки(ЗаписьXML, ПараметрыВыгрузки);
	ВывестиСоставДокументов(ЗаписьXML, ПараметрыВыгрузки, ВидТекста);	
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ДатаСоставления",ПараметрыВыгрузки.ДатаСоставления);
	ВывестиТерриториальныйОрган(ЗаписьXML, ПараметрыВыгрузки);
	ВывестиПочту(ЗаписьXML, ПараметрыВыгрузки);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ТипМассиваПоручений",ПараметрыВыгрузки.ТипМассиваПоручений);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Месяц",ПараметрыВыгрузки.Месяц);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Год",ПараметрыВыгрузки.Год);
	Если ВидТекста = "ТекстПоручений" Тогда
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"СуммаПоМассиву",ПараметрыВыгрузки.СуммаПоМассиву);
	КонецЕсли;                 
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Должность",ПараметрыВыгрузки.Должность);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Руководитель",ПараметрыВыгрузки.Руководитель);
	ЗаписьXML.ЗаписатьКонецЭлемента(); //ВХОДЯЩАЯ_ОПИСЬ
	Если ВидТекста = "ТекстОписи" Тогда
		// Сопроводительная опись поручений
		ВывестиСопроводительнуюОпись(ЗаписьXML, Документ, ПараметрыВыгрузки);
	Иначе	
		// СписокПоручений
		ВывестиСписокПоручений(ЗаписьXML, Документ, ПараметрыВыгрузки);			
	КонецЕсли; 
	ЗаписьXML.ЗаписатьКонецЭлемента(); //ПачкаВходящихДокументов
	ЗаписьXML.ЗаписатьКонецЭлемента(); // корневой элемент
	ЗаписьXML.Закрыть();
	АдресВХ = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла),Новый УникальныйИдентификатор());
	УдалитьФайлы(ИмяВременногоФайла);

	Возврат АдресВХ;
	
КонецФункции

#Область ПолучитьТекстПорученийНаСервере     

&НаСервере
//ПолучитьТекстПорученийНаСервере-->
Процедура ВывестиСписокПоручений(ЗаписьXML, Документ, ПараметрыВыгрузки)
	
	МассивСведений = ПолучитьМассивСведенийПоручений(Документ, ПараметрыВыгрузки);
	Для Каждого СтруктураДанных Из МассивСведений Цикл
		ВывестиСтруктуруВXML(ЗаписьXML,СтруктураДанных,"ПОРУЧЕНИЕ_НА_ДОСТАВКУ_ПОСОБИЙ");	
	КонецЦикла;
	
КонецПроцедуры

#Область ПолучениеДанных

//ПолучитьТекстПорученийНаСервере-->
Функция ПолучитьIdПодразделения(Территория, Строкой = Истина)
		
		idПодразделения = "";
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ХМ_КасперПодразделения.DOC_DEP КАК DOC_DEP
			|ИЗ
			|	РегистрСведений.ХМ_КасперПодразделения КАК ХМ_КасперПодразделения
			|ГДЕ
			|	ХМ_КасперПодразделения.ПодразделениеОрганизации = &Территория";
		
		Запрос.УстановитьПараметр("Территория", Территория);
		
		РезультатЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока РезультатЗапроса.Следующий() Цикл
			idПодразделения = РезультатЗапроса.DOC_DEP;
		КонецЦикла;    
		Если Строкой = Истина Тогда	
			idПодразделения = ?(idПодразделения = "", "00000", ДобавитьЛидирующиеНули(idПодразделения, 5));
	    КонецЕсли;
		Возврат idПодразделения;
		
КонецФункции  
	
//ВывестиСписокПоручений-->
Функция ПолучитьМассивСведенийПоручений(Документ, ПараметрыВыгрузки)
	
	МассивДанных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спУчастники.Участник КАК Участник,
	|	спУчастники.Участник.Код КАК УчастникКод,
	|	спУчастники.Участник.уп_ФизЛицо КАК УчастникФизЛицо,
	|	спУчастники.Выплата КАК Выплата,
	|	спУчастники.ДоставочныйДень КАК ДоставочныйДень,
	|	спУчастники.Участок КАК Участок,
	|	спУчастники.Индекс КАК Индекс,
	|	ЕСТЬNULL(ХМ_АкторИДПоКонтрагентам.ACTOR_ID, 0) КАК ACTOR_ID,
	|	спУчастники.ТипВыплаты КАК ТипВыплаты,
	|	ЕстьNull(ХМ_КБКпоТипуВыплаты.КодВыплатыПоКБК,0) КАК КодВыплатыПоКБК,
	|	ЕстьNull(ХМ_КБКпоТипуВыплаты.НаименованиеВыплатыПоКБК,"""") КАК НаименованиеВыплатыПоКБК
	|ИЗ
	|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.спУчастники КАК спУчастники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_АкторИДПоКонтрагентам КАК ХМ_АкторИДПоКонтрагентам
	|		ПО спУчастники.Участник = ХМ_АкторИДПоКонтрагентам.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХМ_КБКпоТипуВыплаты КАК ХМ_КБКпоТипуВыплаты
	|		ПО спУчастники.ТипВыплаты = ХМ_КБКпоТипуВыплаты.ТипВыплаты
	|ГДЕ
	|	спУчастники.Ссылка = &Документ
	|	И спУчастники.Территория = &Территория
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Территория", ПараметрыВыгрузки.Территория);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	НомерВпачке = 1;

	АдресаУчастников = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
		ТЗ.ВыгрузитьКолонку("УчастникФизЛицо"),
		Перечисления.ТипыКонтактнойИнформации.Адрес, 
		Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица,
		Документ.Дата);
	
	КадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ТЗ.ВыгрузитьКолонку("УчастникФизЛицо"), "Фамилия, Имя, Отчество, ДатаРождения, МестоРождения, Пол, СтраховойНомерПФР, ДокументВид, ДокументСерия, ДокументНомер, ДокументДатаВыдачи, ДокументКемВыдан, ДокументКодПодразделения, Страна");
	
	СоответствиеДУЛ = Новый Соответствие();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХМ_ПочтаУдостоверяющиеДокумент.ДокументФизическогоЛица КАК ДокументФизическогоЛица,
		|	ХМ_ПочтаУдостоверяющиеДокумент.Представление КАК Представление
		|ИЗ
		|	РегистрСведений.ХМ_ПочтаУдостоверяющиеДокумент КАК ХМ_ПочтаУдостоверяющиеДокумент";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СоответствиеДУЛ.Вставить(ВыборкаДетальныеЗаписи.ДокументФизическогоЛица,ВыборкаДетальныеЗаписи.Представление);// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

	Для каждого Выборка Из ТЗ Цикл
		
		НомерВпачке = НомерВпачке + 1;
		СтруктураДанных = Новый Структура("СтраховойНомерПФР,Фамилия,Имя,Отчество,УдостоверяющийДокумент,Участник,Выплата,ДоставочныйДень,Участок,Индекс,НомерВпачке,ACTOR_ID,КодВыплатыПоКБК,НаименованиеВыплатыПоКБК");
		ЗаполнитьЗначенияСвойств(СтруктураДанных,Выборка);
		СтруктураДанных.Вставить("НомерВпачке",НомерВпачке);
		СтруктураДанных.Вставить("НомерВмассиве",ТЗ.Индекс(Выборка)+1);
		мСотрудник = Новый Массив;
		мСотрудник.Добавить(Выборка.УчастникФизЛицо);
		
		КадровыеДанныеФЛ = КадровыеДанныеФизическихЛиц.НайтиСтроки(Новый Структура("ФизическоеЛицо",Выборка.УчастникФизЛицо));
		Если КадровыеДанныеФЛ.Количество() > 0 Тогда			
			стрКД = КадровыеДанныеФЛ[0];
			ЗаполнитьЗначенияСвойств(СтруктураДанных,стрКД);
			СтруктураДанных.Вставить("УдостоверяющийДокумент",ПолучитьСтруктуруУдостоверяющегоДокумента(стрКД,СоответствиеДУЛ));
		Иначе
			Сообщение = Новый СообщениеПользователю();
			Сообщение.КлючДанных = Выборка.УчастникФизЛицо;
			Сообщить("Для " + Выборка.УчастникФизЛицо + " не удалось определить удостоверящий документ");
		КонецЕсли;		
		
		Адрес = АдресаУчастников.НайтиСтроки(Новый Структура("Объект",Выборка.УчастникФизЛицо));
		Если Адрес.Количество() <> 0 Тогда
			ДополнительныеПараметры = Новый Структура("НаименованиеВключаетСокращение", Истина);
			СтруктураАдреса = УправлениеКонтактнойИнформациейБП.СтруктураАдреса(Адрес[0].Значение, ДополнительныеПараметры);
			СтруктураАдреса.ЗначенияПолей = Адрес[0].ЗначенияПолей;
			СтруктураАдреса.ЗначениеJSON  = Адрес[0].Значение;
		Иначе
			СтруктураАдреса = УправлениеКонтактнойИнформациейБП.НовыйСтруктураАдреса();
			Сообщение = Новый СообщениеПользователю();
			Сообщение.КлючДанных = Выборка.УчастникФизЛицо;
			Сообщить("Для " + Выборка.УчастникФизЛицо + " не удалось определить адрес проживания");
		КонецЕсли;
		
		Если Выборка.ACTOR_ID = 0 Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.КлючДанных = Выборка.УчастникФизЛицо;
			Сообщить("Для " + Выборка.УчастникФизЛицо + " не удалось определить номер в Каспер для установки номера выплатного дела");
		КонецЕсли;
		
		Если Выборка.ACTOR_ID = 0 Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.КлючДанных = Выборка.УчастникФизЛицо;
			Сообщить("Для " + Выборка.УчастникФизЛицо + " не удалось определить номер в Каспер для установки номера выплатного дела");
		КонецЕсли;
		Если (Выборка.КодВыплатыПоКБК = 0 ИЛИ Не ЗначениеЗаполнено(Выборка.НаименованиеВыплатыПоКБК)) И ПараметрыВыгрузки.МассивВыплатБезКБК.Найти(Выборка.ТипВыплаты) = Неопределено Тогда
			МЗ = РегистрыСведений.ХМ_КБКпоТипуВыплаты.СоздатьМенеджерЗаписи();
			МЗ.ТипВыплаты = Выборка.ТипВыплаты;
			МЗ.Прочитать();

			Если Не МЗ.Выбран() Тогда
				МЗ.ТипВыплаты = Выборка.ТипВыплаты;
				МЗ.Записать();
			КонецЕсли;
			
			КлючевыеПоля  = Новый Структура();
            КлючевыеПоля.Вставить("ТипВыплаты",Выборка.ТипВыплаты);
			КлючЗаписи = РегистрыСведений.ХМ_КБКпоТипуВыплаты.СоздатьКлючЗаписи(КлючевыеПоля);
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.КлючДанных = КлючЗаписи;
			Сообщить("Для " + Выборка.ТипВыплаты + " не удалось определить данные по КБК"); 
			
			ПараметрыВыгрузки.МассивВыплатБезКБК.Добавить(Выборка.ТипВыплаты);
		КонецЕсли;

		
		
		МассивДанных.Добавить(ПолучитьСтруктуруПоручения(СтруктураДанных, Документ, СтруктураАдреса, ПараметрыВыгрузки));
		
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

Функция ПолучитьСтруктуруУдостоверяющегоДокумента(ДокументФЛ,СоответствиеДУЛ)
	
	СтруктураДокумента = Новый СТруктура;
	Если СоответствиеДУЛ.Получить(ДокументФЛ.ДокументВид) = Неопределено Тогда
		СоответствиеДУЛ.Вставить(ДокументФЛ.ДокументВид,ДокументФЛ.ДокументВид.КодПФР);		

		КлючевыеПоля  = Новый Структура();
        КлючевыеПоля.Вставить("ДокументФизическогоЛица",ДокументФЛ.ДокументВид);
		КлючЗаписи = РегистрыСведений.ХМ_ПочтаУдостоверяющиеДокумент.СоздатьКлючЗаписи(КлючевыеПоля);
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.КлючДанных = КлючЗаписи;
		Сообщить("Для " + ДокументФЛ.ДокументВид + " не удалось определить представление документа для почты"); 
	КонецЕсли;
	
	СтруктураДокумента.Вставить("ТипУдостоверяющего",СоответствиеДУЛ.Получить(ДокументФЛ.ДокументВид));
	Документ = Новый Структура;
	Документ.Вставить("НаименованиеУдостоверяющего",СоответствиеДУЛ.Получить(ДокументФЛ.ДокументВид));  
	
	ПаспортСерия = хм_ПрочиеПроцедуры.УбратьПробелыВСтроке(ДокументФЛ.ДокументСерия);

	Документ.Вставить("СерияРимскиеЦифры",Лев(ПаспортСерия,2));  
	Документ.Вставить("СерияРусскиеБуквы",Сред(ПаспортСерия, 3));
	Документ.Вставить("НомерУдостоверяющего",ДокументФЛ.ДокументНомер);
	Документ.Вставить("ДатаВыдачи",Формат(ДокументФЛ.ДокументДатаВыдачи,"ДФ=dd.MM.yyyy"));
	Документ.Вставить("КемВыдан",ДокументФЛ.ДокументКемВыдан);
	СтруктураДокумента.Вставить("Документ",Документ);
	
	Возврат СтруктураДокумента;
	
КонецФункции

Функция ПолучитьСтруктуруПоручения(СтруктураДанных, Документ, АдресСтруктурой, ПараметрыВыгрузки)
	
	СтруктураПоручения = Новый Структура;
	СтруктураПоручения.Вставить("НомерВпачке",СтруктураДанных.НомерВпачке);
	СтруктураПоручения.Вставить("СистемныйНомерМассива","001-086-000-" +хм_ПрочиеПроцедуры.УбратьПробелыВСтроке(Год(Документ.Дата))+ "0" + ПараметрыВыгрузки.НомерОписиПоручений);
	СтруктураПоручения.Вставить("НомерВмассиве",СтруктураДанных.НомерВмассиве);
	СтруктураПоручения.Вставить("НомерВыплатногоДела",СтруктураДанных.ACTOR_ID);
	СтруктураПоручения.Вставить("НомерОПС",СтруктураДанных.Индекс);
	СтруктураПоручения.Вставить("СтраховойНомер",СтруктураДанных.СтраховойНомерПФР); 
	
	СтруктураПоручения.Вставить("ФИО",Новый Структура("Фамилия,Имя,Отчество",СтруктураДанных.Фамилия,СтруктураДанных.Имя,СтруктураДанных.Отчество));
	СтруктураПоручения.Вставить("УдостоверяющийДокумент",СтруктураДанных.УдостоверяющийДокумент);
	
	МассивВыплат = Новый Массив;
	МассивВыплат.Добавить(ПолучитьСтруктуруВыплаты(СтруктураДанных, Документ));	
	СтруктураПоручения.Вставить("ВсеВыплаты",Новый Структура("Количество,Выплата",1,МассивВыплат)); 
	
	СтруктураПоручения.Вставить("СуммаКдоставке",СтруктураДанных.Выплата);
	СтруктураПоручения.Вставить("ДатаВыдачиПоручения",Формат(Документ.Дата,"ДФ=dd.MM.yyyy"));
	
	СтруктураПоручения.Вставить("АдресДоставки",ПолучитьСтруктуруАдреса(СтруктураДанных.Участник, Документ, АдресСтруктурой, ПараметрыВыгрузки));
	
	СтруктураПоручения.Вставить("НомерУчастка",СтруктураДанных.Участок);      
	Если СтруктураДанных.ДоставочныйДень = 0 Тогда   
		СтруктураПоручения.Вставить("ДатаДоставки", Формат(Документ.ДатаОкончания,"ДФ=dd.MM.yyyy"));
	Иначе
		СтруктураПоручения.Вставить("ДатаДоставки", Формат(Дата(Год(Документ.ДатаОкончания),Месяц(Документ.ДатаОкончания),СтруктураДанных.ДоставочныйДень),"ДФ=dd.MM.yyyy"));
	Конецесли;
	СтруктураПоручения.Вставить("ДополнительнаяИнформация","");
	СтруктураПоручения.Вставить("Сообщение","");
	СтруктураПоручения.Вставить("ШтриховойКод","");
	СтруктураПоручения.Вставить("ПризнакПечатиШтриховогоКода","ПЕЧАТАТЬ");
	
	Возврат СтруктураПоручения;
	
КонецФункции

//ПолучитьСтруктуруПоручения-->
Функция ПолучитьМассивВыплат(ВыплатыПоУчастнику, Документ)
	
	МассивВыплат = Новый Массив;
	
	МассивВыплат.Добавить(ПолучитьСтруктуруВыплаты(ВыплатыПоУчастнику, Документ));
	
	Возврат МассивВыплат;
	
КонецФункции

//ПолучитьМассивВыплат-->
Функция ПолучитьСтруктуруВыплаты(вхСтруктураВыплаты, Документ)
	
	СтруктураВыплаты = Новый Структура;
	СтруктураВыплаты.Вставить("ПризнакВыплаты","Текущая");
	СтруктураВыплаты.Вставить("СуммаКвыплате",вхСтруктураВыплаты.Выплата);
	СтруктураВыплаты.Вставить("ДатаНачалаПериода",Формат(НачалоМесяца(Документ.ДатаОкончания),"ДФ=dd.MM.yyyy"));
	СтруктураВыплаты.Вставить("ДатаКонцаПериода",Формат(КонецМесяца(Документ.ДатаОкончания),"ДФ=dd.MM.yyyy"));
	СтруктураВыплаты.Вставить("КодВыплатыПоКБК",вхСтруктураВыплаты.КодВыплатыПоКБК);
	СтруктураВыплаты.Вставить("НаименованиеВыплатыПоКБК",вхСтруктураВыплаты.НаименованиеВыплатыПоКБК);
	
	Возврат СтруктураВыплаты;
	
КонецФункции

//ПолучитьСтруктуруПоручения-->
Функция ПолучитьСтруктуруАдреса(Участник, Документ, АдресСтруктурой, ПараметрыВыгрузки)
	
	СтруктураАдреса = Новый Структура;
	СтруктураАдреса.Вставить("ТипАдреса","РОССИЙСКИЙ");
	СтруктураАдреса.Вставить("Индекс",АдресСтруктурой.Индекс);
	СтруктураАдреса.Вставить("РоссийскийАдрес",ПолучитьСтруктуруРоссийскогоАдреса(АдресСтруктурой, ПараметрыВыгрузки));
	
	Возврат СтруктураАдреса;
	
КонецФункции

//ПолучитьСтруктуруАдреса-->
Функция ПолучитьСтруктуруРоссийскогоАдреса(АдресСтруктурой, ПараметрыВыгрузки)

	
	
	РосАдрес = Новый Структура;
	
	РосАдрес.Вставить("Регион",Новый Структура("ГеографическоеНазвание,Сокращение",АдресСтруктурой.Регион,АдресСтруктурой.РегионСокращение));
	РосАдрес.Вставить("Район",Новый Структура("ГеографическоеНазвание,Сокращение",АдресСтруктурой.Район,АдресСтруктурой.РайонСокращение));

	Если Не ЗначениеЗаполнено(АдресСтруктурой.Район) И ПараметрыВыгрузки.ЗаполнятьАдресРайонаРегионом = Истина Тогда
		РосАдрес.Вставить("Район",РосАдрес.Регион);
	КонецЕсли;
	
	РосАдрес.Вставить("Город",Новый Структура("ГеографическоеНазвание,Сокращение",АдресСтруктурой.ГородСокращение,ПолучитьСокращениеАдреса(АдресСтруктурой.Город,АдресСтруктурой.ГородСокращение)));
	
	РосАдрес.Вставить("НаселенныйПункт",Новый Структура("ГеографическоеНазвание,Сокращение",АдресСтруктурой.НаселенныйПунктСокращение,ПолучитьСокращениеАдреса(АдресСтруктурой.НаселенныйПункт,АдресСтруктурой.НаселенныйПунктСокращение)));

	Если Не ЗначениеЗаполнено(АдресСтруктурой.НаселенныйПунктСокращение) И ПараметрыВыгрузки.ЗаполнятьНаселенныйПунктГородом = Истина Тогда
		РосАдрес.Вставить("НаселенныйПункт",РосАдрес.Город);
	КонецЕсли;

	РосАдрес.Вставить("Улица",Новый Структура("ГеографическоеНазвание,Сокращение",АдресСтруктурой.УлицаСокращение,ПолучитьСокращениеАдреса(АдресСтруктурой.Улица,АдресСтруктурой.УлицаСокращение)));
	РосАдрес.Вставить("Дом",Новый Структура("Сокращение,Номер",?(ЗначениеЗаполнено(АдресСтруктурой.Дом),"Д.",""),АдресСтруктурой.Дом));
	РосАдрес.Вставить("Корпус",Новый Структура("Сокращение,Номер",?(ЗначениеЗаполнено(АдресСтруктурой.Корпус),"Кор.",""),АдресСтруктурой.Корпус));
	РосАдрес.Вставить("Квартира",Новый Структура("Сокращение,Номер",?(ЗначениеЗаполнено(АдресСтруктурой.Квартира),"Кв.",""),АдресСтруктурой.Квартира));
	
	Возврат РосАдрес;
	
КонецФункции

Функция ПолучитьСокращениеАдреса(Знач ПолныйАдрес, СокращениеАдреса)
	ПолныйАдрес = СтрЗаменить(ПолныйАдрес,СокращениеАдреса,""); 
	ПолныйАдрес = СтрЗаменить(ПолныйАдрес,".",""); 
	ПолныйАдрес = СтрЗаменить(ПолныйАдрес," ","");
	Возврат ПолныйАдрес;
КонецФункции

#КонецОбласти

#Область ВыводДанных

Процедура ВывестиЗаголовок(ЗаписьXML, ПараметрыВыгрузки)
	// заголовок
	ЗаписьXML.ЗаписатьНачалоЭлемента("ЗаголовокФайла");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ВерсияФормата",ПараметрыВыгрузки.Заголовок.ВерсияФормата);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ТипФайла",ПараметрыВыгрузки.Заголовок.ТипФайла);
	ЗаписьXML.ЗаписатьНачалоЭлемента("ПрограммаПодготовкиДанных");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НазваниеПрограммы",ПараметрыВыгрузки.Заголовок.НазваниеПрограммы);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Версия",ПараметрыВыгрузки.Заголовок.Версия);
	ЗаписьXML.ЗаписатьКонецЭлемента(); // программа подготовки данных
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ИсточникДанных",ПараметрыВыгрузки.Заголовок.ИсточникДанных);
	ЗаписьXML.ЗаписатьКонецЭлемента(); // Заголовок файла
КонецПроцедуры

Процедура ВывестиСоставителя(ЗаписьXML, ПараметрыВыгрузки)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СоставительПачки") ;
	ЗаписьXML.ЗаписатьНачалоЭлемента("НалоговыйНомер");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ИНН",ПараметрыВыгрузки.ИНН);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"КПП",ПараметрыВыгрузки.КПП);
	ЗаписьXML.ЗаписатьКонецЭлемента();//НалоговыйНомер
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НаименованиеОрганизации",ПараметрыВыгрузки.НаименованиеОрганизации);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"РегистрационныйНомер",ПараметрыВыгрузки.РегистрационныйНомер);
	ЗаписьXML.ЗаписатьКонецЭлемента();//СоставительПачки
	
КонецПроцедуры

Процедура ВывестиНомерПачки(ЗаписьXML, ПараметрыВыгрузки)
	ЗаписьXML.ЗаписатьНачалоЭлемента("НомерПачки") ;
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ПоПодразделению",ПараметрыВыгрузки.ПоПодразделению);
	ЗаписьXML.ЗаписатьКонецЭлемента();//НомерПачки
КонецПроцедуры

Процедура ВывестиТерриториальныйОрган(ЗаписьXML, ПараметрыВыгрузки)
	ЗаписьXML.ЗаписатьНачалоЭлемента("ТерриториальныйОрган");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НаименованиеОрганизации",ПараметрыВыгрузки.ТеррОрганНаименованиеОрганизации);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"РегистрационныйНомер",ПараметрыВыгрузки.ТеррОрганРегистрационныйНомер);
	ЗаписьXML.ЗаписатьКонецЭлемента(); // Территориальный номер
КонецПроцедуры

Процедура ВывестиПочту(ЗаписьXML, ПараметрыВыгрузки)
	ЗаписьXML.ЗаписатьНачалоЭлемента("Почта");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НаименованиеОрганизации",ПараметрыВыгрузки.ПочтаНаименованиеОрганизации);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"РегистрационныйНомер",ПараметрыВыгрузки.ПочтаРегистрационныйНомер);
	ЗаписьXML.ЗаписатьКонецЭлемента(); // Почта
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область ПолучитьТекстОписиНаСервере     

Функция ПолучитьМассивСведенийОписи(Документ, ПараметрыВыгрузки)
	// тут нужно заполнить массив из структур с данными
	// пока с фонаря заполняю данные
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спУчастники.Индекс КАК Индекс,
	|	спУчастники.Выплата КАК Выплата,
	|	1 КАК КоличествоПоИндексу
	|ПОМЕСТИТЬ спУчастники
	|ИЗ
	|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.спУчастники КАК спУчастники
	|ГДЕ
	|	спУчастники.Ссылка = &Документ
	|	И спУчастники.Территория = &Территория
//	|	И (спУчастники.Ссылка.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПР)
//	|			ИЛИ спУчастники.Ссылка.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПустаяСсылка))
//	|
//	|ОБЪЕДИНИТЬ ВСЕ
//	|
//	|ВЫБРАТЬ
//	|	спУчастники.Индекс,
//	|	спУчастники.Выплата,
//	|	1
//	|ИЗ
//	|	Документ.хм_ВыгрузкаОписиИПорученииПенсияНаПочту.спУчастникиОПС КАК спУчастники
//	|ГДЕ
//	|	спУчастники.Ссылка = &Документ
//	|	И (спУчастники.Ссылка.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ОПС)
//	|			ИЛИ спУчастники.Ссылка.ВидДеятельности = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	спУчастники.Индекс КАК Индекс,
	|	СУММА(спУчастники.Выплата) КАК Выплата,
	|	СУММА(1) КАК КоличествоПоИндексу
	|ИЗ
	|	спУчастники КАК спУчастники
	|
	|СГРУППИРОВАТЬ ПО
	|	спУчастники.Индекс";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Территория", ПараметрыВыгрузки.Территория);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Массив  = Новый Массив;
	
	СуммаПоМассиву = 0;
	КоличествоПоручений = 0;
	Пока Выборка.Следующий() Цикл
		
		СуммаПоМассиву = СуммаПоМассиву + Выборка.Выплата;
		КоличествоПоручений = КоличествоПоручений + Выборка.КоличествоПоИндексу; 
		СтруктураДанных = Новый Структура("ТипСтроки,НомерОПС,КоличествоПоручений,СуммаПоМассиву,СистемныйНомерМассиваПоручений","ДЕТАЛЬНАЯ",Выборка.Индекс,Выборка.КоличествоПоИндексу,Выборка.Выплата,"001-086-000-" +хм_ПрочиеПроцедуры.УбратьПробелыВСтроке(Год(Документ.Дата))+ "0" + ПараметрыВыгрузки.НомерОписиПоручений);
		
		Массив.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	СтруктураДанных = Новый Структура("ТипСтроки,КоличествоПоручений,СуммаПоМассиву","ИТОГО",КоличествоПоручений,СуммаПоМассиву);
	Массив.Добавить(СтруктураДанных);

	
	Возврат Массив;	
	
КонецФункции

Процедура ВывестиСоставДокументов(ЗаписьXML, ПараметрыВыгрузки, ВидТекста)
	ЗаписьXML.ЗаписатьНачалоЭлемента("СоставДокументов");	
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Количество",ПараметрыВыгрузки.Количество);
	ЗаписьXML.ЗаписатьНачалоЭлемента("НаличиеДокументов");
	Если ВидТекста = "ТекстОписи" Тогда
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"ТипДокумента",ПараметрыВыгрузки.ТипДокументаОписи);
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"Количество",ПараметрыВыгрузки.КоличествоДокументовОписи);
	Иначе
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"ТипДокумента",ПараметрыВыгрузки.ТипДокументаПоручения);
		ЗаписатьЗначениеЭлемента(ЗаписьXML,"Количество",ПараметрыВыгрузки.КоличествоДокументовПоручений);
	КонецЕсли;	
	ЗаписьXML.ЗаписатьКонецЭлемента();// НаличеДокументов
	ЗаписьXML.ЗаписатьКонецЭлемента();// состав документов
КонецПроцедуры

Процедура ВывестиСопроводительнуюОпись(ЗаписьXML, Документ, ПараметрыВыгрузки)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("СОПРОВОДИТЕЛЬНАЯ_ОПИСЬ_ПОРУЧЕНИЙ");
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НомерВпачке",ПараметрыВыгрузки.СопостОписьНомерВпачке);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"НомерОписиПоручений",ПараметрыВыгрузки.НомерОписиПоручений);
	
	МассивСведений = ПолучитьМассивСведенийОписи(Документ,ПараметрыВыгрузки);
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"Количество",МассивСведений.Количество()-1);
	Для Каждого СтруктураДанных Из МассивСведений Цикл
		ВывестиСтруктуруВXML(ЗаписьXML,СтруктураДанных,"СведенияОмассивеПоручений");	
	КонецЦикла;
	
	ЗаписатьЗначениеЭлемента(ЗаписьXML,"ДатаВыдачиДокумента",ПараметрыВыгрузки.ДатаВыдачиДокумента);
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); // сопр опись
	                                                           
КонецПроцедуры

#КонецОбласти

#Область РаботаСXML

Функция НачатьЗаписьXML(КорневоеИмя,ИмяВременногоФайла)
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла,"Windows-1251",Истина);
	ЗаписьXML.ЗаписатьОбъявлениеXML(); // это только для файла если в памяти то УстановитьСтроку
	//ЗаписьXML.УстановитьСтроку("Windows-1251");
	ЗаписьXML.ЗаписатьНачалоЭлемента(КорневоеИмя);
	
	Возврат ЗаписьXML;	
	
КонецФункции

Процедура ВставитьЗаголовкиВОтвет(Ответ)
	Ответ.Заголовки.Вставить("Access-Control-Allow-Origin","*");
КонецПроцедуры

Процедура ЗаписатьЗначениеЭлемента(ЗаписьXML,ИмяЭлемента,ЗначениеЭлемента);
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);	
	Если ТипЗнч(ЗначениеЭлемента) = Тип("Число") Тогда
		ЗаписьXML.ЗаписатьТекст(ПроверитьРеквизитНаСумму(ИмяЭлемента,ЗначениеЭлемента));
	ИначеЕсли ТипЗнч(ЗначениеЭлемента) = Тип("Структура") Тогда	
		Для Каждого ЭлементСтруктуры Из ЗначениеЭлемента Цикл
			Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Массив") Тогда
				ЗаписьXML.ЗаписатьНачалоЭлемента(ЭлементСтруктуры.Ключ);	
				Для Каждого Стр Из ЭлементСтруктуры.Значение Цикл
					Если ТипЗнч(Стр) = Тип("Структура") Тогда
						Для Каждого Элемент Из Стр Цикл
							ЗаписатьЗначениеЭлемента(ЗаписьXML,Элемент.Ключ,Элемент.Значение);	
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;	
				ЗаписьXML.ЗаписатьКонецЭлемента();
			Иначе	
				ЗаписатьЗначениеЭлемента(ЗаписьXML,ЭлементСтруктуры.Ключ,ЭлементСтруктуры.Значение);	
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЗаписьXML.ЗаписатьТекст(Строка(ЗначениеЭлемента));
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ЗаписатьАтрибут(ЗаписьXML,Имя,Значение)
	ЗаписьXML.ЗаписатьАтрибут(Имя,ПроверитьРеквизитНаСумму(Имя,Значение));
КонецПроцедуры

Функция ПроверитьРеквизитНаСумму(Имя,Значение)                    
	ПоляДляФорматаСумма = Новый Массив();
	ПоляДляФорматаСумма.Добавить("СуммаПоМассиву");
	ПоляДляФорматаСумма.Добавить("СуммаКвыплате");
	ПоляДляФорматаСумма.Добавить("СуммаКдоставке");
	
	Если ТипЗнч(Значение) = Тип("Число") И НЕ ПоляДляФорматаСумма.Найти(Имя) = Неопределено  Тогда
		Возврат Формат(Значение, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=0");
	Иначе
		Возврат XMLСтрока(Значение);
	КонецЕсли;
КонецФункции

Процедура ВывестиСтруктуруВXML(ЗаписьXML,СтруктураДанных,КорневойЭлемент)
	ЗаписьXML.ЗаписатьНачалоЭлемента(КорневойЭлемент);	
	Для Каждого ЭлементСтруктуры Из СтруктураДанных Цикл
		ЗаписатьЗначениеЭлемента(ЗаписьXML,ЭлементСтруктуры.Ключ,ЭлементСтруктуры.Значение);	
	КонецЦикла;
	ЗаписьXML.ЗаписатьКонецЭлемента();
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ДобавитьЛидирующиеНули(Знач Значение, Длина) Экспорт
       
    Значение = СтрЗаменить(Формат(Значение, "ЧГ="), " ", "");
    НадоНулей = Длина - СтрДлина(Значение);
	Если НадоНулей > 0 Тогда
	    ВедущиеНули = Формат(0,"ЧЦ=" + НадоНулей + ";ЧН=; ЧВН=; ЧГ=0"); 
		Значение = ВедущиеНули + Значение;
	КонецЕсли;
	Возврат  Значение;
   
КонецФункции

#КонецОбласти
