
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ+ 22.08.2016 ШадринАВ // длина номера изменена с 10 до 18 // №459
	// ХМНПФ 16.02.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
КонецПроцедуры

&ИзменениеИКонтроль("ПроверкаСумм")
Функция НПФ_ПроверкаСумм(Заголовок = "")
	
	Принято = Истина;
	
#Вставка
	Если ВестиУчетПоСхемам Тогда
#КонецВставки
	//1-проверка суммы на счете
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РезервыОстатки.НомерСчета КАК НомерСчета,
	               |	РезервыОстатки.ТипРезерва КАК ТипРезерва,
	               |	РезервыОстатки.ТипСуммы КАК ТипСуммы,
	               |	РезервыОстатки.СуммаОстаток КАК Сумма,
	               |	РезервыОстатки.ПенсионнаяСхема КАК ПенсионнаяСхема
	               |ИЗ
	               |	РегистрНакопления.уп_Резервы.Остатки(&МомВремени, НомерСчета В (&СписокСчетов)) КАК РезервыОстатки
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСчета";
#Вставка
	Иначе
		// ХМНПФ+ 15.11.2021 ХлопцевБА // убрал схему из запроса // №3382
		//1-проверка суммы на счете
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РезервыОстатки.НомерСчета КАК НомерСчета,
		|	РезервыОстатки.ТипРезерва КАК ТипРезерва,
		|	РезервыОстатки.ТипСуммы КАК ТипСуммы,
		|	РезервыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Остатки(&МомВремени, НомерСчета В (&СписокСчетов)) КАК РезервыОстатки
		|
		|ДЛЯ ИЗМЕНЕНИЯ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСчета";
	КонецЕсли;
#КонецВставки
	
				   
	СписокСчетов = Пенсионныесчета.ВыгрузитьКолонку("НомерСчета");			   
	Запрос.УстановитьПараметр("СписокСчетов", СписокСчетов);
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ТПС = Остатки.Выгрузить();
#Вставка
	// ХМНПФ+ 15.11.2021 ХлопцевБА // убрал схему из Остатки // №3382
	Если ВестиУчетПоСхемам Тогда
		ТПС.Свернуть("ИнвестДоход,НомерСчета,Схема,ТипРезерва,ТипСуммы","Сумма");
	Иначе
		ТПС.Свернуть("ИнвестДоход,НомерСчета,ТипРезерва,ТипСуммы","Сумма");
	КонецЕсли;
#КонецВставки
	Пока Выборка.Следующий() Цикл
		Если ВестиУчетПоСхемам Тогда
			Строки = ТПС.НайтиСтроки(Новый Структура("НомерСчета,ТипРезерва,ТипСуммы,Схема",Выборка.НомерСчета,Выборка.ТипРезерва,Выборка.ТипСуммы,Выборка.ПенсионнаяСхема));
		Иначе
			Строки = ТПС.НайтиСтроки(Новый Структура("НомерСчета,ТипРезерва,ТипСуммы",Выборка.НомерСчета,Выборка.ТипРезерва,Выборка.ТипСуммы));
		КонецЕсли;
		Если Строки.Количество() > 0 Тогда
			строка = Строки[0];
			Сумма = строка.Сумма;
			строка.Сумма = сумма-Выборка.Сумма;
		КонецЕсли;	
	КонецЦикла;	
	Для каждого строкаТПС из ТПС Цикл
		Если строкаТПС.Сумма<>0 Тогда
			Принято = Ложь;
#Вставка
			// ХМНПФ+ 27.12.2022 ХлопцевБА // корректировка сообщения // №3382
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Остатки по счету "+строкаТПС.НомерСчета+" остаток не совпадает с остатком по регистру Резервы на сумму "+строкаТПС.сумма,,Заголовок);
			// ХМНПФ+ 21.02.2025 ЛыткинАМ // https://redmine.npf.com/issues/4109
			Принято = ЭтотОбъект.ДополнительныеСвойства.Свойство("КасперЗагрузка");
#КонецВставки
#Удаление
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Остатки "+строкаТПС.НомерСтроки+" остаток не совпадает с остатком по регистру Резервы на сумму "+строкаТПС.сумма,,Заголовок);
#КонецУдаления
		КонецЕсли;	            
	КонецЦикла;	
	
	//2-проверка сумм по строкам таблицы распределения
	Если Принято = Истина Тогда
		Для каждого стр из Распределение Цикл
			Если (стр.СуммаНаСчете+стр.СуммаМГД)<(стр.СуммаВыкупная+стр.СуммаВкладчика+стр.СуммаДоговора+стр.СуммаСтраховая) Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Распределение "+стр.НомерСтроки+" распределенная сумма превышает сумму на счете и начисленный доход.",,Заголовок);
			КонецЕсли;
			Если (Стр.СуммаВНачале+стр.СуммаВКонце)<>стр.СуммаВыкупная Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Распределение "+стр.НомерСтроки+" выкупная сумма не равна суммам, выплачиваемым в начале и в конце.",,Заголовок);
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
	//3-проверка сумм по строкам таблицы получателей
	Если Принято = Истина Тогда
		ВозвратВкладчикам = 0;
		ВыкупнаяСумма = 0;
		Для каждого стр из Получатели Цикл
			Если стр.ВидРаспределения = Перечисления.уп_ВидыРаспределения.ВозвратВкладчику Тогда
				ВозвратВкладчикам = ВозвратВкладчикам+стр.СуммаВыплаты;
			Иначе
				ВыкупнаяСумма = ВыкупнаяСумма+стр.СуммаВыплаты;
			КонецЕсли;
			Если (Стр.СуммаВНачале+стр.СуммаВКонце) <> стр.СуммаВыплаты Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Получатели "+стр.НомерСтроки+"  сумма выплаты не равна суммам, выплачиваемым в начале и в конце.",,Заголовок);
			КонецЕсли;
			Если стр.НБ>стр.СуммаВыплаты Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Получатели "+стр.НомерСтроки+"  сумма налоговой базы превышает сумму выплаты.",,Заголовок);
			КонецЕсли;	
			Если стр.НБВНачале>стр.СуммаВНачале Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Получатели "+стр.НомерСтроки+"  сумма налоговой базы превышает сумму выплаты (для суммы, выплачиваемой в начале).",,Заголовок);
			КонецЕсли;
			Если стр.НБВКонце>стр.СуммаВКонце Тогда
				Принято = Ложь;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По строке таблицы Получатели "+стр.НомерСтроки+"  сумма налоговой базы превышает сумму выплаты (для суммы, выплачиваемой в конце).",,Заголовок);
			КонецЕсли;
		КонецЦикла;	
		Для каждого стр из Правопреемники Цикл
			ВыкупнаяСумма = ВыкупнаяСумма+стр.Сумма;
		КонецЦикла;	
		Если ВозвратВкладчикам>Распределение.Итог("СуммаВкладчика") Тогда
			Принято = Ложь;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Сумма возврата вкладчикам по таблице Получатели превышает сумму возврата по таблице Распределение.",,Заголовок);
		КонецЕсли;
		Если ВыкупнаяСумма>Распределение.Итог("СуммаВыкупная") Тогда
			Принято = Ложь;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Выкупная сумма по таблице Получатели превышает выкупную сумму по таблице Распределение.",,Заголовок);
		КонецЕсли;
	КонецЕсли;	
	
	//проверка остатков на отрицательные
	Для Каждого стр из Остатки Цикл 
		Если стр.Сумма<0 Тогда
			Принято = Ложь;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Остаток по ПС "+СокрЛП(стр.НомерСчета)+" по типу суммы "+стр.ТипСуммы+" меньше нуля.",,Заголовок);
		КонецЕсли;
		Если стр.СуммаНезафиксированногоИД<>0 Тогда
			//Принято = Ложь;
			Сообщить("По счету "+стр.НомерСчета+" есть незафиксированный инвест. доход. Необходимо провести фиксацию обязательств.");
		КонецЕсли;	
	КонецЦикла;	 
	
	Возврат Принято;
КонецФункции

&После("РассчитатьДокумент")
Процедура НПФ_РассчитатьДокумент()
	
	// ХМНПФ+ 15.11.2021 ХлопцевБА // убрал схему из Остатки // №3382
	тзОстатки = Остатки.Выгрузить();
	Для Каждого Стр Из  тзОстатки Цикл
		Схема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", Стр.НомерСчета)).Схема;
		Стр.Схема= Схема;
	КонецЦикла;
	
	тзОстатки.Свернуть("НомерСчета,Схема,ТипРезерва,ТипСуммы","Сумма,СуммаРасход,СуммаОбщая,ИнвестДоход");

	Остатки.загрузить(тзОстатки);
	
	тзРаспределение = Распределение.Выгрузить();
	Для Каждого Стр Из  тзРаспределение Цикл
		Схема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", Стр.НомерСчета)).Схема;
		Стр.Схема= Схема;
	КонецЦикла;
	тзРаспределение.Свернуть("НомерСчета,Схема,ТипРезерва,ТипСуммы,ОблагаетсяНДФЛ","СуммаНаСчете,СуммаМГД,СуммаВыкупная,СуммаВНачале,СуммаВКонце,СуммаСтраховая,СуммаДоговора,СуммаВкладчика");

	Распределение.загрузить(тзРаспределение);
	
	Если ВидРасторженияДоговора <> Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
		
		Договор31 = хм_ПрочиеПроцедуры.ПолучитьЗначениеПоИдентификатору("Договор№31");
		
		Для Каждого Стр Из Распределение Цикл
			Если  Стр.НомерСчета.Владелец = Договор31 Тогда
				Если Стр.СуммаНаСчете > 0 Тогда
					Стр.СуммаДоговора =  Стр.СуммаНаСчете;	
					Стр.СуммаВыкупная = 0;
					Стр.СуммаВНачале = 0;
				КонецЕсли;	
			КонецЕсли;	
		КонецЦикла
	КонецЕсли;

КонецПроцедуры

	
// ХМНПФ 16.02.2017 ШадринАВ // Новая ХМ_ЗАГРУЗКА_ОбработкаПроведения() // №779
Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	Если Проведен Тогда
		//Контекст = ЭтотОбъект.ДополнительныеСвойства.КасперЗагрузка.Контекст;
		//ПрошлыеДвижения = ЭтотОбъект.ДополнительныеСвойства.КасперЗагрузка.ПрошлыеДвижения;
		//
		//
		//уп_ОбщегоНазначенияСервер.ХМ_ПеренестиРезервыНачислений(Движения.уп_Резервы,ПрошлыеДвижения.уп_Резервы,Контекст.Структура_Реквизиты.МетаданныеЗначения_Имя);
		//
		//Движения.уп_Резервы.Записать();
		//РучнаяКорректировка = Истина;
	//Иначе
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	МассивСообщений = Новый СписокЗначений;
	СрезПенсионныхПравил = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	ВестиУчетПСпоТипамДоговоровЕПС = СрезПенсионныхПравил.ВестиУчетПСпоТипамДоговоровЕПС;
	ПроводкиПоНачислению = СрезПенсионныхПравил.ПроводкиПоНачислению;
	лРасшифровкаСР = СрезПенсионныхПравил.РасшифровкаИспользованияСР_ИД;
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	//ПроводкиПоНачислению = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация)).ПроводкиПоНачислению;
	ДатаНачалаУчетаСРСхемы = Константы.уп_ДатаНачалаВеденияСтраховогоРезерваПоСхемам.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаУчетаСРСхемы) И ДатаНачалаУчетаСРСхемы<=Дата Тогда
		УчетСРПоСхемам = Истина;
	Иначе
		УчетСРПоСхемам = Ложь;
	КонецЕсли;
	ДатаНачалаВеденияУчетаПоСхемам = Константы.уп_УчетРезерваНПОвРазрезеСхем.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаВеденияУчетаПоСхемам) И ДатаНачалаВеденияУчетаПоСхемам<=Дата Тогда
		ВестиУчетПоСхемам = Истина;
	Иначе
		ВестиУчетПоСхемам = Ложь;
	КонецЕсли;
	
	если не ВсеРеквизитыЗаполнены(Заголовок) тогда
		Отказ=Истина;
		Возврат;
	конецесли;
	
	если не ПроверкаСумм(Заголовок) тогда
		Отказ = Истина;
		Возврат;
	конецесли;
	
	КодДохода = ОпределитьКодДохода();
	Если НЕ ЗначениеЗаполнено(КодДохода) И Получатели.Итог("НБ")<>0 Тогда
		Отказ = Истина;
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("В плане видов расчета ""Основные начисления"" не указан код дохода НДФЛ",,Заголовок);
		Возврат
	КонецЕсли;
	
	УчетнаяПолитикаНПФ = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	СчетНачисленияИДПриЗакрытии = УчетнаяПолитикаНПФ.СчетНачисленияИДПриЗакрытии;
	Если ЗначениеЗаполнено(СчетНачисленияИДПриЗакрытии) Тогда
		СчетИД = СчетНачисленияИДПриЗакрытии;
	Иначе
		СчетИД = ПланыСчетов.Хозрасчетный.СтраховойРезервПР;
	КонецЕсли;
	
	ДатаНачалаУчетаРезервовНачисления = Константы.уп_ДатаНачалаВеденияРезервовНачисленияНПО.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаУчетаРезервовНачисления) И Дата>=ДатаНачалаУчетаРезервовНачисления Тогда
		лУчетРезервовНачисленияНПО = Истина;	
	Иначе
		лУчетРезервовНачисленияНПО = Ложь;	
	КонецЕсли;
	Если НачалоДня(ДатаНачалаВыплат) = НачалоДня(Дата) Тогда
		ДатаНВыплат = Дата;
	Иначе
		ДатаНВыплат = ДатаНачалаВыплат;
	КонецЕсли;
	//списание со счета
	Для каждого стр из Распределение Цикл
		//ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета",Стр.НомерСчета)).Схема;
		//1-зачисление на счет суммы гарантированного дохода
		Если ВестиУчетПоСхемам или ЗначениеЗаполнено(стр.Схема) Тогда
			ТекСхема = стр.Схема;
		Иначе
			ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", стр.НомерСчета)).Схема;
		КонецЕсли;
		Если стр.СуммаМГД<>0 Тогда
			Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное
				или ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
				ВидДвижения = Перечисления.уп_ВидыДвижений.ИДПриЗакрытии;
			ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
				ВидДвижения = Перечисления.уп_ВидыДвижений.ИДПриЗакрытииПоСмерти;
			Иначе
				ВидДвижения = Перечисления.уп_ВидыДвижений.ПустаяСсылка();
			КонецЕсли;
			Если стр.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
				//ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхРезервов.ПереводыВозвратов_в_РПАР;
				ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхРезервов.ПереводыИз_Страхового_В_РПВ;
				ВидДвиженияПоТипуЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.ИД_Страховой;
			ИначеЕсли стр.ТипРезерва = Перечисления.уп_ТипыРезерва.СПС Тогда
				ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхРезервов.ПереводыИз_Страхового_На_СПС;
				ВидДвиженияПоТипуЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.ИД_Инвестиционный;
			Иначе	
				ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхРезервов.ПереводыИз_Страхового_На_ИПС;
				ВидДвиженияПоТипуЕПС = Перечисления.уп_ВидыДвиженийПоТипамЕПС.ИД_Инвестиционный;
			Конецесли;	
			Если СчетИД = ПланыСчетов.Хозрасчетный.СтраховойРезервПР Тогда
				Если УчетСРПоСхемам Тогда
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,Дата,Справочники.уп_ПенсионныеСчета.ПустаяСсылка(),Перечисления.уп_ТипыРезерва.Страховой,Справочники.уп_ТипыСумм.ПустаяСсылка(),стр.СуммаМГД, ВидДвижения, стр.Схема, стр.НомерСчета, ВидДвиженияЕПС, ВидДвиженияПоТипуЕПС);
				Иначе
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,Дата,Справочники.уп_ПенсионныеСчета.ПустаяСсылка(),Перечисления.уп_ТипыРезерва.Страховой,Справочники.уп_ТипыСумм.ПустаяСсылка(),стр.СуммаМГД, ВидДвижения, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(),стр.НомерСчета, ВидДвиженияЕПС, ВидДвиженияПоТипуЕПС);
				КонецЕсли;
			КонецЕсли;
			ТипС = уп_ОбщегоНазначенияУчетРезервов.ОпределитьТипСуммыИнвестДохода(стр.ТипСуммы);
			ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,Дата,стр.НомерСчета,стр.ТипРезерва,ТипС,стр.СуммаМГД, ВидДвижения, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), ВидДвиженияЕПС, ВидДвиженияПоТипуЕПС);
			ДвиженияПоИнвестДоходу(Дата,стр.НомерСчета,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаМГД, ТекСхема);
			ДвижениеПоАналитикеСР_Расход(стр.НомерСчета, лРасшифровкаСР, стр.СуммаМГД);

			Если стр.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
				ПризнакКт = Истина;
			Иначе
				ПризнакКт = Ложь;
			КонецЕсли;	
			
			ДвиженияПоБухУчету(Дата, Перечисления.уп_ВидыДвижений.ИДПриЗакрытии, Справочники.Контрагенты.ПустаяСсылка(), Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), 
			Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, стр.НомерСчета.Владелец.Вкладчик, стр.НомерСчета.Владелец.ДоговорКонтрагента, ТекСхема, ПризнакКт, стр.СуммаМГД);
		КонецЕсли;
		//2-перевод суммы в страховой резерв
		Если стр.СуммаСтраховая>0 Тогда
			ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(стр.ТипРезерва,Перечисления.уп_ТипыРезерва.Страховой);
			//ОПА НПО 2018.04.28 +
			Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
				лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ЗакрытиеСчетов",стр.НомерСчета,,5,ВестиУчетПСпоТипамДоговоровЕПС);
			Иначе
				лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ЗакрытиеСчетов",стр.ТипРезерва,,5,ВестиУчетПСпоТипамДоговоровЕПС);
			КонецЕсли;
			ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,Дата,стр.НомерСчета,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаСтраховая, Перечисления.уп_ВидыДвижений.СтраховойПриЗакрытии, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
			Если УчетСРПоСхемам Тогда
				ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,Дата,Справочники.уп_ПенсионныеСчета.ПустаяСсылка(),Перечисления.уп_ТипыРезерва.Страховой,Справочники.уп_ТипыСумм.ПустаяСсылка(),стр.СуммаСтраховая, Перечисления.уп_ВидыДвижений.СтраховойПриЗакрытии, стр.Схема, стр.НомерСчета, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
			Иначе
				ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,Дата,Справочники.уп_ПенсионныеСчета.ПустаяСсылка(),Перечисления.уп_ТипыРезерва.Страховой,Справочники.уп_ТипыСумм.ПустаяСсылка(),стр.СуммаСтраховая, Перечисления.уп_ВидыДвижений.СтраховойПриЗакрытии, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), стр.НомерСчета, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
			КонецЕсли; 
			ДвижениеПоАналитикеСР(стр.СуммаСтраховая, стр.НомерСчета);
			Если стр.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
				ПризнакДт = Истина;
			Иначе
				ПризнакДт = Ложь;
			КонецЕсли;	
			
			ДвиженияПоБухУчету(Дата, Перечисления.уп_ВидыДвижений.СтраховойПриЗакрытии, стр.НомерСчета.Владелец.Вкладчик, стр.НомерСчета.Владелец.ДоговорКонтрагента, 
			ТекСхема, ПризнакДт, Справочники.Контрагенты.ПустаяСсылка(), Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, стр.СуммаСтраховая);
		КонецЕсли;
		//3-перевод суммы на счет договора
		Если стр.СуммаДоговора>0 Тогда
			ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(стр.ТипРезерва,стр.ТипРезерва);
			//ОПА НПО 2018.04.28 +
			Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
				лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ЗакрытиеСчетов",стр.НомерСчета,стр.НомерСчета.Владелец.ПенсионныйСчет,6,ВестиУчетПСпоТипамДоговоровЕПС);
			Иначе
				лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ЗакрытиеСчетов",стр.ТипРезерва,стр.ТипРезерва,6,ВестиУчетПСпоТипамДоговоровЕПС);
			КонецЕсли;	
#Вставка
			//ЛыктинАМ //https://redmine.npf.com/issues/4149
			Если ЗначениеЗаполнено(стр.НомерСчета.Владелец.ПаритетныйДоговор) Тогда
				НомерСчетаСПС = стр.НомерСчета.Владелец.ПаритетныйДоговор.ПенсионныйСчет;	
			Иначе
				НомерСчетаСПС = стр.НомерСчета.Владелец.ПенсионныйСчет;
			КонецЕсли;
			ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,Дата,стр.НомерСчета,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаДоговора, Перечисления.уп_ВидыДвижений.ВозвратНаСчетДоговораПриЗакрытии, ТекСхема, НомерСчетаСПС, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
#КонецВставки
#Удаление
			ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,Дата,стр.НомерСчета,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаДоговора, Перечисления.уп_ВидыДвижений.ВозвратНаСчетДоговораПриЗакрытии, ТекСхема, стр.НомерСчета.Владелец.ПенсионныйСчет, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
#КонецУдаления
			СхемаДоговора = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", стр.НомерСчета.Владелец.ПенсионныйСчет)).Схема;
#Вставка
            //ЛыктинАМ //https://redmine.npf.com/issues/4110
			БылТипРезерва = стр.ТипРезерва;
			Если стр.ТипРезерва = Перечисления.уп_ТипыРезерва.ИПС Тогда
				стр.ТипРезерва = Перечисления.уп_ТипыРезерва.СПС;
	        КонецЕсли;
			ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,Дата,НомерСчетаСПС,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаДоговора, Перечисления.уп_ВидыДвижений.ВозвратНаСчетДоговораПриЗакрытии, СхемаДоговора, стр.НомерСчета, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
            //ЛыктинАМ //https://redmine.npf.com/issues/4110
			стр.ТипРезерва = БылТипРезерва;
#КонецВставки
#Удаление
			ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,Дата,стр.НомерСчета.Владелец.ПенсионныйСчет,стр.ТипРезерва,стр.ТипСуммы,стр.СуммаДоговора, Перечисления.уп_ВидыДвижений.ВозвратНаСчетДоговораПриЗакрытии, СхемаДоговора, стр.НомерСчета, ВидДвиженияЕПС, лВидДвиженияПоТипамЕПС);
#КонецУдаления
			//здесь не должно быть движений по Бух учету, т.к. перемещение внутри договора
		КонецЕсли;	
	КонецЦикла;	
	
	//начисление
	ТЗ = Распределение.Выгрузить();
	Для каждого стр из Получатели Цикл
		//списание выкупной суммы в объеме распределения
		//списание суммы вкладчику в объеме выплаты
		НадоСписать = стр.СуммаВыплаты;
		НадоСписатьВНачале = стр.СуммаВНачале;
		НадоСписатьВКонце = стр.СуммаВКонце;
		НадоНБВНачале = стр.НБВНачале-стр.НБВЧастиВзносовФЛВНачале;
		НадоНБВКонце = стр.НБВКонце-стр.НБВЧастиВзносовФЛВКонце;
		НадоНДФЛВНачале = стр.НДФЛВНачале;
		НадоНДФЛВКонце = стр.НДФЛВКонце;
		НадоНБВЧастиВзносовФЛВНачале = стр.НБВЧастиВзносовФЛВНачале;
		НадоНБВЧастиВзносовФЛВКонце = стр.НБВЧастиВзносовФЛВКонце;
		Для каждого строкаТЗ из ТЗ Цикл
			Если ВестиУчетПоСхемам или ЗначениеЗаполнено(строкаТЗ.Схема) Тогда
				ТекСхема = строкаТЗ.Схема;
			Иначе
				ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", строкаТЗ.НомерСчета)).Схема;
			КонецЕсли;
			Если стр.ВидРаспределения = Перечисления.уп_ВидыРаспределения.ВозвратВкладчику Тогда
				Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
					лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.НомерСчета,,4,ВестиУчетПСпоТипамДоговоровЕПС);
				Иначе
					лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.ТипРезерва,,4,ВестиУчетПСпоТипамДоговоровЕПС);
				КонецЕсли;
				Если (строкаТЗ.СуммаВкладчика>0) и (НадоСписатьВНачале>0) Тогда
					СуммаКСписаниюВНачале = Мин(НадоСписатьВНачале,строкаТЗ.СуммаВкладчика);
					СуммаНБВНачале = Мин(НадоНБВНачале,строкаТЗ.СуммаВкладчика);
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,ДатаНВыплат,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы,СуммаКСписаниюВНачале, Перечисления.уп_ВидыДвижений.ВозвратВкладчикуПриЗакрытии,  ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), , лВидДвиженияПоТипамЕПС);//списываем из резерва
					СтрокаТЗ.СуммаВкладчика = строкаТЗ.СуммаВкладчика-СуммаКСписаниюВНачале;
					НадоСписатьВНачале = НадоСписатьВНачале-СуммаКСписаниюВНачале;
					НадоНБВНачале = НадоНБВНачале-СуммаНБВНачале;
					
					ДвиженияПоНачислению(ДатаНВыплат,стр.Получатель,СуммаКСписаниюВНачале,СуммаНБВНачале,стр.ВидРаспределения,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы, ТекСхема);//начисляем к выплате
					Если СтрокаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
						ПризнакДт = Истина;
					Иначе
						ПризнакДт = Ложь;
					КонецЕсли;
					Если ПроводкиПоНачислению Тогда
						ДвиженияПоБухУчету(ДатаНВыплат, Перечисления.уп_ВидыДвижений.ВозвратВкладчикуПриЗакрытии, строкаТЗ.НомерСчета.Владелец.Вкладчик, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, 
						ТекСхема, ПризнакДт, стр.Получатель, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, ТекСхема, Ложь, СуммаКСписаниюВНачале);
					КонецЕсли;
				КонецЕсли;
				Если (строкаТЗ.СуммаВкладчика>0) и (НадоСписатьВКонце>0) Тогда
					СуммаКСписаниюВКонце = Мин(НадоСписатьВКонце,строкаТЗ.СуммаВкладчика);
					СуммаНБВКонце = Мин(НадоНБВКонце,строкаТЗ.СуммаВкладчика);
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,КонецПериода,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы,СуммаКСписаниюВКонце, Перечисления.уп_ВидыДвижений.ВозвратВкладчикуПриЗакрытии, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), , лВидДвиженияПоТипамЕПС);//списываем из резерва
					СтрокаТЗ.СуммаВкладчика = строкаТЗ.СуммаВкладчика-СуммаКСписаниюВКонце;
					НадоСписатьВКонце = НадоСписатьВКонце-СуммаКСписаниюВКонце;
					НадоНБВКонце = НадоНБВКонце-СуммаНБВКонце;
					
					ДвиженияПоНачислению(КонецПериода,стр.Получатель,СуммаКСписаниюВКонце,СуммаНБВКонце,стр.ВидРаспределения,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы, ТекСхема);//начисляем к выплате
					Если СтрокаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
						ПризнакДт = Истина;
					Иначе
						ПризнакДт = Ложь;
					КонецЕсли;
					Если ПроводкиПоНачислению Тогда
						ДвиженияПоБухУчету(КонецПериода, Перечисления.уп_ВидыДвижений.ВозвратВкладчикуПриЗакрытии, строкаТЗ.НомерСчета.Владелец.Вкладчик, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, 
						ТекСхема, ПризнакДт, стр.Получатель, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, ТекСхема, Ложь, СуммаКСписаниюВКонце);
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное
					или ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
					ВидДвижения = Перечисления.уп_ВидыДвижений.ЗакрытиеСчетаНПОРасторжение;
				ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
					ВидДвижения = Перечисления.уп_ВидыДвижений.ЗакрытиеСчетаНПОСмерть;
				КонецЕсли;	
				Если (строкаТЗ.СуммаВНачале>0) и (НадоСписатьВНачале>0) Тогда
					СуммаКСписаниюВНачале = Мин(НадоСписатьВНачале,строкаТЗ.СуммаВНачале);
					Если (ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное) И
						(строкаТЗ.ТипСуммы = Справочники.уп_ТипыСумм.ВзносыФиз) Тогда
						//здесь другой код дохода
						Если НадоНБВЧастиВзносовФЛВНачале>0 Тогда
							СуммаНБФЛВНачале = Мин(НадоНБВЧастиВзносовФЛВНачале,строкаТЗ.СуммаВНачале);
							НадоНБВЧастиВзносовФЛВНачале = НадоНБВЧастиВзносовФЛВНачале-СуммаНБФЛВНачале;
							Если (СуммаНБФЛВНачале<>0) Тогда
								ДвиженияПоНДФЛ(ДатаНВыплат,стр.Получатель,СуммаНБФЛВНачале,строкаТЗ.НомерСчета,КодДоходаВзносыФЛ, Истина);
								СуммаНБВНачале = СуммаНБФЛВНачале;
							Иначе
								СуммаНБВНачале = 0;
							КонецЕсли;
						Иначе
							СуммаНБВНачале = 0;
						КонецЕсли;
					Иначе
						СуммаНБВНачале = Мин(НадоНБВНачале,строкаТЗ.СуммаВНачале);
						НадоНБВНачале = НадоНБВНачале-СуммаНБВНачале;
						Если (СуммаНБВНачале<>0) Тогда
							ДвиженияПоНДФЛ(ДатаНВыплат,стр.Получатель,СуммаНБВНачале,строкаТЗ.НомерСчета,КодДохода, Ложь);
						КонецЕсли;
					КонецЕсли;	
					Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда
						Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.НомерСчета,,2,ВестиУчетПСпоТипамДоговоровЕПС);
						Иначе
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.ТипРезерва,,2,ВестиУчетПСпоТипамДоговоровЕПС);
						Конецесли;
					Иначе
						Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.НомерСчета,,1,ВестиУчетПСпоТипамДоговоровЕПС);
						Иначе
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.ТипРезерва,,1,ВестиУчетПСпоТипамДоговоровЕПС);
						Конецесли;
					КонецЕсли;	
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,ДатаНВыплат,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы,СуммаКСписаниюВНачале, ВидДвижения, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), , лВидДвиженияПоТипамЕПС);//списываем из резерва
					Если лУчетРезервовНачисленияНПО Тогда
						лТипРезерваНачисления = строкаТЗ.ТипРезерва;
						Если строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ИПС Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС;	
						ИначеЕсли строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.СПС Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС;	
						ИначеЕсли строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат;	
						КонецЕсли;
						ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,ДатаНВыплат,строкаТЗ.НомерСчета,лТипРезерваНачисления,строкаТЗ.ТипСуммы,СуммаКСписаниюВНачале, ВидДвижения, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка());//записываем в резерв начислений
					КонецЕсли;					
					СтрокаТЗ.СуммаВНачале = строкаТЗ.СуммаВНачале-СуммаКСписаниюВНачале;
					НадоСписатьВНачале = НадоСписатьВНачале-СуммаКСписаниюВНачале;
					
					ДвиженияПоНачислению(ДатаНВыплат,стр.Получатель,СуммаКСписаниюВНачале,СуммаНБВНачале,стр.ВидРаспределения,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы, ТекСхема);//начисляем к выплате
					//еще надо пропорционально разделить НДФЛ
					//Если (НадоНБВНачале = 0) и (НадоНДФЛВНачале>0) Тогда
					//	СуммаНДФЛВНачале = НадоНДФЛВНачале;
					//Иначе
					//	СуммаНДФЛВНачале = Окр((СуммаНБВНачале*стр.НДФЛВНачале/стр.НБВНачале),0,1);
					//КонецЕсли;	
					//НадоНДФЛВНачале = НадоНДФЛВНачале-СуммаНДФЛВНачале;
					Если СтрокаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
						ПризнакДт = Истина;
					Иначе
						ПризнакДт = Ложь;
					КонецЕсли;
					Если ПроводкиПоНачислению Тогда
						//определим на какого получателя формировать проводку
						Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
							ТекПолучатель = стр.Получатель;
						Иначе
							ТекПолучатель = ОпределитьПолучателя(стр.Получатель, СтрокаТЗ.НомерСчета);
						КонецЕсли;
						//если расторжение, то смотрим, какой лицевой счет зарегистрирован на текущий номер счета
						
						//если наследование, то смотрим по пустому номеру счета
						
						ДвиженияПоБухУчету(ДатаНВыплат, ВидДвижения, строкаТЗ.НомерСчета.Владелец.Вкладчик, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, 
						ТекСхема, ПризнакДт, ТекПолучатель, ТекПолучатель.ОсновнойДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаКСписаниюВНачале);
					КонецЕсли;
				КонецЕсли;
				Если (строкаТЗ.СуммаВКонце>0) и (НадоСписатьВКонце>0) Тогда
					СуммаКСписаниюВКонце = Мин(НадоСписатьВКонце,строкаТЗ.СуммаВКонце);
					Если (ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное) И
						(строкаТЗ.ТипСуммы = Справочники.уп_ТипыСумм.ВзносыФиз) Тогда
						//здесь другой код дохода
						Если НадоНБВЧастиВзносовФЛВКонце>0 Тогда
							СуммаНБФЛВКонце = Мин(НадоНБВЧастиВзносовФЛВКонце,строкаТЗ.СуммаВКонце);
							НадоНБВЧастиВзносовФЛВКонце = НадоНБВЧастиВзносовФЛВКонце-СуммаНБФЛВКонце;
							Если (СуммаНБФЛВКонце<>0) Тогда
								ДвиженияПоНДФЛ(КонецПериода,стр.Получатель,СуммаНБФЛВКонце,строкаТЗ.НомерСчета,КодДоходаВзносыФЛ, Истина);
							КонецЕсли;
						КонецЕсли;	
					Иначе
						СуммаНБВКонце = Мин(НадоНБВКонце,строкаТЗ.СуммаВКонце);
						НадоНБВКонце = НадоНБВКонце-СуммаНБВКонце;
						Если (СуммаНБВКонце<>0)  Тогда
							ДвиженияПоНДФЛ(КонецПериода,стр.Получатель,СуммаНБВКонце,строкаТЗ.НомерСчета,КодДохода, Ложь);
						КонецЕсли;
					КонецЕсли;
					Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда
						Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.НомерСчета,,2,ВестиУчетПСпоТипамДоговоровЕПС);
						Иначе
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.ТипРезерва,,2,ВестиУчетПСпоТипамДоговоровЕПС);
						КонецЕсли;
					Иначе
						Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.НомерСчета,,1,ВестиУчетПСпоТипамДоговоровЕПС);
						Иначе
							лВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(ДатаНВыплат,"уп_ЗакрытиеСчетов",строкаТЗ.ТипРезерва,,1,ВестиУчетПСпоТипамДоговоровЕПС);
						КонецЕсли;
					КонецЕсли;	
					ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,КонецПериода,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы,СуммаКСписаниюВКонце, ВидДвижения, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка(), , лВидДвиженияПоТипамЕПС);//списываем из резерва
					Если лУчетРезервовНачисленияНПО Тогда
						лТипРезерваНачисления = строкаТЗ.ТипРезерва;
						Если строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ИПС Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС;	
						ИначеЕсли строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.СПС Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС;	
						ИначеЕсли строкаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
							лТипРезерваНачисления = Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат;	
						КонецЕсли;
						ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,КонецПериода,строкаТЗ.НомерСчета,лТипРезерваНачисления,строкаТЗ.ТипСуммы,СуммаКСписаниюВКонце, ВидДвижения, ТекСхема, Справочники.уп_ПенсионныеСчета.ПустаяСсылка());//записываем в резерв начислений
					КонецЕсли;	
					
					СтрокаТЗ.СуммаВКонце = строкаТЗ.СуммаВКонце-СуммаКСписаниюВКонце;
					НадоСписатьВКонце = НадоСписатьВКонце-СуммаКСписаниюВКонце;
					
					ДвиженияПоНачислению(КонецПериода,стр.Получатель,СуммаКСписаниюВКонце,СуммаНБВКонце,стр.ВидРаспределения,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы, ТекСхема);//начисляем к выплате
					//еще надо пропорционально разделить НДФЛ
					//Если (НадоНБВКонце = 0) и (НадоНДФЛВКонце>0) Тогда
					//	СуммаНДФЛВКонце = НадоНДФЛВКонце;
					//Иначе
					//	СуммаНДФЛВКонце = Окр((СуммаНБВКонце*стр.НДФЛВКонце/стр.НБВКонце),0,1);
					//КонецЕсли;	
					//НадоНДФЛВКонце = НадоНДФЛВКонце-СуммаНДФЛВКонце;
					Если СтрокаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
						ПризнакДт = Истина;
					Иначе
						ПризнакДт = Ложь;
					КонецЕсли;
					Если ПроводкиПоНачислению Тогда
						//определим на какого получателя формировать проводку
						ТекПолучатель = ОпределитьПолучателя(стр.Получатель, СтрокаТЗ.НомерСчета);
						//если расторжение, то смотрим, какой лицевой счет зарегистрирован на текущий номер счета
						
						//если наследование, то смотрим по пустому номеру счета
						
						ДвиженияПоБухУчету(КонецПериода, ВидДвижения, строкаТЗ.НомерСчета.Владелец.Вкладчик, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, 
						ТекСхема, ПризнакДт, ТекПолучатель, ТекПолучатель.ОсновнойДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаКСписаниюВКонце);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
			Если (НадоСписатьВНачале = 0) и (НадоСписатьВКонце = 0) Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		Если (НадоСписатьВНачале <> 0) или (НадоСписатьВКонце <> 0) Тогда
			Отказ = Истина;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Невозможно списать начисленную сумму получателю "+стр.Получатель,,Заголовок);
		КонецЕсли;	
	КонецЦикла;
	
	//по правопреемникам
	Для каждого стр из Правопреемники Цикл
		//списание выкупной суммы в объеме распределения
		//списание суммы вкладчику в объеме выплаты
		НадоСписать = стр.Сумма;
		Для каждого строкаТЗ из ТЗ Цикл
			Если ВестиУчетПоСхемам или ЗначениеЗаполнено(строкаТЗ.Схема) Тогда
				ТекСхема = стр.Схема;
			Иначе
				ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", строкаТЗ.НомерСчета)).Схема;
			КонецЕсли;	
			СхемаПравопреемника = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", стр.НомерСчета)).Схема;
			Если (строкаТЗ.СуммаВНачале>0 или строкаТЗ.СуммаВКонце>0) и (НадоСписать>0) Тогда
				СуммаКСписанию = Мин(НадоСписать,(строкаТЗ.СуммаВНачале+строкаТЗ.СуммаВКонце));
				ВидДвиженияЕПС = уп_НПОСервер.ОпределитьВидДвиженияЕПСПоРезервамНПО(строкаТЗ.ТипРезерва, Перечисления.уп_ТипыРезерва.ИПС);
				ДвиженияПоРезервам(ВидДвиженияНакопления.Расход,ДатаНВыплат,строкаТЗ.НомерСчета,строкаТЗ.ТипРезерва,строкаТЗ.ТипСуммы,СуммаКСписанию, Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва, ТекСхема, стр.НомерСчета, ВидДвиженияЕПС);//списываем из резерва
				//теперь эту же сумму надо зачислить на счет правопреемника
				НовТипСуммы = уп_ОбщегоНазначенияУчетРезервов.ОпределитьТипСуммыПравопреемника(строкаТЗ.ТипСуммы);
				ДвиженияПоРезервам(ВидДвиженияНакопления.Приход,ДатаНВыплат,стр.НомерСчета,Перечисления.уп_ТипыРезерва.ИПС,НовТипСуммы,СуммаКСписанию, Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва, СхемаПравопреемника, строкаТЗ.НомерСчета, ВидДвиженияЕПС);//списываем из резерва
				
				Если СтрокаТЗ.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат Тогда
					ПризнакДт = Истина;
				Иначе
					ПризнакДт = Ложь;
				КонецЕсли;

				ДвиженияПоБухУчету(ДатаНВыплат, Перечисления.уп_ВидыДвижений.ПеремещениеВнутриРезерва, строкаТЗ.НомерСчета.Владелец.Вкладчик, строкаТЗ.НомерСчета.Владелец.ДоговорКонтрагента, 
				ТекСхема, ПризнакДт, стр.НомерСчета.Владелец.Вкладчик, стр.НомерСчета.Владелец.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, СуммаКСписанию);
				
				Если (строкаТЗ.СуммаВНачале-СуммаКСписанию)>=0 Тогда
					СтрокаТЗ.СуммаВНачале = строкаТЗ.СуммаВНачале-СуммаКСписанию;
				Иначе
					Недостача =  СуммаКСписанию-строкаТЗ.СуммаВНачале;
					СтрокаТЗ.СуммаВНачале = 0;
					СтрокаТЗ.СуммаВКонце = СтрокаТЗ.СуммаВКонце-Недостача;
				КонецЕсли;	
				НадоСписать = НадоСписать-СуммаКСписанию;
			КонецЕсли;
			Если (НадоСписать = 0) Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;
		Если (НадоСписать <> 0)Тогда
			Отказ = Истина;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Невозможно списать начисленную сумму правопреемнику "+стр.Получатель,,Заголовок);
		КонецЕсли;	
	КонецЦикла;	
	
	Если Не Отказ Тогда
		//проверка остатков по счетам
		Для каждого строкаТЗ из ТЗ Цикл
			Если (СтрокаТЗ.СуммаВкладчика<>0) или (строкаТЗ.СуммаВНачале<>0) или (строкаТЗ.СуммаВКонце<>0) Тогда
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Остаток на счете "+строкаТЗ.НомерСчета+" по типу резерва "+строкаТЗ.ТипРезерва+", по типу суммы "+строкаТЗ.ТипСуммы+" составляет "+(строкаТЗ.СуммаВкладчика+строкаТЗ.СуммаВНачале+строкаТЗ.СуммаВКонце),,Заголовок);
				МассивСообщений.Добавить("Остаток на счете "+строкаТЗ.НомерСчета+" по типу резерва "+строкаТЗ.ТипРезерва+", по типу суммы "+строкаТЗ.ТипСуммы+" составляет "+(строкаТЗ.СуммаВкладчика+строкаТЗ.СуммаВНачале+строкаТЗ.СуммаВКонце));
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;
	
	Движения.уп_Резервы.Записать();
	Движения.уп_ИнвестДоход.Записать();
	Движения.уп_ПрочиеНачисления.Записать();
	Движения.уп_АналитикаСтраховогоРезерва.Записать();
	
	Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
		ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.ПоСмерти;
		ТекРасшифровка = Перечисления.уп_РасшифровкаСостоянийПС.ПоСмерти;
	ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда
		ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.Досрочное;
		ТекРасшифровка = Перечисления.уп_РасшифровкаСостоянийПС.Расторжение;
	ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
		ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.ПереходВДругойНПФ;
		ТекРасшифровка = Перечисления.уп_РасшифровкаСостоянийПС.Расторжение;
	Иначе
		ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.ДоговорВыполнен;
		ТекРасшифровка = Перечисления.уп_РасшифровкаСостоянийПС.ОбязательстваВыполнены;
	КонецЕсли;
	
	//доли правопреемников
	Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
		Для Каждого стр из ЗаявленияНаследников Цикл
			Если стр.Разрешить Тогда
				Движение = Движения.уп_ДолиПравопреемниковНПО.Добавить();
				Движение.Участник = Участник;
				Движение.Заявление = стр.Заявление;
				Движение.Доля = стр.Доля;
				Движение.Правопреемник = стр.Правопреемник;
				Движение.Период = Дата;
			Конецесли;	
		КонецЦикла;	
	КонецЕсли;	
	
	//проверить остатки не счете после проведения и проверить прочие начисления после проведения - если ни там, ни там ничего нет, то счет можно закрывать
	//ГраницаПолученияОстатков = Новый Граница(ДатаНВыплат, ВидГраницы.Включая);
	ГраницаПолученияОстатков = КонецДня(ДатаНачалаВыплат);  
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_ЗакрытиеСчетовПенсионныеСчета.НомерСчета КАК НомерСчета,
	               |	уп_ЗакрытиеСчетовПенсионныеСчета.ПенсионныйДоговор КАК ПенсионныйДоговор,
	               |	уп_ЗакрытиеСчетовПенсионныеСчета.ПенсионныйДоговор.ПенсионныйСчет КАК СчетДоговора,
	               |	уп_ЗакрытиеСчетовПенсионныеСчета.ПенсионныйДоговор.Вкладчик КАК Вкладчик
	               |ПОМЕСТИТЬ ТабСчетов
	               |ИЗ
	               |	Документ.уп_ЗакрытиеСчетов.ПенсионныеСчета КАК уп_ЗакрытиеСчетовПенсионныеСчета
	               |ГДЕ
	               |	уп_ЗакрытиеСчетовПенсионныеСчета.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	уп_ПрочиеНачисленияОбороты.НомерСчета КАК НомерСчета,
	               |	СУММА(уп_ПрочиеНачисленияОбороты.СуммаПриход) КАК СуммаПриход
	               |ПОМЕСТИТЬ ПрочиеНачисленияПоСчетам
	               |ИЗ
	               |	РегистрНакопления.уп_ПрочиеНачисления.Обороты(&ДатаНачВыплат, &ДатаКонВыплат, Запись, ) КАК уп_ПрочиеНачисленияОбороты
	               |ГДЕ
	               |	уп_ПрочиеНачисленияОбороты.Регистратор = &Ссылка
	               |	И уп_ПрочиеНачисленияОбороты.ТипВыплаты <> &ТипВозврат
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	уп_ПрочиеНачисленияОбороты.НомерСчета
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(уп_ПрочиеНачисленияОбороты.СуммаПриход) КАК СуммаПриход,
	               |	уп_ПрочиеНачисленияОбороты.НомерСчета.Владелец КАК ПенсионныйДоговор
	               |ПОМЕСТИТЬ ПрочиеНачисленияПоДоговорам
	               |ИЗ
	               |	РегистрНакопления.уп_ПрочиеНачисления.Обороты(&ДатаНачВыплат, &ДатаКонВыплат, Запись, ) КАК уп_ПрочиеНачисленияОбороты
	               |ГДЕ
	               |	уп_ПрочиеНачисленияОбороты.Регистратор = &Ссылка
	               |	И уп_ПрочиеНачисленияОбороты.ТипВыплаты <> &ТипВозврат
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	уп_ПрочиеНачисленияОбороты.НомерСчета.Владелец
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
	               |	СУММА(уп_РезервыОстатки.СуммаОстаток) КАК СуммаОстаток
	               |ПОМЕСТИТЬ РезервыПоСчетам
	               |ИЗ
	               |	РегистрНакопления.уп_Резервы.Остатки(&ДатаПослеДокумента, НомерСчета В (&СписокСчетов)) КАК уп_РезервыОстатки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	уп_РезервыОстатки.НомерСчета
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(уп_РезервыОстатки.СуммаОстаток) КАК СуммаОстаток,
	               |	уп_РезервыОстатки.НомерСчета.Владелец КАК ПенсионныйДоговор
	               |ПОМЕСТИТЬ РезервыПоДоговорам
	               |ИЗ
	               |	РегистрНакопления.уп_Резервы.Остатки(
	               |			&ДатаПослеДокумента,
	               |			НомерСчета.Владелец В
	               |				(ВЫБРАТЬ
	               |					ТабСчетов.ПенсионныйДоговор
	               |				ИЗ
	               |					ТабСчетов)) КАК уп_РезервыОстатки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	уп_РезервыОстатки.НомерСчета.Владелец
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТабСчетов.НомерСчета КАК НомерСчета,
	               |	ЕСТЬNULL(РезервыПоСчетам.СуммаОстаток, 0) КАК ОстатокВРезерве,
	               |	ЕСТЬNULL(ПрочиеНачисленияПоСчетам.СуммаПриход, 0) КАК ОборотНачислений,
	               |	ЕСТЬNULL(РезервыПоДоговорам.СуммаОстаток, 0) КАК ОстатокВРезервеДоговор,
	               |	ЕСТЬNULL(ПрочиеНачисленияПоДоговорам.СуммаПриход, 0) КАК ОборотНачисленийДоговор,
	               |	ТабСчетов.ПенсионныйДоговор КАК ПенсионныйДоговор,
	               |	ТабСчетов.Вкладчик КАК Вкладчик,
	               |	уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора КАК ШаблонДоговора,
	               |	ЕСТЬNULL(уп_ПараметрыПенсионногоДоговораСрезПоследних.ШаблонДоговора.ЗакрыватьРегламентом, ЛОЖЬ) КАК ЗакрыватьРегламентом
	               |ИЗ
	               |	ТабСчетов КАК ТабСчетов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РезервыПоСчетам КАК РезервыПоСчетам
	               |		ПО ТабСчетов.НомерСчета = РезервыПоСчетам.НомерСчета
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПрочиеНачисленияПоСчетам КАК ПрочиеНачисленияПоСчетам
	               |		ПО ТабСчетов.НомерСчета = ПрочиеНачисленияПоСчетам.НомерСчета
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РезервыПоДоговорам КАК РезервыПоДоговорам
	               |		ПО ТабСчетов.ПенсионныйДоговор = РезервыПоДоговорам.ПенсионныйДоговор
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПрочиеНачисленияПоДоговорам КАК ПрочиеНачисленияПоДоговорам
	               |		ПО ТабСчетов.ПенсионныйДоговор = ПрочиеНачисленияПоДоговорам.ПенсионныйДоговор
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПараметрыПенсионногоДоговора.СрезПоследних(
	               |				&ДатаДок,
	               |				ПенсионныйДоговор В
	               |					(ВЫБРАТЬ
	               |						ТабСчетов.ПенсионныйДоговор
	               |					ИЗ
	               |						ТабСчетов)) КАК уп_ПараметрыПенсионногоДоговораСрезПоследних
	               |		ПО ТабСчетов.ПенсионныйДоговор = уп_ПараметрыПенсионногоДоговораСрезПоследних.ПенсионныйДоговор";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаПослеДокумента", Новый Граница(ГраницаПолученияОстатков,ВиДГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаНачВыплат", ДатаНВыплат-1);
	Запрос.УстановитьПараметр("ДатаКонВыплат", КонецПериода+1);
	Запрос.УстановитьПараметр("ДатаДок", Дата);
	Запрос.УстановитьПараметр("ТипВозврат", Перечисления.уп_ТипыВыплат.ВозвратВзносов);
	Запрос.УстановитьПараметр("СписокСчетов", ПенсионныеСчета.ВыгрузитьКолонку("НомерСчета"));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НадоЗакрывать = Ложь;
		Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное
			или ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
			Если ЗначениеЗаполнено(Выборка.ШаблонДоговора) Тогда
				Если Выборка.ШаблонДоговора.ПорядокЗакрытияПСПриРасторжении = 1 Тогда
					НадоЗакрывать = Истина;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
		Если (Выборка.ОборотНачислений + Выборка.ОстатокВРезерве) = 0 
			или НадоЗакрывать Тогда //счет надо закрывать
			Движение = Движения.уп_СостоянияПС.Добавить();
			Движение.Период = Дата;
			Движение.Регистратор = Ссылка;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.Состояние = Перечисления.уп_СостоянияПС.Закрыт;
			// НАЧАЛО 14.12.2017 golnev@orticongroup.ru npo
			Движение.Расшифровка = ТекРасшифровка;
			// КОНЕЦ 14.12.2017 golnev@orticongroup.ru
			
			//если это личный договор, то договор закрыть тоже надо
			Если Выборка.Вкладчик.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
				Если (Выборка.ОборотНачисленийДоговор + Выборка.ОстатокВРезервеДоговор) = 0 
					или НадоЗакрывать Тогда //договор надо закрывать
					Движение = Движения.уп_СостоянияДоговора.Добавить();
					Движение.Активность = Истина;
					Движение.Период = Дата;
					Движение.ПенсионныйДоговор = Выборка.ПенсионныйДоговор;
					Движение.Состояние = Перечисления.уп_СостоянияДоговора.Закрыт;
					
					ПД = Выборка.ПенсионныйДоговор.ПолучитьОбъект();
					Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
						ПД.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.ПоСмерти;
					ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда
						ПД.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.Досрочное;
					ИначеЕсли ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда
						ПД.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияПС.ПереходВДругойНПФ;
					КонецЕсли;	
					ПД.Записать();
				КонецЕсли;	
			КонецЕсли; 
			//если будем переносить очистку даты следующей фиксации из заявлений о расторжении, то сюда
			ДатаПоследнейФиксации = РегистрыСведений.уп_ДатаФиксацииОбязательствНПО.ПолучитьПоследнее(Дата-1, Новый Структура("НомерСчета", Выборка.НомерСчета));
			Если ЗначениеЗаполнено(ДатаПоследнейФиксации.ДатаСледующейФиксации) Тогда 
				Движение = Движения.уп_ДатаФиксацииОбязательствНПО.Добавить();
				Движение.НомерСчета = Выборка.НомерСчета; 
				Движение.Период = Дата;
				Движение.ДатаНачалаПериодаФиксации = ДатаПоследнейФиксации.ДатаНачалаПериодаФиксации;  
				Движение.ДатаФиксации = ДатаПоследнейФиксации.ДатаФиксации;
				//дату следующей фиксации не устанавливаем
			КонецЕсли;	
		КонецЕсли;
		ПС = Выборка.НомерСчета.ПолучитьОбъект();
		ПС.ПричинаЗакрытия = ПричинаЗакрытия;
		ПС.Записать();
		//и в регистр записать причину, если ее там еще нет
		ТекПричина = РегистрыСведений.уп_ПричиныЗакрытияПС.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета", Выборка.НомерСчета)).ПричинаЗакрытия;
		//если причина еще не прописана, то
		Если НЕ (ЗначениеЗаполнено(ТекПричина))или ТекПричина<>ПричинаЗакрытия Тогда
			Движение = Движения.уп_ПричиныЗакрытияПС.Добавить();
			Движение.Период = Дата;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.ПричинаЗакрытия = ПричинаЗакрытия;
		КонецЕсли;
	КонецЦикла;
	
	//если мы применяем социальный вычет, то надо еще прописать сумму, которую применили
	
	//проверим остатки на конец года от даты начала выплаты
	ГраницаПолученияОстатков = КонецГода(ДатаНачалаВыплат);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_РезервыОстатки.НомерСчета КАК НомерСчета,
	|	СУММА(уп_РезервыОстатки.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.уп_Резервы.Остатки(
	|			&ДатаПослеДокумента,
	|			НомерСчета В (&СписокСчетов)
	|				И ТипРезерва В (&СписокТиповРезерва)) КАК уп_РезервыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_РезервыОстатки.НомерСчета";
	Запрос.УстановитьПараметр("ДатаПослеДокумента", ГраницаПолученияОстатков);
	Запрос.УстановитьПараметр("СписокСчетов", ПенсионныеСчета.ВыгрузитьКолонку("НомерСчета"));
	СписокТиповРезерва = Новый СписокЗначений;
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ИПС);
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.СПС);
	СписокТиповРезерва.Добавить(Перечисления.уп_ТипыРезерва.ПожизненныхВыплат);
	Запрос.УстановитьПараметр("СписокТиповРезерва", СписокТиповРезерва);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если (Выборка.СуммаОстаток) < 0 Тогда //предупреждение
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Отрицательный остаток на "+ Формат(ГраницаПолученияОстатков,"ДФ=dd.MM.yyyy")+" на счете "+Выборка.НомерСчета+". Проверьте, возможно в системе присутствует дублирующий документ закрытия счетов.",,Заголовок);
			МассивСообщений.Добавить("Отрицательный остаток на "+ Формат(ГраницаПолученияОстатков,"ДФ=dd.MM.yyyy")+" на счете "+Выборка.НомерСчета+". Проверьте, возможно в системе присутствует дублирующий документ закрытия счетов.");
		КонецЕсли;	
	КонецЦикла;
	
	Если ОтражатьВБухгалтерскомУчете   Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		Если ОтражатьВБухгалтерскомУчете Тогда
			уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
		КонецЕсли;
	КонецЕсли;	
	
	Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное
		или ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда 
		Если ЗначениеЗаполнено(РегистрирующийДокумент) Тогда
			//установим статус заявления
			Движение = Движения.уп_СостоянияЗаявленийНПО.Добавить();
			Движение.Период = Дата;
			Движение.Заявление = РегистрирующийДокумент;
			Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда
				Движение.ТипЗаявления = Перечисления.уп_ТипыЗаявленийНПО.ВыплатаВыкупнойСуммы; 
				Движение.Состояние = Перечисления.уп_СостоянияЗаявленийНПО.РешениеОВыплате;
			Иначе
				Движение.ТипЗаявления = Перечисления.уп_ТипыЗаявленийНПО.ПереводВДругойНПФ; 
				Движение.Состояние = Перечисления.уп_СостоянияЗаявленийНПО.РешениеОПереводе;
			КонецЕсли;
			Движение.Регистратор = Ссылка;
		КонецЕсли;
	Иначе  //это по смерти
		Для Каждого стр из ЗаявленияНаследников Цикл
			Если ЗначениеЗаполнено(стр.Заявление) Тогда 
			//установим статус заявления
			Движение = Движения.уп_СостоянияЗаявленийНПО.Добавить();
			Движение.Период = Дата;
			Движение.Заявление = стр.Заявление;
			Если стр.Разрешить Тогда
				Движение.Состояние = Перечисления.уп_СостоянияЗаявленийНПО.РешениеОВыплате;
			Иначе 
				Движение.Состояние = Перечисления.уп_СостоянияЗаявленийНПО.РешениеОбОтказе;
			КонецЕсли;
			Движение.ТипЗаявления = Перечисления.уп_ТипыЗаявленийНПО.ВыплатаПравопреемникам; 
			Движение.Регистратор = Ссылка;
			КонецЕсли;	
		КонецЦикла;	
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("МассивСообщений", МассивСообщений);
	//Движения.уп_Резервы.Записать();
	//Движения.уп_ИнвестДоход.Записать();
	//Движения.уп_ПрочиеНачисления.Записать();
	Движения.уп_НДФЛСведенияОДоходахУчастников.Записать();
	Движения.СведенияОДоходахНДФЛ.Записать();
	//Движения.уп_НДФЛУчастниковРасчетыСБюджетом.Записать();
	Движения.уп_СостоянияПС.Записать();
	Движения.уп_СостоянияДоговора.Записать();
	Движения.Хозрасчетный.Записать();
	
КонецПроцедуры


&ИзменениеИКонтроль("ДвиженияПоРезервам")
Процедура ХМДвиженияПоРезервам(ВидДвижения, ДатаДвижения, НС, ТР, ТС, Сумма, ТекВидДвижения, ТекСхема, НСИсточник, ВидДвиженияЕПС, ВидДвиженияПоТипамЕПС)
	Движение = Движения.уп_Резервы.Добавить();
	Движение.ВидДвижения = ВидДвижения;
	Движение.ДокументРегистрации = ДокументРегистрации;
	Движение.Период = ДатаДвижения;
#Вставка
// ХМНПФ+ 21.02.2025 ЛыткинАМ // https://redmine.npf.com/issues/4109
	Если ЗначениеЗаполнено(ДатаЗакрытия) Тогда
		Движение.Период = ДатаЗакрытия;
	КонецЕсли;
#КонецВставки
	Движение.Организация = Организация;
	Движение.НомерСчета = НС;
	Движение.ТипРезерва = ТР;
	Движение.ТипСуммы = ТС;
	Движение.Схема = ТекСхема;
	Движение.Сумма = Сумма;
	Движение.Движение = ТекВидДвижения;
	Движение.ВидДвиженияЕПС = ВидДвиженияЕПС; 
	Если ТР = Перечисления.уп_ТипыРезерва.Страховой Тогда
		Движение.ПенсионнаяСхема = ТекСхема;
	Иначе
		Если ВестиУчетПоСхемам Тогда
			Движение.ПенсионнаяСхема = ТекСхема;
		КонецЕсли;	
	КонецЕсли;
	Движение.ПенсионныйСчетИсточник = НСИсточник;
	//ОПА НПО 2018.04.28 +
	Движение.ВидДвиженияПоТипамЕПС = ВидДвиженияПоТипамЕПС;
	//ОПА НПО 2018.04.28 -
КонецПроцедуры

&ИзменениеИКонтроль("РассчитатьДокумент")
Процедура ХМРассчитатьДокумент()
	Остатки.Очистить();
	Распределение.Очистить();
	Получатели.Очистить();
	Правопреемники.Очистить();
	СоцВычеты.Очистить();

	НБ = 0;
	НБВНачале = 0;
	НБВКонце = 0;
	НБВЧастиВзносовФЛ = 0;
	НБВЧастиВзносовФЛВНачале = 0;
	НБВЧастиВзносовФЛВКонце = 0;

	КодДохода = ОпределитьКодДохода();

	//Открытие и настройка обработки расчета
	Для каждого стр из ПенсионныеСчета Цикл
		ТекДоговор = Стр.НомерСчета.Владелец;
		ТекСхема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Дата, Новый Структура("НомерСчета",Стр.НомерСчета)).Схема;
		ТекШаблон = РегистрыСведений.уп_ПараметрыПенсионногоДоговора.ПолучитьПоследнее(Дата,Новый Структура("ПенсионныйДоговор",ТекДоговор)).ШаблонДоговора;
		Попытка
			ТекИндекс = Перечисления.уп_АлгоритмыРасчетаВыкупнойСуммы.Индекс(ТекДоговор.АлгоритмРасчетаВыкупнойСуммы);
			Идентификатор = ТекДоговор.АлгоритмРасчетаВыкупнойСуммы.Метаданные().ЗначенияПеречисления.Получить(ТекИндекс).ПолноеИмя();
		Исключение
			Попытка
				ТекИндекс = Перечисления.уп_АлгоритмыРасчетаВыкупнойСуммы.Индекс(ТекШаблон.АлгоритмРасчетаВыкупнойСуммы);
				Идентификатор = ТекШаблон.АлгоритмРасчетаВыкупнойСуммы.Метаданные().ЗначенияПеречисления.Получить(ТекИндекс).ПолноеИмя();
			Исключение
				ТекИндекс = Перечисления.уп_АлгоритмыРасчетаВыкупнойСуммы.Индекс(ТекСхема.АлгоритмРасчетаВыкупнойСуммы);
				Идентификатор = ТекСхема.АлгоритмРасчетаВыкупнойСуммы.Метаданные().ЗначенияПеречисления.Получить(ТекИндекс).ПолноеИмя();
			КонецПопытки;
		КонецПопытки;

		пока Найти(Идентификатор,".")>0 цикл
			Идентификатор = Сред(Идентификатор,Найти(Идентификатор,".")+1);
		конеццикла;

		Попытка
			Обработка = Обработки[СТРОКА("уп_РасчетЗакрытияДоговора_"+Идентификатор)].Создать();
		Исключение
			Сообщить("Не обнаружен модуль расчета для алгоритма """+ТекСхема.АлгоритмРасчетаВыкупнойСуммы+""""+Символы.ПС+"Не найден объект: Обработки.уп_РасчетЗакрытияДоговора_"+Идентификатор);
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;


		Попытка
			Обработка.РасчетныйДокумент = Ссылка;
			Обработка.НомерСчета = Стр.НомерСчета;
			Обработка.Схема = ТекСхема;
			Обработка.ДатаЗакрытия = Макс(ДатаНачалаВыплат, Дата);
			Обработка.ДатаДок = Дата;
			Обработка.ВидРасторжения = ВидРасторженияДоговора;
			Обработка.ДатаНачала = ДатаНачала;
			Обработка.ДатаКонца = ДатаКонца;
			Обработка.ДнейВГоду = ДнейВГоду;
			Обработка.НачислитьДоход = НачислитьДоход;
			Обработка.Ставка = Ставка;
		Исключение
			Сообщить("Модулем расчета по алгоритму: """+ТекСхема.АлгоритмРасчетаВыкупнойСуммы+""" не поддерживаются входные параметры рассчета"+Символы.ПС+"Объект: Обработки.уп_РасчетЗакрытияДоговора_"+Идентификатор);
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;

		Попытка

			Обработка.Рассчитать();

			ТЗ = Обработка.ТЗ;
			Для каждого стр из ТЗ Цикл
				НовСтр = Остатки.Добавить();
				НовСтр.НомерСчета = стр.НомерСчета;
				НовСтр.ТипРезерва = стр.ТипРезерва;
				НовСтр.ТипСуммы = стр.ТипСуммы;
				НовСтр.Сумма = стр.Сумма; 
				#Вставка
				Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда 
					НовСтр.СуммаРасход = стр.СуммаРасход; 
					НовСтр.СуммаОбщая = НовСтр.Сумма + НовСтр.СуммаРасход;
				КонецЕсли;
				#КонецВставки		
				НовСтр.ИнвестДоход = стр.ИнвестДоход;
				Если ЗначениеЗаполнено(стр.ПенсионнаяСхема) Тогда
					НовСтр.Схема = стр.ПенсионнаяСхема;
				Иначе
					НовСтр.Схема = ТекСхема;
				КонецЕсли;  
				Если стр.Сумма<0 Тогда  
					Сообщить("По ПС "+СокрЛП(НовСтр.НомерСчета)+" обнаружен отрицательный остаток. Проведение такого документа невозможно.");
				КонецЕсли;	
				НовСтр.СуммаНезафиксированногоИД = стр.СуммаНезафиксированногоИД;
				Если НовСтр.СуммаНезафиксированногоИД<>0 Тогда  
					Сообщить("По ПС "+СокрЛП(НовСтр.НомерСчета)+" обнаружен незафиксированный ИД. Введите документ фиксации и рассчитайте документ снова.");
				КонецЕсли;
			КонецЦикла;	
			//Остатки.Загрузить(Обработка.ТЗ);

			ТЗРаспределение = Обработка.ТЗРаспределение;
			Для каждого стр из ТЗРаспределение Цикл
				НовСтр = Распределение.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, стр);
				НовСтр.СуммаНаСчете = стр.СуммаНаСчете+стр.ИнвестДоход;
				Если ЗначениеЗаполнено(стр.ПенсионнаяСхема) Тогда
					НовСтр.Схема = стр.ПенсионнаяСхема;
				Иначе
					НовСтр.Схема = ТекСхема;
				КонецЕсли;
				Если стр.ОблагаетсяНДФЛ Тогда
					НБ = НБ + стр.СуммаВыкупная;
					Если стр.СуммаВыкупная>стр.СуммаВКонце Тогда
						НБВКонце = НБВКонце+стр.СуммаВКонце;
						НБВНачале = НБВНачале+(стр.СуммаВыкупная-стр.СуммаВКонце);
					Иначе
						НБВКонце = НБВКонце+стр.СуммаВыкупная;
					КонецЕсли;	
				КонецЕсли;	
			КонецЦикла;	
		Исключение
			Сообщить("Модулем расчета по алгоритму: """+ТекСхема.АлгоритмРасчетаВыкупнойСуммы+""" не поддерживается функция рассчета"+Символы.ПС+"Объект: Обработки.уп_РасчетЗакрытияДоговора_"+Идентификатор);
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
	КонецЦикла;
	//теперь получатели
	//1 - возвраты вкладчикам
	ТабВозвратов = Новый ТаблицаЗначений;
	ТабВозвратов.Колонки.Добавить("Получатель");
	ТабВозвратов.Колонки.Добавить("СуммаВыплаты", Новый ОписаниеТипов("Число"));
	Для каждого стр из Распределение Цикл
		Если стр.СуммаВкладчика <> 0 Тогда
			НовСтр = ТабВозвратов.Добавить();
			НовСтр.Получатель = Стр.НомерСчета.Владелец.Вкладчик;
			НовСтр.СуммаВыплаты = стр.СуммаВкладчика;
		КонецЕсли;
	КонецЦикла;

	ТабВозвратов.Свернуть("Получатель","СуммаВыплаты");
	Для каждого стр из ТабВозвратов Цикл
		НовСтр = Получатели.Добавить();
		НовСтр.Получатель = Стр.Получатель;
		НовСтр.ВидРаспределения = Перечисления.уп_ВидыРаспределения.ВозвратВкладчику;
		НовСтр.СуммаВыплаты = стр.СуммаВыплаты;
		НовСтр.СуммаВНачале = стр.СуммаВыплаты;
	КонецЦикла;	

	//2 - собственно выкупная сумма
	//определим учетную политику
	Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
		Правопреемство = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация)).Правопреемники;
	Иначе
		Правопреемство = Ложь;
	КонецЕсли;

	Получатели.Очистить();
	Правопреемники.Очистить();

	ВыкупнаяСумма = Распределение.Итог("СуммаВыкупная");
	ВыкупнаяВНачале = Распределение.Итог("СуммаВНачале");
	ВыкупнаяВКонце = Распределение.Итог("СуммаВКонце");
	Если ВыкупнаяСумма<>0 Тогда
		Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.ПоСмерти Тогда
			//распределяем по наследникам
			СуммарныйПроцент = 0;
			ВыкупнаяСуммаРаспределено = 0;
			ВыкупнаяВНачалеРаспределено = 0;
			ВыкупнаяВКонцеРаспределено = 0;
			НБРаспределно = 0;
			НБВНачалеРаспределено = 0;
			НБВКонцеРаспределено = 0;
			//если заполнена закладка Заявления, то распределяем в соответствии с этой закладкой
			Если ЗаявленияНаследников.Количество()>0 Тогда
				Для каждого стр из ЗаявленияНаследников Цикл
					Если стр.Разрешить Тогда
						Если стр.Заявление.НеВыплачивать И Правопреемство Тогда //средства зачисляются на личный договор
							НовСтр = Правопреемники.Добавить();
							СуммарныйПроцент = СуммарныйПроцент+стр.Доля;
							НовСтр.Участник= стр.Правопреемник;
							//надо найти индивидуальный договор
							НовСтр.НомерСчета = стр.Заявление.НомерСчета;
							НовСтр.ПенсионныйДоговор = стр.Заявление.ПенсионныйДоговор;
							Если СуммарныйПроцент<100 Тогда
								если ВыкупнаяСумма<>0 тогда
									НовСтр.Сумма = ОКР( (ВыкупнаяСумма * стр.Доля / 100), 2); 
									ВыкупнаяСуммаРаспределено = ВыкупнаяСуммаРаспределено + НовСтр.Сумма;
								конецесли;
							Иначе
								НовСтр.Сумма = ВыкупнаяСумма - ВыкупнаяСуммаРаспределено - ВыкупнаяВНачалеРаспределено - ВыкупнаяВКонцеРаспределено;
								Прервать;
							КонецЕсли;

						Иначе	//средства к выплате
							НовСтр = Получатели.Добавить();
							СуммарныйПроцент = СуммарныйПроцент+стр.Доля;
							НовСтр.Получатель= стр.Правопреемник;
							НовСтр.ВидРаспределения = Перечисления.уп_ВидыРаспределения.ВыкупнаяСумма;
							Если СуммарныйПроцент<100 Тогда
								если ВыкупнаяВНачале<>0 тогда
									НовСтр.СуммаВНачале = ОКР( (ВыкупнаяВНачале * стр.Доля / 100), 2); 
									ВыкупнаяВНачалеРаспределено = ВыкупнаяВНачалеРаспределено + НовСтр.СуммаВНачале;
									//еще НДФЛ
									НовСтр.НБВНачале = ОКР((НБВНачале*стр.Доля / 100), 2);
									НБВНачалеРаспределено = НБВНачалеРаспределено+НовСтр.НБВНачале;
								конецесли;
								если ВыкупнаяВКонце<>0 тогда
									НовСтр.СуммаВКонце = ОКР( (ВыкупнаяВКонце * стр.Доля / 100), 2); 
									ВыкупнаяВКонцеРаспределено = ВыкупнаяВКонцеРаспределено + НовСтр.СуммаВКонце;
									//еще НДФЛ
									НовСтр.НБВКонце = ОКР((НБВКонце*стр.Доля / 100), 2);
									НБВКонцеРаспределено = НБВКонцеРаспределено+НовСтр.НБВНачале;
								конецесли;
								НовСтр.СуммаВыплаты = НовСтр.СуммаВНачале+НовСтр.СуммаВКонце;
								НовСтр.НБ = НовСтр.НБВНачале+НовСтр.НБВКонце;
							Иначе
								НовСтр.СуммаВНачале = ВыкупнаяВНачале - ВыкупнаяВНачалеРаспределено - ВыкупнаяСуммаРаспределено;
								//еще НДФЛ
								НовСтр.НБВНачале = ?(НБВНачале-НБВНачалеРаспределено>НовСтр.СуммаВНачале, НовСтр.СуммаВНачале, НБВНачале-НБВНачалеРаспределено);
								НовСтр.СуммаВКонце = ВыкупнаяВКонце - ВыкупнаяВКонцеРаспределено;
								//еще НДФЛ
								НовСтр.НБВКонце = ?(НБВКонце-НБВКонцеРаспределено>НовСтр.СуммаВКонце, НовСтр.СуммаВКонце, НБВКонце-НБВКонцеРаспределено);
								НовСтр.СуммаВыплаты = НовСтр.СуммаВНачале+НовСтр.СуммаВКонце;
								НовСтр.НБ = НовСтр.НБВНачале+НовСтр.НБВКонце;
								Прервать;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;	
				КонецЦикла;
			Иначе
				Сообщить("По документу имеется выкупная сумма, но нет зарегистрированных правопреемников.");
			КонецЕсли;
		Иначе
			НовСтр = Получатели.Добавить();
			НовСтр.Получатель = Участник;
			НовСтр.ВидРаспределения = Перечисления.уп_ВидыРаспределения.ВыкупнаяСумма;
			НовСтр.СуммаВыплаты = ВыкупнаяСумма;
			НовСтр.СуммаВНачале = Распределение.Итог("СуммаВНачале");
			НовСтр.СуммаВКонце = Распределение.Итог("СуммаВКонце");

			Если ВидРасторженияДоговора <> Перечисления.уп_ВидыРасторжения.ПереходВДругойНПФ Тогда 
				//еще НДФЛ
				Если ВидРасторженияДоговора = Перечисления.уп_ВидыРасторжения.Досрочное Тогда 
					СписокПСРасчетНБ = Новый СписокЗначений;
					Для Каждого стр из ПенсионныеСчета Цикл 
						Если стр.РасторжениеВПериодОхлаждения Тогда
						Иначе
							СписокПСРасчетНБ.Добавить(стр.НомерСчета);
						КонецЕсли;	
					КонецЦикла;	

					//РассчитатьСоциальныеВычеты();
					//Найти последнюю справку
					Запрос = Новый Запрос;
					Запрос.Текст = "ВЫБРАТЬ
					|	уп_СправкаОбИспользованииСоциальногоВычета.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.уп_СправкаОбИспользованииСоциальногоВычета КАК уп_СправкаОбИспользованииСоциальногоВычета
					|ГДЕ
					|	уп_СправкаОбИспользованииСоциальногоВычета.Организация = &Организация
					|	И уп_СправкаОбИспользованииСоциальногоВычета.Участник = &Участник
					|	И уп_СправкаОбИспользованииСоциальногоВычета.Проведен = ИСТИНА
					|	И уп_СправкаОбИспользованииСоциальногоВычета.Дата <= &Дата
					|	И (уп_СправкаОбИспользованииСоциальногоВычета.Модуль = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПР)
					|			ИЛИ уп_СправкаОбИспользованииСоциальногоВычета.Модуль = ЗНАЧЕНИЕ(Перечисление.уп_ВидыДеятельности.ПустаяСсылка))
					|
					|УПОРЯДОЧИТЬ ПО
					|	уп_СправкаОбИспользованииСоциальногоВычета.Дата УБЫВ";

					Запрос.УстановитьПараметр("Организация", Организация);
					Запрос.УстановитьПараметр("Участник", Участник);
					Запрос.УстановитьПараметр("Дата", КонецДня(Дата));

					Результат = Запрос.Выполнить();
					Выборка = Результат.Выбрать();

					Если Выборка.Следующий() Тогда
						ТекСправка = Выборка.Ссылка;
					Иначе
						ТекСправка = Документы.уп_СправкаОбИспользованииСоциальногоВычета.ПустаяСсылка();
					КонецЕсли;

					Запрос = Новый Запрос;
					ТекстЗапроса = "ВЫБРАТЬ
					|	Поступления.Период КАК Год,
					|	Поступления.СуммаОборот КАК Сумма,
					|	Справка.ВычетПредоставлен КАК Предоставлен,
					|	Справка.СуммаВычета КАК СуммаВычета,
					|	ЕСТЬNULL(Справка.СправкаПредоставлена, ЛОЖЬ) КАК СправкаПредоставлена,
					|	Поступления.ПенсионныйДоговор КАК ПенсионныйДоговор
					|ИЗ
					|	(ВЫБРАТЬ
					|		ГОД(ПоступленияОбороты.ГодПоступления) КАК Период,
					|		СУММА(ПоступленияОбороты.СуммаОборот) КАК СуммаОборот,
					|		ПоступленияОбороты.НомерСчета.Владелец КАК ПенсионныйДоговор
					|	ИЗ
					|		РегистрНакопления.уп_Поступления.Обороты(
					|				&НачПериода,
					|				&КонПериода,
					|				Год,
					|				Организация = &Организация
					|					И НомерСчета В (&СписокСчетов)
					|					И ТипРезерва = &ТипРезерва
					|					И ТипСуммы = &ТипСуммы) КАК ПоступленияОбороты
					|	ГДЕ
					|		ПоступленияОбороты.ГодПоступления >= &НачПериода
					|	
					|	СГРУППИРОВАТЬ ПО
					|		ГОД(ПоступленияОбороты.ГодПоступления),
					|		ПоступленияОбороты.НомерСчета.Владелец) КАК Поступления
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			ГОД(уп_СправкаОбИспользованииСоциальногоВычетаВычеты.Год) КАК Год,
					|			уп_СправкаОбИспользованииСоциальногоВычетаВычеты.ВычетПредоставлен КАК ВычетПредоставлен,
					|			уп_СправкаОбИспользованииСоциальногоВычетаВычеты.СуммаВычета КАК СуммаВычета,
					|			ИСТИНА КАК СправкаПредоставлена,
					|			уп_СправкаОбИспользованииСоциальногоВычетаВычеты.ПенсионныйДоговор КАК ПенсионныйДоговор
					|		ИЗ
					|			Документ.уп_СправкаОбИспользованииСоциальногоВычета.Вычеты КАК уп_СправкаОбИспользованииСоциальногоВычетаВычеты
					|		ГДЕ
					|			уп_СправкаОбИспользованииСоциальногоВычетаВычеты.Ссылка = &ТекСправка) КАК Справка
					|		ПО Поступления.Период = Справка.Год
					|			И Поступления.ПенсионныйДоговор = Справка.ПенсионныйДоговор
					|
					|УПОРЯДОЧИТЬ ПО
					|	Год";

					Запрос.УстановитьПараметр("НачПериода", Дата("20070101"));
					Запрос.УстановитьПараметр("КонПериода", Дата);
					Запрос.УстановитьПараметр("СписокСчетов", СписокПСРасчетНБ);
					Запрос.УстановитьПараметр("Организация", Организация);
					Запрос.УстановитьПараметр("ТипРезерва", Перечисления.уп_ТипыРезерва.ИПС);
					Запрос.УстановитьПараметр("ТипСуммы", Справочники.уп_ТипыСумм.ВзносыФиз);
					Запрос.УстановитьПараметр("Участник", Участник);
					Запрос.УстановитьПараметр("Дата", Дата);
					Запрос.УстановитьПараметр("ТекСправка", ТекСправка);

					Запрос.Текст = ТекстЗапроса;


					Результат = Запрос.Выполнить();
					Выборка = Результат.Выбрать();
					Пока Выборка.Следующий() Цикл
						НоваяСтрока = СоцВычеты.Добавить(); 
						НоваяСтрока.ПенсионныйДоговор = Выборка.ПенсионныйДоговор;
						НоваяСтрока.Год = Дата(Выборка.Год,1,1);
						НоваяСтрока.ВычетПредоставлен = Выборка.Предоставлен;
						НоваяСтрока.СуммаВычета = Выборка.СуммаВычета;
						НоваяСтрока.СуммаВзносовЗаГод = Выборка.Сумма;
						НоваяСтрока.СправкаПредоставлена = Выборка.СправкаПредоставлена;
					КонецЦикла;	

					НБПоГодам =0;

					//налоговая база, рассчитанная по поступлениям не должна превысить остаток взносов физических лиц

					//налоговая база, рассчитанная по поступлениям не должна превысить остаток взносов физических лиц
					СуммаВзносовФиз = 0;
					СуммаВзносовФизВНачале = 0;
					СуммаВзносовФизВКонце = 0;
					Для Каждого стр из Распределение Цикл
						Если Стр.ТипСуммы=Справочники.уп_ТипыСумм.ВзносыФиз Тогда
							Если СписокПСРасчетНБ.НайтиПоЗначению(стр.НомерСчета)<>Неопределено Тогда
								СуммаВзносовФиз = СуммаВзносовФиз + Стр.СуммаВыкупная;
								СуммаВзносовФизВНачале = СуммаВзносовФизВНачале+стр.СуммаВНачале;
								СуммаВзносовФизВКонце = СуммаВзносовФизВКонце+стр.СуммаВКонце; 
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;

					Для каждого ТекСтрокаВычеты из СоцВычеты Цикл
						Если ТекСтрокаВычеты.ВычетПредоставлен = Истина Тогда
							НБПоСтроке = min(ТекСтрокаВычеты.СуммаВычета,ТекСтрокаВычеты.СуммаВзносовЗаГод);
							НБПоГодам = НБПоГодам + НБПоСтроке;
							ТекСтрокаВычеты.ПодлежитНалогообложению = НБПоСтроке;
						ИначеЕсли ТекСтрокаВычеты.СправкаПредоставлена Тогда
							//вычет не предоставлялся, т.е. НДФЛ-ом не облагаем
							ТекСтрокаВычеты.ПодлежитНалогообложению = 0;
						Иначе //справки не было, считаем, что все облагать
							//НБПоСтроке = ТекСтрокаВычеты.СуммаВзносовЗаГод;
							//смотрим предел вычетов 
							ПределВычета = РегистрыСведений.уп_ПредельнаяВеличинаСоциальногоВычета.ПолучитьПоследнее(КонецГода(ТекСтрокаВычеты.Год),).Размер;
							НБПоСтроке = min(ТекСтрокаВычеты.СуммаВзносовЗаГод,ПределВычета);
							НБПоГодам = НБПоГодам + НБПоСтроке;
							ТекСтрокаВычеты.ПодлежитНалогообложению = НБПоСтроке;
						КонецЕсли;
					КонецЦикла;
					Если НБПоГодам>СуммаВзносовФиз Тогда
						НБПоГодам = СуммаВзносовФиз;
					КонецЕсли;	

					НБ = НБПоГодам;
					НБВНачале = 0;
					НБВКонце = 0;
					//Добавляем еще из таблицы Распределение
					Для Каждого стр из Распределение Цикл
						Если Стр.ОблагаетсяНДФЛ и Стр.ТипСуммы<>Справочники.уп_ТипыСумм.ВзносыФиз Тогда
							НБ = НБ + Стр.СуммаВыкупная;
							НБВНачале = НБВНачале+стр.СуммаВНачале;
							НБВКонце = НБВКонце+стр.СуммаВКонце;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;

				НовСтр.НБ = НБ;
				Новстр.НБВЧастиВзносовФЛ = НБПоГодам;
				Если СуммаВзносовФизВНачале>0 и НБПоГодам>0 Тогда
					НовСтр.НБВЧастиВзносовФЛВНачале = МИН(СуммаВзносовФизВНачале,НБПоГодам);
					НовСтр.НБВЧастиВзносовФЛВКонце = НБПоГодам-НовСтр.НБВЧастиВзносовФЛВНачале;
				КонецЕсли;

				НББезФЛ = НБ-НБПоГодам;
				НовСтр.НБВНачале = НБВНачале+НовСтр.НБВЧастиВзносовФЛВНачале;
				НовСтр.НБВКонце = НБВКонце+НовСтр.НБВЧастиВзносовФЛВКонце;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура ХМОбработкаПроведенияПосле(Отказ, Режим)
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли; 
	
	Если РольДоступна("хм_СпециалистНПООперОтдел") Тогда
		Отказ = Истина;
	Иначе	
		Документы.уп_ЗакрытиеСчетов.уп_ЗакрытиеСчетов(ЭтотОбъект, Отказ, Режим);
	КонецЕсли;
		
КонецПроцедуры

