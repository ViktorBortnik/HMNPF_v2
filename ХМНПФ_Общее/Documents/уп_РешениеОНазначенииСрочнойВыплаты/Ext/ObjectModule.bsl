
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	// ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	// ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	// ХМНПФ 28.07.2016 ШадринАВ // ПроверитьОстатки() // №447
	// ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешения() // №590
	// ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешенияСервер() // №590
	// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
	// ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешения() // №2340
    // ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешенияСервер() // №2340
	// ХМНПФ 09.08.2019 ШадринАВ // ОбработкаПроведения() // №2389
	// ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешенияСервер() // №3103
	// ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешения() // №3103
	// ХМНПФ 01.03.2021 ШадринАВ // ОбработкаЗаполнения() // №3103
КонецПроцедуры

&ИзменениеИКонтроль("ПечатьРешения")
Функция РасшОбщ_ПечатьРешения()

	ТабДок = Новый ТабличныйДокумент;
	Если Дата < Дата("20150101") Тогда
		Макет = ПолучитьМакет("Макет");
	Иначе
		Макет = ПолучитьМакет("Макет2015");
	КонецЕсли;
	
	// Заголовок
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьОтчет"); // Шапка документа
	
	СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	
	// выводим данные о руководителях организации
	
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерДок = "№ "+СокрЛП(Номер);
	ОбластьМакета.Параметры.НаименованиеФонда = СведенияОбОрг.НаименованиеДляПечатныхФорм;
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;                
	Отчество = ФИОФизЛица.Отчество;
	ОбластьМакета.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
    ОбластьМакета.Параметры.НомерПенсионногоСчета = ДоговорОПС.НомерЛицевогоСчета;
	#Удаление
	РазмерСрочнойВыплаты = РазмерСрочнойВыплаты;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешения() // №590
	#КонецВставки
	ОбластьМакета.Параметры.РазмерСВ = РазмерСрочнойВыплаты;
	ОбластьМакета.Параметры.РазмерСВПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(РазмерСрочнойВыплаты, Константы.ВалютаРегламентированногоУчета.Получить());
	#Удаление
	Срок = Срок;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешения() // №590
	#КонецВставки
	ПоследнийСимвол = Прав(Строка(Срок),1);
	Если ПоследнийСимвол = 1 Тогда	
		СтрМесяц = "месяц";
	ИначеЕсли ПоследнийСимвол = 2 или ПоследнийСимвол = 3 или ПоследнийСимвол = 4 Тогда	
		СтрМесяц = "месяца";
	Иначе
		СтрМесяц = "месяцев";
	КонецЕсли;	
	ОбластьМакета.Параметры.Срок = Строка(Срок)+" "+СтрМесяц;

	#Вставка
	// +ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешения() // №2340
	//ОбластьМакета.Параметры.ДатаНачала = ДокументОснование.ДатаРегистрации;
	// -ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешения() // №2340
	// +ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешения() // №3103
	#КонецВставки
	ОбластьМакета.Параметры.ДатаНачала = НачальнаяДата;
	
	СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОНазначенииСрочнойВыплаты.ПустаяСсылка(),Организация,ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Решение",Дата);
	Если ЗначениеЗаполнено(СведенияОбОтветственном.Фамилия) Тогда
		ОбластьМакета.Параметры.ДолжностьРуководителя = СведенияОбОтветственном.Должность;
		ОбластьМакета.Параметры.ФИОРуководителя = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
	Иначе
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		ОбластьМакета.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
		ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
	КонецЕсли;	
	
	ТабДок.Вывести(ОбластьМакета);
	ТабДок.АвтоМасштаб = Истина;
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	#Вставка
	// +ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	ХМ_ФункцииОтчетов.ПечатьРешения_ДобавитьРасчет(ЭтотОбъект,ТабДок);
	// -ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешения() // №84
	#КонецВставки

   Возврат ТабДок;
КонецФункции // ПечатьПриложения1()

&ИзменениеИКонтроль("ПечатьРешенияСервер")
Функция РасшОбщ_ПечатьРешенияСервер(ТабДок, ВыводитьАдрес = Ложь) Экспорт

	Если ТабДок=Неопределено Тогда
		ТабДок = Новый ТабличныйДокумент;
	Иначе
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Если Дата < Дата("20150101") Тогда
		Макет = ПолучитьМакет("Макет");
	Иначе
		Макет = ПолучитьМакет("Макет2015");
	КонецЕсли;
	
	// Заголовок
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьОтчет"); // Шапка документа
	
	СведенияОбОрг = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
	Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
	
	// выводим данные о руководителях организации
	
	ОбластьМакета.Параметры.ДатаДок = Формат(Дата,"ДЛФ=DD");
	ОбластьМакета.Параметры.НомерДок = "№ "+СокрЛП(Номер);
	ОбластьМакета.Параметры.НаименованиеФонда = СведенияОбОрг.НаименованиеДляПечатныхФорм;
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;                
	Отчество = ФИОФизЛица.Отчество;
	ОбластьМакета.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
    ОбластьМакета.Параметры.НомерПенсионногоСчета = ДоговорОПС.НомерЛицевогоСчета;
	#Удаление
	РазмерСрочнойВыплаты = РазмерСрочнойВыплаты;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешенияСервер() // №590
	#КонецВставки
	ОбластьМакета.Параметры.РазмерСВ = РазмерСрочнойВыплаты;
	ОбластьМакета.Параметры.РазмерСВПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(РазмерСрочнойВыплаты, Константы.ВалютаРегламентированногоУчета.Получить());
	#Удаление
	Срок = Срок;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 18.10.2016 ШадринАВ // ПечатьРешенияСервер() // №590
	#КонецВставки
	ПоследнийСимвол = Прав(Строка(Срок),1);
	Если ПоследнийСимвол = 1 Тогда	
		СтрМесяц = "месяц";
	ИначеЕсли ПоследнийСимвол = 2 или ПоследнийСимвол = 3 или ПоследнийСимвол = 4 Тогда	
		СтрМесяц = "месяца";
	Иначе
		СтрМесяц = "месяцев";
	КонецЕсли;	
	ОбластьМакета.Параметры.Срок = Строка(Срок)+" "+СтрМесяц;
	#Вставка
	// +ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешенияСервер() // №2340
	//ОбластьМакета.Параметры.ДатаНачала = ДокументОснование.ДатаРегистрации;
	// -ХМНПФ 04.07.2019 ГригорьевМА // ПечатьРешенияСервер() // №2340
	// +ХМНПФ 01.03.2021 ШадринАВ // ПечатьРешенияСервер() // №3103
	#КонецВставки
	ОбластьМакета.Параметры.ДатаНачала = НачальнаяДата;
	
	СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОНазначенииСрочнойВыплаты.ПустаяСсылка(),Организация,ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Решение",Дата);
	Если ЗначениеЗаполнено(СведенияОбОтветственном.Фамилия) Тогда
		ОбластьМакета.Параметры.ДолжностьРуководителя = СведенияОбОтветственном.Должность;
		ОбластьМакета.Параметры.ФИОРуководителя = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
	Иначе
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		ОбластьМакета.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
		ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
	КонецЕсли;	
	
	ТабДок.Вывести(ОбластьМакета);
	
	Если ВыводитьАдрес Тогда
		ОбластьКудаКому = Макет.ПолучитьОбласть("ОбластьКудаКому");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка,
		|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление
		|ПОМЕСТИТЬ АдресФЛ
		|ИЗ
		|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
		|ГДЕ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка = &ФЛ
		|	И ФизическиеЛицаКонтактнаяИнформация.Тип = &Тип
		|	И ФизическиеЛицаКонтактнаяИнформация.Вид = &Вид
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ФИОФизическихЛицСрезПоследних.ФизическоеЛицо, АдресФЛ.Ссылка) КАК ФизическоеЛицо,
		|	ФИОФизическихЛицСрезПоследних.Фамилия + "" "" + ФИОФизическихЛицСрезПоследних.Имя + "" "" + ФИОФизическихЛицСрезПоследних.Отчество КАК ФИО,
		|	АдресФЛ.Представление КАК Адрес
		|ИЗ
		|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних(&ДатаДок, ФизическоеЛицо = &ФЛ) КАК ФИОФизическихЛицСрезПоследних
		|		ПОЛНОЕ СОЕДИНЕНИЕ АдресФЛ КАК АдресФЛ
		|		ПО ФИОФизическихЛицСрезПоследних.ФизическоеЛицо = АдресФЛ.Ссылка";
		
		Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("Вид", Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
		Запрос.УстановитьПараметр("ФЛ", ЗастрахованноеЛицо.уп_ФизЛицо);
		Запрос.УстановитьПараметр("ДатаДок", Дата);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Если Выборка.Следующий() Тогда
			Куда = Выборка.Адрес;
			Кому = Выборка.ФИО;
			ОбластьКудаКому.Рисунки.ТекстКудаКому.Текст = "Кому: "+Кому+Символы.ПС+"Куда: "+Куда;
		КонецЕсли;
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		ТабДок.Вывести(ОбластьКудаКому);
	КонецЕсли;	
	
	ТабДок.АвтоМасштаб = Истина;
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	#Вставка
	// +ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	ХМ_ФункцииОтчетов.ПечатьРешения_ДобавитьРасчет(ЭтотОбъект,ТабДок);
	// -ХМНПФ 14.12.2015 ЛыткинАМ // ПечатьРешенияСервер() // №84
	#КонецВставки

	Возврат ТабДок;
КонецФункции // ПечатьПриложения1()

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура РасшОбщ_ОбработкаЗаполнения(Основание, СтандартнаяОбработка)   Экспорт
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, Основание);

	Если ТипЗнч(Основание) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание);
		Если Основание.Свойство("Расшифровка") Тогда   //параметры заполнены
			Для а=1 По Основание.Расшифровка.Количество() Цикл
				СтруктураСтроки = Основание.Расшифровка.Получить(а-1);
				НовСтр = Остатки.Добавить();
				//НовСтр.ДоговорОПС = СтруктураСтроки.ДоговорОПС;
				//НовСтр.ТипРезерва = СтруктураСтроки.ТипРезерва;
				НовСтр.ТипСуммы = СтруктураСтроки.ТипСуммы;
				НовСтр.Сумма = СтруктураСтроки.Сумма;
				НовСтр.СуммаГарантированная = СтруктураСтроки.СуммаГарантированная;
				//СуммаНаСчете = СуммаНаСчете+СтруктураСтроки.Сумма;
			КонецЦикла;
		КонецЕсли;
	Иначе
	Если (Основание = Неопределено) ИЛИ Не ЭтотОбъект.Метаданные().Реквизиты.ДокументОснование.Тип.СодержитТип(ТипЗнч(Основание)) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыДокументаОснования = уп_ОсновнаяПоставкаСервер.СформироватьСтруктуруШапкиДокумента(Основание);
	ДокументОснование = ПараметрыДокументаОснования.Ссылка;
	Организация   = ПараметрыДокументаОснования.Организация;
	ЗастрахованноеЛицо = ПараметрыДокументаОснования.ЗастрахованноеЛицо;
	ДоговорОПС = ПараметрыДокументаОснования.ДоговорОПС;
	ПодразделениеОрганизации = ДоговорОПС.ПодразделениеБУ;
	ВыплачиватьСрочнуюПенсию = Истина;
	Срок =   ПараметрыДокументаОснования.ПериодВыплаты;
	ФормаВыплаты = ПараметрыДокументаОснования.ФормаВыплаты;
	ТипЛицевогоСчета = ПараметрыДокументаОснования.ТипЛицевогоСчета;
	НомерЛицевогоСчета = ПараметрыДокументаОснования.НомерЛицевогоСчета;
	Получатель =  ПараметрыДокументаОснования.Получатель;
	ДоверенноеЛицо =  ПараметрыДокументаОснования.ДоверенноеЛицо;
	РасчетныйСчет = ПараметрыДокументаОснования.РасчетныйСчет;
	#Вставка
	// +ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	//НачальнаяДата = НачалоМесяца(ДобавитьМесяц(ПараметрыДокументаОснования.Дата,1)); // №248
	//НачальнаяДата = ПараметрыДокументаОснования.ДатаРегистрации; // ХМНПФ 15.07.2019 ШадринАВ // ОбработкаЗаполнения() // №2355
	// -ХМНПФ 02.03.2016 ШадринАВ // ОбработкаЗаполнения() // №248
	// +ХМНПФ 01.03.2021 ШадринАВ // ОбработкаЗаполнения() // №3103
	#КонецВставки
	НачальнаяДата = ПараметрыДокументаОснования.Дата;

	ЗаполнитьОстатки();
	Если Срок<>0 Тогда
		РазмерСрочнойВыплаты = Остатки.Итог("Сумма")/Срок;
	КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не выбрано заявление о назначении срочной выплаты.",Отказ,Заголовок);
		Отказ = Истина;
	ИначеЕсли НЕ ДокументОснование.Проведен Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Заявление о назначении срочной выплаты не проведено.",Отказ,Заголовок);
		Отказ = Истина;
	ИначеЕсли ДокументОснование.Дата>КонецДня(Дата) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Заявление зарегистрировано после решения.",Отказ,Заголовок);
		Отказ = Истина;
	КонецЕсли;	
	
	МассивСообщений = Новый СписокЗначений;
	Если КонецДня(НачальнаяДата)> КонецДня(Дата) Тогда
		уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Обратите внимание: дата начала выплат превышает дату решения",,Заголовок);
		МассивСообщений.Добавить("Обратите внимание: дата начала выплат превышает дату решения");
	КонецЕсли;	
	
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	УчетнаяПолитикаНПФ = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	ПерваяСВПропорциональноДням = УчетнаяПолитикаНПФ.ПерваяСВПропорциональноДням;
	// состояния договора  - здесь надо проверить - если уже выплатной, то не надо ставить
	Движения.уп_СостоянияДоговораОПС.Записывать = Истина;
	Движения.уп_СостоянияДоговораОПС.Очистить();
	Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
	#Удаление
	Если НачальнаяДата<=КонецДня(Дата) Тогда
		Движение.Период = Дата;
	Иначе
		Движение.Период = НачальнаяДата;
	КонецЕсли;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 09.08.2019 ШадринАВ // ОбработкаПроведения() // №2389
	Движение.Период = НачальнаяДата;
	// -ХМНПФ 09.08.2019 ШадринАВ // ОбработкаПроведения() // №2389
	#КонецВставки
	Движение.ДатаУстановкиСостояния = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
	
	КоличествоВыплатВПервойВыплате = уп_ОПС.ПолучитьКоличествоВыплат(НачальнаяДата, ПлановаяДатаПервойВыплаты);
	
	// размер выплат
	ДатаНачПериода= НачалоМесяца(НачальнаяДата);
	Если НачальнаяДата = ДатаНачПериода ИЛИ НЕ ПерваяСВПропорциональноДням Тогда  // это типовое условие
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
		Движение.Размер = РазмерСрочнойВыплаты;
		
		РазмерПервойВыплаты = РазмерСрочнойВыплаты*КоличествоВыплатВПервойВыплате;
	Иначе       // а это мой расчет в слуае неполной выплаты
		ДатаКонПериода = КонецМесяца(НачальнаяДата);
		ДоКонцаПериода = ДатаКонПериода-НачальнаяДата+1;
		СуммаВПервомПериоде = РазмерСрочнойВыплаты*ДоКонцаПериода/(ДатаКонПериода-ДатаНачПериода+1);
		
		//размер выплаты за 1-й месяц
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = НачальнаяДата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
		Движение.Размер = СуммаВПервомПериоде;
		
		//размер выплаты за 2-й и последующий месяц
		Движение = Движения.уп_РазмерПенсииОПС.Добавить();
		Движение.Период = ДатаКонПериода+1;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
		Движение.Размер = РазмерСрочнойВыплаты;
		
		//и еще нужно сохранить дробь для расчета последней выплаты
		Движение = Движения.уп_ОставшаясяЧастьСрочнойВыплатыОПС.Добавить();
		Движение.Период = Дата;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ОставшаясяЧасть = 1-(ДоКонцаПериода/(ДатаКонПериода-ДатаНачПериода+1));
		Движение.Количество = 1;
		
		РазмерПервойВыплаты = СуммаВПервомПериоде+РазмерСрочнойВыплаты*(КоличествоВыплатВПервойВыплате-1);
	КонецЕсли;	
	
	Движения.уп_ДанныеПервойВыплатыОПС.Записывать = Истина;
	Движения.уп_ДанныеПервойВыплатыОПС.Очистить();
	Движение = Движения.уп_ДанныеПервойВыплатыОПС.Добавить();
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Период = Дата;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.СуммаПервойВыплаты = РазмерПервойВыплаты;
	Движение.ПлановаяДатаПервойВыплаты = ПлановаяДатаПервойВыплаты;
	Движение.КоличествоВыплатВПервойВыплате = КоличествоВыплатВПервойВыплате;
	
	Движения.уп_КоличествоВыплатОПС.Записывать = Истина;
	Движения.уп_КоличествоВыплатОПС.Очистить();
	Движение = Движения.уп_КоличествоВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.КоличествоВыплат = Срок;
	
	Движения.уп_НазначеныСрочныеВыплатыОПС.Записывать = Истина;
	Движения.уп_НазначеныСрочныеВыплатыОПС.Очистить();
	Движение = Движения.уп_НазначеныСрочныеВыплатыОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ПризнакСрочнойВыплаты = Истина;
	
	Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Записывать = Истина;
	Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Очистить();
	Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
	#Удаление
	Если НачальнаяДата<=КонецДня(Дата) Тогда
		Движение.Период = Дата;
	Иначе
		Движение.Период = НачальнаяДата;
	КонецЕсли;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 09.08.2019 ШадринАВ // ОбработкаПроведения() // №2389
	Движение.Период = НачальнаяДата;
	// -ХМНПФ 09.08.2019 ШадринАВ // ОбработкаПроведения() // №2389
	#КонецВставки
	Движение.ДатаУстановкиСостояния = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.СрочнаяВыплата;
	
	// периодичность выплат  - здесь надо проверить - если уже стоит, то не надо ставить
	Движения.уп_ПериодичностьВыплатОПС.Записывать = Истина;
	Движения.уп_ПериодичностьВыплатОПС.Очистить();
	Движение = Движения.уп_ПериодичностьВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
	
	
	// регистр уп_ЛицевыеСчетаЗастрахованныхЛиц
	// проверить дату записи и совпадение полей
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Период,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Регистратор,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета,
	|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипВыплат
	|ИЗ
	|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(&ДатаДок, ДоговорОПС = &ДоговорОПС И ТипВыплат = &ТипВыплат) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДок", Дата);
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("ТипВыплат", Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ЗаписьЕсть = Ложь;
	ЗаписьСовпадает = Ложь;
	ДатаСовпадает = Ложь;
	
	Если Выборка.Следующий() Тогда
	    ЗаписьЕсть = Истина;
		ДатаЗаписи = Выборка.Период;
		
		Если Выборка.ФормаВыплаты=ФормаВыплаты
			и Выборка.Банк = Получатель
			и Выборка.РасчетныйСчет = РасчетныйСчет
			и Выборка.ТипЛицевогоСчета = ТипЛицевогоСчета
			и Выборка.НомерЛицевогоСчета = НомерЛицевогоСчета Тогда
			ЗаписьСовпадает = Истина;
		КонецЕсли;	
		
	    //проверим соответствие
		Если ДатаЗаписи = НачалоДня(Дата) и НЕ ЗаписьСовпадает Тогда
			//мы не можем переписать
			Сообщить("Параметры выплат необходимо изменить в "+Выборка.Регистратор);
		КонецЕсли;	
	КонецЕсли;
	
	
	Если НЕ ЗаписьСовпадает и НЕ ДатаСовпадает Тогда
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
		Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ЗастрахованноеЛицо = ДоговорОПС.Участник;
		Движение.ФормаВыплаты = ФормаВыплаты;
		Движение.Банк = Получатель;
		Движение.РасчетныйСчет = РасчетныйСчет;
		Движение.ТипЛицевогоСчета = ТипЛицевогоСчета;
		#Удаление
		Движение.ТипВыплат = Перечисления.уп_ТипыВыплат.ВозвратСрочнойПенсииОПС;
		#КонецУдаления
		#Вставка
		Движение.ТипВыплат = Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС;
		#КонецВставки
		Движение.НомерЛицевогоСчета = НомерЛицевогоСчета;
	КонецЕсли;
	
	// проверим доверенное лицо
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.ДоверенноеЛицо,
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.Период,
	               |	уп_ДоверенныеЛицаУчастниковСрезПоследних.Регистратор
	               |ИЗ
	               |	РегистрСведений.уп_ДоверенныеЛицаУчастников.СрезПоследних(&ДатаДок, Участник = &Участник) КАК уп_ДоверенныеЛицаУчастниковСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДок", Дата);
	Запрос.УстановитьПараметр("Участник", ЗастрахованноеЛицо);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ЗаписьЕсть = Ложь;
	ЗаписьСовпадает = Ложь;
	ДатаСовпадает = Ложь;
	
	Если Выборка.Следующий() Тогда
	    ЗаписьЕсть = Истина;
		ДатаЗаписи = Выборка.Период;
		Если Выборка.ДоверенноеЛицо=ДоверенноеЛицо Тогда
			ЗаписьСовпадает = Истина;
		КонецЕсли;	
		
	    //проверим соответствие
		Если ДатаЗаписи = НачалоМесяца(Дата) и НЕ ЗаписьСовпадает Тогда
			//мы не можем переписать
			Сообщить("Параметры выплат необходимо изменить в "+Выборка.Регистратор);
		КонецЕсли;	
	КонецЕсли;
	
			// доверенные лица
	Если НЕ ЗаписьСовпадает и НЕ ДатаСовпадает Тогда
		Если ЗаписьЕсть ИЛИ ЗначениеЗаполнено(ДоверенноеЛицо) Тогда
			Движения.уп_ДоверенныеЛицаУчастников.Записывать = Истина;
			Движения.уп_ДоверенныеЛицаУчастников.Очистить();
			Движение = Движения.уп_ДоверенныеЛицаУчастников.Добавить();
			Движение.Период = Дата;
			Движение.Участник = ЗастрахованноеЛицо;
			Движение.ДоверенноеЛицо = ДоверенноеЛицо;
		КонецЕсли;
	КонецЕсли;	
	
	//номер очереди
	Движения.уп_НомераОчередейОПС.Записывать = Истина;
	Движения.уп_НомераОчередейОПС.Очистить();
	Движение = Движения.уп_НомераОчередейОПС.Добавить();
	Движение.Период = Дата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.НомерОчереди = НомерОчереди;
	
	//параметры зачисления
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоДоговорам,
	|	уп_ПенсионныеПравилаСрезПоследних.ВестиРезервВыплатОПСПоТипамСумм
	|ИЗ
	|	РегистрСведений.уп_ПенсионныеПравила.СрезПоследних(&Дата, Организация = &Организация) КАК уп_ПенсионныеПравилаСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	    ПоДоговорам = Выборка.ВестиРезервВыплатОПСПоДоговорам;
		ПоТипамСумм = Выборка.ВестиРезервВыплатОПСПоТипамСумм;
	Иначе
		ПоДоговорам = Ложь;
		ПоТипамСумм = Ложь;
	КонецЕсли;
	
	//проверим остатки
	ПроверитьОстатки(Отказ);
	Движения.уп_РезервыОПС.Записывать = Истина;
	Движения.уп_РезервыОПС.Очистить();
	
	Движения.уп_РезервыВыплатОПС.Записывать = Истина;
	Движения.уп_РезервыВыплатОПС.Очистить();
		
	ДатаВступления = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
	Если НЕ Отказ Тогда
		Для Каждого стр из Остатки Цикл
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
			Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ДоговорОПС;
			Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
			Движение.ТипСуммы = стр.ТипСуммы;
			Движение.Сумма = стр.Сумма;
			Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
			Движение.СуммаВосполнения = стр.СуммаВосполнения;
			Движение.СуммаВозмещения = стр.СуммаВозмещения;
				
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
			Движение.ВидДвиженияЕПС = Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РСВ;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ДоговорОПС;
			Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
			Движение.ТипСуммы = ПреобразоватьТипСуммы(стр.ТипСуммы);
			Движение.Сумма = стр.Сумма;
			Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
			Движение.СуммаВосполнения = стр.СуммаВосполнения;
			Движение.СуммаВозмещения = стр.СуммаВозмещения;
				
			Если ОтражатьВБухгалтерскомУчете Тогда
				ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
				уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, ЗастрахованноеЛицо,ДоговорОПС.ДоговорКонтрагента,
				Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, ЗастрахованноеЛицо, ДоговорОПС.ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, стр.Сумма);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
				//еще фиксированные обязательства
				//фиксируем в сумме на дату выплаты (вот не знаю - или как раз стоит вычесть)
				Движение = Движения.уп_РазмерЗафиксированныхОбязательств.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ДоговорОПС;
				Движение.ТипСуммы = стр.ТипСуммы;
				Движение.Сумма = стр.Сумма;
				
				//еще текущая сумма гарантирования (вот тут точно реальную пишем, т.е. уменьшаем на выплату)
				Движение = Движения.уп_ТекущаяСуммаГарантирования.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.ДоговорОПС = ДоговорОПС;
				Движение.Организация = Организация;
				Движение.ТипСуммы = стр.ТипСуммы;
				Движение.Сумма = стр.СуммаГарантированная;
				Если стр.СуммаКомпенсации<>0 Тогда
					Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.Организация = Организация;
					Движение.ТипСуммы = стр.ТипСуммы;
					Движение.Вариант = Перечисления.уп_ВариантВосполнения.Компенсация;
					Движение.Сумма = стр.СуммаКомпенсации;
				КонецЕсли;
				
				Если стр.СуммаВосполнения<>0 Тогда
					Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.Организация = Организация;
					Движение.ТипСуммы = стр.ТипСуммы;
					Движение.Вариант = Перечисления.уп_ВариантВосполнения.Восполнение;
					Движение.Сумма = стр.СуммаВосполнения;
				КонецЕсли;
				
				Если стр.СуммаВозмещения<>0 Тогда
					Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.Период = Дата;
					Движение.ДоговорОПС = ДоговорОПС;
					Движение.Организация = Организация;
					Движение.ТипСуммы = стр.ТипСуммы;
					Движение.Вариант = Перечисления.уп_ВариантВосполнения.Возмещение;
					Движение.Сумма = стр.СуммаВозмещения;
				Конецесли;	
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
		Если  уп_СистемаГарантирования.ПроверитьНеобходимостьФиксации(Ссылка, ДоговорОПС) Тогда
			//еще дата фиксации
			Движение = Движения.уп_ДатаФиксацииОбязательств.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ДоговорОПС;
			Движение.ДатаФиксации = Дата;
		КонецЕсли;
	КонецЕсли;	
	
	//запишем дату назначения
	ДогОПСОбъект = ДоговорОПС.ПолучитьОбъект();
	ДогОПСОбъект.ДатаНазначенияСрочнойВыплаты = НачальнаяДата;
	ДогОПСОбъект.Записать();
	
	Если ОтражатьВБухгалтерскомУчете Тогда
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("МассивСообщений", МассивСообщений);
КонецПроцедуры

&ИзменениеИКонтроль("ПроверитьОстатки")
Процедура РасшОбщ_ПроверитьОстатки(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	уп_ПереводВВыплатнойПериодОПСОстатки.ТипСуммы,
	|	уп_ПереводВВыплатнойПериодОПСОстатки.Сумма,
	|	уп_ПереводВВыплатнойПериодОПСОстатки.СуммаГарантированная
	|ПОМЕСТИТЬ ОстаткиПоДокументу
	|ИЗ
	|	Документ.уп_РешениеОНазначенииСрочнойВыплаты.Остатки КАК уп_ПереводВВыплатнойПериодОПСОстатки
	|ГДЕ
	|	уп_ПереводВВыплатнойПериодОПСОстатки.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(уп_РезервыОПСОстатки.СуммаОстаток, 0) КАК СуммаВРегистре,
	|	ОстаткиПоДокументу.ТипСуммы,
	|	ОстаткиПоДокументу.Сумма,
	|	ОстаткиПоДокументу.СуммаГарантированная
	|ИЗ
	|	ОстаткиПоДокументу КАК ОстаткиПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_РезервыОПС.Остатки(
	|				&МомВремени,
	|				ДоговорОПС = &ДоговорОПС
	|					И ТипРезерва = &Накопительный) КАК уп_РезервыОПСОстатки
	|		ПО ОстаткиПоДокументу.ТипСуммы = уп_РезервыОПСОстатки.ТипСуммы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОстаткиПоДокументу";
	
	Запрос.УстановитьПараметр("МомВремени", МоментВремени());
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Накопительный", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДатаВступления = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаВРегистре-Выборка.Сумма>0 Тогда
			//сообщение
			Сообщить("Сумма остатка в резерве по типу суммы "+Выборка.ТипСуммы+" больше указанной в документе. Возможно, документ требуется пересчитать.");
		ИначеЕсли  Выборка.СуммаВРегистре-Выборка.Сумма<0 Тогда
			//отказ
			Сообщить("Сумма остатка в табличной части документа по типу суммы "+Выборка.ТипСуммы+" превышает остаток на счете. Документ требуется пересчитать.");
            Отказ = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаВступления) И Дата>=ДатаВступления Тогда
			Если Выборка.Сумма<Выборка.СуммаГарантированная Тогда
				#Удаление
				Отказ = Истина;
				#КонецУдаления
				#Вставка
				// +ХМНПФ 28.07.2016 ШадринАВ // ПроверитьОстатки() // №447
				#КонецВставки
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По договору №"+ДоговорОПС+" по типу суммы "+Выборка.ТипСуммы+" остаток на счете меньше гарантированной суммы. Необходимо оформить документ ""Перевод средств из резерва ОПС"".");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
// для получения печатных форм со стороны Web сервисов
// аналог процедуры печать типового варианта пользовательского интерфейса
Функция ХМ_ПечатьWeb(ИмяМакета) Экспорт
	ТабДокумент = Новый ТабличныйДокумент; // заглушка на случай если не сформируется ни чего 
	Если ИмяМакета = "Решение" тогда
		ТабДокумент = ПечатьРешения();
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции 

Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ = Неопределено, СтруктураТЧ = Неопределено) Экспорт
	Если НЕ ПРОВЕДЕН Тогда
		#Если Сервер Тогда
//			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостоянияДоговораОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_КоличествоВыплатОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НазначеныСрочныеВыплатыОПС);
//			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ПериодичностьВыплатОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ДоверенныеЛицаУчастников);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НомераОчередейОПС);
			ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
			Сообщить("ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(ДогОПСОбъект)");
		#КонецЕсли
		Возврат;	
	КонецЕсли;

	Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
	
	// размер выплат
	Движение = Движения.уп_РазмерПенсииОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.Размер = РазмерСрочнойВыплаты;
	
	Движение = Движения.уп_КоличествоВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.КоличествоВыплат = Срок;
	
	Движение = Движения.уп_НазначеныСрочныеВыплатыОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ПризнакСрочнойВыплаты = Истина;
	
	Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.СрочнаяВыплата;
		
	
	Движение = Движения.уп_ПериодичностьВыплатОПС.Добавить();
	Движение.Период = НачальнаяДата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
	
	
	Если ЗначениеЗаполнено(НомерЛицевогоСчета) Тогда
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
		Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ЗастрахованноеЛицо = ДоговорОПС.Участник;
		Движение.ФормаВыплаты = ФормаВыплаты;
		Движение.Банк = Получатель;
		Движение.РасчетныйСчет = РасчетныйСчет;
		Движение.ТипЛицевогоСчета = ТипЛицевогоСчета;
		Движение.НомерЛицевогоСчета = НомерЛицевогоСчета;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоверенноеЛицо) Тогда
		Движение = Движения.уп_ДоверенныеЛицаУчастников.Добавить();
		Движение.Период = Дата;
		Движение.Участник = ЗастрахованноеЛицо;
		Движение.ДоверенноеЛицо = ДоверенноеЛицо;
	КонецЕсли;
	
	//номер очереди
	Движение = Движения.уп_НомераОчередейОПС.Добавить();
	Движение.Период = Дата;
	Движение.ДоговорОПС = ДоговорОПС;
	Движение.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная;
	Движение.НомерОчереди = НомерОчереди;
	
	Для Каждого стр из Остатки Цикл
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
		Движение.Период =  стр.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
		Движение.ТипСуммы = стр.ТипСуммы;
		Движение.Сумма = стр.Сумма;
		
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВВыплатнойПериодОПССрочныеВыплаты;
		Движение.Период =  стр.ХМ_ДатаВыплаты;
		Движение.Организация = Организация;
		Движение.ДоговорОПС = ДоговорОПС;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
		Движение.ТипСуммы = стр.ТипСуммы;
//		Движение.ТипСуммы = ПреобразоватьТипСуммы(стр.ТипСуммы);
		Движение.Сумма = стр.Сумма;
	КонецЦикла;
	
	//запишем дату назначения
	ДогОПСОбъект = ДоговорОПС.ПолучитьОбъект();
	ДогОПСОбъект.ДатаНазначенияСрочнойВыплаты = НачальнаяДата;
	ДогОПСОбъект.Записать();
	
#Если Сервер Тогда
//	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостоянияДоговораОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РазмерПенсииОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_КоличествоВыплатОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НазначеныСрочныеВыплатыОПС);
//	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ПериодичностьВыплатОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_ДоверенныеЛицаУчастников);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_НомераОчередейОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(Движения.уп_РезервыОПС);
	ХМ_ЗагрузкаТЗ_Сервер.ЗаписатьОбъект(ДогОПСОбъект);
#КонецЕсли

КонецПроцедуры

&После("ПриЗаписи")
Процедура ХМПриЗаписи(Отказ)
//ЛыткинАМ //[GLPI #0007905] Новая заявка 1 С //Открепить решение из основания при пометке на удаление
	Если Отказ = Ложь Тогда
		Если ПометкаУдаления = Истина И ЗначениеЗаполнено(ДокументОснование) Тогда
			уп_ОбработчикУдаления.ХМ_ОткрепитьРешениеВДокументОснование(ДокументОснование, Ссылка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, Режим)
	ХМ_ПодпискиНаСобытия.ХМ_ОбработкаПроведенияРешений(ЭтотОбъект, Отказ, Режим);
	ХМ_ПодпискиНаСобытия_Сервер.ХМ_ОбработкаПоСозданиюОповещений(ЭтотОбъект, Отказ, Режим); 
КонецПроцедуры

&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);	
КонецПроцедуры

