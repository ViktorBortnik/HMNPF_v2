
&НаСервере
&ИзменениеИКонтроль("ПолучитьСведения")
Функция НПФ_ПолучитьСведения()
#Вставка  
	если Объект.СписокПенсионеров.Количество()>1000 И ПолучатьДополнительныеСведения = Ложь тогда
		Сообщить("В связи с большим количеством участников вывод дополнительных сведений отменен. 
		|При необходимости можете получить дополнительные сведения по нажатию на соответствующую кнопку сверху в шапке");
		ТЗ = Новый ТаблицаЗначений;
		ТЗ.Колонки.Добавить("НомерСчета");
		Возврат ТЗ;
	конецесли;
#КонецВставки
	если Объект.СписокПенсионеров.Количество()>0 тогда
		//ДатаАктуальности = Объект.СписокПенсионеров[0].КонецПериодаНачисления; 
		ДатаАктуальности = Объект.Дата;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеПенсийСписокПенсионеров.НомерСчета,
		|	НачислениеПенсийСписокПенсионеров.НомерСчета.Участник КАК Участник,
		|	НачислениеПенсийСписокПенсионеров.Участник.уп_ФизЛицо КАК УчастникФизЛицо
		|ИЗ
		|	Документ.уп_НачислениеПенсий.СписокПенсионеров КАК НачислениеПенсийСписокПенсионеров
		|ГДЕ
		|	НачислениеПенсийСписокПенсионеров.Ссылка = &Ссылка";

		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);

		Таблица = Запрос.Выполнить().Выгрузить();
		Списоксчетов = Таблица.ВыгрузитьКолонку("НомерСчета");
		СписокУчастников = Таблица.ВыгрузитьКолонку("Участник");
		СписокФизЛиц = Таблица.ВыгрузитьКолонку("УчастникФизЛицо");


		ДопИнформация = Новый Запрос;
		ДопИнформация.Текст =  
		"ВЫБРАТЬ
		|	НачислениеПенсийСписокПенсионеров.НомерСчета,
		|	НачислениеПенсийСписокПенсионеров.НомерСчета.Владелец КАК ПенсионныйДоговор,
		|	НачислениеПенсийСписокПенсионеров.НомерСчета.Владелец.Родитель КАК ГруппаДоговоров,
#Вставка  
	// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
		|	НачислениеПенсийСписокПенсионеров.НомерСчета.Владелец.Вкладчик КАК Вкладчик,
#КонецВставки
		|	НачислениеПенсийСписокПенсионеров.НомерСчета.Участник КАК Участник,
		|	ЛицевыеСчетаУчастниковСрезПоследних.Банк КАК Получатель,
		|	НомераОчередейСрезПоследних.НомерОчереди КАК ОчередностьВыплаты,
		|	ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат,
		|	СхемаСчетаСрезПоследних.Схема,
		|	СостоянияПССрезПоследних.Состояние КАК СостояниеСчета,
		|	РазмерПенсииСрезПоследних.Размер КАК РазмерПенсии,
		|	ПаспортныеДанные.ДокументНомер,
		|	ПаспортныеДанные.ДокументСерия,
		|	ПаспортныеДанные.ДокументДатаВыдачи,
		|	ПаспортныеДанные.ДокументКемВыдан,
		|	ДоверенныеЛицаУчастниковСрезПоследних.ДоверенноеЛицо
		|ИЗ
		|	Документ.уп_НачислениеПенсий.СписокПенсионеров КАК НачислениеПенсийСписокПенсионеров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_НомераОчередей.СрезПоследних(&ДатаДок, НомерСчета В (&СписокСчетов)) КАК НомераОчередейСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = НомераОчередейСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(&ДатаДок, НомерСчета В (&СписокСчетов)) КАК ПериодичностьВыплатСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = ПериодичностьВыплатСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(&ДатаДок, НомерСчета В (&СписокСчетов)) КАК СхемаСчетаСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = СхемаСчетаСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаДок, НомерСчета В (&списокСчетов)) КАК СостоянияПССрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = СостоянияПССрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_РазмерПенсии.СрезПоследних(&ДатаДок, НомерСчета В (&СписокСчетов)) КАК РазмерПенсииСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = РазмерПенсииСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ПаспортныеДанныеФизЛицСрезПоследних.Физлицо КАК ФизЛицо,
		|			ПаспортныеДанныеФизЛицСрезПоследних.Номер КАК ДокументНомер,
		|			ПаспортныеДанныеФизЛицСрезПоследних.Серия КАК ДокументСерия,
		|			ПаспортныеДанныеФизЛицСрезПоследних.ДатаВыдачи КАК ДокументДатаВыдачи,
		|			ПаспортныеДанныеФизЛицСрезПоследних.КемВыдан КАК ДокументКемВыдан
		|		ИЗ
		|			РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
		|					&ДатаДок,
		|					Физлицо В (&СписокФизЛиц)
		|						И ВидДокумента = &ПаспортРФ) КАК ПаспортныеДанныеФизЛицСрезПоследних) КАК ПаспортныеДанные
		|		ПО НачислениеПенсийСписокПенсионеров.Участник.уп_ФизЛицо = ПаспортныеДанные.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ЛицевыеСчетаУчастников.СрезПоследних(
		|				&ДатаДок,
		|				Организация = &Организация
		|					И Участник В (&СписокУчастников)) КАК ЛицевыеСчетаУчастниковСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.НомерСчета = ЛицевыеСчетаУчастниковСрезПоследних.НомерСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ДоверенныеЛицаУчастников.СрезПоследних(&ДатаДок, Участник В (&СписокУчастников)) КАК ДоверенныеЛицаУчастниковСрезПоследних
		|		ПО НачислениеПенсийСписокПенсионеров.Участник = ДоверенныеЛицаУчастниковСрезПоследних.Участник
		|ГДЕ
		|	НачислениеПенсийСписокПенсионеров.Ссылка = &Ссылка";


		ДопИнформация.УстановитьПараметр("ДатаДок",ДатаАктуальности); //?? Это не совсем правильно
		ДопИнформация.УстановитьПараметр("СписокСчетов",СписокСчетов);
		ДопИнформация.УстановитьПараметр("СписокУчастников",СписокУчастников);
		ДопИнформация.УстановитьПараметр("СписокФизЛиц",СписокФизЛиц);
		ДопИнформация.УстановитьПараметр("Ссылка",Объект.Ссылка);
		ДопИнформация.УстановитьПараметр("Организация",Объект.Организация);
		ДопИнформация.УстановитьПараметр("ПаспортРФ",Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);

		Результат = ДопИнформация.Выполнить();
		ТЗ = Результат.Выгрузить();
	Иначе
		ТЗ = Новый ТаблицаЗначений;
	конецесли;
	Возврат ТЗ;
КонецФункции

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьВкладчиков")
Процедура НПФ_ЗаполнитьВкладчиков()
#Вставка  
	// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
	//Заполняется в ПолучитьСведения
#КонецВставки
#Удаление
	Для Каждого Стр Из Объект.СписокПенсионеров Цикл
		Если НЕ Стр.НомерСчета.Владелец.Пустая() Тогда
			Стр.Вкладчик=Стр.НомерСчета.Владелец.Вкладчик;
		КонецЕсли;
	КонецЦикла;
#КонецУдаления
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("пр_ПолучитьДополнительныеСведенияСервер")
Процедура НПФ_пр_ПолучитьДополнительныеСведенияСервер()

	ТЗ=ПолучитьСведения();
#Вставка
	// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
	Если ТЗ.Количество() > 0 Тогда
		ТЗ.Индексы.Добавить("НомерСчета");
	Иначе
		Возврат;
	КонецЕсли;
#КонецВставки
	Для Каждого стрПенсия Из Объект.СписокПенсионеров Цикл
		ТекСтрока = ТЗ.Найти(стрПенсия.НомерСчета,"НомерСчета");
		если ТекСтрока = Неопределено тогда    
			Продолжить;
		конецесли;
		ЗаполнитьЗначенияСвойств(стрПенсия,ТекСтрока,,"Схема");
		стрПенсия.ПенсионныйДоговор1=ТекСтрока.ПенсионныйДоговор;  
		Если Не ЗначениеЗаполнено(стрПенсия.Схема) Тогда
			стрПенсия.Схема = ТекСтрока.Схема;
			стрПенсия.Схема1=ТекСтрока.Схема;
		Иначе 
			стрПенсия.Схема1 = стрПенсия.Схема;
		КонецЕсли;	
#Вставка
	// +ХМНПФ 23.05.2024 ЛыткинАМ // хм_Ускорение // №3873
#КонецВставки
#Удаление
		стрПенсия.Участник=стрПенсия.НомерСчета.Участник;
#КонецУдаления
	КонецЦикла; 

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("РассчитатьСервер")
Процедура НПФ_РассчитатьСервер()

	#Вставка
	Документы.уп_НачислениеПенсий.НПФ_РассчитатьСервер(Объект);
	Возврат;
	#КонецВставки
	
	//Проверим на заполнение НачалоПериода, КонецПериода, ОчередностьВыплаты
	
	ТЗДокумента = Новый ТаблицаЗначений();
	ТЗДокумента.Колонки.Добавить("НомерСчета");
	ТЗДокумента.Колонки.Добавить("Вкладчик");
	ТЗДокумента.Колонки.Добавить("Схема");
	ТЗДокумента.Колонки.Добавить("ТипПенсии");   
	ТЗДокумента.Колонки.Добавить("ТипВкладчика");   
	ТЗДокумента.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата"));
	ТЗДокумента.Колонки.Добавить("КонецПериода", Новый ОписаниеТипов("Дата"));
	ТЗДокумента.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ТЗДокумента.Колонки.Добавить("Пенсия", Новый ОписаниеТипов("Число")); //Бардошина +-
	ТЗДокумента.Колонки.Добавить("Доплата", Новый ОписаниеТипов("Число")); //Бардошина +-
	ТЗДокумента.Колонки.Добавить("ПоследняяВыплата", Новый ОписаниеТипов("Булево"));
	ТЗДокумента.Колонки.Добавить("Пусто");
	ТЗДокумента.Колонки.Добавить("Получатель");
	ТЗДокумента.Колонки.Добавить("ГруппаДоговора");
	ТЗДокумента.Колонки.Добавить("ПенсионныйДоговор");         
	ТЗДокумента.Колонки.Добавить("ОчередностьВыплаты");
	ТЗДокумента.Колонки.Добавить("Периодичность");
	ТЗДокумента.Колонки.Добавить("ПодразделениеБУ");
	ТЗДокумента.Колонки.Добавить("ЭтоЗадолженность"); //Бардошина +-
	ТЗДокумента.Колонки.Добавить("ЭтоВосстановлениеЗадолженности"); //Бардошина +-
	
	РассчитыватьТЧ = Истина;
	
	//проверить, что период везде заполнен
	Для каждого стр из Объект.СписокПенсионеров Цикл
		Если НЕ значениеЗаполнено(стр.НачалоПериодаНачисления) или
			НЕ ЗначениеЗаполнено(стр.КонецПериодаНачисления) Тогда
			Сообщить("В строке "+стр.НомерСтроки+" не указан период начисления. Расчет произведен быть не может.");
			РассчитыватьТЧ = Ложь;
		КонецЕсли;	
	КонецЦикла;	 
	
	ИмяРасчетаПенсий = уп_НПОСервер.ПолучитьИмяМодуляРасчетаПенсий();   
	Если СтрНайти(ИмяРасчетаПенсий, "Ошибка") <> 0 Тогда 
		Сообщить("Расчет невозможен. "+ИмяРасчетаПенсий);  
		Возврат;
	Иначе
		МодульРасчетаПенсий = ОбщегоНазначения.ОбщийМодуль(ИмяРасчетаПенсий);   //по умолчанию  уп_ОбщегоНазначенияУчетРезервов
	КонецЕсли;	
	
	Если РассчитыватьТЧ Тогда
		// Выбрать в отдельные списки с разной периодичностью
		// и с разными датами начала периода
		
		//соберем даты начала периода
		РезультатПоПериодам = СформироватьТаблицуПериодов();
		
		ТаблицаДоплат = Новый ТаблицаЗначений;
		ТаблицаДоплат.Колонки.Добавить("НомерСчета", Новый ОписаниеТипов("СправочникСсылка.уп_ПенсионныеСчета"));
		ТаблицаДоплат.Колонки.Добавить("КоличествоНачисленныхДоплат", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
		
		ВыборкаПериодичность = РезультатПоПериодам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПериодичность.Следующий() Цикл
			ВыборкаПериодов = ВыборкаПериодичность.Выбрать();
			Пока ВыборкаПериодов.Следующий() Цикл
				ТаблицаСчетов = СформироватьТаблицуСчетов(ВыборкаПериодичность.Периодичность,ВыборкаПериодов.НачалоПериодаНачисления);
				ТаблицаСчетов.Колонки.Добавить("НомерНачисления", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(5));
				
				ТаблицаСчетов.ЗаполнитьЗначения(1,"НомерНачисления");
				//		
				Если ТаблицаСчетов.Количество()>0 Тогда
					//добавлено
					Если ТЗДокумента.Количество()>0 Тогда
						Для Каждого строкаТС из ТаблицаСчетов Цикл
							СтрокиТЗДокумента = ТЗДокумента.НайтиСтроки(Новый Структура("НомерСчета", строкаТС.НомерСчета));
							Если СтрокиТЗДокумента.Количество()>0 Тогда
								СтрокаТС.НомерНачисления = строкаТС.НомерНачисления+СтрокиТЗДокумента.Количество();
								Для а=0 по СтрокиТЗДокумента.Количество()-1 Цикл
									СтрокаТС.НачисленоРаннее = СтрокаТС.НачисленоРаннее+СтрокиТЗДокумента.Получить(а).Сумма;
								КонецЦикла;	
							КонецЕсли;	
						КонецЦикла;	
					КонецЕсли;
					//
					//Бардошина +
					
					ТЗОстаткиПоЗадолженности = МодульРасчетаПенсий.ПолучитьТаблицуОстатковПоЗадолженности(ТаблицаСчетов, Объект.Дата, ВыборкаПериодов.НачалоПериодаНачисления);
					МодульРасчетаПенсий.СформироватьТЗНачисления(Объект.Организация,ТЗДокумента, ВыборкаПериодичность.Периодичность, ТаблицаСчетов
					, Объект.Дата, ВыборкаПериодов.НачалоПериодаНачисления, ТЗОстаткиПоЗадолженности, ТаблицаДоплат);
					//Бардошина -
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;	
		//
		Для каждого стр из Объект.СписокПенсионеров Цикл
			//Если Стр.
			//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю();
			МассивСтрТЗ = ТЗДокумента.НайтиСтроки(Новый Структура("НомерСчета,КонецПериода,ЭтоЗадолженность",стр.НомерСчета,КонецДня(стр.КонецПериодаНачисления),Ложь));
			Если МассивСтрТЗ.Количество()<>0 Тогда
				СтрТЗ = МассивСтрТЗ.Получить(0);
				//Бардошина +
				стр.Сумма = СтрТЗ.Сумма;
				стр.Пенсия = СтрТЗ.Пенсия;	
				стр.Доплата = СтрТЗ.Доплата; 
				//Бардошина -
			Иначе
				// Молчанов Ортикон НАЧАЛО 28.12.2017
				//Сообщить("Для счета № "+СокрЛП(стр.НомерСчета.Код)+" расчет либо не сделан, либо сумма = 0.");
				Сообщить("Для счета № "+СокрЛП(стр.НомерСчета)+" расчет либо не сделан, либо сумма = 0.");
				// Молчанов Ортикон КОНЕЦ 28.12.2017
			КонецЕсли;	
		КонецЦикла;
		
		//Бардошина +
		//расчет по ТЧ npo_НачислениеЗадолженностиПоПриостановкам
		Для каждого стр из Объект.НачислениеЗадолженностиПоПриостановкам Цикл
			МассивСтрТЗ = ТЗДокумента.НайтиСтроки(Новый Структура("НомерСчета,НачалоПериода, ЭтоЗадолженность",стр.НомерСчета,стр.НачалоПериодаНачисления,Истина));
			Если МассивСтрТЗ.Количество()<>0 Тогда
				СтрТЗ = МассивСтрТЗ.Получить(0);
				стр.Сумма = СтрТЗ.Сумма;
				стр.Пенсия = СтрТЗ.Пенсия;	
				стр.Доплата = СтрТЗ.Доплата; 
			Иначе
				// Молчанов Ортикон НАЧАЛО 28.12.2017
				//Сообщить("Для счета № "+СокрЛП(стр.НомерСчета.Код)+" расчет либо не сделан, либо сумма = 0.");
				Сообщить("Для счета № "+СокрЛП(стр.НомерСчета)+" расчет либо не сделан, либо сумма = 0.");
				// Молчанов Ортикон КОНЕЦ 28.12.2017
			КонецЕсли;	
		КонецЦикла;
		//Бардошина -
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура НПФ_ПолучитьДополнительныеСведенияПосле(Команда)
	ПолучатьДополнительныеСведения = Истина;
	пр_ПолучитьДополнительныеСведенияСервер();
КонецПроцедуры

&НаСервере
Процедура НПФ_ПеренестиВДругоеНачислениеПослеНаСервере(ДокументПереноса)
	ОбъектПереноса = ДокументПереноса.ПолучитьОбъект();
	МассивСтрок = Новый Массив;
	Для каждого ИдентификаторСтроки Из Элементы.СписокПенсионеров.ВыделенныеСтроки Цикл
		СтараяСтрока = Объект.СписокПенсионеров.НайтиПоИдентификатору(ИдентификаторСтроки);
		МассивСтрок.Добавить(СтараяСтрока);
		НоваяСтрока = ОбъектПереноса.СписокПенсионеров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтараяСтрока);		
	КонецЦикла;
	ОбъектПереноса.Записать();
	Для каждого СтараяСтрока Из МассивСтрок Цикл
		Объект.СписокПенсионеров.Удалить(СтараяСтрока);
	КонецЦикла;
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НПФ_ПеренестиВДругоеНачислениеПосле(Команда)
	АСИНХ_НПФ_ПеренестиВДругоеНачислениеПосле();
КонецПроцедуры

&НаКлиенте
Асинх Процедура АСИНХ_НПФ_ПеренестиВДругоеНачислениеПосле()
	ДокументПереноса = Объект.Ссылка;
	Обещание = ВвестиЗначениеАсинх(ДокументПереноса);
	ДокументПереноса = Ждать Обещание;
	НПФ_ПеренестиВДругоеНачислениеПослеНаСервере(ДокументПереноса);               
	Сообщение = Новый СообщениеПользователю();
	Сообщение.КлючДанных = ДокументПереноса;
	Сообщение.Текст = "Перенос в " + ДокументПереноса + " выполнен.";
	Сообщение.Сообщить();
КонецПроцедуры


