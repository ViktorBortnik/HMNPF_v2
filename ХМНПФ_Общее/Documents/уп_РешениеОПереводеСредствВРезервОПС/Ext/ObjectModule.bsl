
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 15.11.2017 ШадринАВ // СформироватьДвиженияПоВозвратам() // №1228
	// ХМНПФ 29.03.2018 ШадринАВ // ПроверкаКорректности() // №1472
	// ХМНПФ 02.04.2018 ШадринАВ // ПроверкаКорректности() // №1485
	// ХМНПФ 02.04.2018 ШадринАВ // ОбработкаПроведения() // №1485
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки.
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;
	
	ПроверкаКорректности(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	
	//корректировка резервов
	Движения.уп_РезервыОПС.Записывать = Истина;
	Движения.уп_РезервыОПС.Очистить();
	
	Движения.уп_АналитикаРезерваОПС.Записывать = Истина;
	Движения.уп_АналитикаРезерваОПС.Очистить();
	
	Движения.уп_СуммыГарантийногоВосполнения.Записывать = Истина;
	Движения.уп_СуммыГарантийногоВосполнения.Очистить();
	
	Движения.уп_АналитикаРезерваОПСДосрочныеПереходы.Записывать = Истина;
	Движения.уп_АналитикаРезерваОПСДосрочныеПереходы.Очистить();
	
	Движения.уп_АналитикаРезерваОПСПрочее.Записывать = Истина;
	Движения.уп_АналитикаРезерваОПСПрочее.Очистить();
	
	Движения.уп_СостоянияДоговораОПС.Записывать = Истина;
	Движения.уп_СостоянияДоговораОПС.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС,
		|	ВЫБОР
		|		КОГДА уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия = ЗНАЧЕНИЕ(Перечисление.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПричинаЗакрытия
		|ПОМЕСТИТЬ ПричиныЗакрытия
		|ИЗ
		|	РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоровОПС)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ДоговорОПС,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипСуммы,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.Ссылка.Дата КАК Период,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаНаСчете КАК Сумма,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.Ссылка.Организация,
		|	ЕСТЬNULL(ПричиныЗакрытия.ПричинаЗакрытия, 0) КАК ПричинаЗакрытия,
		|	ВЫБОР
		|		КОГДА уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва = &РПН
		|			ТОГДА &ДвижениеРПН
		|		КОГДА уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва = &РСВ
		|			ТОГДА &ДвижениеРСВ
		|		КОГДА уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва = &РПНУмершие
		|			ТОГДА &ДвижениеРПНУмершие
		|		КОГДА уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва = &РСВУмершие
		|			ТОГДА &ДвижениеРСВУмершие
		|		ИНАЧЕ &Пустое
		|	КОНЕЦ КАК ВидДвиженияЕПС,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаКомпенсации,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаВосполнения,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаВозмещения
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.РасшифровкаСумм КАК уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПричиныЗакрытия КАК ПричиныЗакрытия
		|		ПО уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ДоговорОПС = ПричиныЗакрытия.ДоговорОПС
		|ГДЕ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.Ссылка = &Ссылка
		|	И уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаНаСчете <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДатаДок", КонецДня(Дата));
	Запрос.УстановитьПараметр("СписокДоговоровОПС", ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС"));
	Запрос.УстановитьПараметр("РПН", Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений);
	Запрос.УстановитьПараметр("РСВ", Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты);
	Запрос.УстановитьПараметр("РПНУмершие", Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН);
	Запрос.УстановитьПараметр("РСВУмершие", Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРСВ);
	Запрос.УстановитьПараметр("ДвижениеРПН", Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПН_в_РОПС);
	Запрос.УстановитьПараметр("ДвижениеРСВ", Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РСВ_в_РОПС);
	Запрос.УстановитьПараметр("ДвижениеРПНУмершие", Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РПНУмерших_в_РОПС);
	Запрос.УстановитьПараметр("ДвижениеРСВУмершие", Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.Перевод_Из_РСВУмерших_в_РОПС);
	Запрос.УстановитьПараметр("Пустое", Перечисления.епс_ВидыДвиженияПенсионныхНакоплений.ПустаяСсылка());
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	ТЗ.Колонки.Добавить("ВидДвижения");
	ТЗ.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	ТЗ.Колонки.Добавить("Движение");
	Если ТипРешения = 2 Тогда
		ТЗ.ЗаполнитьЗначения(Перечисления.уп_ВидыДвижений.ПереводВРОПССредствПоДосрочнымПереходам,"Движение");
	Иначе
		ТЗ.ЗаполнитьЗначения(Перечисления.уп_ВидыДвижений.ПереводВРОПССредствПоУмершимЗЛ,"Движение");
	КонецЕсли;	

	Движения.уп_РезервыОПС.Загрузить(ТЗ);
	
	//Если ДоговорыОПС.Итог("СуммаНаСчете")<>0 Тогда
	//	Движение = Движения.уп_РезервыОПС.Добавить();
	//	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	//	Движение.Период = Дата;
	//	Движение.Организация = Организация;
	//	Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервОПС;
	//	Движение.Сумма = ДоговорыОПС.Итог("СуммаНаСчете");
	//КонецЕсли;
	
	//еще аналитику резерва ОПС
	Если ТипРешения = 2 Тогда
		Для Каждого стр из РасшифровкаСумм Цикл
			Движение = Движения.уп_АналитикаРезерваОПСДосрочныеПереходы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.ТипРезерва = стр.ТипРезерва;
			Движение.ТипСуммы   = стр.ТипСуммы;
			Движение.Сумма = стр.СуммаНаСчете;
			//Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
			//Движение.СуммаВосполнения = стр.СуммаВосполнения;
			//Движение.СуммаВозмещения  = стр.СуммаВозмещения;
			
			Движение = Движения.уп_АналитикаРезерваОПСПрочее.Добавить();
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = стр.ДоговорОПС;
			Движение.ТипРезерва = стр.ТипРезерва;
			Движение.ТипСуммы   = стр.ТипСуммы;
			Движение.Сумма = стр.СуммаНаСчете;
			Движение.Расшифровка = Справочники.уп_ИсточникиПополненияРезерваОПС.ДосрочныеПереходыВРОПС;
		КонецЦикла;
	Иначе
		#Удаление
		Если ТЗ.Итог("ПричинаЗакрытия")>0 Тогда
		#КонецУдаления
		#Вставка
		// +ХМНПФ 02.04.2018 ШадринАВ // ОбработкаПроведения() // №1485
		Если ТЗ.Итог("ПричинаЗакрытия")>0 ИЛИ (СокрЛП(Ссылка.Номер) = "000000028" И Ссылка.Дата = Дата(2017,9,28,9,0,31)) Тогда
		// -ХМНПФ 02.04.2018 ШадринАВ // ОбработкаПроведения() // №1485
		#КонецВставки
			
		Для Каждого стр из ТЗ Цикл
				#Удаление
				Если стр.ПричинаЗакрытия = 1 Тогда
				#КонецУдаления
				#Вставка
				// +ХМНПФ 02.04.2018 ШадринАВ // ОбработкаПроведения() // №1485
				Если стр.ПричинаЗакрытия = 1 ИЛИ (СокрЛП(Ссылка.Номер) = "000000028" И Ссылка.Дата = Дата(2017,9,28,9,0,31)) Тогда
				// -ХМНПФ 02.04.2018 ШадринАВ // ОбработкаПроведения() // №1485
				#КонецВставки
					Движение = Движения.уп_АналитикаРезерваОПС.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.ДоговорОПС = стр.ДоговорОПС;
					Движение.ТипРезерва = стр.ТипРезерва;
					Движение.ТипСуммы   = стр.ТипСуммы;
					//Если  стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты 
					//	или стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНачисленийСВ
					//	или стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНачисленийНаследованиеРСВ Тогда
					//	Движение.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_СрочныхВыплат;
					//Иначе
					//	Движение.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_НЧТП;
					//КонецЕсли;
					Движение.Сумма = стр.Сумма;
					Движение.СуммаКомпенсации = стр.СуммаКомпенсации;
					Движение.СуммаВосполнения = стр.СуммаВосполнения;
					Движение.СуммаВозмещения  = стр.СуммаВозмещения;
					
					Если стр.СуммаКомпенсации>0 Тогда
						
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = стр.ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Компенсация;
						Движение.Сумма = стр.СуммаКомпенсации;
						
					КонецЕсли;
					
					Если стр.СуммаВосполнения>0 Тогда
						
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = стр.ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Восполнение;
						Движение.Сумма = стр.СуммаВосполнения;
						
					КонецЕсли;
					
					Если стр.СуммаВозмещения>0 Тогда
						
						Движение = Движения.уп_СуммыГарантийногоВосполнения.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.Период = Дата;
						Движение.ДоговорОПС = стр.ДоговорОПС;
						Движение.Организация = Организация;
						Движение.ТипСуммы = стр.ТипСуммы;
						Движение.Вариант = Перечисления.уп_ВариантВосполнения.Возмещение;
						Движение.Сумма = стр.СуммаВозмещения;
						
					КонецЕсли;
					
				КонецЕсли;	
			КонецЦикла;	
		КонецЕсли;
		
	КонецЕсли;
	
	
	ТЗ.Свернуть("ВидДвиженияЕПС, Движение", "Сумма");
	Для Каждого стр из ТЗ Цикл
		Движение = Движения.уп_РезервыОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервОПС;
		Движение.ВидДвиженияЕПС = стр.ВидДвиженияЕПС;
		Движение.Сумма = стр.Сумма;
		Движение.Движение = стр.Движение;
	КонецЦикла;	
	
	//состояния
	Если ТипРешения = 0 И ЗакрыватьДоговорыОПС Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_СостоянияДоговораОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
		|	уп_СостоянияДоговораОПССрезПоследних.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоровОПС)) КАК уп_СостоянияДоговораОПССрезПоследних";
		
		Запрос.УстановитьПараметр("ДатаДок", Дата-1);
		Запрос.УстановитьПараметр("СписокДоговоровОПС", ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС"));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Состояние<>Перечисления.уп_СостоянияДоговоровОПС.Закрыт Тогда
				Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.Период = Дата;
				Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт;
			КонецЕсли;	
		КонецЦикла;
		
	КонецЕсли;

	//возможно еще текущую сумму гарантирования, но для досрочно перешедших точно не надо
	
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.Хозрасчетный.Очистить();
	//тут еще по бухучету движения нужны:
	Если ОтражатьВБухгалтерскомУчете Тогда
		Для Каждого стр из РасшифровкаСумм Цикл
			Если стр.СуммаНаСчете<>0 Тогда
				Если стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты Тогда
					ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВРезервОПССрочныеВыплаты;
				ИначеЕсли стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений Тогда
					ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВРезервОПСНакопления;
				Иначе
					ВидДвижения = Перечисления.уп_ВидыДвижений.ПереводВРезервОПСВыплаты;
				КонецЕсли;	
				ДвиженияПоБухУчету(Дата, ВидДвижения, стр.ДоговорОПС.Участник, стр.ДоговорОПС.ДоговорКонтрагента, 
				Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, Справочники.Контрагенты.ПустаяСсылка(), Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, стр.СуммаНаСчете);
			КонецЕсли;
		КонецЦикла;
		
		уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
		уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
	КонецЕсли;
	
	//bardoshina+
	Если ТипРешения = 3 Тогда
		ПроверитьВозможностьПроведенияНевыплат(Отказ);
		Если Отказ Тогда Возврат КонецЕсли;
		СформироватьДвиженияПоНевыплатам(Отказ);
	ИначеЕсли ВозвратыВыплатПравопреемникам.Количество() > 0 Тогда
		СформироватьДвиженияПоВозвратам(Отказ);
	КонецЕсли;	
	//bardoshina-
КонецПроцедуры

&ИзменениеИКонтроль("ПроверкаКорректности")
Процедура РасшОбщ_ПроверкаКорректности(Отказ)
	//проверить, что расшифровка по суммам соответствует основной таблице
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
		|	уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС.СуммаНаСчете
		|ПОМЕСТИТЬ ОбщиеСуммы
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.ДоговорыОПС КАК уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС
		|ГДЕ
		|	уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ДоговорОПС КАК ДоговорОПС,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаНаСчете
		|ПОМЕСТИТЬ Расшифровка
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.РасшифровкаСумм КАК уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм
		|ГДЕ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ОбщиеСуммы.ДоговорОПС, Расшифровка.ДоговорОПС) КАК ДоговорОПС,
		|	ЕСТЬNULL(ОбщиеСуммы.СуммаНаСчете, 0) КАК СуммаНаСчете,
		|	ЕСТЬNULL(Расшифровка.СуммаНаСчете, 0) КАК СуммаНаСчетеР
		|ИЗ
		|	ОбщиеСуммы КАК ОбщиеСуммы
		|		ПОЛНОЕ СОЕДИНЕНИЕ Расшифровка КАК Расшифровка
		|		ПО ОбщиеСуммы.ДоговорОПС = Расшифровка.ДоговорОПС
		|ИТОГИ
		|	МАКСИМУМ(СуммаНаСчете),
		|	СУММА(СуммаНаСчетеР)
		|ПО
		|	ДоговорОПС";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	Результат = Запрос.Выполнить();

	ВыборкаДоговорОПС = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаДоговорОПС.Следующий() Цикл
		Если ВыборкаДоговорОПС.СуммаНаСчете<>ВыборкаДоговорОПС.СуммаНаСчетеР Тогда
			Отказ = Истина;
			Сообщить("По договору "+ВыборкаДоговорОПС.ДоговорОПС+" сумма расшифровки не соответствует суммам в основной таблице.");
		КонецЕсли;	
	КонецЦикла;
	Если Дата>='20170801' Тогда
		//проверяем, что тип решения соответствует причине закрытия
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС.ДоговорОПС
		|ПОМЕСТИТЬ СписокДоговоров
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.ДоговорыОПС КАК уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС
		|ГДЕ
		|	уп_РешениеОПереводеСредствВРезервОПСДоговорыОПС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия,
		|	СписокДоговоров.ДоговорОПС КАК ДоговорОПС
		|ПОМЕСТИТЬ ДоговорыСПричинами
		|ИЗ
		|	СписокДоговоров КАК СписокДоговоров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоровОПС)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
		|		ПО СписокДоговоров.ДоговорОПС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыСПричинами.ПричинаЗакрытия,
		|	ДоговорыСПричинами.ДоговорОПС
		|ИЗ
		|	ДоговорыСПричинами КАК ДоговорыСПричинами
		|ГДЕ
		|	ДоговорыСПричинами.ПричинаЗакрытия ЕСТЬ NULL ИЛИ ДоговорыСПричинами.ПричинаЗакрытия <> &ПричинаЗакрытия";
		Запрос.УстановитьПараметр("ДатаДок", Дата);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Если ТипРешения = 0 Тогда //умершие
			Запрос.УстановитьПараметр("ПричинаЗакрытия", Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти);
			Запрос.УстановитьПараметр("СписокДоговоровОПС", ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС"));
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			#Вставка
			// +ХМНПФ 02.04.2018 ШадринАВ // ПроверкаКорректности() // №1485
			Если СокрЛП(Ссылка.Номер) = "000000028" И Ссылка.Дата = Дата(2017,9,28,9,0,31) Тогда ХМ_Отказ = Отказ КонецЕсли;
			// -ХМНПФ 02.04.2018 ШадринАВ // ПроверкаКорректности() // №1485
			#КонецВставки
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Отказ = Истина;
				Сообщить("У договора "+ВыборкаДетальныеЗаписи.ДоговорОПС+" причина закрытия или дата установки причины не соответствует документу.");
			КонецЦикла;

			#Вставка
			// +ХМНПФ 02.04.2018 ШадринАВ // ПроверкаКорректности() // №1485
			Если СокрЛП(Ссылка.Номер) = "000000028" И Ссылка.Дата = Дата(2017,9,28,9,0,31) Тогда Отказ = ХМ_Отказ КонецЕсли;
			// -ХМНПФ 02.04.2018 ШадринАВ // ПроверкаКорректности() // №1485
			#КонецВставки
			Если Ссылка.ВозвратыВыплатПравопреемникам.Количество()>0 Тогда
				Отказ = Истина;
				Сообщить("Документ не соответствует заявленному типу - таблица возвратов не пуста.");
			КонецЕсли;	
		ИначеЕсли ТипРешения = 2 Тогда //досрочное расторжение
			#Удаление
			Запрос.УстановитьПараметр("ПричинаЗакрытия", Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПереходДосрочный);
			#КонецУдаления
			#Вставка
			// +ХМНПФ 29.03.2018 ШадринАВ // ПроверкаКорректности() // №1472
			Запрос.УстановитьПараметр("ПричинаЗакрытия", Перечисления.уп_ПричиныЗакрытияДоговораОПС.Досрочное);
			// -ХМНПФ 29.03.2018 ШадринАВ // ПроверкаКорректности() // №1472
			#КонецВставки
			Запрос.УстановитьПараметр("СписокДоговоровОПС", ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС"));
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Отказ = Истина;
				Сообщить("У договора "+ВыборкаДетальныеЗаписи.ДоговорОПС+" причина закрытия или дата установки причины не соответствует документу.");
			КонецЦикла;
			Если ВозвратыВыплатПравопреемникам.Количество()>0 Тогда
				Отказ = Истина;
				Сообщить("Документ не соответствует заявленному типу - таблица возвратов не пуста.");
			КонецЕсли;	
		ИначеЕсли ТипРешения = 1 Тогда  // это возвраты
			//проверяем, что таблицы договоров ОПС и расшифровок пусты
			Если ДоговорыОПС.Количество()>0 Тогда
				Отказ = Истина;
				Сообщить("Документ не соответствует заявленному типу - таблица договоров ОПС не пуста.");
			КонецЕсли;	
			Если РасшифровкаСумм.Количество()>0 Тогда
				Отказ = Истина;
				Сообщить("Документ не соответствует заявленному типу - таблица расшифровок не пуста.");
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	//еще проверить, что списываем не больше, чем то, что там осталось
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ДоговорОПС КАК ДоговорОПС,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипСуммы КАК ТипСуммы,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва КАК ТипРезерва,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаНаСчете КАК СуммаНаСчете,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаКомпенсации КАК СуммаКомпенсации,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаВосполнения КАК СуммаВосполнения,
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.СуммаВозмещения КАК СуммаВозмещения,
		|	ВЫБОР
		|		КОГДА уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.ТипРезерва = ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.Выплатной)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПризнакВыплатногоРезерва
		|ПОМЕСТИТЬ ТабДокумента
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.РасшифровкаСумм КАК уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм
		|ГДЕ
		|	уп_РешениеОПереводеСредствВРезервОПСРасшифровкаСумм.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОПСОстатки.ДоговорОПС КАК ДоговорОПС,
		|	уп_РезервыОПСОстатки.ТипСуммы КАК ТипСуммы,
		|	уп_РезервыОПСОстатки.ТипРезерва КАК ТипРезерва,
		|	уп_РезервыОПСОстатки.СуммаОстаток КАК СуммаОстаток,
		|	уп_РезервыОПСОстатки.СуммаКомпенсацииОстаток КАК СуммаКомпенсацииОстаток,
		|	уп_РезервыОПСОстатки.СуммаВосполненияОстаток КАК СуммаВосполненияОстаток,
		|	уп_РезервыОПСОстатки.СуммаВозмещенияОстаток КАК СуммаВозмещенияОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.уп_РезервыОПС.Остатки(
		|			&ДатаДок,
		|			ДоговорОПС В
		|				(ВЫБРАТЬ
		|					ТабДокумента.ДоговорОПС
		|				ИЗ
		|					ТабДокумента)) КАК уп_РезервыОПСОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабДокумента.ДоговорОПС КАК ДоговорОПС,
		|	ТабДокумента.ТипСуммы КАК ТипСуммы,
		|	ТабДокумента.ТипРезерва КАК ТипРезерва,
		|	ТабДокумента.СуммаНаСчете КАК СуммаНаСчете,
		|	ТабДокумента.СуммаКомпенсации КАК СуммаКомпенсации,
		|	ТабДокумента.СуммаВосполнения КАК СуммаВосполнения,
		|	ТабДокумента.СуммаВозмещения КАК СуммаВозмещения,
		|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(Остатки.СуммаКомпенсацииОстаток, 0) КАК СуммаКомпенсацииОстаток,
		|	ЕСТЬNULL(Остатки.СуммаВосполненияОстаток, 0) КАК СуммаВосполненияОстаток,
		|	ЕСТЬNULL(Остатки.СуммаВозмещенияОстаток, 0) КАК СуммаВозмещенияОстаток,
		|	ТабДокумента.ПризнакВыплатногоРезерва КАК ПризнакВыплатногоРезерва
		|ИЗ
		|	ТабДокумента КАК ТабДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		|		ПО ТабДокумента.ДоговорОПС = Остатки.ДоговорОПС
		|			И ТабДокумента.ТипСуммы = Остатки.ТипСуммы
		|			И ТабДокумента.ТипРезерва = Остатки.ТипРезерва
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоговорОПС
		|ИТОГИ
		|	СУММА(СуммаНаСчете),
		|	СУММА(СуммаОстаток),
		|	СУММА(ПризнакВыплатногоРезерва)
		|ПО
		|	ДоговорОПС";
	
	Запрос.УстановитьПараметр("ДатаДок", Новый Граница(Ссылка.МоментВремени(),ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДоговорОПС = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДоговорОПС.Следующий() Цикл
		Если ВыборкаДоговорОПС.ПризнакВыплатногоРезерва>0 Тогда
			Отказ = Истина;
			Сообщить("По договору "+ВыборкаДоговорОПС.ДоговорОПС+" в табличной части присутствует выплатной резерв.");
		ИначеЕсли ВыборкаДоговорОПС.СуммаНаСчете<=ВыборкаДоговорОПС.СуммаОстаток Тогда
			//проверяем дальше
			ВыборкаДетальныеЗаписи = ВыборкаДоговорОПС.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				#Удаление
				Если ВыборкаДетальныеЗаписи.СуммаНаСчете>ВыборкаДоговорОПС.СуммаОстаток Тогда
				#КонецУдаления
				#Вставка
				// +ХМНПФ 29.03.2018 ШадринАВ // ПроверкаКорректности() // №1472
				Если ВыборкаДетальныеЗаписи.СуммаНаСчете>ВыборкаДетальныеЗаписи.СуммаОстаток Тогда
				// -ХМНПФ 29.03.2018 ШадринАВ // ПроверкаКорректности() // №1472
				#КонецВставки
					Отказ = Истина;
					Сообщить("Сумма остатка по договору "+ВыборкаДоговорОПС.ДоговорОПС+" по типу резерва "+ВыборкаДетальныеЗаписи.ТипРезерва+" и типу суммы "+ВыборкаДетальныеЗаписи.ТипСуммы+" на дату документа меньше суммы, указанной в документе");
				КонецЕсли;	
			КонецЦикла;
		Иначе
			Отказ = Истина;
			Сообщить("Сумма остатка по договору "+ВыборкаДоговорОПС.ДоговорОПС+" на дату документа меньше суммы, указанной в документе");
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры	

&ИзменениеИКонтроль("СформироватьДвиженияПоВозвратам")
Процедура РасшОбщ_СформироватьДвиженияПоВозвратам(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование.Организация КАК Организация,
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование КАК ДокументОснование,
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование.ДоговорОПС КАК ДоговорОПС,
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование.Правопреемник КАК Правопреемник,
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.НомерСтроки,
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование.ДоговорОПС.Представление КАК ПредставлениеДоговора,
		|	уп_ВозвратПрочихНачисленийОПСРасшифровкаСуммы.ТипРезерва,
		|	уп_ВозвратПрочихНачисленийОПСРасшифровкаСуммы.ТипСуммы,
		|	уп_ВозвратПрочихНачисленийОПСРасшифровкаСуммы.Сумма
		|ПОМЕСТИТЬ ДокументыОснования
		|ИЗ
		|	Документ.уп_РешениеОПереводеСредствВРезервОПС.ВозвратыВыплатПравопреемникам КАК уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.уп_ВозвратПрочихНачисленийОПС.РасшифровкаСуммы КАК уп_ВозвратПрочихНачисленийОПСРасшифровкаСуммы
		|		ПО уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.ДокументОснование = уп_ВозвратПрочихНачисленийОПСРасшифровкаСуммы.Ссылка
		|ГДЕ
		|	уп_ПередачаВРезервОПСВозвратыВыплатПравопреемникам.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС,
		|	Правопреемник,
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыОснования.Организация,
		|	ДокументыОснования.ДокументОснование,
		|	ДокументыОснования.ДоговорОПС КАК ДоговорОПС,
		|	ДокументыОснования.Правопреемник КАК Правопреемник,
		|	ДокументыОснования.ТипРезерва,
		|	СУММА(ДокументыОснования.Сумма) КАК СуммаДокумента
		|ПОМЕСТИТЬ ДокументыОснованияСГруппировкой
		|ИЗ
		|	ДокументыОснования КАК ДокументыОснования
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументыОснования.Организация,
		|	ДокументыОснования.ДоговорОПС,
		|	ДокументыОснования.Правопреемник,
		|	ДокументыОснования.ТипРезерва,
		|	ДокументыОснования.ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС,
		|	Правопреемник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыОснованияСГруппировкой.Организация,
		|	ДокументыОснованияСГруппировкой.ДоговорОПС,
		|	ДокументыОснованияСГруппировкой.Правопреемник,
		|	ДокументыОснованияСГруппировкой.ТипРезерва,
		|	ЕСТЬNULL(уп_ПрочиеНачисленияОПСОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ДокументыОснованияСГруппировкой.СуммаДокумента,
		|	ДокументыОснованияСГруппировкой.ДокументОснование КАК ДокументОснование,
		|	ЕСТЬNULL(уп_ВозвратыПрочихНачисленийОПСОстатки.СуммаОстаток, 0) КАК СуммаОстатокВозврат
		|ПОМЕСТИТЬ ОстаткиПрочихНачислений
		|ИЗ
		|	ДокументыОснованияСГруппировкой КАК ДокументыОснованияСГруппировкой
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ПрочиеНачисленияОПС.Остатки(
		|				&МоментВремени,
		|				ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыНаследникамОПС)
		|					И (ДоговорОПС, Участник) В
		|						(ВЫБРАТЬ
		|							ДокументыОснованияСГруппировкой.ДоговорОПС,
		|							ДокументыОснованияСГруппировкой.Правопреемник
		|						ИЗ
		|							ДокументыОснованияСГруппировкой КАК ДокументыОснованияСГруппировкой)) КАК уп_ПрочиеНачисленияОПСОстатки
		|		ПО ДокументыОснованияСГруппировкой.Организация = уп_ПрочиеНачисленияОПСОстатки.Организация
		|			И ДокументыОснованияСГруппировкой.ДоговорОПС = уп_ПрочиеНачисленияОПСОстатки.ДоговорОПС
		|			И ДокументыОснованияСГруппировкой.Правопреемник = уп_ПрочиеНачисленияОПСОстатки.Участник
		|			И ДокументыОснованияСГруппировкой.ТипРезерва = уп_ПрочиеНачисленияОПСОстатки.ТипРезерва
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ВозвратыПрочихНачисленийОПС.Остатки(
		|				&МоментВремени,
		|				ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыНаследникамОПС)
		|					И (ДоговорОПС, Участник) В
		|						(ВЫБРАТЬ
		|							ДокументыОснованияСГруппировкой.ДоговорОПС,
		|							ДокументыОснованияСГруппировкой.Правопреемник
		|						ИЗ
		|							ДокументыОснованияСГруппировкой КАК ДокументыОснованияСГруппировкой)) КАК уп_ВозвратыПрочихНачисленийОПСОстатки
		|		ПО ДокументыОснованияСГруппировкой.Организация = уп_ВозвратыПрочихНачисленийОПСОстатки.Организация
		|			И ДокументыОснованияСГруппировкой.ДоговорОПС = уп_ВозвратыПрочихНачисленийОПСОстатки.ДоговорОПС
		|			И ДокументыОснованияСГруппировкой.Правопреемник = уп_ВозвратыПрочихНачисленийОПСОстатки.Участник
		|			И ДокументыОснованияСГруппировкой.ТипРезерва = уп_ВозвратыПрочихНачисленийОПСОстатки.ТипРезерва
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыОснования.Организация,
		|	ДокументыОснования.ДоговорОПС,
		|	ДокументыОснования.Правопреемник,
		|	ДокументыОснования.ТипРезерва,
		|	ЕСТЬNULL(уп_ПрочиеНачисленияОПСОстатки.СуммаОстаток, 0) КАК СуммаОстатокТипСуммы,
		|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
		|	ЕСТЬNULL(уп_ВозвратыПрочихНачисленийОПСОстатки.СуммаОстаток, 0) КАК СуммаОстатокВозвратТипСуммы,
		|	ДокументыОснования.ТипСуммы,
		|	ДокументыОснования.Сумма КАК СуммаПоСтроке
		|ПОМЕСТИТЬ ОстаткиПрочихНачисленийТипыСумм
		|ИЗ
		|	ДокументыОснования КАК ДокументыОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ПрочиеНачисленияОПС.Остатки(
		|				&МоментВремени,
		|				ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыНаследникамОПС)
		|					И (ДоговорОПС, Участник) В
		|						(ВЫБРАТЬ
		|							ДокументыОснованияСГруппировкой.ДоговорОПС,
		|							ДокументыОснованияСГруппировкой.Правопреемник
		|						ИЗ
		|							ДокументыОснованияСГруппировкой КАК ДокументыОснованияСГруппировкой)) КАК уп_ПрочиеНачисленияОПСОстатки
		|		ПО ДокументыОснования.Организация = уп_ПрочиеНачисленияОПСОстатки.Организация
		|			И ДокументыОснования.ДоговорОПС = уп_ПрочиеНачисленияОПСОстатки.ДоговорОПС
		|			И ДокументыОснования.Правопреемник = уп_ПрочиеНачисленияОПСОстатки.Участник
		|			И ДокументыОснования.ТипРезерва = уп_ПрочиеНачисленияОПСОстатки.ТипРезерва
		|			И ДокументыОснования.ТипСуммы = уп_ПрочиеНачисленияОПСОстатки.ТипСуммы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_ВозвратыПрочихНачисленийОПС.Остатки(
		|				&МоментВремени,
		|				ТипВыплаты = ЗНАЧЕНИЕ(Перечисление.уп_ТипыВыплат.ВыплатыНаследникамОПС)
		|					И (ДоговорОПС, Участник) В
		|						(ВЫБРАТЬ
		|							ДокументыОснованияСГруппировкой.ДоговорОПС,
		|							ДокументыОснованияСГруппировкой.Правопреемник
		|						ИЗ
		|							ДокументыОснованияСГруппировкой КАК ДокументыОснованияСГруппировкой)) КАК уп_ВозвратыПрочихНачисленийОПСОстатки
		|		ПО ДокументыОснования.Организация = уп_ВозвратыПрочихНачисленийОПСОстатки.Организация
		|			И ДокументыОснования.ДоговорОПС = уп_ВозвратыПрочихНачисленийОПСОстатки.ДоговорОПС
		|			И ДокументыОснования.Правопреемник = уп_ВозвратыПрочихНачисленийОПСОстатки.Участник
		|			И ДокументыОснования.ТипРезерва = уп_ВозвратыПрочихНачисленийОПСОстатки.ТипРезерва
		|			И ДокументыОснования.ТипСуммы = уп_ВозвратыПрочихНачисленийОПСОстатки.ТипСуммы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыОснования.Организация КАК Организация,
		|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
		|	ДокументыОснования.ДоговорОПС КАК ДоговорОПС,
		|	ДокументыОснования.Правопреемник КАК Правопреемник,
		|	ДокументыОснования.НомерСтроки КАК НомерСтроки,
		|	ДокументыОснования.ПредставлениеДоговора КАК ПредставлениеДоговора,
		|	ДокументыОснования.ТипРезерва КАК ТипРезерва,
		|	ЕстьNULL(ОстаткиПрочихНачислений.СуммаДокумента,0) КАК СуммаДокумента,
		|	ЕстьNULL(ОстаткиПрочихНачислений.СуммаОстаток,0) КАК СуммаОстаток,
		|	ДокументыОснования.ТипСуммы,
		|	ДокументыОснования.Сумма,
		|	ОстаткиПрочихНачисленийТипыСумм.СуммаОстатокТипСуммы КАК СуммаОстатокТипСуммы,
		|	ОстаткиПрочихНачисленийТипыСумм.СуммаОстатокВозвратТипСуммы КАК СуммаОстатокВозвратТипСуммы,
		|	ОстаткиПрочихНачислений.СуммаОстатокВозврат КАК СуммаОстатокВозврат
		|ИЗ
		|	ДокументыОснования КАК ДокументыОснования
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПрочихНачислений КАК ОстаткиПрочихНачислений
		|		ПО ДокументыОснования.Организация = ОстаткиПрочихНачислений.Организация
		|			И ДокументыОснования.ТипРезерва = ОстаткиПрочихНачислений.ТипРезерва
		|			И ДокументыОснования.ДокументОснование = ОстаткиПрочихНачислений.ДокументОснование
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиПрочихНачисленийТипыСумм КАК ОстаткиПрочихНачисленийТипыСумм
		|		ПО ДокументыОснования.Организация = ОстаткиПрочихНачисленийТипыСумм.Организация
		|			И ДокументыОснования.ТипРезерва = ОстаткиПрочихНачисленийТипыСумм.ТипРезерва
		|			И ДокументыОснования.ДокументОснование = ОстаткиПрочихНачисленийТипыСумм.ДокументОснование
		|			И ДокументыОснования.ТипСуммы = ОстаткиПрочихНачисленийТипыСумм.ТипСуммы
		|ИТОГИ
		|	МИНИМУМ(Организация),
		|	МИНИМУМ(ДоговорОПС),
		|	МИНИМУМ(Правопреемник),
		|	МИНИМУМ(НомерСтроки),
		|	МИНИМУМ(ПредставлениеДоговора),
		|	МИНИМУМ(ТипРезерва),
		|	МИНИМУМ(СуммаДокумента),
		|	МИНИМУМ(СуммаОстаток),
		|	СУММА(СуммаОстатокТипСуммы),
		|	СУММА(СуммаОстатокВозвратТипСуммы),
		|	МИНИМУМ(СуммаОстатокВозврат)
		|ПО
		|	ДокументОснование";
		

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Результат = Запрос.Выполнить();

	ВыборкаДокументы = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Движения.уп_АналитикаРезерваОПС.Записывать = Истина;
	Движения.уп_АналитикаРезерваОПС.Очистить();
	
	Движения.уп_ПрочиеНачисленияОПС.Записывать = Истина;
	Движения.уп_ПрочиеНачисленияОПС.Очистить();
	
	Движения.уп_ВозвратыПрочихНачисленийОПС.Записывать = Истина;
	Движения.уп_ВозвратыПрочихНачисленийОПС.Очистить();
	
	Пока ВыборкаДокументы.Следующий() Цикл
		СписываемСВозвратов = Ложь;
		Если ВыборкаДокументы.СуммаОстаток > 0 Тогда
			СписываемСВозвратов = Ложь;
			Если ВыборкаДокументы.СуммаДокумента > ВыборкаДокументы.СуммаОстаток Тогда
				Нехватка = ВыборкаДокументы.СуммаДокумента - ВыборкаДокументы.СуммаОстаток;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не хватает для списания суммы " + Нехватка + " руб. по договору "  + СокрЛП(ВыборкаДокументы.ПредставлениеДоговора) +" (строка "+ ВыборкаДокументы.НомерСтроки +").");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		ИначеЕсли ВыборкаДокументы.СуммаОстатокВозврат > 0 Тогда
			СписываемСВозвратов = Истина;
			Если ВыборкаДокументы.СуммаДокумента > ВыборкаДокументы.СуммаОстатокВозврат Тогда
				Нехватка = ВыборкаДокументы.СуммаДокумента - ВыборкаДокументы.СуммаОстатокВозврат;
				уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не хватает для списания суммы " + Нехватка + " руб. по договору "  + СокрЛП(ВыборкаДокументы.ПредставлениеДоговора) +" (строка "+ ВыборкаДокументы.НомерСтроки +").");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если НЕ СписываемСВозвратов И  ВыборкаДокументы.СуммаДокумента > ВыборкаДокументы.СуммаОстатокТипСуммы Тогда
			//списываем без типа сумм
			Движение = Движения.уп_ПрочиеНачисленияОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = ВыборкаДокументы.Организация;
			Движение.ДоговорОПС = ВыборкаДокументы.ДоговорОПС;
			Движение.Участник = ВыборкаДокументы.Правопреемник;
			Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВыплатыНаследникамОПС;
			Движение.ТипРезерва = ВыборкаДокументы.ТипРезерва;
			Движение.Сумма = -ВыборкаДокументы.СуммаДокумента;
			#Вставка
			// +ХМНПФ 15.11.2017 ШадринАВ // СформироватьДвиженияПоВозвратам() // №1228
			Движение.ХМ_Возврат = Истина;
			// -ХМНПФ 15.11.2017 ШадринАВ // СформироватьДвиженияПоВозвратам() // №1228
			#КонецВставки
			НачисленияСписаны = Истина;
		Иначе
			НачисленияСписаны = Ложь;
		КонецЕсли;
		
		Выборка = ВыборкаДокументы.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СписываемСВозвратов Тогда
				Если Выборка.Сумма>Выборка.СуммаОстатокВозвратТипСуммы Тогда
					Нехватка = Выборка.Сумма - Выборка.СуммаОстатокВозвратТипСуммы;
					уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не хватает для списания суммы по типу "+Выборка.ТипСуммы+" "+ Нехватка + " руб. по договору "  + СокрЛП(ВыборкаДокументы.ПредставлениеДоговора) +" (строка "+ ВыборкаДокументы.НомерСтроки +").");
					Отказ = Истина;
				КонецЕсли;
				Движение = Движения.уп_ВозвратыПрочихНачисленийОПС.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Организация = Выборка.Организация;
				Движение.ДоговорОПС = Выборка.ДоговорОПС;
				Движение.Участник = Выборка.Правопреемник;
				Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВыплатыНаследникамОПС;
				Движение.ТипРезерва = Выборка.ТипРезерва;
				Движение.ТипСуммы = Выборка.ТипСуммы;
				Движение.Сумма = Выборка.Сумма;
				Движение.ВидДвиженияРегистра = Перечисления.уп_ВидыДвиженияВозвратов.ВозвратВРезерв;
			Иначе
				Если НЕ НачисленияСписаны Тогда
					Если Выборка.Сумма>Выборка.СуммаОстатокТипСуммы Тогда
						Нехватка = Выборка.Сумма - Выборка.СуммаОстатокТипСуммы;
						уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Не хватает для списания суммы по типу "+Выборка.ТипСуммы+" "+ Нехватка + " руб. по договору "  + СокрЛП(ВыборкаДокументы.ПредставлениеДоговора) +" (строка "+ ВыборкаДокументы.НомерСтроки +").");
						Отказ = Истина;
					КонецЕсли;
					Движение = Движения.уп_ПрочиеНачисленияОПС.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.Период = Дата;
					Движение.Организация = Выборка.Организация;
					Движение.ДоговорОПС = Выборка.ДоговорОПС;
					Движение.Участник = Выборка.Правопреемник;
					Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВыплатыНаследникамОПС;
					Движение.ТипРезерва = Выборка.ТипРезерва;
					Движение.ТипСуммы = Выборка.ТипСуммы;
					Движение.Сумма = -Выборка.Сумма;
					#Вставка
					// +ХМНПФ 15.11.2017 ШадринАВ // СформироватьДвиженияПоВозвратам() // №1228
					Движение.ХМ_Возврат = Истина;
					// -ХМНПФ 15.11.2017 ШадринАВ // СформироватьДвиженияПоВозвратам() // №1228
					#КонецВставки
				КонецЕсли;
			КонецЕсли;
			
			//РН "Резервы ОПС" по типу резерва = РОПС
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Выборка.Организация;
			//Движение.ДоговорОПС = Выборка.ДоговорОПС;
			Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервОПС;
			//Движение.ТипСуммы = Выборка.ТипСуммы;
			Движение.Сумма = Выборка.Сумма;
			Движение.Движение = Перечисления.уп_ВидыДвижений.ПереводВРОПССредствПоУмершимЗЛ;
			
			//РН "уп_АналитикаРезерваОПС"
			Движение = Движения.уп_АналитикаРезерваОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Выборка.Организация;
			Движение.ДоговорОПС = Выборка.ДоговорОПС;
			Движение.ТипРезерва = Выборка.ТипРезерва;
			Движение.ТипСуммы = Выборка.ТипСуммы;
			//Если  Выборка.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений ИЛИ Выборка.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервОПС Тогда
			//	Движение.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_НЧТП;
			//Иначе
			//	Движение.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_СрочныхВыплат;
			//КонецЕсли;	
			Движение.Сумма = Выборка.Сумма;
			////для отображения в форме договора о возврате выплат ПП в резерве ОПС
			//Движение.СуммаВозвратВыплатПП = Выборка.Сумма;
		КонецЦикла;
	КонецЦикла;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
КонецПроцедуры
