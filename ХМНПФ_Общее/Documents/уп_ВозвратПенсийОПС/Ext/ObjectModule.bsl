&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	
	Отказ = Ложь;

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;

	ПенсионныеПравила = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	ЛицевыеСчетаПоТипамВыплат = ПенсионныеПравила.ЛицевыеСчетаПоТипамВыплат;
				   
	// Заголовок для сообщений об ошибках проведения.
	ТабБухУчет = уп_ОбщегоНазначенияУчетРезервов.СформироватьСтруктуруТаблицыБУ();
	
	Если НЕ ВидОперации = Перечисления.уп_ВидыОперацийВозвратПрочихНачисленийОПС.НеисполненнаяВыплата Тогда
		ПроверкаЗаполнения(Отказ, Заголовок);
	КонецЕсли;

	Если Не Отказ Тогда
		Движения.уп_ВыплатыОПС.Записывать = Истина;
		Движения.уп_ВыплатыОПС.Очистить();
		
		Движения.уп_НачисленияПенсийОПС.Записывать = Истина;
		Движения.уп_НачисленияПенсийОПС.Очистить();
		
		Движения.уп_ВозвратыПенсийОПС.Записывать = Истина;
		Движения.уп_ВозвратыПенсийОПС.Очистить();
		
		//Движения.уп_ОтвергнутыеПлатежиПенсийОПС.Записывать = Истина;
		//Движения.уп_ОтвергнутыеПлатежиПенсийОПС.Очистить();
		
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
		Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
		
		Движения.уп_РезервыОПС.Записывать = Истина;
		Движения.уп_РезервыОПС.Очистить();
		
		//куда возвращаем
		ДатаНачалаРегистраВозвратов = Константы.уп_ДатаНачалаОтдельногоВеденияВозвратов.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаРегистраВозвратов) И ДатаНачалаРегистраВозвратов<=Дата Тогда
			ИспользоватьРегистрВозвратов = Истина;
		Иначе
			ИспользоватьРегистрВозвратов = Ложь;
		КонецЕсли;
		
		ДатаНачалаУчетаОтвергнутыеПлатежи = Константы.уп_ДатаНачалаОтдельногоВеденияОтвергнутыхПлатежей.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаУчетаОтвергнутыеПлатежи) И ДатаНачалаУчетаОтвергнутыеПлатежи<=Дата Тогда
			ИспользоватьОтвергнутыеПлатежи = Истина;
		Иначе
			ИспользоватьОтвергнутыеПлатежи = Ложь;
		КонецЕсли;
		
		ДатаНачалаУчетаРезервовНачисления = Константы.уп_ДатаНачалаВеденияРезервовНачисления.Получить();
		
		ДатаНачалаВеденияРезерваНаследников = Константы.уп_ДатаНачалаВеденияРезерваНаследников.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаВеденияРезерваНаследников) И Дата>=ДатаНачалаВеденияРезерваНаследников Тогда
			ВедетсяРезервНаследников = Истина;
		Иначе
			ВедетсяРезервНаследников = Ложь;
		КонецЕсли;
		
		Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПрочихНачисленийОПС.НеисполненнаяВыплата Тогда
			Если ИспользоватьОтвергнутыеПлатежи Тогда
			КонецЕсли;
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.Ссылка,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.НомерСтроки,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДоговорОПС,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ТипПенсии,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ТипСуммы,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.Сумма,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДатаПериода,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДоговорОПС.Участник КАК ЗЛ,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДокументВыплаты
			|ПОМЕСТИТЬ ОснТаблица
			|ИЗ
			|	Документ.уп_ВозвратПенсийОПС.РасшифровкаСуммы КАК уп_ВозвратПенсийОПСРасшифровкаСуммы
			|ГДЕ
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК Получатель,
			|	уп_ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо
			|ПОМЕСТИТЬ Получатели
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|				(ВЫБРАТЬ
			|					ОснТаблица.ДоговорОПС
			|				ИЗ
			|					ОснТаблица)) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК уп_ПолучателиПоДоговорамОбслуживания
			|		ПО уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк = уп_ПолучателиПоДоговорамОбслуживания.Получатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательНП,
			|	уп_ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении КАК ПолучательВПлатежномПорученииНП,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо
			|ПОМЕСТИТЬ ПолучателиНП
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			|				И ТипВыплат = &НП) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК уп_ПолучателиПоДоговорамОбслуживания
			|		ПО уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк = уп_ПолучателиПоДоговорамОбслуживания.Получатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательСВ,
			|	уп_ПолучателиПоДоговорамОбслуживания.ПолучательВПлатежномПоручении КАК ПолучательВПлатежномПорученииСВ,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо
			|ПОМЕСТИТЬ ПолучателиСВ
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			|				И ТипВыплат = &СВ) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПолучателиПоДоговорамОбслуживания КАК уп_ПолучателиПоДоговорамОбслуживания
			|		ПО уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк = уп_ПолучателиПоДоговорамОбслуживания.Получатель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОснТаблица.ДоговорОПС,
			|	ОснТаблица.ТипПенсии,
			|	ОснТаблица.ТипСуммы,
			|	ОснТаблица.Сумма,
			|	Получатели.Получатель,
			|	Получатели.ПолучательВПлатежномПоручении,
			|	ОснТаблица.ДатаПериода,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ПолучательНП
			|		ИНАЧЕ ПолучателиСВ.ПолучательСВ
			|	КОНЕЦ КАК ПолучательПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ПолучательВПлатежномПорученииНП
			|		ИНАЧЕ ПолучателиСВ.ПолучательВПлатежномПорученииСВ
			|	КОНЕЦ КАК ПолучательВПлатежномПорученииПоТипу,
			|	ОснТаблица.ДокументВыплаты
			|ИЗ
			|	ОснТаблица КАК ОснТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Получатели КАК Получатели
			|		ПО ОснТаблица.ДоговорОПС = Получатели.ДоговорОПС
			|			И ОснТаблица.ЗЛ = Получатели.ЗастрахованноеЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиНП КАК ПолучателиНП
			|		ПО ОснТаблица.ЗЛ = ПолучателиНП.ПолучательНП
			|			И ОснТаблица.ДоговорОПС = ПолучателиНП.ДоговорОПС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиСВ КАК ПолучателиСВ
			|		ПО ОснТаблица.ЗЛ = ПолучателиСВ.ПолучательСВ
			|			И ОснТаблица.ДоговорОПС = ПолучателиСВ.ДоговорОПС";
			
			Запрос.УстановитьПараметр("ДатаДок", Дата-1);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("Пожизненная", Перечисления.уп_ТипыПенсийОПС.Пожизненная);
			Запрос.УстановитьПараметр("НП", Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
			Запрос.УстановитьПараметр("СВ", Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ТекСтрокаРасшифровкаСуммы = РезультатЗапроса.Выбрать();
			
			//Для Каждого ТекСтрокаРасшифровкаСуммы Из РасшифровкаСуммы Цикл
			Пока ТекСтрокаРасшифровкаСуммы.Следующий() Цикл	
				// сторнируем  выплаты по ТЧ расшифровок
				Движение = Движения.уп_ВыплатыОПС.Добавить();
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
				Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
				Движение.Участник = ТекСтрокаРасшифровкаСуммы.ДоговорОПС.Участник;
				Если ТекСтрокаРасшифровкаСуммы.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная Тогда
					Если ТекСтрокаРасшифровкаСуммы.ДокументВыплаты.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВозвратСрочнойПенсииОПС Тогда
						Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВозвратСрочнойПенсииОПС;
					Иначе
						Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС;
					КонецЕсли;
				Иначе
					Если ТекСтрокаРасшифровкаСуммы.ДокументВыплаты.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВозвратНакопительнойПенсииОПС Тогда
						Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВозвратНакопительнойПенсииОПС;
					Иначе
						Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС;
					КонецЕсли;
				КонецЕсли;	
				Движение.Сумма = -ТекСтрокаРасшифровкаСуммы.Сумма;
				Движение.Выплата = -ТекСтрокаРасшифровкаСуммы.Сумма;
				
				Если ИспользоватьРегистрВозвратов Тогда
					Движение = Движения.уп_ВозвратыПенсийОПС.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
					Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
					Движение.НачПериода = ТекСтрокаРасшифровкаСуммы.ДатаПериода;
					Движение.Период = Дата;
					Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
					Движение.Сумма = ТекСтрокаРасшифровкаСуммы.Сумма;
					Движение.ТипПенсии = ТекСтрокаРасшифровкаСуммы.ТипПенсии;
					Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
					Движение.ДокументВыплаты = ТекСтрокаРасшифровкаСуммы.ДокументВыплаты;
					Движение.ВидДвиженияРегистра = Перечисления.уп_ВидыДвиженияВозвратов.Возврат;
					
					ЗачислятьВРезерв = ПроверитьНаЗачислятьВРезервУмерших(Организация, ТекСтрокаРасшифровкаСуммы.ДоговорОПС, Дата);
					Если  ЗачислятьВРезерв Тогда
						//спишем с возврата и закинем в резерв умерших
						Движение = Движения.уп_ВозвратыПенсийОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
						Движение.НачПериода = ТекСтрокаРасшифровкаСуммы.ДатаПериода;
						Движение.Период = Дата;
						Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
						Движение.Сумма = ТекСтрокаРасшифровкаСуммы.Сумма;
						Движение.ТипПенсии = ТекСтрокаРасшифровкаСуммы.ТипПенсии;
						Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
						Движение.ДокументВыплаты = ТекСтрокаРасшифровкаСуммы.ДокументВыплаты;
						Движение.ВидДвиженияРегистра = Перечисления.уп_ВидыДвиженияВозвратов.ВозвратВРезерв;						
						
						Движение = Движения.уп_РезервыОПС.Добавить();
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.Организация = Организация;
						Движение.Период = Дата;
						Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
						Если ТекСтрокаРасшифровкаСуммы.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная Тогда
							Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
							Если ВедетсяРезервНаследников Тогда
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРСВ;
							Иначе
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
							КонецЕсли;
						Иначе
							Движение.ДоговорОПС = Справочники.уп_ДоговорыОПС.ПустаяСсылка();
							Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
						Конецесли;
						Движение.Сумма = ТекСтрокаРасшифровкаСуммы.Сумма;
					КонецЕсли;	
				Иначе
					//сторнируем списание начислений по ТЧ расшифровок
					Движение = Движения.уп_НачисленияПенсийОПС.Добавить();
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
					Движение.НачПериода = ТекСтрокаРасшифровкаСуммы.ДатаПериода;
					Движение.Период = Дата;
					Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
					Движение.Сумма = -ТекСтрокаРасшифровкаСуммы.Сумма;
					Движение.ТипПенсии = ТекСтрокаРасшифровкаСуммы.ТипПенсии;
					Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
					#Вставка
					// +ХМНПФ 18.03.2016 ШадринАВ // ОбработкаПроведения() // №277
					Движение.ХМ_Возврат = Истина;
					// -ХМНПФ 18.03.2016 ШадринАВ // ОбработкаПроведения() // №277
					#КонецВставки
				КонецЕсли;
				
				//здесь должна быть проводка Д74 - К76 на сумму (-стр.Сумма)
				Если ЛицевыеСчетаПоТипамВыплат Тогда
					Если ЗначениеЗаполнено(ТекСтрокаРасшифровкаСуммы.ПолучательВПлатежномПорученииПоТипу) Тогда
						Получ = ТекСтрокаРасшифровкаСуммы.ПолучательВПлатежномПорученииПоТипу;
					ИначеЕсли ЗначениеЗаполнено(ТекСтрокаРасшифровкаСуммы.ПолучательПоТипу) Тогда
						Получ = ТекСтрокаРасшифровкаСуммы.ПолучательПоТипу;
					Иначе
						Получ = Справочники.Контрагенты.ПустаяСсылка();
					КонецЕсли;
				Иначе
					Если ЗначениеЗаполнено(ТекСтрокаРасшифровкаСуммы.ПолучательВПлатежномПоручении) Тогда
						Получ = ТекСтрокаРасшифровкаСуммы.ПолучательВПлатежномПоручении;
					ИначеЕсли ЗначениеЗаполнено(ТекСтрокаРасшифровкаСуммы.Получатель) Тогда
						Получ = ТекСтрокаРасшифровкаСуммы.Получатель;
					Иначе
						Получ = Справочники.Контрагенты.ПустаяСсылка();
					КонецЕсли;
				КонецЕсли;
				Если ТекСтрокаРасшифровкаСуммы.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная Тогда 
					ВидДвижения = Перечисления.уп_ВидыДвижений.ВозвратСрочнойПенсииОПС;
				Иначе
					ВидДвижения = Перечисления.уп_ВидыДвижений.ВозвратНакопительнойПенсииОПС;
				КонецЕсли;	
				//здесь еще надо определить какой получатель, если через кассу - по ним надо аналитику загнать
				Если НЕ ЗначениеЗаполнено(Получ) Тогда //через кассу
					Получ = ТекСтрокаРасшифровкаСуммы.ДоговорОПС.Участник;
					ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
				Иначе
					ДоговорКонтрагента = Получ.ОсновнойДоговорКонтрагента;
				КонецЕсли;
				
				уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, Получ, ДоговорКонтрагента,
				Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, Получ, ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, -ТекСтрокаРасшифровкаСуммы.Сумма);
				
			КонецЦикла;
			#Вставка
			// +ХМНПФ 25.04.2016 ШадринАВ // ОбработкаПроведения() // №326
			ХМ_ДополнитьДвижения();
			// -ХМНПФ 25.04.2016 ШадринАВ // ОбработкаПроведения() // №326
			#КонецВставки
			
			Если ОтражатьВБухгалтерскомУчете    Тогда
				уп_ОбщегоНазначенияУчетРезервов.ДополнитьТаблицуБухУчета(Дата, ТабБухУчет, Ссылка, Отказ);
				уп_ОбщегоНазначенияУчетРезервов.СделатьДвиженияПоБухУчету(ЭтотОбъект, ТабБухУчет);
			КонецЕсли;
			
		КонецЕсли;
		//свернем таблицу возвратов
		ТабВозвратов = ДоговорыОПС.Выгрузить();
		ТабВозвратов.Свернуть("ДоговорОПС","Сумма");
		
		//Для Каждого СтрокаТабВозвратов из ТабВозвратов Цикл
		//	Сообщить("По договору ОПС "+СтрокаТабВозвратов.ДоговорОПС+" вплоть до выяснения, необходимо приостановить выплаты.");
		//КонецЦикла;	
		
		Если НекорректныеРеквизиты Тогда
			Запрос = Новый Запрос;
			Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПрочихНачисленийОПС.НеисполненнаяВыплата Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	уп_ВозвратПенсийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
			|	уп_ВозвратПенсийОПСДоговорыОПС.ТипПенсии КАК ТипПенсии,
			|	уп_ВозвратПенсийОПСДоговорыОПС.ДоговорОПС.Участник КАК ЗЛ
			|ПОМЕСТИТЬ ОснТаблица
			|ИЗ
			|	Документ.уп_ВозвратПенсийОПС.ДоговорыОПС КАК уп_ВозвратПенсийОПСДоговорыОПС
			|ГДЕ
			|	уп_ВозвратПенсийОПСДоговорыОПС.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС КАК ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК Получатель,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты КАК ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета КАК ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета КАК НомерЛицевогоСчета
			|ПОМЕСТИТЬ Получатели
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			#Удаление
			|				И Организация = &Организация) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			#КонецУдаления
			#Вставка
			|) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			#КонецВставки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС КАК ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательНП,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты КАК ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета КАК ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета КАК НомерЛицевогоСчета
			|ПОМЕСТИТЬ ПолучателиНП
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			#Удаление
			|				И Организация = &Организация
			#КонецУдаления
			|				И ТипВыплат = &НП) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС КАК ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательСВ,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо КАК ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты КАК ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет КАК РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета КАК ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета КАК НомерЛицевогоСчета
			|ПОМЕСТИТЬ ПолучателиСВ
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			#Удаление
			|				И Организация = &Организация
			#КонецУдаления
			|				И ТипВыплат = &СВ) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОснТаблица.ДоговорОПС КАК ДоговорОПС,
			|	ОснТаблица.ТипПенсии КАК ТипПенсии,
			|	Получатели.Получатель КАК Получатель,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ПолучательНП
			|		ИНАЧЕ ПолучателиСВ.ПолучательСВ
			|	КОНЕЦ КАК ПолучательПоТипу,
			|	ОснТаблица.ЗЛ КАК ЗЛ,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ФормаВыплаты
			|		ИНАЧЕ ПолучателиСВ.ФормаВыплаты
			|	КОНЕЦ КАК ФормаВыплатыПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.РасчетныйСчет
			|		ИНАЧЕ ПолучателиСВ.РасчетныйСчет
			|	КОНЕЦ КАК РасчетныйСчетПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ТипЛицевогоСчета
			|		ИНАЧЕ ПолучателиСВ.ТипЛицевогоСчета
			|	КОНЕЦ КАК ТипЛицевогоСчетаПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.НомерЛицевогоСчета
			|		ИНАЧЕ ПолучателиСВ.НомерЛицевогоСчета
			|	КОНЕЦ КАК НомерЛицевогоСчетаПоТипу,
			|	Получатели.ФормаВыплаты КАК ФормаВыплаты,
			|	Получатели.РасчетныйСчет КАК РасчетныйСчет,
			|	Получатели.ТипЛицевогоСчета КАК ТипЛицевогоСчета,
			|	Получатели.НомерЛицевогоСчета КАК НомерЛицевогоСчета
			|ИЗ
			|	ОснТаблица КАК ОснТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Получатели КАК Получатели
			|		ПО ОснТаблица.ДоговорОПС = Получатели.ДоговорОПС
			|			И ОснТаблица.ЗЛ = Получатели.ЗастрахованноеЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиНП КАК ПолучателиНП
			|		ПО ОснТаблица.ЗЛ = ПолучателиНП.ЗастрахованноеЛицо
			|			И ОснТаблица.ДоговорОПС = ПолучателиНП.ДоговорОПС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиСВ КАК ПолучателиСВ
			|		ПО ОснТаблица.ЗЛ = ПолучателиСВ.ЗастрахованноеЛицо
			|			И ОснТаблица.ДоговорОПС = ПолучателиСВ.ДоговорОПС
			|ИТОГИ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТипПенсии)
			|ПО
			|	ДоговорОПС";
				
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДоговорОПС,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ТипПенсии,
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.ДоговорОПС.Участник КАК ЗЛ
			|ПОМЕСТИТЬ ОснТаблица
			|ИЗ
			|	Документ.уп_ВозвратПенсийОПС.РасшифровкаСуммы КАК уп_ВозвратПенсийОПСРасшифровкаСуммы
			|ГДЕ
			|	уп_ВозвратПенсийОПСРасшифровкаСуммы.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК Получатель,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета
			|ПОМЕСТИТЬ Получатели
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|				(ВЫБРАТЬ
			|					ОснТаблица.ДоговорОПС
			|				ИЗ
			#Удаление
			|					ОснТаблица) И Организация = &Организация) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			#КонецУдаления
			#Вставка
			|					ОснТаблица)) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			#КонецВставки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательНП,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета
			|ПОМЕСТИТЬ ПолучателиНП
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица) 
			#Удаление
			|				И Организация = &Организация
			#КонецУдаления
			|				И ТипВыплат = &НП) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ДоговорОПС,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.Банк КАК ПолучательСВ,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ЗастрахованноеЛицо,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ФормаВыплаты,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.РасчетныйСчет,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.ТипЛицевогоСчета,
			|	уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних.НомерЛицевогоСчета
			|ПОМЕСТИТЬ ПолучателиСВ
			|ИЗ
			|	РегистрСведений.уп_ЛицевыеСчетаЗастрахованныхЛиц.СрезПоследних(
			|			&ДатаДок,
			|			ДоговорОПС В
			|					(ВЫБРАТЬ
			|						ОснТаблица.ДоговорОПС
			|					ИЗ
			|						ОснТаблица)
			#Удаление
			|				И Организация = &Организация
			#КонецУдаления
			|				И ТипВыплат = &СВ) КАК уп_ЛицевыеСчетаЗастрахованныхЛицСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОснТаблица.ДоговорОПС КАК ДоговорОПС,
			|	ОснТаблица.ТипПенсии КАК ТипПенсии,
			|	Получатели.Получатель,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ПолучательНП
			|		ИНАЧЕ ПолучателиСВ.ПолучательСВ
			|	КОНЕЦ КАК ПолучательПоТипу,
			|	ОснТаблица.ЗЛ,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ФормаВыплаты
			|		ИНАЧЕ ПолучателиСВ.ФормаВыплаты
			|	КОНЕЦ КАК ФормаВыплатыПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.РасчетныйСчет
			|		ИНАЧЕ ПолучателиСВ.РасчетныйСчет
			|	КОНЕЦ КАК РасчетныйСчетПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.ТипЛицевогоСчета
			|		ИНАЧЕ ПолучателиСВ.ТипЛицевогоСчета
			|	КОНЕЦ КАК ТипЛицевогоСчетаПоТипу,
			|	ВЫБОР
			|		КОГДА ОснТаблица.ТипПенсии = &Пожизненная
			|			ТОГДА ПолучателиНП.НомерЛицевогоСчета
			|		ИНАЧЕ ПолучателиСВ.НомерЛицевогоСчета
			|	КОНЕЦ КАК НомерЛицевогоСчетаПоТипу,
			|	Получатели.ФормаВыплаты,
			|	Получатели.РасчетныйСчет,
			|	Получатели.ТипЛицевогоСчета,
			|	Получатели.НомерЛицевогоСчета
			|ИЗ
			|	ОснТаблица КАК ОснТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Получатели КАК Получатели
			|		ПО ОснТаблица.ДоговорОПС = Получатели.ДоговорОПС
			|			И ОснТаблица.ЗЛ = Получатели.ЗастрахованноеЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиНП КАК ПолучателиНП
			|		ПО ОснТаблица.ЗЛ = ПолучателиНП.ЗастрахованноеЛицо
			|			И ОснТаблица.ДоговорОПС = ПолучателиНП.ДоговорОПС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолучателиСВ КАК ПолучателиСВ
			|		ПО ОснТаблица.ЗЛ = ПолучателиСВ.ЗастрахованноеЛицо
			|			И ОснТаблица.ДоговорОПС = ПолучателиСВ.ДоговорОПС
			|ИТОГИ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТипПенсии)
			|ПО
			|	ДоговорОПС";
			КонецЕсли;
			Запрос.УстановитьПараметр("ДатаДок", Дата-1);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("Пожизненная", Перечисления.уп_ТипыПенсийОПС.Пожизненная);
			Запрос.УстановитьПараметр("НП", Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
			Запрос.УстановитьПараметр("СВ", Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС); 
			#Удаление
			Запрос.УстановитьПараметр("Организация", Организация); 
			#КонецУдаления
			
			Результат = Запрос.Выполнить();
			ВыборкаИтогов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИтогов.Следующий() Цикл
				ДатаДок = Дата-1;
				Выборка = ВыборкаИтогов.Выбрать();
				Пока Выборка.Следующий() Цикл
					ДатаДок = ДатаДок+1;
					Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
					Движение.Организация = Организация;
					Движение.Период = ДатаДок;
					Движение.ДоговорОПС = Выборка.ДоговорОПС;
					Движение.ЗастрахованноеЛицо = Выборка.ЗЛ;
					Движение.НеДействует = Истина;
					Если Выборка.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная Тогда
						Движение.ТипВыплат = Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС;
					Иначе
						Движение.ТипВыплат = Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС;
					КонецЕсли;	
					Если ЛицевыеСчетаПоТипамВыплат Тогда
						Движение.Банк = Выборка.ПолучательПоТипу;
						Движение.НомерЛицевогоСчета = Выборка.НомерЛицевогоСчетаПоТипу;
						Движение.РасчетныйСчет = Выборка.РасчетныйСчетПоТипу;
						Движение.ТипЛицевогоСчета = Выборка.ТипЛицевогоСчетаПоТипу;
						Движение.ФормаВыплаты = Выборка.ФормаВыплатыПоТипу;
					Иначе
						Движение.Банк = Выборка.Получатель;
						Движение.НомерЛицевогоСчета = Выборка.НомерЛицевогоСчета;
						Движение.РасчетныйСчет = Выборка.РасчетныйСчет;
						Движение.ТипЛицевогоСчета = Выборка.ТипЛицевогоСчета;
						Движение.ФормаВыплаты = Выборка.ФормаВыплаты;
					КонецЕсли;
				КонецЦикла;	
			КонецЦикла;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&ИзменениеИКонтроль("РассчитатьДокумент")
Процедура РасшОбщ_РассчитатьДокумент() Экспорт
	
	//поставить проверку на заполненность дат
		
	РасшифровкаСуммы.Очистить();
	
	//собрать таблицу по датам, а внутри списки значений договоров ОПС
	ТабДат = Новый ТаблицаЗначений;
	ТабДат.Колонки.Добавить("ДатаНачисления");
	ТабДат.Колонки.Добавить("СписокДоговоров");
	ТекДат = "";
	ТабВозврата = ДоговорыОПС.Выгрузить();
	ТабВозврата.Сортировать("ДатаПериода");
	ТекДата = "";
	//СписокДоговоров = Новый СписокЗначений;
	СписокДоговоров = Новый ТаблицаЗначений;
	СписокДоговоров.Колонки.Добавить("ДоговорОПС", Новый ОписаниеТипов("СправочникСсылка.уп_ДоговорыОПС"));
	СписокДоговоров.Колонки.Добавить("ТипПенсии", Новый ОписаниеТипов("ПеречислениеСсылка.уп_ТипыПенсийОПС"));
	Для Каждого стр из ТабВозврата Цикл
		Если ТекДата<>стр.ДатаПериода Тогда
			Если ТекДата<>"" Тогда
				НовСтр = ТабДат.Добавить();
				НовСтр.ДатаНачисления = ТекДата;
				НовСтр.СписокДоговоров = СписокДоговоров;
			КонецЕсли;
			ТекДата = стр.ДатаПериода;
			//СписокДоговоров = Новый СписокЗначений;
			СписокДоговоров = Новый ТаблицаЗначений;
			СписокДоговоров.Колонки.Добавить("ДоговорОПС", Новый ОписаниеТипов("СправочникСсылка.уп_ДоговорыОПС"));
			СписокДоговоров.Колонки.Добавить("ТипПенсии", Новый ОписаниеТипов("ПеречислениеСсылка.уп_ТипыПенсийОПС"));
		КонецЕсли;
		//СписокДоговоров.Добавить(стр.ДоговорОПС);
		НовСтрДоговоров = СписокДоговоров.Добавить();
		НовСтрДоговоров.ДоговорОПС = стр.ДоговорОПС;
		НовСтрДоговоров.ТипПенсии = стр.ТипПенсии;
	КонецЦикла;	
	Если СписокДоговоров.Количество()>0 Тогда
		НовСтр = ТабДат.Добавить();
		НовСтр.ДатаНачисления = ТекДата;
		НовСтр.СписокДоговоров = СписокДоговоров;
	КонецЕсли;
	
	Для Каждого стр из ТабДат Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписокДоговоров.ДоговорОПС,
		|	СписокДоговоров.ТипПенсии
		|ПОМЕСТИТЬ СписокДоговоровОПС
		|ИЗ
		|	&СписокДоговоровОПС КАК СписокДоговоров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_НачисленияПенсийОПСОбороты.ДоговорОПС,
		|	уп_НачисленияПенсийОПСОбороты.Период КАК Период,
		|	уп_НачисленияПенсийОПСОбороты.ТипПенсии,
		|	уп_НачисленияПенсийОПСОбороты.НачПериода
		|ПОМЕСТИТЬ ДатыНачислений
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсийОПС.Обороты(
		|			&НачВыплат,
		|			&ДатаДок,
		|			Регистратор,
		|			НачПериода = &НачПериода
		|				И ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоровОПС.ДоговорОПС
		|					ИЗ
		|						СписокДоговоровОПС)) КАК уп_НачисленияПенсийОПСОбороты
		|ГДЕ
		|	уп_НачисленияПенсийОПСОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	уп_ВозвратыПенсийОПСОбороты.ДоговорОПС,
		|	уп_ВозвратыПенсийОПСОбороты.Период,
		|	уп_ВозвратыПенсийОПСОбороты.ТипПенсии,
		|	уп_ВозвратыПенсийОПСОбороты.НачПериода
		|ИЗ
		|	РегистрНакопления.уп_ВозвратыПенсийОПС.Обороты(
		|			&НачВыплат,
		|			&ДатаДок,
		|			Регистратор,
		|			НачПериода = &НачПериода
		|				И ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоровОПС.ДоговорОПС
		|					ИЗ
		|						СписокДоговоровОПС)) КАК уп_ВозвратыПенсийОПСОбороты
		|ГДЕ
		|	уп_ВозвратыПенсийОПСОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыНачислений.ДоговорОПС,
		|	МАКСИМУМ(ДатыНачислений.Период) КАК Период,
		|	ДатыНачислений.ТипПенсии
		|ПОМЕСТИТЬ ПоследниеНачисления
		|ИЗ
		|	ДатыНачислений КАК ДатыНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыНачислений.ДоговорОПС,
		|	ДатыНачислений.ТипПенсии
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_НачисленияПенсийОПСОбороты.НачПериода,
		|	уп_НачисленияПенсийОПСОбороты.ТипСуммы,
		|	уп_НачисленияПенсийОПСОбороты.ТипПенсии,
		|	уп_НачисленияПенсийОПСОбороты.СуммаРасход,
		|	уп_НачисленияПенсийОПСОбороты.Регистратор,
		|	уп_НачисленияПенсийОПСОбороты.Период КАК Период,
		|	уп_НачисленияПенсийОПСОбороты.ДоговорОПС
		|ПОМЕСТИТЬ Начисления
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсийОПС.Обороты(
		|			&НачВыплат,
		|			&ДатаДок,
		|			Регистратор,
		|			НачПериода = &НачПериода
		|				И ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоровОПС.ДоговорОПС
		|					ИЗ
		|						СписокДоговоровОПС)) КАК уп_НачисленияПенсийОПСОбороты
		|ГДЕ
		|	уп_НачисленияПенсийОПСОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	уп_ВозвратыПенсийОПСОбороты.НачПериода,
		|	уп_ВозвратыПенсийОПСОбороты.ТипСуммы,
		|	уп_ВозвратыПенсийОПСОбороты.ТипПенсии,
		|	уп_ВозвратыПенсийОПСОбороты.СуммаРасход,
		|	уп_ВозвратыПенсийОПСОбороты.Регистратор,
		|	уп_ВозвратыПенсийОПСОбороты.Период,
		|	уп_ВозвратыПенсийОПСОбороты.ДоговорОПС
		|ИЗ
		|	РегистрНакопления.уп_ВозвратыПенсийОПС.Обороты(
		|			&НачВыплат,
		|			&ДатаДок,
		|			Регистратор,
		|			НачПериода = &НачПериода
		|				И ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокДоговоровОПС.ДоговорОПС
		|					ИЗ
		|						СписокДоговоровОПС)) КАК уп_ВозвратыПенсийОПСОбороты
		|ГДЕ
		|	уп_ВозвратыПенсийОПСОбороты.Регистратор ССЫЛКА Документ.уп_ВыплатныеРеестры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоследниеНачисления.ДоговорОПС,
		|	ПоследниеНачисления.Период,
		|	ПоследниеНачисления.ТипПенсии,
		|	Начисления.ТипСуммы,
		|	Начисления.СуммаРасход,
		|	Начисления.Регистратор,
		|	Начисления.НачПериода
		|ПОМЕСТИТЬ ПоследниеНачисленияССуммами
		|ИЗ
		|	ПоследниеНачисления КАК ПоследниеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ Начисления КАК Начисления
		|		ПО ПоследниеНачисления.ДоговорОПС = Начисления.ДоговорОПС
		|			И ПоследниеНачисления.ТипПенсии = Начисления.ТипПенсии
		|			И ПоследниеНачисления.Период = Начисления.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокДоговоровОПС.ДоговорОПС,
		|	ПоследниеНачисленияССуммами.ТипСуммы,
		|	ПоследниеНачисленияССуммами.СуммаРасход,
		|	ПоследниеНачисленияССуммами.Регистратор,
		|	СписокДоговоровОПС.ТипПенсии,
		|	ПоследниеНачисленияССуммами.НачПериода
		|ИЗ
		|	СписокДоговоровОПС КАК СписокДоговоровОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПоследниеНачисленияССуммами КАК ПоследниеНачисленияССуммами
		|		ПО СписокДоговоровОПС.ТипПенсии = ПоследниеНачисленияССуммами.ТипПенсии";
		
		Запрос.УстановитьПараметр("НачПериода", стр.ДатаНачисления);
		Запрос.УстановитьПараметр("СписокДоговоровОПС", стр.СписокДоговоров);
		Запрос.УстановитьПараметр("НачВыплат", ДобавитьМесяц(стр.ДатаНачисления,-1));
		Запрос.УстановитьПараметр("ДатаДок", Дата-1);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НовСтр = РасшифровкаСуммы.Добавить();
			НовСтр.ДатаПериода = Выборка.НачПериода;
			НовСтр.ДоговорОПС = Выборка.ДоговорОПС;
			НовСтр.ТипПенсии = Выборка.ТипПенсии;
			НовСтр.ТипСуммы = Выборка.ТипСуммы;
			НовСтр.Сумма = Выборка.СуммаРасход;
			НовСтр.ДокументВыплаты = Выборка.Регистратор;
		КонецЦикла;
	КонецЦикла;
	
	#Вставка
	// +ХМНПФ 20.02.2016 ШадринАВ // КоманднаяПанель2РассчитатьВозврат() // №279
	ХМ_ОпределитьДокументВыплаты();
	// -ХМНПФ 20.02.2016 ШадринАВ // КоманднаяПанель2РассчитатьВозврат() // №279
	#КонецВставки

КонецПроцедуры

// ХМНПФ 25.04.2016 ШадринАВ // Новая ХМ_ДополнитьДвижения() // №326
Процедура ХМ_ДополнитьДвижения()
	Движения.уп_РезервыОПС.Записывать = Истина;
	Движения.уп_РезервыОПС.Очистить();
	Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Записывать = Истина;
	Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Очистить();
	Если ХМ_ВозвратВРезерв Тогда 
		Для Каждого ТекСтрокаРасшифровкаСуммы Из РасшифровкаСуммы Цикл
			//сторнируем начисление по ТЧ расшифровок
			Движение = Движения.уп_НачисленияПенсийОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
			Движение.НачПериода = ТекСтрокаРасшифровкаСуммы.ДатаПериода;
			Движение.Период = Дата;
			Движение.ПериодичностьВыплат = Перечисления.уп_ПериодичностьВыплат.Ежемесячно;
			Движение.Сумма = -ТекСтрокаРасшифровкаСуммы.Сумма;
			Движение.ТипПенсии = ТекСтрокаРасшифровкаСуммы.ТипПенсии;
			Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
			Движение.ХМ_Возврат = Истина;
			
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ТекСтрокаРасшифровкаСуммы.ДоговорОПС;
			Движение.ТипСуммы = ТекСтрокаРасшифровкаСуммы.ТипСуммы;
			Если ТекСтрокаРасшифровкаСуммы.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная Тогда
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.Выплатной;
			ИначеЕсли ТекСтрокаРасшифровкаСуммы.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная Тогда
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты;
			КонецЕсли;
			Движение.Сумма = ТекСтрокаРасшифровкаСуммы.Сумма;
		КонецЦикла;
	КонецЕсли;
	// ХМНПФ 04.05.2017 ШадринАВ // Приостановка выплат // №820 // не актуально с релиза 4.0.24.2 ШадринАВ 07.08.2017
	//Если ХМ_ПрекращатьВыплаты Тогда
	//	ПенсионныеПравила = РегистрыСведений.уп_ПенсионныеПравила.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	//	ЛицевыеСчетаПоТипамВыплат = ПенсионныеПравила.ЛицевыеСчетаПоТипамВыплат;
	//	
	//	ТЗРасшифровка = РасшифровкаСуммы.Выгрузить();
	//	Если ЛицевыеСчетаПоТипамВыплат Тогда
	//		ТЗРасшифровка.Свернуть("ДоговорОПС,ТипПенсии");
	//	Иначе
	//		ТЗРасшифровка.Свернуть("ДоговорОПС");
	//	КонецЕсли;
	//	Для Каждого ТекСтрока Из ТЗРасшифровка Цикл
	//		//параметры выплат
	//		Если ЛицевыеСчетаПоТипамВыплат Тогда
	//			ТипВыплаты = ?(ТекСтрока.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная, Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС, Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
	//		Иначе
	//			ТипВыплаты = Перечисления.уп_ТипыВыплат.ПустаяСсылка();
	//		КонецЕсли;
	//		ТекПараметры = уп_ОПС.ПолучитьПараметрыВыплат(Дата, Организация, ТекСтрока.ДоговорОПС, ТекСтрока.ДоговорОПС.Участник, ТипВыплаты, ЛицевыеСчетаПоТипамВыплат);
	//		
	//		Движение = Движения.уп_ЛицевыеСчетаЗастрахованныхЛиц.Добавить();
	//		Движение.ДоговорОПС = ТекСтрока.ДоговорОПС;
	//		Движение.Период = Дата;
	//		Движение.ЗастрахованноеЛицо = ТекСтрока.ДоговорОПС.Участник;
	//		Движение.ТипВыплат = ТипВыплаты;
	//		Движение.ФормаВыплаты = ТекПараметры.ФормаВыплаты;
	//		Движение.НомерЛицевогоСчета = ТекПараметры.НомерЛицевогоСчета;
	//		Движение.НеДействует = Истина;
	//		Движение.Организация = Организация;
	//		Движение.РасчетныйСчет = ТекПараметры.РасчетныйСчет;
	//		Движение.ТипЛицевогоСчета = ТекПараметры.ТипЛицевогоСчета;
	//		Движение.ФормаВыплаты = ТекПараметры.ФормаВыплаты;
	//		Движение.Банк = ТекПараметры.Банк;
	//	КонецЦикла;
	//КонецЕсли;
КонецПроцедуры		

// ХМНПФ 23.03.2016 ШадринАВ // Новая ХМ_ОпределитьДокументВыплаты() // №279
Процедура ХМ_ОпределитьДокументВыплаты()
	Если РасшифровкаСуммы.Количество()>0 Тогда
		ТекущиеДанныеРасшифровки = РасшифровкаСуммы[0];
	Иначе
		Возврат;
	КонецЕсли;
	// +ХМНПФ 27.12.2018 ШадринАВ // ХМ_ОпределитьДокументВыплаты() // №1965
	//Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//               |	уп_ВыплатныеРеестры.Ссылка
	//               |ИЗ
	//               |	Документ.уп_ВыплатныеРеестры КАК уп_ВыплатныеРеестры
	//               |ГДЕ
	//               |	уп_ВыплатныеРеестры.Организация = &Организация
	//               |	И уп_ВыплатныеРеестры.ДоговорыОПС.ДоговорОПС = &ДоговорОПС
	//               |	И уп_ВыплатныеРеестры.ДоговорыОПС.Участник = &Участник
	//               |	И уп_ВыплатныеРеестры.ТипВыплаты = &ТипВыплаты
	//               |
	//               |УПОРЯДОЧИТЬ ПО
	//               |	уп_ВыплатныеРеестры.Дата УБЫВ";
	//
	//Запрос.УстановитьПараметр("Организация", Организация);
	//Запрос.УстановитьПараметр("ДоговорОПС", ТекущиеДанныеРасшифровки.ДоговорОПС);
	//Если ТекущиеДанныеРасшифровки.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Пожизненная Тогда
	//	Запрос.УстановитьПараметр("ТипВыплаты", Перечисления.уп_ТипыВыплат.ВыплатыПенсийОПС);
	//ИначеЕсли ТекущиеДанныеРасшифровки.ТипПенсии = Перечисления.уп_ТипыПенсийОПС.Срочная Тогда
	//	Запрос.УстановитьПараметр("ТипВыплаты", Перечисления.уп_ТипыВыплат.СрочнаяПенсияОПС);
	//КонецЕсли;
	//Запрос.УстановитьПараметр("Участник", ТекущиеДанныеРасшифровки.ДоговорОПС.Участник);
	//
	//Результат = Запрос.Выполнить();
	//Выборка = Результат.Выбрать();
	//
	//Если Выборка.Следующий() Тогда
	//	СписокДокументов = Новый СписокЗначений;
	//	ВидДокумента = "ПлатежноеПоручение";
	//	СписокДокументов = ХМ_ФункцииОтчетов.НайтиПодчиненныйПлатежныйДокумент(Выборка.Ссылка, ВидДокумента, СписокДокументов);
	//	Если СписокДокументов.Количество() > 0 Тогда
	//		ХМ_ВозвратСПлатежногоПоручения = СписокДокументов[0].Значение;
	//	КонецЕсли;
	//КонецЕсли;
	// -ХМНПФ 27.12.2018 ШадринАВ // ХМ_ОпределитьДокументВыплаты() // №1965
	
	СписокДокументов = Новый СписокЗначений;
	ВидДокумента = "ПлатежноеПоручение";
	СписокДокументов = ХМ_ФункцииОтчетов.НайтиПодчиненныйПлатежныйДокумент(ТекущиеДанныеРасшифровки.ДокументВыплаты, ВидДокумента, СписокДокументов);
	Если СписокДокументов.Количество() > 0 Тогда
		ХМ_ВозвратСПлатежногоПоручения = СписокДокументов[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 18.03.2016 ШадринАВ // ОбработкаПроведения() // №277
	// ХМНПФ 25.04.2016 ШадринАВ // ОбработкаПроведения() // №326
	// ХМНПФ 25.04.2016 ШадринАВ // Новая ХМ_ДополнитьДвижения() // №326
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, Режим) 
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;

	Документы.уп_ВозвратПенсийОПС.уп_ВозвратПенсийОПС(ЭтотОбъект, Отказ, Режим);
	
КонецПроцедуры


