
&НаСервере
&ИзменениеИКонтроль("ПоДаннымСистемыЗакрытиеСчетовНаСервере")
Функция ХМПоДаннымСистемыЗакрытиеСчетовНаСервере()

	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
#Вставка
//++https://redmine.npf.com/issues/4274
	|	уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|	СУММА(уп_СчетчикНачислений.КоличествоОборот) КАК КоличествоОборот
	|ПОМЕСТИТЬ уп_СчетчикНачисленийОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		уп_СчетчикНачислений.НомерСчета КАК НомерСчета,
	|		уп_СчетчикНачислений.Количество КАК КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.уп_СчетчикНачислений КАК уп_СчетчикНачислений
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК хм_СрезСчетчик
	|			ПО уп_СчетчикНачислений.НомерСчета = хм_СрезСчетчик.НомерСчета
	|				И уп_СчетчикНачислений.Период < хм_СрезСчетчик.ДатаУстановкиСостояния
	|				И (хм_СрезСчетчик.ДатаУстановкиСостояния >= &ДатаНач)
	|	ГДЕ
	|		уп_СчетчикНачислений.Период <= &ДатаДок
	|	    И уп_СчетчикНачислений.НомерСчета.Владелец.Организация = &Организация
	|		И уп_СчетчикНачислений.Период >= &ДатаНач
	|		И хм_СрезСчетчик.ДатаУстановкиСостояния ЕСТЬ NULL
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		хм_СрезСчетчик.НомерСчета,
	|		хм_СрезСчетчик.Количество
	|	ИЗ
	|		РегистрСведений.хм_СрезСчетчикНачисленийНПО.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК хм_СрезСчетчик
	|	ГДЕ
	|		хм_СрезСчетчик.ДатаУстановкиСостояния >= &ДатаНач
	|	) КАК уп_СчетчикНачислений
    |
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СчетчикНачислений.НомерСчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
#КонецВставки
	|	СостоянияПССрезПоследних.НомерСчета КАК НомерСчета,
	|	СостоянияПССрезПоследних.Состояние КАК Состояние,
	|	СхемаСчетаСрезПоследних.Схема КАК Схема,
	|	ВЫБОР 
	|		КОГДА СхемаСчетаСрезПоследних.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПустаяСсылка) 
	|			ТОГДА СхемаСчетаСрезПоследних.Схема.ТипПенсионнойСхемы
	|		ИНАЧЕ СхемаСчетаСрезПоследних.ТипПенсии 
	|	КОНЕЦ КАК ТипПенсионнойСхемы,
	|	ЕСТЬNULL(КоличествоНазначенныхВыплатСрезПоследних.КоличествоВыплат, 0) КАК КоличествоНазначенныхВыплат,
	|	ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат КАК ПериодичностьВыплат,
	|	СостоянияПССрезПоследних.Период КАК ДатаНачалаВыплат,
	|	ЕСТЬNULL(РезервыОстатки.СуммаОстаток, 0) КАК Сумма,
	|	СостоянияПССрезПоследних.НомерСчета.Владелец КАК Договор,
	|	ЕСТЬNULL(уп_СчетчикНачисленийОбороты.КоличествоОборот, 0) КАК КоличествоСделанныхВыплат,
	|	СостоянияПССрезПоследних.НомерСчета.Участник КАК Участник
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК СостоянияПССрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СхемаСчета.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК СхемаСчетаСрезПоследних
	|		ПО СостоянияПССрезПоследних.НомерСчета = СхемаСчетаСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_КоличествоНазначенныхВыплат.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК КоличествоНазначенныхВыплатСрезПоследних
	|		ПО СостоянияПССрезПоследних.НомерСчета = КоличествоНазначенныхВыплатСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПериодичностьВыплат.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК ПериодичностьВыплатСрезПоследних
	|		ПО СостоянияПССрезПоследних.НомерСчета = ПериодичностьВыплатСрезПоследних.НомерСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_Резервы.Остатки(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК РезервыОстатки
	|		ПО СостоянияПССрезПоследних.НомерСчета = РезервыОстатки.НомерСчета
#Вставка
//++https://redmine.npf.com/issues/4274
	|		ЛЕВОЕ СОЕДИНЕНИЕ уп_СчетчикНачисленийОбороты КАК уп_СчетчикНачисленийОбороты
//--https://redmine.npf.com/issues/4274
#КонецВставки
#Удаление
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уп_СчетчикНачислений.Обороты(&ДатаНач, &ДатаДок, Период, НомерСчета.Владелец.Организация = &Организация) КАК уп_СчетчикНачисленийОбороты
#КонецУдаления
	|		ПО СостоянияПССрезПоследних.НомерСчета = уп_СчетчикНачисленийОбороты.НомерСчета
	|ГДЕ
	|	ВЫБОР 
	|		КОГДА СхемаСчетаСрезПоследних.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПустаяСсылка) 
	|			ТОГДА СхемаСчетаСрезПоследних.Схема.ТипПенсионнойСхемы
	|		ИНАЧЕ СхемаСчетаСрезПоследних.ТипПенсии 
	|	КОНЕЦ В(&СписокТипов)
	|	И ПериодичностьВыплатСрезПоследних.ПериодичностьВыплат В(&ПериодичностьВыплат)
	|	И СостоянияПССрезПоследних.Состояние = &Выплатное
	|
	|УПОРЯДОЧИТЬ ПО
	|	Договор,
	|	НомерСчета";

	Запрос.УстановитьПараметр("ДатаДок", Объект.Дата-1);
	Запрос.УстановитьПараметр("ДатаНач", '19900101');
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Выплатное", Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить(Перечисления.уп_ТипыПенсионныхСхем.ДоИсчерпанияСредствНаСчете);
	СписокТипов.Добавить(Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат);
	Запрос.УстановитьПараметр("СписокТипов", СписокТипов);
	ПериодичностьВыплат = Новый СписокЗначений;
	ПериодичностьВыплат.Добавить(Перечисления.уп_ПериодичностьВыплат.Ежемесячно);
	Если КонецМесяца(Объект.Дата) = КонецКвартала(Объект.Дата) Тогда
		ПериодичностьВыплат.Добавить(Перечисления.уп_ПериодичностьВыплат.Ежеквартально);
	КонецЕсли;	
	Если КонецМесяца(Объект.Дата) = КонецГода(Объект.Дата) Тогда
		ПериодичностьВыплат.Добавить(Перечисления.уп_ПериодичностьВыплат.Ежегодно);
	КонецЕсли;
	Если Месяц(Объект.Дата)=6 или Месяц(Объект.Дата)=12 Тогда
		ПериодичностьВыплат.Добавить(Перечисления.уп_ПериодичностьВыплат.Полугодие);
	КонецЕсли;
	Запрос.УстановитьПараметр("ПериодичностьВыплат", ПериодичностьВыплат);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Данные = Новый Массив;

	Пока Выборка.Следующий() Цикл
		Если Выборка.Сумма = 0 Тогда  //возможно обязательства выполнены
			Если Выборка.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.ДоИсчерпанияСредствНаСчете Тогда
				//Данные.Добавить(Выборка.НомерСчета);
				СтруктураДанных = Новый Структура();
				СтруктураДанных.Вставить("НомерСчета", Выборка.НомерСчета);
				СтруктураДанных.Вставить("Договор", Выборка.Договор);
				СтруктураДанных.Вставить("Участник", Выборка.Участник);
#Вставка
//++https://redmine.npf.com/issues/4260
				СтруктураДанных.Вставить("ХМ_ПричинаПрекращенияВыплаты", Перечисления.ХМ_ПричиныПрекращенияВыплаты.ИсчерпаниеДенежныхСредств);
//--https://redmine.npf.com/issues/4260
#КонецВставки
				Данные.Добавить(СтруктураДанных);
			Иначе // значит схема срочная и надо проверить сколько периодов прошло с начала выплат
#Вставка
//++https://redmine.npf.com/issues/4260
			КонецЕсли;
		КонецЕсли;
		Если Объект.ХМ_ПриостановкаВыплатПриНаличииПричиныПрекращения ИЛИ Выборка.Сумма = 0 Тогда
			Если Выборка.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.СрочныхВыплат Тогда
//--https://redmine.npf.com/issues/4260
#КонецВставки
				КолОстВыплат = Выборка.КоличествоНазначенныхВыплат - Выборка.КоличествоСделанныхВыплат;
				Если КолОстВыплат<=0 Тогда
					//Данные.Добавить(Выборка.НомерСчета);
					СтруктураДанных = Новый Структура();
					СтруктураДанных.Вставить("НомерСчета", Выборка.НомерСчета);
					СтруктураДанных.Вставить("Договор", Выборка.Договор);
					СтруктураДанных.Вставить("Участник", Выборка.Участник);
#Вставка
//++https://redmine.npf.com/issues/4260
				СтруктураДанных.Вставить("ХМ_ПричинаПрекращенияВыплаты", Перечисления.ХМ_ПричиныПрекращенияВыплаты.ИстечениеСрокаВыплатыПостоянно);
//--https://redmine.npf.com/issues/4260
#КонецВставки
					Данные.Добавить(СтруктураДанных);
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;

	Возврат Данные;

КонецФункции

&НаКлиенте
&ИзменениеИКонтроль("ПоДаннымСистемыЗакрытиеСчетов")
Процедура ХМПоДаннымСистемыЗакрытиеСчетов(Команда)

	//Если Объект.Ссылка.Пустая() Тогда
	//	Если Вопрос("Перед заполнением документ будет записан. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
	//		Возврат;
	//	Иначе
	//		Записать();
	//	КонецЕсли;	
	//КонецЕсли; 

	Если Объект.ЗакрытиеСчетов.Количество()>0 Тогда
		//Если Вопрос("Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		//	Возврат;
		//КонецЕсли;
		//Объект.ЗакрытиеСчетов.Очистить();
		Оповещение = Новый ОписаниеОповещения("ОбработкаОчиститьТЧЗакрытиеСчетовПередЗаполнением", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет,);
	Иначе
		Данные = ПоДаннымСистемыЗакрытиеСчетовНаСервере();

		Для Каждого СчетСсылка Из Данные Цикл

			Стр = Объект.ЗакрытиеСчетов.Добавить();
			Стр.НомерСчета = СчетСсылка.НомерСчета;
			Стр.ПенсионныйДоговор = СчетСсылка.Договор;
			Стр.Участник = СчетСсылка.Участник;
#Вставка
//++https://redmine.npf.com/issues/4260
			Стр.ХМ_ПричинаПрекращенияВыплаты = СчетСсылка.ХМ_ПричинаПрекращенияВыплаты;
//--https://redmine.npf.com/issues/4260
#КонецВставки

		КонецЦикла;  

		Модифицированность = Истина;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ОбработкаОчиститьТЧЗакрытиеСчетовПередЗаполнением")
Процедура ХМОбработкаОчиститьТЧЗакрытиеСчетовПередЗаполнением(Результат, Параметры)

	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ЗакрытиеСчетов.Очистить();
		Данные = ПоДаннымСистемыЗакрытиеСчетовНаСервере();

		Для Каждого СчетСсылка Из Данные Цикл

			Стр = Объект.ЗакрытиеСчетов.Добавить();
			Стр.НомерСчета = СчетСсылка.НомерСчета;
			Стр.ПенсионныйДоговор = СчетСсылка.Договор;
			Стр.Участник = СчетСсылка.Участник;
#Вставка
//++https://redmine.npf.com/issues/4260
			Стр.ХМ_ПричинаПрекращенияВыплаты = СчетСсылка.ХМ_ПричинаПрекращенияВыплаты;
//--https://redmine.npf.com/issues/4260
#КонецВставки

		КонецЦикла;  

		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
&После("ПриСозданииНаСервере")
Процедура ХМПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	ХМ_ДополнитьФорму();
КонецПроцедуры

Процедура ХМ_ДополнитьФорму()
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма, "ХМ_ПриостановкаВыплатПриНаличииПричиныПрекращения", Элементы.ГруппаЗакрытиеСчетов, "Объект.ХМ_ПриостановкаВыплатПриНаличииПричиныПрекращения", 
		Новый Структура("Вид, ВставитьПередЭлементом", ВидПоляФормы.ПолеФлажка,Элементы.ЗакрытиеСчетов));
		
//ЛыткинАМ //Присостановка за все периоды
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьКомандуСКнопкой(ЭтаФорма, "ХМ_ПоДаннымСистемыПриостановкаЗаВсеПериоды",  "По данным системы за все периоды", Элементы.ПриостановкаГруппаЗаполнить,
		Новый Структура("ВставитьПередЭлементом", Элементы.ПриостановкаИзСправочникаПриостановка));
	
//ЛыткинАМ //Присостановка за все периоды
	уп_ДинамическиеЭлементыСервер.ХМ_ДобавитьРеквизит(ЭтаФорма,"ХМ_ПриостановкаЗаВсеПериоды", Новый ОписаниеТипов("Булево"));
КонецПроцедуры


//ЛыткинАМ //Присостановка за все периоды
&НаКлиенте
Процедура ХМ_ПоДаннымСистемыПриостановкаЗаВсеПериоды(Команда)
//ЛыткинАМ //Присостановка за все периоды
	ЭтаФорма.ХМ_ПриостановкаЗаВсеПериоды = Истина;
	ПоДаннымСистемыПриостановка(Команда); 
	ЭтаФорма.ХМ_ПриостановкаЗаВсеПериоды = Ложь;

КонецПроцедуры


//ЛыткинАМ //Присостановка за все периоды
&НаСервереБезКонтекста
Функция ХМ_ПоДаннымСистемыПриостановкаЗаВсеПериодыНаСервере(Организация, Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	уп_СостоянияПССрезПоследних.НомерСчета,
	|	уп_СостоянияПССрезПоследних.Состояние,
	|	уп_СостоянияПССрезПоследних.ДатаОкончанияОснований,
	|	уп_СостоянияПССрезПоследних.Основание
	|ИЗ
	|	РегистрСведений.уп_СостоянияПС.СрезПоследних(&ДатаДок, НомерСчета.Владелец.Организация = &Организация) КАК уп_СостоянияПССрезПоследних
	|ГДЕ
	|	уп_СостоянияПССрезПоследних.Состояние = &Выплатное
	|	И уп_СостоянияПССрезПоследних.ДатаОкончанияОснований МЕЖДУ &ДатаНач И &ДатаКон";

	Запрос.УстановитьПараметр("Выплатное", Перечисления.уп_СостоянияПС.ВыплатнойПериод);
	Запрос.УстановитьПараметр("ДатаДок", Дата-1);
	Запрос.УстановитьПараметр("ДатаКон", КонецМесяца(Дата));
//	Запрос.УстановитьПараметр("ДатаНач", НачалоМесяца(Дата));
//#Вставка
//ЛыткинАМ //Присостановка за все периоды (дата не должна быть пустой)
//	Если ЭтаФорма.ХМ_ПриостановкаЗаВсеПериоды Тогда
		Запрос.УстановитьПараметр("ДатаНач", Дата(1,1,1,1,1,1));
//	КонецЕсли;
//#КонецВставки
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Данные = Новый Массив;

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		Элемент = Новый Структура;
		Элемент.Вставить("НомерСчета", ВыборкаДетальныеЗаписи.НомерСчета);
		Элемент.Вставить("ПенсионныйДоговор", ВыборкаДетальныеЗаписи.НомерСчета.Владелец);
		Элемент.Вставить("Участник", ВыборкаДетальныеЗаписи.НомерСчета.Участник); 
		Элемент.Вставить("Основание", ВыборкаДетальныеЗаписи.Основание);
		Элемент.Вставить("ДатаОкончанияОснований", ВыборкаДетальныеЗаписи.ДатаОкончанияОснований);
		Элемент.Вставить("ПричинаПриостановки", Справочники.уп_ПричиныПриостановок.ОкончаниеПенсионныхОснований);

		Данные.Добавить(Элемент);

	КонецЦикла;

	Возврат Данные;

КонецФункции


//ЛыткинАМ //Присостановка за все периоды
&НаКлиенте
&ИзменениеИКонтроль("ПоДаннымСистемыПриостановка")
Процедура ХМ_ПоДаннымСистемыПриостановка(Команда)

	Если Объект.Приостановка.Количество() > 0 Тогда
		//Если Вопрос("Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		//	Возврат;
		//КонецЕсли;
		//Объект.Приостановка.Очистить();
		Оповещение = Новый ОписаниеОповещения("ОбработкаОчиститьТЧПриостановкаПередЗаполнением", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет,);

	Иначе
		Данные = ПоДаннымСистемыПриостановкаНаСервере(Объект.Организация, Объект.Дата);
#Вставка
//ЛыткинАМ //Присостановка за все периоды
		Если ЭтаФорма.ХМ_ПриостановкаЗаВсеПериоды Тогда
			Данные = ХМ_ПоДаннымСистемыПриостановкаЗаВсеПериодыНаСервере(Объект.Организация, Объект.Дата);
		КонецЕсли;
#КонецВставки

		Для каждого СтрокаДанных Из Данные Цикл

			НовСтр = Объект.Приостановка.Добавить();
			НовСтр.НомерСчета = СтрокаДанных.НомерСчета;
			НовСтр.ПенсионныйДоговор = СтрокаДанных.ПенсионныйДоговор;
			НовСтр.Участник = СтрокаДанных.Участник;
			НовСтр.Основание = СтрокаДанных.Основание;
			НовСтр.ДатаОкончанияОснований = СтрокаДанных.ДатаОкончанияОснований;	
			НовСтр.ПричинаПриостановки = СтрокаДанных.ПричинаПриостановки;
		КонецЦикла; 

		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

//ЛыткинАМ //Присостановка за все периоды
&НаКлиенте
&ИзменениеИКонтроль("ОбработкаОчиститьТЧПриостановкаПередЗаполнением")
Процедура ХМОбработкаОчиститьТЧПриостановкаПередЗаполнением(Результат, Параметры)

	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Приостановка.Очистить();
		Данные = ПоДаннымСистемыПриостановкаНаСервере(Объект.Организация, Объект.Дата);
#Вставка
//ЛыткинАМ //Присостановка за все периоды
		Если ЭтаФорма.ХМ_ПриостановкаЗаВсеПериоды Тогда
			Данные = ХМ_ПоДаннымСистемыПриостановкаЗаВсеПериодыНаСервере(Объект.Организация, Объект.Дата);
		КонецЕсли;
#КонецВставки

		Для каждого СтрокаДанных Из Данные Цикл

			НовСтр = Объект.Приостановка.Добавить();
			НовСтр.НомерСчета = СтрокаДанных.НомерСчета;
			НовСтр.ПенсионныйДоговор = СтрокаДанных.ПенсионныйДоговор;
			НовСтр.Участник = СтрокаДанных.Участник;
			НовСтр.Основание = СтрокаДанных.Основание;
			НовСтр.ДатаОкончанияОснований = СтрокаДанных.ДатаОкончанияОснований;	
			НовСтр.ПричинаПриостановки = СтрокаДанных.ПричинаПриостановки;
		КонецЦикла; 

		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры
