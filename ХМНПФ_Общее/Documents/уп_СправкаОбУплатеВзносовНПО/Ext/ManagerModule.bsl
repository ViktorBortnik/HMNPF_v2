
&ИзменениеИКонтроль("ОпределитьПодписанта")
Функция ХМОпределитьПодписанта(ДокСсылка)
	ДанныеПодписанта = Новый Структура("ПрПодп, ПодпФамилия, ПодпИмя, ПодпОтчество, НаимДокПодп");

	Если ДокСсылка.УказатьПодписанта = 1 Тогда 
		ДанныеПодписанта.ПрПодп = "2"; 

		Если ЗначениеЗаполнено(ДокСсылка.Подписант)	Тогда
			ФИОПредставителя = СокрЛП(ДокСсылка.Подписант.ФИО);
			СоставляющиеФИО = СоставляющиеФИО(ФИОПредставителя);
			ДанныеПодписанта.ПодпФамилия = СоставляющиеФИО.Фамилия;
			ДанныеПодписанта.ПодпИмя = СоставляющиеФИО.Имя;
			ДанныеПодписанта.ПодпОтчество = СоставляющиеФИО.Отчество;
		КонецЕсли; 
		ДанныеПодписанта.НаимДокПодп = ДокСсылка.ДокументПредставителя; 
#Вставка
		//ЛыткинАМ //Для печать факсимилие
		ДанныеПодписанта.Вставить("ПодписантСсылка",ДокСсылка.Подписант);	
#КонецВставки
	Иначе	
		СведенияОПредставителе = РегламентированнаяОтчетностьВызовСервера.ПолучитьПоКодамСведенияОПредставителе(
		ДокСсылка.Организация, ДокСсылка.Организация.КодНалоговогоОргана, ДокСсылка.Организация.КПП);
		ИФНС = ДокСсылка.Организация.РегистрацияВНалоговомОргане;
		ТипПодписанта = СведенияОПредставителе.ТипПодписанта; 

		ДанныеПодписанта.ПрПодп = ТипПодписанта;

		Если ТипПодписанта = "1" Тогда
			ФИОПодписанта = СведенияОРуководителе(ДокСсылка);
#Вставка
			//ЛыткинАМ //Для печать факсимилие
			Если ЗначениеЗаполнено(ДокСсылка.Ответственный.ФизическоеЛицо) Тогда
				ФИОПодписанта = ДокСсылка.Ответственный.ФизическоеЛицо;	
			КонецЕсли;
			ДанныеПодписанта.Вставить("ПодписантСсылка",ФИОПодписанта.Ссылка);	     
#КонецВставки
			ДанныеПодписанта.ПодпФамилия = ФИОПодписанта.Фамилия;
			ДанныеПодписанта.ПодпИмя = ФИОПодписанта.Имя;
			ДанныеПодписанта.ПодпОтчество = ФИОПодписанта.Отчество;
		ИначеЕсли ТипПодписанта = "2" Тогда  
			ФИОПредставителя = СведенияОПредставителе.ФИОПредставителя;
			ДокументПредставителя = СведенияОПредставителе.ДокументПредставителя;
			ДанныеПодписанта.НаимДокПодп = ДокументПредставителя; 

			СоставляющиеФИО = СоставляющиеФИО(ФИОПредставителя);
			ДанныеПодписанта.ПодпФамилия = СоставляющиеФИО.Фамилия;
			ДанныеПодписанта.ПодпИмя = СоставляющиеФИО.Имя;
			ДанныеПодписанта.ПодпОтчество = СоставляющиеФИО.Отчество;
#Вставка
			//ЛыткинАМ //Для печать факсимилие
			ДанныеПодписанта.Вставить("ПодписантСсылка",СведенияОПредставителе.ПредставительСсылка);	
#КонецВставки
		КонецЕсли;   
	КонецЕсли;
	Возврат ДанныеПодписанта;
КонецФункции

&ИзменениеИКонтроль("СведенияОРуководителе")
Функция ХМСведенияОРуководителе(ВыгружаемыйДокумент)

	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");

	РеквизитыОтветственныхЛиц = ОтветственныеЛицаБП.ОтветственныеЛица(ВыгружаемыйДокумент.Организация, ВыгружаемыйДокумент.Дата);
	Если ЗначениеЗаполнено(РеквизитыОтветственныхЛиц.Руководитель) Тогда
		Результат.Фамилия 	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Фамилия);
		Результат.Имя 		= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Имя);
		Результат.Отчество	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Отчество);
	КонецЕсли;

	#Вставка
	//ЛыткинАМ //Для печать факсимилие
	Результат.Вставить("Ссылка",РеквизитыОтветственныхЛиц.Руководитель);	
	#КонецВставки
	Возврат Результат;

КонецФункции

&ИзменениеИКонтроль("ПечатьСправок")
Функция ХМПечатьСправок(МассивОбъектов, ВыводитьQR = Истина, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено)
	ТабДокумент = Новый ТабличныйДокумент; 
	//Макет = ПолучитьМакет("ПечатныйБланк_стр1_2023");
	//ОбластьОтчет = Макет.ПолучитьОбласть("Страница1");
	//
	//Макет2 = ПолучитьМакет("ПечатныйБланк_стр2_2023");
	//ОбластьОтчет2 = Макет2.ПолучитьОбласть("Страница2"); 
    #Удаление
	Для А = 0 по МассивОбъектов.Количество()-1 Цикл
		СсылкаНаОбъект = МассивОбъектов.Получить(А); 
	#КонецУдаления
	#Вставка
	//++ЛыткинАМ //https://redmine.npf.com/issues/4269
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = СправкаОбУплатеВзносовНПОТекстЗапросаШапки();

	СсылкаНаОбъект = Запрос.Выполнить().Выбрать();
	Пока СсылкаНаОбъект.Следующий() Цикл
		//--ЛыткинАМ //https://redmine.npf.com/issues/4269
    #КонецВставки
		Если СсылкаНаОбъект.Год>2024 Тогда
			Макет = ПолучитьМакет("ПечатныйБланк_стр1_2025");
			ОбластьОтчет = Макет.ПолучитьОбласть("Страница1");

			Макет2 = ПолучитьМакет("ПечатныйБланк_стр2_2025");
			ОбластьОтчет2 = Макет2.ПолучитьОбласть("Страница2");
		Иначе
			Макет = ПолучитьМакет("ПечатныйБланк_стр1_2023");
			ОбластьОтчет = Макет.ПолучитьОбласть("Страница1");

			Макет2 = ПолучитьМакет("ПечатныйБланк_стр2_2023");
			ОбластьОтчет2 = Макет2.ПолучитьОбласть("Страница2"); 
		КонецЕсли;

		Если СсылкаНаОбъект.ТипСправки<>0 Тогда
			ЭтоКорректировка = Истина; 
		Иначе
			ЭтоКорректировка = Ложь; 
		КонецЕсли;	

		#Вставка
		//Факсимильная печать //http://redmine.npf.com/issues/3738
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
        #КонецВставки
	
		//номер справки - цифрой 
		Если СсылкаНаОбъект.ТипСправки=0 Тогда
			НомерСправки = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Формат(СсылкаНаОбъект.НомерСведений,"ЧГ=0"), Истина, Истина); 
		Иначе
			НомерСправки = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Формат(СсылкаНаОбъект.ИсходнаяСправка.НомерСведений,"ЧГ=0"), Истина, Истина); 
		КонецЕсли;	

		СуммаВзносовФЛ = СсылкаНаОбъект.СуммаВзносовФЛ;
		ДатаСправки = СсылкаНаОбъект.Дата;
		НомерКорректировки = 0; 
		//Подписант = СсылкаНаОбъект.Подписант;

		Если ЭтоКорректировка Тогда
			Если СсылкаНаОбъект.ТипСправки = 2 Тогда
				НомерКорректировки = 999;
			ИначеЕсли СсылкаНаОбъект.ТипСправки = 1 Тогда
				НомерКорректировки = СсылкаНаОбъект.НомерКорректировки;
			Иначе
				Продолжить;  //здесь не зарегистрировано корректировок 
			КонецЕсли;
		КонецЕсли;
		Если СсылкаНаОбъект.Налогоплательщик<>СсылкаНаОбъект.Выгодоприобретатель Тогда
			ВПользуТретьихЛиц = "0"; 
			КолЛистов = "002";
		Иначе
			ВПользуТретьихЛиц = "1";  
			КолЛистов = "001";
		КонецЕсли;	


		//СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		СтрокаВОбластиМакета(СсылкаНаОбъект.Организация.ИНН, "ИНН_", ОбластьОтчет.Области, 10);
		СтрокаВОбластиМакета(СсылкаНаОбъект.Организация.КПП, "КПП_", ОбластьОтчет.Области, 9); 
		СтрокаВОбластиМакета(СсылкаНаОбъект.Организация.НаименованиеПолное, "НаимОрг_", ОбластьОтчет.Области, 160);

		СтрокаВОбластиМакета(НомерСправки, "НомерСправки", ОбластьОтчет.Области, 12);   
		#Вставка
		//+ЛыткинАМ//Изменения выявленные при обновлении
		НомерКорректировки = ЧислоСРазделителемВСтроку(НомерКорректировки, 3, 0, "-");
		//-ЛыткинАМ//Изменения выявленные при обновлении
		#КонецВставки
		СтрокаВОбластиМакета(НомерКорректировки, "НомерКорректировки", ОбластьОтчет.Области, 3);  
		СтрокаВОбластиМакета(Формат(СсылкаНаОбъект.Год, "ЧГ=0"), "ОтчетГод", ОбластьОтчет.Области, 4);   

		ДанныеФЛ = ПолучитьДанныеФЛ(СсылкаНаОбъект.Налогоплательщик.уп_ФизЛицо, СсылкаНаОбъект.Дата);

		СтрокаВОбластиМакета(ДанныеФЛ.Фамилия, "Фамилия_", ОбластьОтчет.Области, 36); 
		СтрокаВОбластиМакета(ДанныеФЛ.Имя, "Имя_", ОбластьОтчет.Области, 36); 
		СтрокаВОбластиМакета(ДанныеФЛ.Отчество, "Отчество_", ОбластьОтчет.Области, 36);
		СтрокаВОбластиМакета(ДанныеФЛ.ИНН, "ИННФЛ_", ОбластьОтчет.Области, 12);
		ДатаВОбластиМакета(ДанныеФЛ.ДатаРождения, "ДР_", ОбластьОтчет.Области); 

		#Вставка
		//+ЛыткинАМ//Изменения выявленные при обновлении
		Если ПустаяСтрока(ДанныеФЛ.ИНН) Тогда
			//-ЛыткинАМ//Изменения выявленные при обновлении
        #КонецВставки
		
		СтрокаВОбластиМакета(ДанныеФЛ.ДокКодФНС, "КодДУЛ_", ОбластьОтчет.Области, 2);
		СерияНомерДУЛ = ""+СтрЗаменить(ДанныеФЛ.ДокСерия," ","")+" "+СтрЗаменить(ДанныеФЛ.ДокНомер," ","");
		СтрокаВОбластиМакета(СерияНомерДУЛ, "СерияНомерДУЛ_", ОбластьОтчет.Области, 20);
		ДатаВОбластиМакета(ДанныеФЛ.ДокДатаВыдачи, "ДВД_", ОбластьОтчет.Области);
		
		#Удаление
		ОбластьОтчет.Области["ВПользуТретьихЛиц"].Текст = ВПользуТретьихЛиц;  
		#КонецУдаления
		#Вставка
		КонецЕсли;
		ОбластьОтчет.Области["ВПользуТретьихЛиц"].Текст = "1";
		//-ЛыткинАМ//Изменения выявленные при обновлении
        #КонецВставки
		
		ДатаВОбластиМакета(СсылкаНаОбъект.ПенсионныйДоговор.ДатаЗаключения, "ДД_", ОбластьОтчет.Области);
		СтрокаВОбластиМакета(СокрЛП(СсылкаНаОбъект.ПенсионныйДоговор.Код), "НомерДоговора_", ОбластьОтчет.Области, 34); 

		Если СсылкаНаОбъект.Год>2024 Тогда
			СуммаСтрокой = ЧислоСРазделителемВСтрокуДо(СуммаВзносовФЛ, 13, 2, "-"); 
		Иначе
			СуммаСтрокой = ЧислоСРазделителемВСтроку(СуммаВзносовФЛ, 13, 2, "-"); 
		КонецЕсли;	
		СтрокаВОбластиМакета(СуммаСтрокой, "Сумма_", ОбластьОтчет.Области, 15);

		ДанныеПодписанта = ОпределитьПодписанта(СсылкаНаОбъект); 
		//Если ЗначениеЗаполнено(Подписант) Тогда
		//ФИООтветственного = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(ДатаСправки, Новый Структура("ФизическоеЛицо", Подписант));
		СтрокаВОбластиМакета(ДанныеПодписанта.ПодпФамилия, "ОргПодписантФамилия_", ОбластьОтчет.Области, 20);
		СтрокаВОбластиМакета(ДанныеПодписанта.ПодпИмя, "ОргПодписантИмя_", ОбластьОтчет.Области, 20);
		СтрокаВОбластиМакета(ДанныеПодписанта.ПодпОтчество, "ОргПодписантОтчество_", ОбластьОтчет.Области, 20);
		//КонецЕсли;
		ДатаВОбластиМакета(ДатаСправки, "ДатаПодписи", ОбластьОтчет.Области);
		СтрокаВОбластиМакета(КолЛистов, "КолЛистов_", ОбластьОтчет.Области, 3);  

		//пытаемся штрих код вывести 
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(СсылкаНаОбъект);

		Попытка  
			//Если ЗначениеЗаполнено(ДанныеПодписанта.ПодпФамилия) и ВыводитьQR Тогда	
			Если ВыводитьQR Тогда	
				// Если макет поддерживает QR код
				Если Макет.Области.Найти("КартинкаQRкода") <> Неопределено Тогда
					СоответствиеСтрокBase64 = Документы.уп_СправкаОбУплатеВзносовНПО.ВыгрузитьФайлыНаСервере(МассивДокументов, ); 
					Для Каждого КлючИЗначение из СоответствиеСтрокBase64 Цикл
						ДД = Base64Значение(КлючИЗначение.Значение);  //это двоичные данные
						СтрокаДляШтрихкода = ПолучитьСтрокуИзДвоичныхДанных(ДД, КодировкаТекста.ANSI);

						QRСтрока = СтрокаДляШтрихкода;
						ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(QRСтрока, 0, 500);
						ОбластьОтчет.Рисунки.КартинкаQRкода.Картинка = Новый Картинка(ДанныеQRКода)
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Исключение
			//не сформировали файлы, штрих код не выводим
		КонецПопытки;

		//конец вывода штрих кода

		#Вставка
		//Факсимильная печать //http://redmine.npf.com/issues/3738
		Если ЗначениеЗаполнено(ДанныеПодписанта.ПодписантСсылка) Тогда
			Если ОбластьОтчет.Области.Найти("Подпись") <> Неопределено Тогда
				ДвоичныеДанныеПодписьИсполнителя = УправлениеПечатьюБП.ДвоичныеДанныеФаксимилеФизическогоЛица(ДанныеПодписанта.ПодписантСсылка);
				ОбластьОтчет.Рисунки.Подпись.Картинка = Новый Картинка(ДвоичныеДанныеПодписьИсполнителя);

				ЦветФонаАвто  = Новый Цвет();
				ЛинияРамки    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);

				ОбластьОтчет.Рисунки.Подпись.ЦветФона = ЦветФонаАвто;
				ОбластьОтчет.Рисунки.Подпись.Линия    = ЛинияРамки;
			КонецЕсли;
		Иначе
			УправлениеПечатьюБП.ОчиститьОбластьФаксимиле(ОбластьОтчет, "Подпись");
		КонецЕсли;
        #КонецВставки
	
		ТабДокумент.Вывести(ОбластьОтчет);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); 

		Если ВПользуТретьихЛиц = "0" Тогда 
			СтрокаВОбластиМакета(СсылкаНаОбъект.Организация.ИНН, "ИНН_", ОбластьОтчет2.Области, 10);
			СтрокаВОбластиМакета(СсылкаНаОбъект.Организация.КПП, "КПП_", ОбластьОтчет2.Области, 9);

			ДанныеФЛ = ПолучитьДанныеФЛ(СсылкаНаОбъект.Выгодоприобретатель.уп_ФизЛицо, СсылкаНаОбъект.Дата);

			СтрокаВОбластиМакета(ДанныеФЛ.Фамилия, "Фамилия_", ОбластьОтчет2.Области, 36); 
			СтрокаВОбластиМакета(ДанныеФЛ.Имя, "Имя_", ОбластьОтчет2.Области, 36); 
			СтрокаВОбластиМакета(ДанныеФЛ.Отчество, "Отчество_", ОбластьОтчет2.Области, 36);
			СтрокаВОбластиМакета(ДанныеФЛ.ИНН, "ИННФЛ_", ОбластьОтчет2.Области, 12);
			ДатаВОбластиМакета(ДанныеФЛ.ДатаРождения, "ДР_", ОбластьОтчет2.Области); 

			#Вставка
			//+ЛыткинАМ//Изменения выявленные при обновлении
			Если ПустаяСтрока(ДанныеФЛ.ИНН) Тогда
				//-ЛыткинАМ//Изменения выявленные при обновлении
			#КонецВставки				
			СтрокаВОбластиМакета(ДанныеФЛ.ДокКодФНС, "КодДУЛ_", ОбластьОтчет2.Области, 2);
			СерияНомерДУЛ = ""+СтрЗаменить(ДанныеФЛ.ДокСерия," ","")+" "+СтрЗаменить(ДанныеФЛ.ДокНомер," ","");
			СтрокаВОбластиМакета(СерияНомерДУЛ, "СерияНомерДУЛ_", ОбластьОтчет2.Области, 20);
			ДатаВОбластиМакета(ДанныеФЛ.ДокДатаВыдачи, "ДВД_", ОбластьОтчет2.Области);  
			#Вставка
				//+ЛыткинАМ//Изменения выявленные при обновлении
			КонецЕсли;
			//-ЛыткинАМ//Изменения выявленные при обновлении
			#КонецВставки	

			ТабДокумент.Вывести(ОбластьОтчет2);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц(); 
		КонецЕсли;	
	КонецЦикла;

	ТабДокумент.АвтоМасштаб = Истина; 
	Возврат ТабДокумент;
КонецФункции

&ИзменениеИКонтроль("Печать")
Процедура ХМПечать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОВзносах") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СправкаОВзносах", "Справка о взносах"
		#Удаление
		,ПечатьСправок(МассивОбъектов, Истина),,"Документ.уп_СправкаОбУплатеВзносовНПО.ПечатныйБланк_стр1_2025");
		#КонецУдаления 
		#Вставка
		,ПечатьСправок(МассивОбъектов, Истина, ОбъектыПечати, ПараметрыПечати),,"Документ.уп_СправкаОбУплатеВзносовНПО.ПечатныйБланк_стр1_2025");
		#КонецВставки
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОВзносахБезQR") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СправкаОВзносахБезQR", "Справка о взносах"
		#Удаление
		,ПечатьСправок(МассивОбъектов, Ложь),,"Документ.уп_СправкаОбУплатеВзносовНПО.ПечатныйБланк_стр1_2025"); 
		#КонецУдаления
		#Вставка
		,ПечатьСправок(МассивОбъектов, Ложь, ОбъектыПечати, ПараметрыПечати),,"Документ.уп_СправкаОбУплатеВзносовНПО.ПечатныйБланк_стр1_2025"); 
		#КонецВставки
	КонецЕсли;

	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов,КоллекцияПечатныхФорм,ОбъектыПечати,ПараметрыВывода);

КонецПроцедуры  


Функция СправкаОбУплатеВзносовНПОТекстЗапросаШапки()
	
	//ХМНПФ //24.01.2025 //ЛыткинАМ // //http://redmine.npf.com/issues/4028
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДокументСправкаОбУплатеВзносовНПО.Ссылка КАК Документ,
	|	ДокументСправкаОбУплатеВзносовНПО.*
	|ИЗ
	|	Документ.уп_СправкаОбУплатеВзносовНПО КАК ДокументСправкаОбУплатеВзносовНПО
	|ГДЕ
	|	ДокументСправкаОбУплатеВзносовНПО.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументСправкаОбУплатеВзносовНПО.Ссылка";	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ХМ_РассчитатьВзносыОбъекта(Объект) Экспорт
	
	ЗначениеВозврата = 0; 
	Объект.ТаблицаВзносов.Очистить();
		
	МассивВзносов = РассчитатьВзносы(Объект.ПенсионныйДоговор, Объект.Выгодоприобретатель, Объект.Год);
	Объект.ТаблицаВзносов.Очистить();
	Для а=0 по МассивВзносов.Количество()-1 Цикл  
		СтрокаВзносов = МассивВзносов.Получить(а);
		
		НовСтр = Объект.ТаблицаВзносов.Добавить();
		НовСтр.ДатаВзноса = СтрокаВзносов.ДатаВзноса;
		НовСтр.СуммаВзносовФЛ = СтрокаВзносов.СуммаВзноса;
		
		ЗначениеВозврата = ЗначениеВозврата + СтрокаВзносов.СуммаВзноса;
	КонецЦикла;
	
	Возврат ЗначениеВозврата;

КонецФункции	

