

&ИзменениеИКонтроль("ЗаполнитьЗаявления")
Процедура ХМЗаполнитьЗаявления()
	РасшифровкаСуммы.Очистить();
	ДоговорыОПС.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних.ДоговорОПС КАК ДоговорОПС,
	|	МАКСИМУМ(уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних.Заявление) КАК Заявление
	|ПОМЕСТИТЬ ВТ_Заявления
	|ИЗ
	|	РегистрСведений.уп_СостояниеЗаявленийОЕдиновременномВзносе.СрезПоследних(&ДатаСостоянияЗаявлений, Заявление.Организация = &Организация) КАК уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних
	|ГДЕ
	|	уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних.Состояние В(&СостоянияЗаявлений)
	|	И уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних.Заявление.Дата МЕЖДУ &ДатаЗаявленийНачало И &ДатаЗаявленийКонец
	|
	|СГРУППИРОВАТЬ ПО
	|	уп_СостояниеЗаявленийОЕдиновременномВзносеСрезПоследних.ДоговорОПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заявления.ДоговорОПС КАК ДоговорОПС
	|ПОМЕСТИТЬ ВТ_ДоговорыОПС
	|ИЗ
	|	ВТ_Заявления КАК ВТ_Заявления
	|ГДЕ
	|	НЕ ВТ_Заявления.ДоговорОПС = &ДоговорОПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	уп_РезервыОПСОстатки.ДоговорОПС КАК ДоговорОПС,
	|	уп_РезервыОПСОстатки.ТипРезерва КАК ТипРезерва,
	|	уп_РезервыОПСОстатки.ТипСуммы КАК ТипСуммы,
	|	уп_РезервыОПСОстатки.СуммаОстаток КАК Сумма,
	|	0 КАК СуммаГарантированная,
	|	ВЫБОР
	|		КОГДА уп_РезервыОПСОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.МатКапитал)
	|			ТОГДА уп_РезервыОПСОстатки.СуммаОстаток
	|		КОГДА уп_РезервыОПСОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДМатКапитал)
	|			ТОГДА уп_РезервыОПСОстатки.СуммаОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаМСК
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрНакопления.уп_РезервыОПС.Остатки(
	|			&ДатаОстатков,
	|			ДоговорОПС В
	|				(ВЫБРАТЬ
	|					ВТ_ДоговорыОПС.ДоговорОПС КАК ДоговорОПС
	|				ИЗ
	|					ВТ_ДоговорыОПС КАК ВТ_ДоговорыОПС)) КАК уп_РезервыОПСОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уп_РезервыОПСНераспределенныеПлатежиОстатки.ДоговорОПС,
	|	уп_РезервыОПСНераспределенныеПлатежиОстатки.ТипРезерва,
	|	уп_РезервыОПСНераспределенныеПлатежиОстатки.ТипСуммы,
	|	уп_РезервыОПСНераспределенныеПлатежиОстатки.СуммаОстаток,
	|	0,
	|	ВЫБОР
	|		КОГДА уп_РезервыОПСНераспределенныеПлатежиОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.МатКапитал)
	|			ТОГДА уп_РезервыОПСНераспределенныеПлатежиОстатки.СуммаОстаток
	|		КОГДА уп_РезервыОПСНераспределенныеПлатежиОстатки.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСуммОПС.ИДМатКапитал)
	|			ТОГДА уп_РезервыОПСНераспределенныеПлатежиОстатки.СуммаОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.уп_РезервыОПСНераспределенныеПлатежи.Остатки(
	|			&ДатаОстатков,
	|			ДоговорОПС В
	|				(ВЫБРАТЬ
	|					ВТ_ДоговорыОПС.ДоговорОПС КАК ДоговорОПС
	|				ИЗ
	|					ВТ_ДоговорыОПС КАК ВТ_ДоговорыОПС)) КАК уп_РезервыОПСНераспределенныеПлатежиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	уп_ТекущаяСуммаГарантированияОстатки.ДоговорОПС,
	|	ЗНАЧЕНИЕ(Перечисление.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений),
	|	уп_ТекущаяСуммаГарантированияОстатки.ТипСуммы,
	|	0,
	|	уп_ТекущаяСуммаГарантированияОстатки.СуммаОстаток,
	|	0
	|ИЗ
	|	РегистрНакопления.уп_ТекущаяСуммаГарантирования.Остатки(
	|			&ДатаОстатков,
	|			ДоговорОПС В
	|				(ВЫБРАТЬ
	|					ВТ_ДоговорыОПС.ДоговорОПС КАК ДоговорОПС
	|				ИЗ
	|					ВТ_ДоговорыОПС КАК ВТ_ДоговорыОПС)) КАК уп_ТекущаяСуммаГарантированияОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.ДоговорОПС КАК ДоговорОПС,
	|	ВТ_Данные.ТипРезерва КАК ТипРезерва,
	|	ВТ_Данные.ТипСуммы КАК ТипСуммы,
	|	СУММА(ВТ_Данные.Сумма) КАК Сумма,
	|	СУММА(ВТ_Данные.СуммаГарантированная) КАК СуммаГарантированная,
	|	СУММА(ВТ_Данные.СуммаМСК) КАК СуммаМСК
	|ПОМЕСТИТЬ ВТ_ПромДанные
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Данные.ТипРезерва,
	|	ВТ_Данные.ТипСуммы,
	|	ВТ_Данные.ДоговорОПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заявления.ДоговорОПС КАК ДоговорОПС,
	|	ВТ_ПромДанные.ТипРезерва КАК ТипРезерва,
	|	ВТ_ПромДанные.ТипСуммы КАК ТипСуммы,
	|	ЕСТЬNULL(ВТ_ПромДанные.Сумма, 0) КАК Сумма,
	|	ЕСТЬNULL(ВТ_ПромДанные.СуммаГарантированная, 0) КАК СуммаГарантированная,
	|	ЕСТЬNULL(ВТ_ПромДанные.СуммаМСК, 0) КАК СуммаМСК,
	|	ВТ_Заявления.Заявление КАК Заявление
	|ИЗ
	|	ВТ_Заявления КАК ВТ_Заявления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПромДанные КАК ВТ_ПромДанные
	|		ПО ВТ_Заявления.ДоговорОПС = ВТ_ПромДанные.ДоговорОПС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорОПС
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаГарантированная),
	|	СУММА(СуммаМСК)
	|ПО
	|	Заявление
	|АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Организация",Организация);
	СостоянияЗаявлений = Новый СписокЗначений;
	СостоянияЗаявлений.Добавить(Перечисления.уп_СостоянияЗаявленийОЕВ.ПодтвержденСФР);
	//СостоянияЗаявлений.Добавить(Перечисления.уп_СостоянияЗаявленийОЕВ.Исполнено);
	Запрос.УстановитьПараметр("СостоянияЗаявлений",СостоянияЗаявлений);
	Запрос.УстановитьПараметр("ДатаОстатков",Новый Граница(КонецДня(ДатаОстатков),ВидГраницы.Включая)); 
	Запрос.УстановитьПараметр("ДатаЗаявленийНачало",НачалоГода(НачалоГода(Дата)-1)); 
	Запрос.УстановитьПараметр("ДатаЗаявленийКонец",НачалоГода(Дата)-1); 
	Запрос.УстановитьПараметр("ДатаСостоянияЗаявлений",Новый Граница(КонецДня(ДатаОстатков),ВидГраницы.Исключая)); 
	Запрос.УстановитьПараметр("ДоговорОПС",Справочники.уп_ДоговорыОПС.ПустаяСсылка());
	
	
	ВыборкаЗаявление = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаявление.Следующий() Цикл
		НовСтрока = ДоговорыОПС.Добавить();
		НовСтрока.Заявление = ВыборкаЗаявление.Заявление;
		НовСтрока.ЗастрахованноеЛицо = ВыборкаЗаявление.Заявление.ЗастрахованноеЛицо;
		НовСтрока.ДоговорОПС = ВыборкаЗаявление.Заявление.ДоговорОПС;
		НовСтрока.ДоговорПДС = ВыборкаЗаявление.Заявление.ДоговорПДС;
		НовСтрока.НомерСчета = НовСтрока.ДоговорПДС.ПенсионныйСчет; 
		
		НовСтрока.СуммаСПН = ВыборкаЗаявление.Сумма;
		НовСтрока.СуммаМСК = ВыборкаЗаявление.СуммаМСК;
		НовСтрока.СуммаПереводаСПН = ВыборкаЗаявление.Сумма - ВыборкаЗаявление.СуммаМСК;
		#Вставка
        //БортникВЕ 03.04.2025 https://redmine.npf.com/issues/4188
		НовСтрока.СуммаПереводаРОПС = ?(СуммаСПН = 0,0,Окр(СуммаРОПС * (НовСтрока.СуммаПереводаСПН / СуммаСПН),2));
		#КонецВставки
		#Удаление
		НовСтрока.СуммаПереводаРОПС = ?(СуммаСПН = 0,0,Окр(СуммаРОПС * (НовСтрока.СуммаСПН / СуммаСПН),2));
		#КонецУдаления
		Выборка = ВыборкаЗаявление.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = РасшифровкаСуммы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка);
		КонецЦикла;
	КонецЦикла;
	ПересчитатьСуммуДокумента();
КонецПроцедуры

