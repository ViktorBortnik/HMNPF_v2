Функция ДвиженияПоРезервамНПО(Ссылка, Дата, Организация, ТабНачислений, ТабРезервов = Неопределено, пТипВыплаты = 1, ВернутьИсхРезерв = Ложь, ВидОперации = Неопределено) Экспорт
	
	ДатаНачалаВеденияУчетаПоСхемам = Константы.уп_УчетРезерваНПОвРазрезеСхем.Получить();
	Если ЗначениеЗаполнено(ДатаНачалаВеденияУчетаПоСхемам) И ДатаНачалаВеденияУчетаПоСхемам<=Дата Тогда
		ВестиУчетПоСхемам = Истина;
	Иначе
		ВестиУчетПоСхемам = Ложь;
	КонецЕсли;	

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	Если ТабРезервов = Неопределено Тогда
		ТабРезервов        = уп_ОсновнаяПоставкаСервер.СформироватьСтуктуруТаблицыПоРегиструНакопления("уп_Резервы");
	КонецЕсли;
	
	Если ВернутьИсхРезерв = Истина Тогда
		Попытка
			ТабРезервов.Колонки.Добавить("ТипРезерваИсх");
		Исключение
		КонецПопытки
	КонецЕсли;

	Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсийВРезерв Тогда
		СтрокиПрихода = ТабНачислений.НайтиСтроки(Новый Структура("ВидДвижения",ВидДвиженияНакопления.Приход));
		Для каждого СтрокаНачисления Из СтрокиПрихода Цикл
			ТабНачислений.Удалить(СтрокаНачисления);	
		КонецЦикла;
	КонецЕсли;

	//списываем резерв начислений
	Если пТипВыплаты = 1 Тогда 
		ТабНачислений.Свернуть("НомерСчета,Схема,ТипРезерва,ТипСуммы, ТипПенсии", "Сумма");
	Иначе
		ТабНачислений.Свернуть("НомерСчета,Схема,ТипРезерва,ТипСуммы", "Сумма");
	КонецЕсли;
	ТабНачислений.Колонки.Добавить("ИПС", Новый ОписаниеТипов("Булево")); 
	ТабНачислений.Колонки.Добавить("СПС", Новый ОписаниеТипов("Булево")); 
	
	
	ТЗСол = ТабНачислений.Скопировать();
	ТЗСол.Очистить();
	
	Если пТипВыплаты = 1 Тогда
		Запрос = Новый Запрос; 
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТабНачислений.НомерСчета КАК Справочник.уп_ПенсионныеСчета) КАК НомерСчета,
		|	ВЫРАЗИТЬ(ТабНачислений.Схема КАК Справочник.уп_ПенсионныеСхемы) КАК Схема,
		|	ТабНачислений.ТипРезерва КАК ТипРезерва,
		|	ТабНачислений.ТипСуммы КАК ТипСуммы,
		|	ТабНачислений.ТипПенсии КАК ТипПенсии,
		|	ТабНачислений.ИПС КАК ИПС,
		|	ТабНачислений.СПС КАК СПС,
		|	ТабНачислений.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_ТабНачислений
		|ИЗ
		|	&ТабНачислений КАК ТабНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ПенсионныеСхемыИсточникиВыплат_ИПС.Ссылка КАК Схема,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ уп_ПенсионныеСхемыИсточникиВыплат_ИПС.НомерСтроки) КАК КолИсточниковИПС
		|ПОМЕСТИТЬ ВТ_СхемыИсточникиИПС
		|ИЗ
		|	Справочник.уп_ПенсионныеСхемы.ИсточникиВыплат_ИПС КАК уп_ПенсионныеСхемыИсточникиВыплат_ИПС
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПенсионныеСхемыИсточникиВыплат_ИПС.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка КАК Схема,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ уп_ПенсионныеСхемыИсточникиВыплат_СПС.НомерСтроки) КАК КолИсточниковСПС
		|ПОМЕСТИТЬ ВТ_СхемыИсточникиСПС
		|ИЗ
		|	Справочник.уп_ПенсионныеСхемы.ИсточникиВыплат_СПС КАК уп_ПенсионныеСхемыИсточникиВыплат_СПС
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_ПенсионныеСхемыИсточникиВыплат_СПС.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_ПенсионныеСхемы.Ссылка КАК Схема,
		|	ЕСТЬNULL(ВТ_СхемыИсточникиИПС.КолИсточниковИПС, 0) КАК КолИсточниковИПС,
		|	ЕСТЬNULL(ВТ_СхемыИсточникиСПС.КолИсточниковСПС, 0) КАК КолИсточниковСПС,
		|	уп_ПравилаВеденияРезерваПожизненныхВыплатСрезПоследних.Правило КАК Правило
		|ПОМЕСТИТЬ ВТ_ПравилаСхем
		|ИЗ
		|	Справочник.уп_ПенсионныеСхемы КАК уп_ПенсионныеСхемы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СхемыИсточникиИПС КАК ВТ_СхемыИсточникиИПС
		|		ПО уп_ПенсионныеСхемы.Ссылка = ВТ_СхемыИсточникиИПС.Схема
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СхемыИсточникиСПС КАК ВТ_СхемыИсточникиСПС
		|		ПО уп_ПенсионныеСхемы.Ссылка = ВТ_СхемыИсточникиСПС.Схема
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПравилаВеденияРезерваПожизненныхВыплат.СрезПоследних(&ДатаДок, ) КАК уп_ПравилаВеденияРезерваПожизненныхВыплатСрезПоследних
		|		ПО уп_ПенсионныеСхемы.Ссылка = уп_ПравилаВеденияРезерваПожизненныхВыплатСрезПоследних.Схема
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВТ_ТабНачислений.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПожизненныхВыплат)
		|				И ВТ_ПравилаСхем.Правило = ЗНАЧЕНИЕ(Перечисление.уп_РезервПожизненныхВыплат.БезАналитики)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.уп_ПенсионныеСчета.ПустаяСсылка)
		|		ИНАЧЕ ВТ_ТабНачислений.НомерСчета
		|	КОНЕЦ КАК НомерСчета,
		|	ВТ_ТабНачислений.Схема КАК Схема,
		|	ВТ_ТабНачислений.ТипРезерва КАК ТипРезерва,
		|	ВТ_ТабНачислений.ТипСуммы КАК ТипСуммы,
		|	ВЫБОР
		|		КОГДА ВТ_ТабНачислений.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПожизненныхВыплат)
		|				И ВТ_ПравилаСхем.Правило = ЗНАЧЕНИЕ(Перечисление.уп_РезервПожизненныхВыплат.БезАналитики)
		|			ТОГДА ИСТИНА
		|		КОГДА ВТ_ПравилаСхем.КолИсточниковИПС > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ИПС,
		|	ВЫБОР
		|		КОГДА ВТ_ТабНачислений.ТипПенсии = ЗНАЧЕНИЕ(Перечисление.уп_ТипыПенсионныхСхем.ПожизненныхВыплат)
		|				И ВТ_ПравилаСхем.Правило = ЗНАЧЕНИЕ(Перечисление.уп_РезервПожизненныхВыплат.БезАналитики)
		|			ТОГДА ЛОЖЬ
		|		КОГДА ВТ_ПравилаСхем.КолИсточниковСПС > 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СПС,
		|	ВТ_ТабНачислений.Сумма КАК Сумма
		|ИЗ
		|	ВТ_ТабНачислений КАК ВТ_ТабНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПравилаСхем КАК ВТ_ПравилаСхем
		|		ПО ВТ_ТабНачислений.Схема = ВТ_ПравилаСхем.Схема
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТабНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СхемыИсточникиИПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СхемыИсточникиСПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ПравилаСхем"; 
		
		Запрос.УстановитьПараметр("ДатаДок", Дата); 
		Запрос.УстановитьПараметр("ТабНачислений", ТабНачислений);
		
		Результат = Запрос.Выполнить();
		
		ТабНачислений = Результат.Выгрузить();
		ТабНачислений.Свернуть("НомерСчета,Схема,ТипРезерва,ТипСуммы,ИПС,СПС", "Сумма");
		
		//Для Каждого стр из ТабНачислений Цикл
		//	ТекСхема = стр.Схема;
		//	Порядок_ИПС = ТекСхема.ИсточникиВыплат_ИПС.Выгрузить();
		//	Порядок_СПС = ТекСхема.ИсточникиВыплат_СПС.Выгрузить();
		//	Если (Порядок_ИПС.Количество() = 0) и (Порядок_СПС.Количество() = 0) Тогда
		//		Если ТекСхема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.ПожизненныхВыплат Тогда
		//			//посмотрим порядок ведения резерва ПВ
		//			ПорядокВеденияРПВ = уп_НПФ.ОпределитьПорядокВеденияПожизненногоРезерва(ТекСхема, Дата);
		//			Если ПорядокВеденияРПВ = Перечисления.уп_РезервПожизненныхВыплат.БезАналитики Тогда
		//				//стр.РПВ = Истина;
		//				Стр.НомерСчета = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
		//			КонецЕсли;	
		//		КонецЕсли;
		//	Иначе
		//		Если ТекСхема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.ПожизненныхВыплат Тогда
		//			//посмотрим порядок ведения резерва ПВ
		//			ПорядокВеденияРПВ = уп_НПФ.ОпределитьПорядокВеденияПожизненногоРезерва(ТекСхема, Дата);
		//			Если ПорядокВеденияРПВ = Перечисления.уп_РезервПожизненныхВыплат.БезАналитики Тогда
		//				Стр.НомерСчета = Справочники.уп_ПенсионныеСчета.ПустаяСсылка();
		//			Иначе
		//				Если (Порядок_СПС.Количество() > 0) Тогда
		//					//Стр.НомерСчета = стр.НомерСчета.Владелец.ПенсионныйСчет;
		//					Стр.СПС = Истина;
		//				КонецЕсли;
		//			КонецЕсли;
		//		Иначе
		//			Если (Порядок_СПС.Количество() > 0) Тогда
		//				//Стр.НомерСчета = стр.НомерСчета.Владелец.ПенсионныйСчет;
		//				Стр.СПС = Истина;
		//			КонецЕсли;
		//		КонецЕсли;	
		//	КонецЕсли;
		//КонецЦикла; 
	Иначе
		ТабНачислений.ЗаполнитьЗначения(ИСТИНА, "ИПС");
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабСписания.НомерСчета КАК НомерСчета,
	|	ТабСписания.Схема КАК Схема,
	|	ТабСписания.ТипРезерва КАК ТипРезерва,
	|	ТабСписания.ТипСуммы КАК ТипСуммы,
	|	ТабСписания.Сумма КАК Сумма,
	|	ТабСписания.СПС КАК СПС,
	|	ВЫБОР
	|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.ИПС)
	|			ТОГДА &РезервНачисленийИПС
	|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.СПС)
	|			ТОГДА &РезервНачисленийСПС
	|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.ПожизненныхВыплат)
	|			ТОГДА &РезервНачисленийПожизненныхВыплат
	|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.РезервНаследниковИПС)
	|			ТОГДА &РезервНачисленийНаследниковИПС
	|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.РезервНаследниковСПС)
	|			ТОГДА &РезервНачисленийНаследниковСПС
	|		ИНАЧЕ ТабСписания.ТипРезерва
	|	КОНЕЦ КАК ТипРезерваНачислений
	|ПОМЕСТИТЬ ТабСписания
	|ИЗ
	|	&ТабСписания КАК ТабСписания
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСчета,
	|	Схема
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСписания.НомерСчета КАК НомерСчета,
	|	ТабСписания.Номерсчета.Владелец.ПенсионныйСчет КАК ПенсионныйСчетИсточник,
	|	ТабСписания.Схема КАК Схема,
	|	ТабСписания.ТипРезерва КАК ТипРезерва,
	|	ТабСписания.ТипСуммы КАК ТипСуммы,
	|	ТабСписания.Сумма КАК Сумма,
	|	ТабСписания.СПС КАК СПС,
	|	ТабСписания.Схема КАК ПенсионнаяСхема,
	|	ТабСписания.ТипРезерваНачислений КАК ТипРезерваНачислений,
	|	ТабСписания.ТипРезерва КАК ТипРезерваИсх,
	|	ТабСписания.ТипСуммы КАК ТипСуммыИсх
	|ИЗ
	|	ТабСписания КАК ТабСписания";
	
	Запрос.УстановитьПараметр("ТабСписания", ТабНачислений);
	ТипыРезервовНачислений = Новый СписокЗначений;
	Если пТипВыплаты = 1 Тогда
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
		Запрос.УстановитьПараметр("РезервНачисленийИПС", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
		Запрос.УстановитьПараметр("РезервНачисленийСПС", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
		Запрос.УстановитьПараметр("РезервНачисленийПожизненныхВыплат", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
		Запрос.УстановитьПараметр("РезервНачисленийНаследниковИПС", Перечисления.уп_ТипыРезерва.РезервНаследниковИПС);
		Запрос.УстановитьПараметр("РезервНачисленийНаследниковСПС", Перечисления.уп_ТипыРезерва.РезервНаследниковСПС);
	Иначе
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
		ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);

		Запрос.УстановитьПараметр("РезервНачисленийИПС", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
		Запрос.УстановитьПараметр("РезервНачисленийСПС", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
		Запрос.УстановитьПараметр("РезервНачисленийПожизненныхВыплат", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);
		Запрос.УстановитьПараметр("РезервНачисленийНаследниковИПС", Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковИПС);
		Запрос.УстановитьПараметр("РезервНачисленийНаследниковСПС", Перечисления.уп_ТипыРезерва.РезервНачисленийНаследниковСПС);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТипыРезервовНачислений", ТипыРезервовНачислений);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ТЗСол.Колонки.Добавить("ПенсионныйСчетИсточник", Новый ОписаниеТипов("СправочникСсылка.уп_ПенсионныеСчета"));
	Пока Выборка.Следующий() Цикл
		Если Выборка.СПС И НЕ Выборка.ТипРезерва = Перечисления.уп_ТипыРезерва.ИПС Тогда
			//кандидат на списание с СПС
			СтрТЗСол = ТЗСол.Добавить();
			СтрТЗСол.НомерСчета = Выборка.ПенсионныйСчетИсточник;
			СтрТЗСол.ПенсионныйСчетИсточник = Выборка.Номерсчета;
			СтрТЗСол.ТипРезерва = Выборка.ТипРезерваИсх; 
			СтрТЗСол.ТипСуммы = Выборка.ТипСуммыИсх; 
			СтрТЗСол.Схема = Выборка.Схема;
			СтрТЗСол.Сумма = Выборка.Сумма;
		Иначе
			Движение = ТабРезервов.Добавить();
			Движение.Активность = Истина;

			Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсийВРезерв Тогда
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.ТипРезерва = Выборка.ТипРезерваИсх;
				Движение.Сумма = Выборка.Сумма * - 1;  
            Иначе
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.ТипРезерва = Выборка.ТипРезерваНачислений;
				Движение.Сумма = Выборка.Сумма;  
			КонецЕсли;			
			
			Движение.Регистратор = Ссылка;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.Организация = Организация;
			Движение.Период = Дата;
			Если ВернутьИсхРезерв = Истина Тогда
				Движение.ТипРезерваИсх = Выборка.ТипРезерваИсх;
			КонецЕсли;
			Движение.ТипСуммы = Выборка.ТипСуммы;
			Если ВестиУчетПоСхемам = Истина Тогда
				Движение.ПенсионнаяСхема = Выборка.ПенсионнаяСхема;
			КонецЕсли;
			Движение.Схема = Выборка.Схема;
			Движение.Движение = Перечисления.уп_ВидыДвижений.Начисление;
		КонецЕсли;
	КонецЦикла;
	
	Если ТЗСол.Количество() > 0 Тогда
		ТЗСол.Свернуть("ПенсионныйСчетИсточник,НомерСчета,Схема,ТипРезерва,ТипСуммы", "Сумма");
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТабСписания.ПенсионныйСчетИсточник КАК ПенсионныйСчетИсточник,
		|	ТабСписания.НомерСчета КАК НомерСчета,
		|	ТабСписания.Схема КАК Схема,
		|	ТабСписания.ТипРезерва КАК ТипРезерва,
		|	ТабСписания.ТипСуммы КАК ТипСуммы,
		|	ТабСписания.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.ИПС)
		|			ТОГДА &РезервНачисленийИПС
		|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.СПС)
		|			ТОГДА &РезервНачисленийСПС
		|		КОГДА ТабСписания.ТипРезерва = ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.УП_ТИПЫРЕЗЕРВА.ПожизненныхВыплат)
		|			ТОГДА &РезервНачисленийПожизненныхВыплат
		|		ИНАЧЕ ТабСписания.ТипРезерва
		|	КОНЕЦ КАК ТипРезерваНачислений
		|ПОМЕСТИТЬ ТабСписания
		|ИЗ
		|	&ТабСписания КАК ТабСписания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСчета,
		|	Схема
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабСписания.ПенсионныйСчетИсточник КАК ПенсионныйСчетИсточник,
		|	ТабСписания.НомерСчета КАК НомерСчета,
		|	ТабСписания.Схема КАК Схема,
		|	ТабСписания.ТипРезерва КАК ТипРезерва,
		|	ТабСписания.ТипСуммы КАК ТипСуммы,
		|	ТабСписания.Сумма КАК Сумма,
		|	ТабСписания.Схема КАК ПенсионнаяСхема,
		|	ТабСписания.ТипРезерва КАК ТипРезерваИсх,
		|	ТабСписания.ТипСуммы КАК ТипСуммыИсх,
		|	ТабСписания.ТипРезерваНачислений КАК ТипРезерваНачислений
		|ИЗ
		|	ТабСписания КАК ТабСписания";
		
		Запрос.УстановитьПараметр("ТабСписания", ТЗСол);
		ТипыРезервовНачислений = Новый СписокЗначений;
		Если пТипВыплаты = 1 Тогда
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
			Запрос.УстановитьПараметр("РезервНачисленийИПС", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийИПС);
			Запрос.УстановитьПараметр("РезервНачисленийСПС", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийСПС);
			Запрос.УстановитьПараметр("РезервНачисленийПожизненныхВыплат", Перечисления.уп_ТипыРезерва.РезервНачисленийПенсийПожизненныхВыплат);
		Иначе
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
			ТипыРезервовНачислений.Добавить(Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);
			Запрос.УстановитьПараметр("РезервНачисленийИПС", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммИПС);
			Запрос.УстановитьПараметр("РезервНачисленийСПС", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммСПС);
			Запрос.УстановитьПараметр("РезервНачисленийПожизненныхВыплат", Перечисления.уп_ТипыРезерва.РезервНачисленийВыкупныхСуммПожизненныхВыплат);
		КонецЕсли;
		Запрос.УстановитьПараметр("ТипыРезервовНачислений", ТипыРезервовНачислений);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение = ТабРезервов.Добавить();
			Движение.Активность = Истина;
			Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсийВРезерв Тогда
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.ТипРезерва = Выборка.ТипРезерваИсх;
				Движение.Сумма = Выборка.Сумма * - 1;  
            Иначе
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.ТипРезерва = Выборка.ТипРезерваНачислений;
				Движение.Сумма = Выборка.Сумма;  
			КонецЕсли;			
			Движение.Регистратор = Ссылка;
			Движение.ПенсионныйСчетИсточник = Выборка.ПенсионныйСчетИсточник;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.Организация = Организация;
			Движение.Период = Дата;
			Если ВернутьИсхРезерв = Истина Тогда
				Движение.ТипРезерваИсх = Выборка.ТипРезерваИсх;
			КонецЕсли;
			Движение.ТипСуммы = Выборка.ТипСуммы;
			Если ВестиУчетПоСхемам = Истина Тогда
				Движение.ПенсионнаяСхема = Выборка.ПенсионнаяСхема;
			КонецЕсли;
			Движение.Схема = Выборка.Схема;
			Движение.Движение = Перечисления.уп_ВидыДвижений.Начисление;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТабРезервов;
КонецФункции		

Процедура РассчитатьДокументНаСервере(Объект,Отказ) Экспорт
	
	Объект.Распределение.Очистить();
	
	ТабСчетов = Объект.СписокСчетов.Выгрузить();
	
	ЗапросСвертка = Новый Запрос;
	ЗапросСвертка.Текст = 
		"ВЫБРАТЬ
		|	ТабСчетов.НомерСчета,
		|	ТабСчетов.ДатаНачалаПериода,
		|	ТабСчетов.ДатаКонцаПериода,
		|	ТабСчетов.НДФЛ,     
		|	ТабСчетов.РКО,     
		|	ТабСчетов.Сумма
		|ПОМЕСТИТЬ ТабСчетов
		|ИЗ
		|	&ТабСчетов КАК ТабСчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабСчетов.НомерСчета,
		|	МИНИМУМ(ТабСчетов.ДатаНачалаПериода) КАК ДатаНачалаПериода,
		|	МАКСИМУМ(ТабСчетов.ДатаКонцаПериода) КАК ДатаКонцаПериода,
		|	СУММА(ТабСчетов.Сумма) КАК Сумма, 
		|	СУММА(ТабСчетов.РКО) КАК РКО, 
		|	СУММА(ТабСчетов.НДФЛ) КАК НДФЛ
		|ИЗ
		|	ТабСчетов КАК ТабСчетов
		|
		|СГРУППИРОВАТЬ ПО
		|	ТабСчетов.НомерСчета";
		
	ЗапросСвертка.УстановитьПараметр("ТабСчетов", ТабСчетов);	
	РезультатЗапросаСвертка = ЗапросСвертка.Выполнить();
	
	ВыборкаСвертка = РезультатЗапросаСвертка.Выбрать();
	
	Пока ВыборкаСвертка.Следующий() Цикл 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_НачисленияПенсийОбороты.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ТабРегистраторов
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсий.Обороты(
		|			&ДатаНачалаДок,
		|			&ДатаДок,
		|			Регистратор,
		|			НомерСчета = &НомерСчета
		|				И НачПериода >= &ДатаНач
		|				И КонПериода <= &ДатаКон) КАК уп_НачисленияПенсийОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_РезервыОбороты.ТипРезерва КАК ТипРезерва,
		|	уп_РезервыОбороты.ТипСуммы КАК ТипСуммы,
		|	уп_РезервыОбороты.СуммаРасход КАК СуммаРасход
		|ПОМЕСТИТЬ ТабСумм
		|ИЗ
		|	РегистрНакопления.уп_Резервы.Обороты(
		|			&ДатаНачалаДок,
		|			&ДатаДок,
		|			Регистратор,
		|			Организация = &Организация
		|				И НомерСчета = &НомерСчета
		|				И ТипРезерва В (&ОснСписокТР)) КАК уп_РезервыОбороты
		|ГДЕ
		|	уп_РезервыОбороты.Регистратор В
		|			(ВЫБРАТЬ
		|				ТабРегистраторов.Регистратор
		|			ИЗ
		|				ТабРегистраторов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабСумм.ТипРезерва КАК ТипРезерва,
		|	ВЫБОР
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыЮр)
		|			ТОГДА 1
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДЮр)
		|			ТОГДА 2
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыТр)
		|			ТОГДА 3
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДТр)
		|			ТОГДА 4
		|		ИНАЧЕ 5
		|	КОНЕЦ КАК ПолеСортировки,
		|	СУММА(ТабСумм.СуммаРасход) КАК СуммаРасход,
		|	ТабСумм.ТипСуммы КАК ТипСуммы
		|ИЗ
		|	ТабСумм КАК ТабСумм
		|
		|СГРУППИРОВАТЬ ПО
		|	ТабСумм.ТипРезерва,
		|	ВЫБОР
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыЮр)
		|			ТОГДА 1
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДЮр)
		|			ТОГДА 2
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыТр)
		|			ТОГДА 3
		|		КОГДА ТабСумм.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДТр)
		|			ТОГДА 4
		|		ИНАЧЕ 5
		|	КОНЕЦ,
		|	ТабСумм.ТипСуммы
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПолеСортировки УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТабРегистраторов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТабСумм";
		
		Запрос.УстановитьПараметр("ДатаДок", Объект.Дата);
		Запрос.УстановитьПараметр("ДатаКон", ВыборкаСвертка.ДатаКонцаПериода);
		Запрос.УстановитьПараметр("ДатаНач", ВыборкаСвертка.ДатаНачалаПериода);
		Запрос.УстановитьПараметр("ДатаНачалаДок", ДобавитьМесяц(ВыборкаСвертка.ДатаНачалаПериода,-1));
		Запрос.УстановитьПараметр("НомерСчета", ВыборкаСвертка.НомерСчета);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		
		ОснСписокТР = Новый СписокЗначений;
		ОснСписокТР.Добавить(Перечисления.уп_ТипыРезерва.ИПС);
		ОснСписокТР.Добавить(Перечисления.уп_ТипыРезерва.СПС);
		ОснСписокТР.Добавить(Перечисления.уп_ТипыРезерва.ПожизненныхВыплат);
		Запрос.УстановитьПараметр("ОснСписокТР", ОснСписокТР);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			//Если Объект.ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ОтвергнутыйПлатежВРезерв Тогда
			//	СуммаОсталось = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ;   
			Если Объект.ВозвращатьНДФЛ Тогда
				СуммаОсталось = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ; 
			ИначеЕсли Объект.ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ОтвергнутыйПлатежВРезерв 
				И Объект.НДФЛНеУдержанВБюджет Тогда
				СуммаОсталось = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ;
			Иначе	
				СуммаОсталось = ВыборкаСвертка.Сумма;
			КонецЕсли; 
			Если Объект.ВозвращатьРКО Тогда
				СуммаОсталось = СуммаОсталось+ВыборкаСвертка.РКО;	
			КонецЕсли;	
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СуммаКСписанию = Мин(СуммаОсталось,ВыборкаДетальныеЗаписи.СуммаРасход);
				НовСтрРасшифровки = Объект.Распределение.Добавить();
				НовСтрРасшифровки.НомерСчета = ВыборкаСвертка.НомерСчета;
				НовСтрРасшифровки.ТипРезерва = ВыборкаДетальныеЗаписи.ТипРезерва;
				НовСтрРасшифровки.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
				НовСтрРасшифровки.Сумма = СуммаКСписанию;
				СуммаОсталось = СуммаОсталось-СуммаКСписанию;
				Если СуммаОсталось = 0 Тогда
					Прервать;
				КонецЕсли;	
			КонецЦикла;
			Если СуммаОсталось > 0 Тогда
				Сообщить("По пенсионному счету "+ВыборкаСвертка.НомерСчета+" не удалось полностью распределить сумму. Необходима ручная корректировка.");
				Отказ = Истина;
			КонецЕсли;
		Иначе
			//это или пожизненная или солидарная схема
			Схема = РегистрыСведений.уп_СхемаСчета.ПолучитьПоследнее(Объект.Дата, Новый Структура("НомерСчета", ВыборкаСвертка.НомерСчета)).Схема;
			Если Схема.ИсточникиВыплат_СПС.Количество()>0 Тогда
				ОснИсточник = Схема.ИсточникиВыплат_СПС.Получить(0);
				НовСтрРасшифровки = Объект.Распределение.Добавить();
				НовСтрРасшифровки.НомерСчета = ВыборкаСвертка.НомерСчета;
				НовСтрРасшифровки.ТипРезерва = ОснИсточник.ТипРезерва;
				НовСтрРасшифровки.ТипСуммы = Справочники.уп_ТипыСумм.ВзносыЮр;
				Если Объект.ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ОтвергнутыйПлатежВРезерв Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ+ВыборкаСвертка.РКО;
				ИначеЕсли Объект.ВозвращатьНДФЛ И Объект.ВозвращатьРКО Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ+ВыборкаСвертка.РКО;   
				ИначеЕсли Объект.ВозвращатьНДФЛ Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ; 
				ИначеЕсли Объект.ВозвращатьРКО Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.РКО; 
				Иначе
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма;
				КонецЕсли;
				НовСтрРасшифровки.СчетДоговора = Истина;
			Иначе
				НовСтрРасшифровки = Объект.Распределение.Добавить();
				НовСтрРасшифровки.НомерСчета = ВыборкаСвертка.НомерСчета;
				НовСтрРасшифровки.ТипСуммы = Справочники.уп_ТипыСумм.ВзносыЮр;
				Если Объект.ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ОтвергнутыйПлатежВРезерв Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ+ВыборкаСвертка.РКО;
				ИначеЕсли Объект.ВозвращатьНДФЛ И Объект.ВозвращатьРКО Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ+ВыборкаСвертка.РКО;   
				ИначеЕсли Объект.ВозвращатьНДФЛ Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.НДФЛ; 
				ИначеЕсли Объект.ВозвращатьРКО Тогда
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма+ВыборкаСвертка.РКО; 
				Иначе
					НовСтрРасшифровки.Сумма = ВыборкаСвертка.Сумма;
				КонецЕсли;
				НовСтрРасшифровки.СчетДоговора = Истина;
				Если Схема.ТипПенсионнойСхемы = Перечисления.уп_ТипыПенсионныхСхем.ПожизненныхВыплат Тогда
					НовСтрРасшифровки.ТипРезерва = Перечисления.уп_ТипыРезерва.ПожизненныхВыплат;
				Иначе
					НовСтрРасшифровки.ТипРезерва = Перечисления.уп_ТипыРезерва.СПС;
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры
