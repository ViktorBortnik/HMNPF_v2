
&После("ОбработкаЗаполнения")
Процедура НПФ_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание")  Тогда
			
			Для Каждого Стр Из ДанныеЗаполнения.Основание.СписокСчетов Цикл
				
				нСтр = СписокСчетов.Добавить();
				
				нСтр.НомерСчета        = Стр.НомерСчета;
				нСтр.Участник          = Стр.Участник;
				нСтр.ПенсионныйДоговор = Стр.ПенсионныйДоговор;
				
				Прервать;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
 
&После("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, РежимПроведения)

	// +ХМНПФ 23.05.2024 ЛыткинАМ // движения по резервам начислений НПО, в случае возврата выплаты в начисление
	Если НЕ Отказ Тогда
		Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсий
			ИЛИ ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсийВРезерв Тогда
			Если Движения.уп_Резервы.Количество() = 0 Тогда
				ДатаНачалаУчетаРезервовНачисления = Константы.уп_ДатаНачалаВеденияРезервовНачисленияНПО.Получить();
				Если ЗначениеЗаполнено(ДатаНачалаУчетаРезервовНачисления) И Дата>=ДатаНачалаУчетаРезервовНачисления Тогда
					лУчетРезервовНачисленияНПО = Истина;	
				Иначе
					лУчетРезервовНачисленияНПО = Ложь;	
				КонецЕсли;
				Если лУчетРезервовНачисленияНПО Тогда
					ТабНачислений = Движения.уп_НачисленияПенсий.Выгрузить();
					ТабРезервовНПО = Документы.уп_ВозвратПенсий.ДвиженияПоРезервамНПО(Ссылка, Дата, Организация, ТабНачислений,,,,ВидОперации);
					Движения.уп_Резервы.Загрузить(ТабРезервовНПО);
					Движения.уп_Резервы.Записать();
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры

&После("СформироватьДвиженияВозвратВНачисленные")
Процедура ХМСформироватьДвиженияВозвратВНачисленные(Отказ)
	// Вставить содержимое метода.
КонецПроцедуры


Процедура ХМ_ЗАГРУЗКА_ОбработкаПроведения(Отказ, Режим, СтрокаТЗ, СтруктураТЧ) Экспорт
	Если Отказ = Ложь Тогда
		Объект = ЭтотОбъект;
		Если Объект.ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсий Тогда
			Сумма_Каспер = Объект.СписокСчетов.Итог("Сумма");
			МассивСчетов = Объект.СписокСчетов.ВыгрузитьКолонку("НомерСчета");
			Объект.СписокСчетов.Очистить();
			Объект.Распределение.Очистить();
			Документы.уп_ВозвратПенсий.РасчетПоДокументуВыплатыОбщий(Объект);
			МассивНаУдаление = Новый Массив();
			Для каждого СтрокаСчета Из Объект.СписокСчетов Цикл
				Если МассивСчетов.Найти(СтрокаСчета.НомерСчета) = Неопределено Тогда
					МассивНаУдаление.Добавить(СтрокаСчета);	
				КонецЕсли;
			КонецЦикла;                                    
			Для каждого СтрокаСчета Из МассивНаУдаление Цикл
				Объект.СписокСчетов.Удалить(СтрокаСчета);
			КонецЦикла;                                    
			
			Сумма_1С = Объект.СписокСчетов.Итог("Сумма");
			Если Не Сумма_1С = Сумма_Каспер Тогда
				Отказ = Истина;	
			КонецЕсли;
			Если Отказ = Ложь Тогда
				Документы.уп_ВозвратПенсий.РассчитатьДокументНаСервере(Объект,Отказ);
			КонецЕсли;
			Если Отказ = Ложь Тогда
				Объект.ОбменДанными.Загрузка = Ложь;
				Объект.Записать(РежимЗаписиДокумента.Проведение);
				Объект.ОбменДанными.Загрузка = Истина;
			КонецЕсли;
//			Если Отказ = Ложь Тогда
//				уп_Начисления = Объект.Движения.уп_НачисленияПенсий;
//				уп_Начисления.Прочитать();
//				Для каждого СтрокаНачисления Из уп_Начисления Цикл
//					СтрокаНачисления.НачПериода = НачалоМесяца(Объект.Дата);		
//					СтрокаНачисления.КонПериода = КонецМесяца(Объект.Дата);		
//				КонецЦикла;
//				Объект.РучнаяКорректировка = Истина;
//				Объект.Записать();
//			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&ИзменениеИКонтроль("ОборотыПоНачислениямРасход")
Функция ХМОборотыПоНачислениямРасход(стр)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НачисленияПенсийОбороты.НомерСчета КАК НомерСчета,
	|	НачисленияПенсийОбороты.НачПериода КАК НачПериода,
	|	НачисленияПенсийОбороты.КонПериода КАК КонПериода,
	|	НачисленияПенсийОбороты.СуммаРасход КАК СуммаРасход,
	|	НачисленияПенсийОбороты.НалоговаяБазаПоНДФЛРасход КАК НалоговаяБазаПоНДФЛРасход,
	|	НачисленияПенсийОбороты.ДнейВПериодеРасход КАК ДнейВПериодеРасход,
	|	НачисленияПенсийОбороты.ТипРезерва КАК ТипРезерва,
	|	ВЫБОР
	|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыЮр)
	|			ТОГДА 1
	|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДЮр)
	|			ТОГДА 2
	|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыТр)
	|			ТОГДА 3
	|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДТр)
	|			ТОГДА 4
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК ПолеСортировки,
	|	НачисленияПенсийОбороты.ТипСуммы КАК ТипСуммы
	|ИЗ
	|	РегистрНакопления.уп_НачисленияПенсий.Обороты(
	|			&ДатаНачПериода,
	|			&ДатаДок,
	|			Период,
	|			НомерСчета = &НомерСчета
	|				И НачПериода >= &ДатаНач
	|				И КонПериода <= &ДатаКон) КАК НачисленияПенсийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачПериода,
	|	КонПериода,
	|	ПолеСортировки УБЫВ";

#Вставка 
    Если ЗначениеЗаполнено(ДокументВыплаты) Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияПенсийОбороты.НомерСчета КАК НомерСчета,
		|	НачисленияПенсийОбороты.НачПериода КАК НачПериода,
		|	НачисленияПенсийОбороты.КонПериода КАК КонПериода,
		|	НачисленияПенсийОбороты.СуммаРасход КАК СуммаРасход,
		|	НачисленияПенсийОбороты.НалоговаяБазаПоНДФЛРасход КАК НалоговаяБазаПоНДФЛРасход,
		|	НачисленияПенсийОбороты.ДнейВПериодеРасход КАК ДнейВПериодеРасход,
		|	НачисленияПенсийОбороты.ТипРезерва КАК ТипРезерва,
		|	ВЫБОР
		|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыЮр)
		|			ТОГДА 1
		|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДЮр)
		|			ТОГДА 2
		|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ВзносыТр)
		|			ТОГДА 3
		|		КОГДА НачисленияПенсийОбороты.ТипСуммы = ЗНАЧЕНИЕ(Справочник.уп_ТипыСумм.ИДТр)
		|			ТОГДА 4
		|		ИНАЧЕ 5
		|	КОНЕЦ КАК ПолеСортировки,
		|	НачисленияПенсийОбороты.ТипСуммы КАК ТипСуммы
		|ИЗ
		|	РегистрНакопления.уп_НачисленияПенсий.Обороты(
		|			&ДатаНачПериода,
		|			&ДатаДок,
		|			Регистратор,
		|			НомерСчета = &НомерСчета
		|				И НачПериода >= &ДатаНач
		|				И КонПериода <= &ДатаКон) КАК НачисленияПенсийОбороты
		|ГДЕ
		|	НачисленияПенсийОбороты.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачПериода,
		|	КонПериода,
		|	ПолеСортировки УБЫВ";
		Запрос.УстановитьПараметр("Регистратор", ДокументВыплаты);
    КонецЕсли;
#КонецВставки
	Запрос.УстановитьПараметр("ДатаНачПериода", ДобавитьМесяц(НачалоДня(стр.ДатаНачалаПериода),-1));
	Запрос.УстановитьПараметр("ДатаДок", КонецДня(Дата));
	Запрос.УстановитьПараметр("НомерСчета", стр.НомерСчета);
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(стр.ДатаНачалаПериода));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(стр.ДатаКонцаПериода));

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Возврат Выборка;
КонецФункции


&ИзменениеИКонтроль("СформироватьДвиженияВозвратВРезерв")
Процедура ХМСформироватьДвиженияВозвратВРезерв(Отказ)
	//сторно выплаты +
	//сторно расходы и прихода начислений +
	//возврат в резерв +
	//списать НБ +
	//списать счетчик начислений +
	//формирование задолженности если надо +

	Для каждого стр из СписокСчетов  Цикл
		Выборка = ОборотыПоНачислениямРасход(стр);
		НадоСписать = Стр.Сумма+?(ВозвращатьНДФЛ,стр.НДФЛ,0)+?(ВозвращатьРКО,стр.РКО,0);
		НадоСписатьВозврат = Стр.Сумма+?(ВозвращатьРКО,стр.РКО,0);

		НадосписатьНБ = стр.НБ;
		Пока (Выборка.Следующий()) и ((Надосписать<>0) или (НадоСписатьНБ<>0)) Цикл
			Списываем = Мин(Выборка.СуммаРасход, НадоСписать);
			СписываемНБ = Мин(Выборка.НалоговаяБазаПоНДФЛРасход, НадоСписатьНБ);
			СписываемВозврат = Мин(Выборка.СуммаРасход, НадоСписатьВозврат); 

			Движение = Движения.уп_НачисленияПенсий.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.НомерСчета = стр.НомерСчета;
			Движение.НачПериода = Выборка.НачПериода;
			Движение.КонПериода = Выборка.КонПериода;
			Движение.ТипРезерва = Выборка.ТипРезерва;
			Движение.ТипСуммы   = Выборка.ТипСуммы;
			Движение.Сумма = -Списываем;
			Движение.НалоговаяБазаПоНДФЛ = -СписываемНБ;
			Движение.Схема = стр.Схема;
			Движение.ТипПенсии = стр.ТипПенсии;

			Движение = Движения.уп_НачисленияПенсий.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.НомерСчета = стр.НомерСчета;
			Движение.НачПериода = Выборка.НачПериода;
			Движение.КонПериода = Выборка.КонПериода;
			Движение.ТипРезерва = Выборка.ТипРезерва;
			Движение.ТипСуммы   = Выборка.ТипСуммы;
			Движение.Сумма = -Списываем;
			Движение.НалоговаяБазаПоНДФЛ = -СписываемНБ;
			Движение.Схема = стр.Схема;
			Движение.ТипПенсии = стр.ТипПенсии;

#Вставка 
	// +ХМНПФ 11.09.2024 ЛыткинАМ // Необходимость списывать задолженность по периодам
			Если ФормироватьЗадолженость Тогда
				Движение = Движения.уп_ЗадолженностьПоПенсиям.ДобавитьПриход();
				Движение.Период = Дата;
				Движение.НомерСчета = Выборка.НомерСчета;
				Движение.НачалоПериода = Выборка.НачПериода;
				Движение.КонецПериода = Выборка.КонПериода;
				Движение.Счетчик = 1;
				Движение.СуммаПенсии = Списываем;
				Движение.СуммаДоплаты = 0;
			КонецЕсли;

	// +ХМНПФ 11.09.2024 ЛыткинАМ // Необходимость уменьшать счетчик на количество начислений
			Движение = Движения.уп_СчетчикНачислений.Добавить();
			Движение.Период = Дата;
			Движение.НомерСчета = стр.НомерСчета;
			Движение.Количество = -1;
#КонецВставки
			Если ИспользоватьРегистрВозвратов И СписываемВозврат>0 Тогда
				Движение = Движения.уп_ВозвратыПенсийНПО.ДобавитьПриход();
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = стр.НомерСчета;
				Движение.НачПериода = Выборка.НачПериода;
				Движение.КонПериода = Выборка.КонПериода;
				Движение.ТипРезерва = Выборка.ТипРезерва;
				Движение.ТипСуммы   = Выборка.ТипСуммы;
				Движение.Сумма = СписываемВозврат;
				Движение.НалоговаяБазаПоНДФЛ = СписываемНБ;
				Движение.Схема = стр.Схема;
				Движение.ТипПенсии = стр.ТипПенсии;
				Движение.ДокументВыплаты = стр.ДокументВыплаты;
				Движение.ВидДвиженияРегистра = Перечисления.уп_ВидыДвиженияВозвратов.Возврат;

				Движение = Движения.уп_ВозвратыПенсийНПО.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.НомерСчета = стр.НомерСчета;
				Движение.НачПериода = Выборка.НачПериода;
				Движение.КонПериода = Выборка.КонПериода;
				Движение.ТипРезерва = Выборка.ТипРезерва;
				Движение.ТипСуммы   = Выборка.ТипСуммы;
				Движение.Сумма = СписываемВозврат;
				Движение.НалоговаяБазаПоНДФЛ = СписываемНБ;
				Движение.Схема = стр.Схема;
				Движение.ТипПенсии = стр.ТипПенсии;
				Движение.ДокументВыплаты = стр.ДокументВыплаты;
				Движение.ВидДвиженияРегистра = Перечисления.уп_ВидыДвиженияВозвратов.ВозвратВРезерв;
			КонецЕсли;	

			НадоСписать = НадоСписать-Списываем;
			НадоСписатьНБ = НадоСписатьНБ-СписываемНБ; 
			НадоСписатьВозврат = НадоСписатьВозврат - СписываемВозврат;
		КонецЦикла;

		Если (НадоСписать<>0) или (НадосписатьНБ<>0) Тогда
			Отказ = Истина;
			уп_ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("По Счету №"+стр.НомерСчета+" нет возможности списать указанную налоговую базу или начисление.",,Заголовок);
		КонецЕсли;

		//сторно выплаты
		Движение = Движения.уп_Выплаты.Добавить();
		Движение.Организация = Организация;
		Движение.Период = Дата;
		Движение.ТипВыплаты = Перечисления.уп_ТипыВыплат.ВыплатыПенсий;
		Движение.НомерСчета = Стр.НомерСчета;
		Движение.Участник = Стр.НомерСчета.Участник;
		Движение.Сумма = -Стр.Сумма-?(ВозвращатьНДФЛ,стр.НДФЛ,0)-?(ВозвращатьРКО,стр.РКО,0);
		Если ВозвращатьНДФЛ Тогда
			Движение.НДФЛ = -стр.НДФЛ;
		Конецесли;
		Если ВозвращатьРКО Тогда
			Движение.РКО = -стр.РКО;
		Конецесли;
		Движение.Выплата = - Стр.Сумма;
		Движение.Схема = Стр.Схема;
		Движение.ТипПенсии = стр.ТипПенсии;

		////здесь должна быть проводка Д74 - К76 на сумму (-стр.Сумма)
		//СтрПолуч = ТабПолучателей.Найти(стр.НомерСчета, "НомерСчета");
		//Если СтрПолуч = Неопределено Тогда
		//	Получатель = Справочники.Контрагенты.ПустаяСсылка();
		//Иначе
		//	Если СтрПолуч.ПолучательВПлатежномПоручении = Null Тогда
		//		Получатель = СтрПолуч.Банк;
		//	Иначе
		//		Получатель = СтрПолуч.ПолучательВПлатежномПоручении;
		//	КонецЕсли;
		//КонецЕсли;
		//ВидДвижения = Перечисления.уп_ВидыДвижений.Возврат;
		//ДоговорКонтрагента = Получатель.ОсновнойДоговорКонтрагента;
		////здесь еще надо определить какой получатель, если через кассу - по ним надо аналитику загнать
		//Если НЕ ЗначениеЗаполнено(Получатель) Тогда //через кассу
		//	Получатель = Стр.НомерСчета.Участник;
		//	ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		//КонецЕсли;
		//
		//уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, Получатель, ДоговорКонтрагента,
		//Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, Получатель, ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, -Стр.Сумма);


		//счетчики
		//формирование задолженности
		//сторнируем счетчики, формируем задолженность по пенсии
#Вставка 
	// +ХМНПФ 11.09.2024 ЛыткинАМ // Необходимость уменьшать счетчик на количество начислений
#КонецВставки
#Удаление
		Движение = Движения.уп_СчетчикНачислений.Добавить();
		Движение.Период = Дата;
		Движение.НомерСчета = стр.НомерСчета;
		Движение.Количество = -1;
#КонецУдаления

		//здесь должна быть бух проводка Дт96 - Кт74 на сумму  (-стр.Сумма)
		СтрПолуч = ТабПолучателей.Найти(стр.НомерСчета, "НомерСчета");
		Если СтрПолуч.ПолучательВПлатежномПоручении = Null Тогда
			Получатель = СтрПолуч.Банк;
		Иначе
			Получатель = СтрПолуч.ПолучательВПлатежномПоручении;
		КонецЕсли;
		ВидДвижения = Перечисления.уп_ВидыДвижений.ВозвратВРезерв;
		Если СтрПолуч.ТипПенсии = Перечисления.уп_ТипыПенсионныхСхем.ПожизненныхВыплат Тогда
			ПризнакДт = Истина;
		Иначе
			ПризнакДт = Ложь;
		КонецЕсли;
		//здесь еще надо определить какой получатель, если через кассу - по ним надо аналитику загнать
		Если НЕ ЗначениеЗаполнено(Получатель) Тогда //через кассу
			Получатель = Стр.НомерСчета.Участник;
			ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Иначе
			ДоговорКонтрагента = Получатель.ОсновнойДоговорКонтрагента;
		КонецЕсли; 

		СуммаПроводки = Стр.Сумма+?(ВозвращатьНДФЛ,стр.НДФЛ,0)+?(ВозвращатьРКО,стр.РКО,0);
		уп_ОбщегоНазначенияУчетРезервов.ДобавитьСтрокуВТабБухУчет(ТабБухУчет, Дата, Организация, ВидДвижения, Стр.НомерСчета.Владелец.Вкладчик, Стр.НомерСчета.Владелец.ДоговорКонтрагента,
		СтрПолуч.Схема, ПризнакДт, Получатель, ДоговорКонтрагента, Справочники.уп_ПенсионныеСхемы.ПустаяСсылка(), Ложь, -СуммаПроводки);

	КонецЦикла;

	//запрос по доплатам
	Выборка = ОпределитьДоплатыЗаПериодВсе();
	СуммаДоплаты = 0;
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаДоплаты <> 0 Тогда
			Движение = Движения.уп_СчетчикДоплат.Добавить();
			Движение.Период = Дата;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.НачПериода = Выборка.НачПериода;
			Движение.КонПериода = Выборка.КонПериода;
			Движение.Сумма = -Выборка.СуммаДоплаты;
			Движение.Счетчик = -1;

		КонецЕсли;

		Если ФормироватьЗадолженость Тогда
#Вставка 
	// +ХМНПФ 11.09.2024 ЛыткинАМ // Необходимость списывать задолженность по периодам
			Если Выборка.СуммаДоплаты = 0 Тогда
				Продолжить;
			КонецЕсли;
#КонецВставки
			Движение = Движения.уп_ЗадолженностьПоПенсиям.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.НомерСчета = Выборка.НомерСчета;
			Движение.НачалоПериода = Выборка.НачПериода;
			Движение.КонецПериода = Выборка.КонПериода;
			Движение.Счетчик = 1;
			Движение.СуммаПенсии = Выборка.Сумма+?(ВозвращатьНДФЛ, Выборка.НДФЛ,0)+?(ВозвращатьРКО, Выборка.РКО,0);
			Движение.СуммаДоплаты = Выборка.СуммаДоплаты;
#Вставка 
	// +ХМНПФ 11.09.2024 ЛыткинАМ // Необходимость списывать задолженность по периодам
			Движение.СуммаПенсии = 0;
			Движение.Счетчик = 0;
#КонецВставки
		КонецЕсли;
	КонецЦикла;

	ЗачислитьВРезерв();
	СписаниеНалоговойБазы(Отказ);

	Если ВозвращатьНДФЛ Тогда 
		ВозвратНДФЛ();
	КонецЕсли;

	Если НЕ Отказ Тогда 
		Если НЕ ВозвращатьНДФЛ Тогда	
			Сообщить("Для возврата НДФЛ необходимо: пересчитать НДФЛ документом ""Расчет НДФЛ"", вернуть сумму в соответствии с перерасчетом документом ""НДФЛ возврат налога участникам"".");
			МассивСообщений.Добавить("Для возврата НДФЛ необходимо: пересчитать НДФЛ документом ""Расчет НДФЛ"", вернуть сумму в соответствии с перерасчетом документом ""НДФЛ возврат налога участникам"".");
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&После("ПередЗаписью")
Процедура ХМПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата = НачалоДня(Дата)+60*60*18;
КонецПроцедуры


&ИзменениеИКонтроль("ЗачислитьВРезерв")
Процедура ХМЗачислитьВРезерв()
	//зачислим в резерв
#Вставка 
	// +ХМНПФ 11.09.2024 ЛыткинАМ // 	Будем проводить начисление в резерв исходя из начислений, см. ХМОбработкаПроведения
	Если ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсий
		ИЛИ ВидОперации = Перечисления.уп_ВидыОперацийВозвратПенсий.ВозвратПенсийВРезерв Тогда
		Возврат;
	КонецЕсли;
#КонецВставки
	Для каждого стр из Распределение Цикл
		Движение = Движения.уп_Резервы.ДобавитьПриход();
		Движение.Организация = Организация;
		Движение.Период = Дата;
		//по-хорошему надо еще определить на какой счет зачислить деньги
		Если стр.СчетДоговора Тогда
			Движение.НомерСчета = стр.НомерСчета.Владелец.ПенсионныйСчет;
		Иначе
			Движение.НомерСчета = стр.НомерСчета;
		КонецЕсли;
		Движение.ТипРезерва = стр.ТипРезерва;
		Движение.ТипСуммы = стр.ТипСуммы;
		Движение.Сумма = стр.Сумма;
		Движение.Движение = Перечисления.уп_ВидыДвижений.Возврат;
		Если ВестиУчетПСпоТипамДоговоровЕПС Тогда
			Движение.ВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ВозвратПенсий",,стр.НомерСчета,,ВестиУчетПСпоТипамДоговоровЕПС);
		Иначе
			Движение.ВидДвиженияПоТипамЕПС = уп_ОбщегоНазначенияСервер.npo_ОпределитьВидДвиженияПоТипамЕПС(Дата,"уп_ВозвратПенсий",,стр.ТипРезерва,,ВестиУчетПСпоТипамДоговоровЕПС);
		КонецЕсли;	
		СтрокаТЧ = СписокСчетов.Найти(стр.НомерСчета,"НомерСчета");
		Если СтрокаТЧ<>Неопределено Тогда
			Движение.Схема = СтрокаТЧ.Схема;
			Если ВестиУчетПоСхемам Тогда
				Движение.ПенсионнаяСхема = СтрокаТЧ.Схема;
			КонецЕсли;	
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

