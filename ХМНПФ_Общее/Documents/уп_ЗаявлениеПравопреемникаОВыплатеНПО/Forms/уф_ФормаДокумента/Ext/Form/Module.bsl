
&НаСервере
&ИзменениеИКонтроль("ПриЧтенииНаСервере")
Процедура ХМПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	#Вставка
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	#КонецВставки
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ПриСозданииНаСервере")
Процедура ХМПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда // проверить объект на то, что он еще не внесен в ИБ
		
		// Заполнить реквизиты значениями по умолчанию. 
		Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь; 
		
		Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
			Объект.ДатаРегистрации = ТекущаяДата();
			Объект.ТипЗаявления = Перечисления.уп_ТипыЗаявленийНаследников.ПоПравуНаследования;
			Объект.СпособРасчета = Перечисления.уп_СпособыРасчетаНаследников.Процентом;
			Объект.Основание = 2;  
			
			Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
				ЗаполнитьЗначенияСвойств(Объект, Параметры.ЗначенияЗаполнения);
				Если ЗначениеЗаполнено(Объект.Участник) Тогда  
					ПриИзмененииУчастникаНаСервере();
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;  
		Объект.ДатаРегистрации = ТекущаяДата();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Участник) Тогда
		ДатаПравил = ОпределитьДатуРегистрацииСмерти(Объект.Участник);
	Иначе
		ДатаПравил = Объект.Дата;
	КонецЕсли;	
	ПолучитьПенсионныеПравила(Объект.Организация, ДатаПравил);
	УстановитьСрок();
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	#Вставка
	ХМДополнитьФорму();
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства	
    #КонецВставки
	//ВосстановитьОснованиеИРодство();
	УстановитьВидимость();
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// СтандартныеПодсистемы.ДополнительныеРеквизиты
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

Процедура ХМДополнитьФорму() 
	
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьОбычнуюГруппу(ЭтаФорма,"ГруппаДополнительныеРеквизиты",ЭтаФорма);
	
КонецПроцедуры  

