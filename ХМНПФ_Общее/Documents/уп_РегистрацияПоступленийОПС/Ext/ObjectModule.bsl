
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96	
	// ХМНПФ+ 02.02.2016 ШадринАВ // Новый реквизит ХМ_НомерРеестра // №169
	// ХМНПФ 02.02.2016 ШадринАВ // ПередЗаписью() // №169
	// ХМНПФ 11.07.2016 ШадринАВ // ОбработкаПроведения() // №425 // отменено задачей №867
	// ХМНПФ 06.03.2016 ШадринАВ // ОбработкаПроведения() // №832
	// ХМНПФ 13.03.2016 ШадринАВ // ОбработкаПроведения() // №859
	// ХМНПФ 13.03.2016 ШадринАВ // Новая ХМ_УбратьДублиДоговоров() // №859
	// ХМНПФ 18.01.2018 КлименкоМИ // ОбработкаПроведения() // №1285	
	// ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
	// ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаПроведения() // №1376
	// ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаУдаленияПроведения() // №1376  
	// ХМНПФ 05.03.2018 КлименкоМИ // Новая ХМ_ПеревестиВНакопительныйПериод() // №1376
	// ХМНПФ 05.03.2018 КлименкоМИ // Новая ХМ_ПометкаНаУдалениеОперации() // №1376	
	// ХМНПФ 09.04.2018 КлименкоМИ // ОбработкаПроведения() // №1496
	// ХМНПФ 26.12.2018 ШадринАВ // ПроверкаКорректности() // №1955
	// ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;
	МассивСообщений = Новый СписокЗначений;
	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = Новый Структура("Организация, Ответственный");
	#Вставка
	// +ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96	
	СтруктураОбязательныхПолей.Вставить("ДокументОснование");
	// -ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96
	#КонецВставки
	уп_ОсновнаяПоставкаСервер.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);
   	
	Если КатегорияВзноса = Перечисления.уп_КатегорияВзносаОПС.ВозвратСредствОтСледущегоСтраховщика
		И НЕ ЭтоНераспределенныйПлатеж Тогда
		ПроверитьКорректностьЗаполненияТЧ(Отказ);
	КонецЕсли;	
	
	Если ЭтоНераспределенныйПлатеж Тогда
		Движение = Движения.уп_НераспределенныеПлатежиОПС.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Организация = Организация;
		Движение.Период = Дата;
		Движение.Плательщик = ПенсионныйФонд;
		Движение.ДокументОснование = Ссылка;
		Движение.Сумма = СуммаДокумента;
	Иначе
		ПроверкаКорректности(Отказ, Заголовок);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			//ищем начисление резрвов
			#Удаление
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	уп_НачислениеРезервовПлВходящие.Ссылка,
			|	уп_НачислениеРезервовПлВходящие.ДокументОснование,
			|	СУММА(уп_НачислениеРезервовПлВходящие.СуммаВзноса) КАК СуммаВзноса
			|ИЗ
			|	Документ.уп_НачислениеРезервов.ДокументыОснования КАК уп_НачислениеРезервовПлВходящие
			|ГДЕ
			|	уп_НачислениеРезервовПлВходящие.ДокументОснование = &ПлПоручениеВходящее
			|
			|СГРУППИРОВАТЬ ПО
			|	уп_НачислениеРезервовПлВходящие.Ссылка,
			|	уп_НачислениеРезервовПлВходящие.ДокументОснование";

 			Запрос.УстановитьПараметр("ПлПоручениеВходящее", ДокументОснование);
  		
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
   		
			Если Выборка.Следующий() Тогда
				СуммаОснования = Выборка.СуммаВзноса;
				Если СуммаОснования<>ДоговорыОПС.Итог("Сумма") Тогда
					Сообщить("Сумма взносов в табличной части не совпадает с суммой документа основания.", СтатусСообщения.Внимание);

    				МассивСообщений.Добавить("Сумма взносов в табличной части не совпадает с суммой документа основания.");
				КонецЕсли;	
			КонецЕсли;
 			#КонецУдаления
			#Вставка
			СуммаОснования = ДокументОснование.СуммаДокумента;
			Если СуммаОснования<>ДоговорыОПС.Итог("Сумма") Тогда
				Сообщить("Сумма взносов в табличной части не совпадает с суммой документа основания.", СтатусСообщения.Внимание);
					МассивСообщений.Добавить("Сумма взносов в табличной части не совпадает с суммой документа основания.");
				Отказ = Истина;
				Возврат;
			КонецЕсли;	
			// -ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96
			#КонецВставки
		КонецЕсли;	
		
		//установить накопительный период 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияДоговораОПССрезПоследних.ДоговорОПС,
		|	СостоянияДоговораОПССрезПоследних.Состояние
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоров)) КАК СостоянияДоговораОПССрезПоследних
		|ГДЕ
		|	СостоянияДоговораОПССрезПоследних.Состояние В(&СписокСостояний)";
		
		Запрос.УстановитьПараметр("ДатаДок", КонецДня(Дата));
		Запрос.УстановитьПараметр("СписокДоговоров", ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС"));
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Заключен);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПодтвержденияПФР);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ПриостановленПоОтказуИзПФР);
		Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		#Вставка
		// +ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		ХМ_ЗапомнитьОтказ = Отказ;
		// -ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		#КонецВставки
		Пока Выборка.Следующий() Цикл
			Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = Выборка.ДоговорОПС;
			Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод;
			Если Выборка.Состояние <> Перечисления.уп_СостоянияДоговоровОПС.ОжиданиеПоступленияВзносов Тогда
				ТекстСообщения = "По договору №"+СокрЛП(Выборка.ДоговорОПС.Код)+" не введен документ ""Подтвержение""!";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
				МассивСообщений.Добавить(ТекстСообщения);
			КонецЕсли;
			Если Дата>='20140101000000' Тогда
				Движение = Движения.уп_ДатаФиксацииОбязательств.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = Выборка.ДоговорОПС;
				Движение.ДатаНачалаПериодаФиксации = Дата;
				Движение.ДатаСледующейФиксации = ДобавитьМесяц(НачалоГода(Дата),60)-1;
			КонецЕсли;
		КонецЦикла;
     	
		#Вставка
		// +ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		Отказ = ХМ_ЗапомнитьОтказ;
		// -ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		#КонецВставки
		//надо проставить состояния по частям счета
		
		//основная часть
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС,
		|	СУММА(уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма) КАК Сумма
		|ПОМЕСТИТЬ СписокОсновных
		|ИЗ
		|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|ГДЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
		|	И уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы В(&СписокТиповОсновных)
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокОсновных.ДоговорОПС,
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние,
		|	СписокОсновных.Сумма
		|ПОМЕСТИТЬ СостоянияОснЧасти
		|ИЗ
		|	СписокОсновных КАК СписокОсновных
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(&ТекДата, ) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|		ПО СписокОсновных.ДоговорОПС = уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияОснЧасти.ДоговорОПС,
		|	СостоянияОснЧасти.Сумма,
		|	СостоянияОснЧасти.Состояние
		|ИЗ
		|	СостоянияОснЧасти КАК СостоянияОснЧасти
		|ГДЕ
		|	(СостоянияОснЧасти.Состояние ЕСТЬ NULL ИЛИ СостоянияОснЧасти.Состояние = &ПустоеСостояние
		|			ИЛИ СостоянияОснЧасти.Состояние = &ОбязательстваВыполнены)";
		
		
		СписокТиповОсновных = Новый СписокЗначений;
		СписокТиповОсновных.Добавить(Справочники.уп_ТипыСуммОПС.ВзносыФиз);
		СписокТиповОсновных.Добавить(Справочники.уп_ТипыСуммОПС.ИДФиз);
		
		Запрос.УстановитьПараметр("СписокТиповОсновных", СписокТиповОсновных);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТекДата", Дата);
		Запрос.УстановитьПараметр("ОбязательстваВыполнены", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ОбязательстваВыполнены);
		Запрос.УстановитьПараметр("ПустоеСостояние", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ПустаяСсылка());
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Сумма = 0 И ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ОбязательстваВыполнены Тогда
				//ничего не делаем
			Иначе
				Движение = Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НакопительныйПериод;
			КонецЕсли;
		КонецЦикла;
		
		//состояние дополнительной части
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
		|	СУММА(уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма) КАК Сумма
		|ПОМЕСТИТЬ СписокОсновных
		|ИЗ
		|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|ГДЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
		|	И НЕ уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы В (&СписокТиповОсновных)
		|
		|СГРУППИРОВАТЬ ПО
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокОсновных.ДоговорОПС КАК ДоговорОПС,
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние,
		|	СписокОсновных.Сумма
		|ПОМЕСТИТЬ СостоянияДопЧасти
		|ИЗ
		|	СписокОсновных КАК СписокОсновных
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|				&ТекДата,
		|				ДоговорОПС В
		|					(ВЫБРАТЬ
		|						СписокОсновных.ДоговорОПС
		|					ИЗ
		|						СписокОсновных)) КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних
		|		ПО СписокОсновных.ДоговорОПС = уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние
		|ПОМЕСТИТЬ ЕВ
		|ИЗ
		|	РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.СрезПоследних(
		|			&ТекДата,
		|			ДоговорОПС В
		|				(ВЫБРАТЬ
		|					СписокОсновных.ДоговорОПС
		|				ИЗ
		|					СписокОсновных)) КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПССрезПоследних.Состояние = &ЕдиновременнаяВыплата
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеДоговораОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
		|	уп_СостояниеДоговораОПССрезПоследних.Состояние
		|ПОМЕСТИТЬ СостоянияНеОткрывать
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
		|			&ТекДата,
		|			ДоговорОПС В
		|				(ВЫБРАТЬ
		|					СписокОсновных.ДоговорОПС
		|				ИЗ
		|					СписокОсновных)) КАК уп_СостояниеДоговораОПССрезПоследних
		|ГДЕ
		|	(уп_СостояниеДоговораОПССрезПоследних.Состояние = &Прекращен
		|			ИЛИ уп_СостояниеДоговораОПССрезПоследних.Состояние = &ПодготовкаКЗакрытию)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	уп_СостояниеДоговораОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
		|	уп_СостояниеДоговораОПССрезПоследних.Состояние,
		|	ВЫБОР
		|		КОГДА уп_СостояниеДоговораОПССрезПоследних.Регистратор ССЫЛКА Документ.уп_РешениеОНазначенииНЧТП
		|			ТОГДА уп_СостояниеДоговораОПССрезПоследних.Регистратор.УчитыватьДопВзносы
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК УчитыватьДопВзносы
		|ПОМЕСТИТЬ СостоянияНЧТП
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(
		|			&ТекДата,
		|			ДоговорОПС В
		|				(ВЫБРАТЬ
		|					СписокОсновных.ДоговорОПС
		|				ИЗ
		|					СписокОсновных)) КАК уп_СостояниеДоговораОПССрезПоследних
		|ГДЕ
		|	уп_СостояниеДоговораОПССрезПоследних.Состояние = &НЧТП
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДоговорОПС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияДопЧасти.ДоговорОПС,
		|	СостоянияДопЧасти.Сумма,
		|	СостоянияДопЧасти.Состояние,
		|	уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДатаОбращения,
		|	уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ВключатьДопВзносы,
		|	ЕВ.Состояние КАК СостояниеОснЧасти,
		|	СостоянияНеОткрывать.Состояние КАК СостояниеЗакрытое,
		|	СостоянияНЧТП.Состояние КАК СостояниеНЧТП,
		|	СостоянияНЧТП.УчитыватьДопВзносы
		|ИЗ
		|	СостоянияДопЧасти КАК СостоянияДопЧасти
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ДатыОбращенияЗаЕдиновременнойВыплатой.СрезПоследних(&ТекДата, ) КАК уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних
		|		ПО СостоянияДопЧасти.ДоговорОПС = уп_ДатыОбращенияЗаЕдиновременнойВыплатойСрезПоследних.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЕВ КАК ЕВ
		|		ПО СостоянияДопЧасти.ДоговорОПС = ЕВ.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНеОткрывать КАК СостоянияНеОткрывать
		|		ПО СостоянияДопЧасти.ДоговорОПС = СостоянияНеОткрывать.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНЧТП КАК СостоянияНЧТП
		|		ПО СостоянияДопЧасти.ДоговорОПС = СостоянияНЧТП.ДоговорОПС
		|ГДЕ
		|	(СостоянияДопЧасти.Состояние ЕСТЬ NULL 
		|			ИЛИ СостоянияДопЧасти.Состояние = &ПустоеСостояние
		|			ИЛИ СостоянияДопЧасти.Состояние = &ОбязательстваВыполнены)";
		
		Запрос.УстановитьПараметр("СписокТиповОсновных", СписокТиповОсновных);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("ТекДата", Дата);
		Запрос.УстановитьПараметр("ОбязательстваВыполнены", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ОбязательстваВыполнены);
		Запрос.УстановитьПараметр("ПустоеСостояние", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ПустаяСсылка());
		Запрос.УстановитьПараметр("ЕдиновременнаяВыплата", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ЕдиновременнаяВыплата);
		Запрос.УстановитьПараметр("НЧТП", Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП);
		Запрос.УстановитьПараметр("Прекращен", Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
		Запрос.УстановитьПараметр("ПодготовкаКЗакрытию", Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию);
		
		Результат = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = Результат.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Сумма = 0 И ВыборкаДетальныеЗаписи.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ОбязательстваВыполнены Тогда
				//ничего не делаем
			ИначеЕсли ВыборкаДетальныеЗаписи.СостояниеОснЧасти = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ЕдиновременнаяВыплата И 
				ВыборкаДетальныеЗаписи.ВключатьДопВзносы Тогда
				//единовременная выплата должна пойти по этой части счета
				Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.ЕдиновременнаяВыплата;
			ИначеЕсли ВыборкаДетальныеЗаписи.СостояниеОснЧасти = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП И 
				ВыборкаДетальныеЗаписи.УчитыватьДопВзносы Тогда
				//НЧТП выплата должна пойти по этой части счета
				Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НЧТП;
			ИначеЕсли ВыборкаДетальныеЗаписи.СостояниеЗакрытое = Перечисления.уп_СостоянияДоговоровОПС.Закрыт
				ИЛИ ВыборкаДетальныеЗаписи.СостояниеЗакрытое = Перечисления.уп_СостоянияДоговоровОПС.ПодготовкаКЗакрытию Тогда
				//ничего не делаем
			Иначе
				Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НакопительныйПериод;
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	СостоянияДоговораОПССрезПоследних.ДоговорОПС,
		|	СостоянияДоговораОПССрезПоследних.Состояние,
		|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия КАК ПричинаЗакрытия,
		|	СостоянияДоговораОПССрезПоследних.ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР КАК СНИЛС,
		|	Суммы.Сумма
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоров)) КАК СостоянияДоговораОПССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоров)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
		|		ПО СостоянияДоговораОПССрезПоследних.ДоговорОПС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
		|			СУММА(уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма) КАК Сумма
		|		ИЗ
		|			Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|		ГДЕ
		|			уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
		|		
		|		СГРУППИРОВАТЬ ПО
		|			уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС) КАК Суммы
		|		ПО СостоянияДоговораОПССрезПоследних.ДоговорОПС = Суммы.ДоговорОПС
		|ГДЕ
		|	СостоянияДоговораОПССрезПоследних.Состояние В(&СписокСостояний)
		|ИТОГИ ПО
		|	ПричинаЗакрытия";
		
		Запрос.УстановитьПараметр("ДатаДок", КонецДня(Дата));
		СписокДоговоров = ДоговорыОПС.ВыгрузитьКолонку("ДоговорОПС");
		Запрос.УстановитьПараметр("СписокДоговоров", СписокДоговоров);
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.Закрыт);
		СписокСостояний.Добавить(Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены);
		Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		ВыборкаПричин = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		#Вставка
		// +ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		ХМ_ЗапомнитьОтказ = Отказ;
		// -ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		#КонецВставки
		Пока ВыборкаПричин.Следующий() Цикл
			ТекстСообщения = "Зачислены взносы на договоры, закрытые по причине:" + ВыборкаПричин.ПричинаЗакрытия; 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
			МассивСообщений.Добавить(ТекстСообщения);
			Выборка = ВыборкаПричин.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.Сумма<>0 Тогда
					ТекстСообщения = "Договор:" + Выборка.ДоговорОПС+ " СНИЛС: "+Выборка.СНИЛС + " в сумме: " + Выборка.Сумма;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
					МассивСообщений.Добавить(ТекстСообщения);
				КонецЕсли;
				Если Выборка.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЕдиновременнаяВыплата
					И Выборка.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены
					И Выборка.Сумма<>0 Тогда
					Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
					Движение.Период = Дата;
					Движение.ДатаУстановкиСостояния = Дата;
					Движение.ДоговорОПС = Выборка.ДоговорОПС;
					Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;
		#Вставка
		// +ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		Отказ = ХМ_ЗапомнитьОтказ;
		// -ХМНПФ 06.02.2018 ШадринАВ // ОбработкаПроведения() // №1353
		#КонецВставки
		
		ДатаНачалаВеденияРезерваНаследников = Константы.уп_ДатаНачалаВеденияРезерваНаследников.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаВеденияРезерваНаследников) И Дата>=ДатаНачалаВеденияРезерваНаследников Тогда
			ВедетсяРезервНаследников = Истина;
		Иначе
			ВедетсяРезервНаследников = Ложь;
		КонецЕсли;	
		ДатаНачалаВеденияРезерваЕВ = Константы.уп_ДатаНачалаВеденияРезерваЕВ.Получить();
		Если ЗначениеЗаполнено(ДатаНачалаВеденияРезерваЕВ) И Дата>=ДатаНачалаВеденияРезерваЕВ Тогда
			ВедетсяРезервЕВ = Истина;
		Иначе
			ВедетсяРезервЕВ = Ложь;
		КонецЕсли;	
		ТабДоговоровОПС = уп_ОПССервер.ДополнитьТаблицуДоговорыОПСПоступления(Ссылка, Дата-1, ВедетсяРезервНаследников);
		ДатаВступленияВСистемуГарантирования = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
		//Для Каждого ТекСтрокаДоговорыОПС Из ДоговорыОПС Цикл
		Для Каждого ТекСтрокаДоговорыОПС Из ТабДоговоровОПС Цикл
			// регистр РезервыОПС Приход
			Движение = Движения.уп_РезервыОПС.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			#Удаление
			Движение.Период = Дата;
			#КонецУдаления
			#Вставка
			// +ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96	
			Движение.Период = ДокументОснование.Дата;
			// -ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96	
			// +ХМНПФ 09.04.2018 КлименкоМИ // ОбработкаПроведения() // №1496
			Движение.СуммаКомпенсации = ТекСтрокаДоговорыОПС.СуммаКомпенсации;
			Движение.СуммаВосполнения = ТекСтрокаДоговорыОПС.СуммаВосполнения;
			// -ХМНПФ 09.04.2018 КлименкоМИ // ОбработкаПроведения() // №1496
			#КонецВставки
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
			Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
			Движение.Сумма = ТекСтрокаДоговорыОПС.Сумма;
			Если ЗначениеЗаполнено(ТекСтрокаДоговорыОПС.ДоговорОПС.ДатаРегистрацииСмерти) 
				#Вставка
				// +ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
				И ТекСтрокаДоговорыОПС.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПоСмерти
				// -ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
				#КонецВставки
				И Дата > ТекСтрокаДоговорыОПС.ДоговорОПС.ДатаРегистрацииСмерти Тогда
				ЭтоУмерший = Истина;
			Иначе
				ЭтоУмерший = Ложь;
			КонецЕсли;	
			Если ВедетсяРезервНаследников И ЭтоУмерший И ТекСтрокаДоговорыОПС.ПричинаЗакрытия<>Перечисления.уп_ПричиныЗакрытияДоговораОПС.НеВступилВСилу Тогда
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН;
			Иначе
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
				Движение.ТипРезерва = ТекСтрокаДоговорыОПС.ТипРезерва;
				Если ВедетсяРезервЕВ 
					И (НЕ ЭтоУмерший) 
					И (НЕ КатегорияВзноса = Перечисления.уп_КатегорияВзносаОПС.ПереводСредствОтПредыдущегоСтраховщика) 
					И ТекСтрокаДоговорыОПС.ТипРезерва<>Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН Тогда
					//проверим на обращение за ЕВ
					Если ЗначениеЗаполнено(ТекСтрокаДоговорыОПС.КварталОбращения) Тогда
					#Вставка
					// +ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
					Если НЕ ЗначениеЗаполнено(ДатаКонцаПериодаВзносов) Тогда
						Если ТекСтрокаДоговорыОПС.КварталОбращения > ТекСтрокаДоговорыОПС.ХМ_ДатаКрайнейДопЕДВ Тогда
							Если ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ГосПоддержка
								ИЛИ ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДГосПоддержка
								ИЛИ ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ДопВзносы
								ИЛИ ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДДопВзносы
								ИЛИ ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.МатКапитал
								ИЛИ ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
									Если ТекСтрокаДоговорыОПС.ВключатьДопВзносы Тогда
										Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
									КонецЕсли;
							Иначе // страховые взносы
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
							КонецЕсли;
						КонецЕсли;
					Иначе
					// -ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
					#КонецВставки
					Если ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ГосПоддержка Тогда
							Если ТекСтрокаДоговорыОПС.ВключатьДопВзносы Тогда
								Если ДатаКонцаПериодаВзносов < ТекСтрокаДоговорыОПС.НачалоГодаОбращения Тогда
									Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
								ИначеЕсли КонецДня(ДатаКонцаПериодаВзносов) = КонецДня(ТекСтрокаДоговорыОПС.КонецГодаОбращения) И ТекСтрокаДоговорыОПС.Сумма>0 Тогда
									//СуммаВРезервЕВ = Мин(ТекСтрокаДоговорыОПС.СуммаДСВ-ТекСтрокаДоговорыОПС.СуммаГП, ТекСтрокаДоговорыОПС.Сумма);
									Коэффициент = ТекСтрокаДоговорыОПС.Сумма/ТекСтрокаДоговорыОПС.ДопВзносыЗаГод;
									СуммаСФДолжнаОстаться = Окр(ТекСтрокаДоговорыОПС.ДопВзносыПосле*Коэффициент,2);
									СуммаВРезервЕВ = ТекСтрокаДоговорыОПС.Сумма - СуммаСФДолжнаОстаться;
									Если СуммаВРезервЕВ>0 Тогда
										Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
										Движение.Сумма = СуммаВРезервЕВ;
									КонецЕсли;	
								КонецЕсли;	
							КонецЕсли;
						ИначеЕсли ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДГосПоддержка Тогда
							Если ТекСтрокаДоговорыОПС.ВключатьДопВзносы 
								И ДатаКонцаПериодаВзносов<=КонецДня(ТекСтрокаДоговорыОПС.КонецГодаОбращения) Тогда
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
							КонецЕсли;
						ИначеЕсли ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ДопВзносы
							или ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДДопВзносы
							или ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.МатКапитал
							или ТекСтрокаДоговорыОПС.ТипСуммы = Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
							Если ТекСтрокаДоговорыОПС.ВключатьДопВзносы 
								И ДатаКонцаПериодаВзносов <= ТекСтрокаДоговорыОПС.КварталОбращения Тогда
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
							КонецЕсли;
						Иначе
							Если ДатаКонцаПериодаВзносов <= ТекСтрокаДоговорыОПС.КварталОбращения Тогда
								Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервЕВ;
							КонецЕсли;	
						КонецЕсли;
						#Вставка
						// +ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
						КонецЕсли;
						// -ХМНПФ 13.10.2021 ШадринАВ // ОбработкаПроведения() // №3356
						#КонецВставки
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
			Движение.Движение = Перечисления.уп_ВидыДвижений.ВзносОПС;
			Движение.ДокументРегистрации = ДокументРегистрации;
			
			Если Движение.Сумма<>ТекСтрокаДоговорыОПС.Сумма Тогда
				//дополнительная запись
				СуммаДоСтроки = ТекСтрокаДоговорыОПС.Сумма-Движение.Сумма;
				Движение = Движения.уп_РезервыОПС.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
				Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
				Движение.Сумма = СуммаДоСтроки;
				Движение.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений;
				Движение.Движение = Перечисления.уп_ВидыДвижений.ВзносОПС;
				Движение.ДокументРегистрации = ДокументРегистрации;
			КонецЕсли;	
			
			// регистр ПоступленияОПС 
			Движение = Движения.уп_ПоступленияОПС.Добавить();
			#Удаление
			Движение.Период = Дата;
			#КонецУдаления
			#Вставка
			// +ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96
			Движение.Период = ДокументОснование.Дата;
			// -ХМНПФ 17.12.2015 ШадринАВ // ОбработкаПроведения() // №96
			#КонецВставки
			Движение.Организация = Организация;
			Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
			Движение.Плательщик = ПенсионныйФонд;
			Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
			Движение.Дополнительно = Дополнительно;
			Движение.ДатаКонцаПериода = ДатаКонцаПериодаВзносов;
			Движение.Сумма = ТекСтрокаДоговорыОПС.Сумма;
			Движение.СуммаКомпенсации = ТекСтрокаДоговорыОПС.СуммаКомпенсации;
			Движение.СуммаВосполнения = ТекСтрокаДоговорыОПС.СуммаВосполнения;
			Движение.ДокументРегистрации = ДокументОснование;
			Движение.ДатаПлатежа = Дата;
			Движение.КатегорияСредств = КатегорияВзноса;
			Если КатегорияВзноса = Перечисления.уп_КатегорияВзносаОПС.ВозвратСредствОтСледущегоСтраховщика Тогда
				Движение.ТипСредств = ТекСтрокаДоговорыОПС.ТипСредств;
				Если ТекСтрокаДоговорыОПС.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.ВзносЗаПериодПослеПередачи
					или ТекСтрокаДоговорыОПС.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.ИДЗаПериодПослеПередачи Тогда
					Движение.КонПериода = КонецГода(Дата(ТекСтрокаДоговорыОПС.ГодИД,1,1));
				КонецЕсли;	
			КонецЕсли;	
			
			//номинал
			Если РегистрироватьНоминал Тогда
				Если ТекСтрокаДоговорыОПС.ТипСуммы.Родитель = Справочники.уп_ТипыСуммОПС.Взносы Тогда
					Движение = Движения.уп_НоминалВзносовОПС.Добавить();
					Движение.Период = Дата;
					Движение.Организация = Организация;
					Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
					Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
					Движение.Сумма = ТекСтрокаДоговорыОПС.Сумма;
				КонецЕсли;	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДатаВступленияВСистемуГарантирования) И Дата>=ДатаВступленияВСистемуГарантирования Тогда
				Если ТекСтрокаДоговорыОПС.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод
					или (ТекСтрокаДоговорыОПС.Состояние = Перечисления.уп_СостоянияДоговоровОПС.Закрыт
					И ТекСтрокаДоговорыОПС.ПричинаЗакрытия<>Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПереходДосрочный)Тогда
					//текущую сумму гарантирования не регистрируем
				ИначеЕсли ТекСтрокаДоговорыОПС.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ЕдиновременнаяВыплата Тогда
					//была ЕВ,текущую сумму гарантирования не регистрируем
				Иначе
					Если КатегорияВзноса<>Перечисления.уп_КатегорияВзносаОПС.ВозвратСредствОтСледущегоСтраховщика Тогда
						Движение = Движения.уп_ТекущаяСуммаГарантирования.ДобавитьПриход();
						Движение.Период = Дата;
						Движение.Организация = Организация;
						Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
						Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
						Движение.Сумма = ТекСтрокаДоговорыОПС.Сумма;
					Иначе
						Если ТекСтрокаДоговорыОПС.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.РанееПереданныеСуммы
							или ТекСтрокаДоговорыОПС.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.ВзносЗаПериодПослеПередачи
							или (ТекСтрокаДоговорыОПС.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.ИДЗаПериодПослеПередачи
							и КонецДня(ТекСтрокаДоговорыОПС.ДатаФиксации)>=КонецДня(Дата(ТекСтрокаДоговорыОПС.ГодИД,12,31))) Тогда
							Движение = Движения.уп_ТекущаяСуммаГарантирования.ДобавитьПриход();
							Движение.Период = Дата;
							Движение.Организация = Организация;
							Движение.ДоговорОПС = ТекСтрокаДоговорыОПС.ДоговорОПС;
							Движение.ТипСуммы = ТекСтрокаДоговорыОПС.ТипСуммы;
							Движение.Сумма = ТекСтрокаДоговорыОПС.Сумма;
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗачитыватьВиртуальныеВзносы Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС,
			|	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы,
			|	СУММА(уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ДоговорыПоступления
			|ИЗ
			|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
			|ГДЕ
			|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС,
			|	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ВиртуальныеПоступленияОПСОстатки.ДоговорОПС,
			|	уп_ВиртуальныеПоступленияОПСОстатки.ДатаКонцаПериода,
			|	уп_ВиртуальныеПоступленияОПСОстатки.Организация,
			|	уп_ВиртуальныеПоступленияОПСОстатки.ТипСуммы,
			|	СУММА(уп_ВиртуальныеПоступленияОПСОстатки.СуммаОстаток) КАК СуммаОстаток
			|ПОМЕСТИТЬ Остатки
			|ИЗ
			|	РегистрНакопления.уп_ВиртуальныеПоступленияОПС.Остатки(
			|			&ДатаДок,
			|			Организация = &Организация
			|				И ДоговорОПС В (&СписокДоговоровОПС)) КАК уп_ВиртуальныеПоступленияОПСОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	уп_ВиртуальныеПоступленияОПСОстатки.ДоговорОПС,
			|	уп_ВиртуальныеПоступленияОПСОстатки.Организация,
			|	уп_ВиртуальныеПоступленияОПСОстатки.ДатаКонцаПериода,
			|	уп_ВиртуальныеПоступленияОПСОстатки.ТипСуммы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДоговорыПоступления.ДоговорОПС КАК ДоговорОПС,
			|	ДоговорыПоступления.ТипСуммы КАК ТипСуммы,
			|	ДоговорыПоступления.Сумма КАК Сумма,
			|	Остатки.ДатаКонцаПериода КАК ДатаКонцаПериода,
			|	ЕстьNULL(Остатки.СуммаОстаток,0) КАК СуммаОстаток
			|ИЗ
			|	ДоговорыПоступления КАК ДоговорыПоступления
			|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
			|		ПО ДоговорыПоступления.ДоговорОПС = Остатки.ДоговорОПС
			|			И ДоговорыПоступления.ТипСуммы = Остатки.ТипСуммы
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДоговорОПС,
			|	ДатаКонцаПериода
			|ИТОГИ
			|	МАКСИМУМ(Сумма),
			|	СУММА(СуммаОстаток)
			|ПО
			|	ДоговорОПС,
			|	ТипСуммы";
			
			Запрос.УстановитьПараметр("ДатаДок", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("СписокДоговоровОПС", СписокДоговоров);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Результат = Запрос.Выполнить();
			
			ВыборкаДоговорОПС = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаДоговорОПС.Следующий() Цикл
				Если ВыборкаДоговорОПС.Сумма <> 0 И ВыборкаДоговорОПС.СуммаОстаток <> 0 Тогда
					ВыборкаТипСуммы = ВыборкаДоговорОПС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаТипСуммы.Следующий() Цикл
						СуммаВзноса = ВыборкаТипСуммы.Сумма;
						
						ВыборкаДетальныеЗаписи = ВыборкаТипСуммы.Выбрать();
						
						Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
							СуммаКСписанию = Мин(ВыборкаДетальныеЗаписи.СуммаОстаток,СуммаВзноса);
							
							Если СуммаКСписанию<>0 Тогда
								Движение = Движения.уп_ВиртуальныеПоступленияОПС.Добавить();
								Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
								Движение.Период = Дата;
								Движение.Организация = Организация;
								Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
								Движение.ТипСуммы = ВыборкаДетальныеЗаписи.ТипСуммы;
								Движение.Сумма = СуммаКСписанию;
								Движение.ДатаКонцаПериода = ВыборкаДетальныеЗаписи.ДатаКонцаПериода;
							КонецЕсли;
							
							СуммаВзноса = СуммаВзноса-СуммаКСписанию;
							Если СуммаКСписанию = 0 Тогда
								Прервать;
							КонецЕсли;	
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		//ДатаВступленияВСистемуГарантирования = Константы.уп_ДатаВступленияВСистемуГарантирования.Получить();
		//
		//Если ЗначениеЗаполнено(ДатаВступленияВСистемуГарантирования) И Дата>=ДатаВступленияВСистемуГарантирования Тогда
		//	ТЗ = ДоговорыОПС.Выгрузить();
		//	ТЗ.Колонки.Добавить("Организация");
		//	ТЗ.Колонки.Добавить("ТипРезерва");
		//	ТЗ.Колонки.Добавить("Период");
		//	ТЗ.ЗаполнитьЗначения(Организация, "Организация");
		//	ТЗ.ЗаполнитьЗначения(Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений, "ТипРезерва");
		//	ТЗ.ЗаполнитьЗначения(Дата, "Период");
		//	Движения.уп_ТекущаяСуммаГарантирования.Загрузить(ТЗ);
		//КонецЕсли;
		
		//даты первого взноса от предыдущего страховщика
		Если КатегорияВзноса = Перечисления.уп_КатегорияВзносаОПС.ПереводСредствОтПредыдущегоСтраховщика Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	уп_ПоступленияОПС.ДоговорОПС КАК ДоговорОПС,
			|	МИНИМУМ(уп_ПоступленияОПС.Период) КАК Период,
			|	уп_ПоступленияОПС.Плательщик КАК Плательщик
			|ПОМЕСТИТЬ ПервыеПоступления
			|ИЗ
			|	РегистрНакопления.уп_ПоступленияОПС КАК уп_ПоступленияОПС
			|ГДЕ
			|	уп_ПоступленияОПС.Период <= &ДатаДок
			|	И уп_ПоступленияОПС.ДоговорОПС В(&СписокДоговоровОПС)
			|	И уп_ПоступленияОПС.Регистратор <> &Ссылка
			|	И уп_ПоступленияОПС.Плательщик = &Плательщик
			|
			|СГРУППИРОВАТЬ ПО
			|	уп_ПоступленияОПС.ДоговорОПС,
			|	уп_ПоступленияОПС.Плательщик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ДоговорОПС КАК ДоговорОПС,
			|	уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ПредыдущийСтраховщик КАК ПредыдущийСтраховщик
			|ПОМЕСТИТЬ ПредыдущиеСтраховщики
			|ИЗ
			|	РегистрСведений.уп_ПредыдущиеСтраховщикиПоДоговорамОПС.СрезПоследних(&ДатаДок, ДоговорОПС В (&СписокДоговоровОПС)) КАК уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних
			|ГДЕ
			|	уп_ПредыдущиеСтраховщикиПоДоговорамОПССрезПоследних.ПредыдущийСтраховщик = &Плательщик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПредыдущиеСтраховщики.ДоговорОПС КАК ДоговорОПС,
			|	&ДатаДок КАК ДатаДок,
			|	ПервыеПоступления.Период КАК Период
			|ПОМЕСТИТЬ ПолнаяТаблицаСПервымВзносом
			|ИЗ
			|	ПредыдущиеСтраховщики КАК ПредыдущиеСтраховщики
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПервыеПоступления КАК ПервыеПоступления
			|		ПО ПредыдущиеСтраховщики.ДоговорОПС = ПервыеПоступления.ДоговорОПС
			|			И ПредыдущиеСтраховщики.ПредыдущийСтраховщик = ПервыеПоступления.Плательщик
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПолнаяТаблицаСПервымВзносом.ДоговорОПС КАК ДоговорОПС,
			|	ПолнаяТаблицаСПервымВзносом.ДатаДок КАК ДатаВзноса,
			|	ИСТИНА КАК Активность,
			|	&Плательщик КАК Страховщик
			|ПОМЕСТИТЬ ТаблицаДляЗагрузки
			|ИЗ
			|	ПолнаяТаблицаСПервымВзносом КАК ПолнаяТаблицаСПервымВзносом
			|ГДЕ
			|	ПолнаяТаблицаСПервымВзносом.Период ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	уп_ДатыПервогоВзносаПредыдущегоСтраховщика.ДоговорОПС КАК ДоговорОПС,
			|	уп_ДатыПервогоВзносаПредыдущегоСтраховщика.ДатаВзноса КАК ДатаВзноса,
			|	уп_ДатыПервогоВзносаПредыдущегоСтраховщика.Страховщик КАК Страховщик
			|ПОМЕСТИТЬ УжеЗарегистрированныйПервыйВзнос
			|ИЗ
			|	РегистрСведений.уп_ДатыПервогоВзносаПредыдущегоСтраховщика КАК уп_ДатыПервогоВзносаПредыдущегоСтраховщика
			|ГДЕ
			|	уп_ДатыПервогоВзносаПредыдущегоСтраховщика.ДоговорОПС В
			|			(ВЫБРАТЬ
			|				ТаблицаДляЗагрузки.ДоговорОПС
			|			ИЗ
			|				ТаблицаДляЗагрузки)
			|	И уп_ДатыПервогоВзносаПредыдущегоСтраховщика.Регистратор <> &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаДляЗагрузки.ДоговорОПС КАК ДоговорОПС,
			|	ТаблицаДляЗагрузки.ДатаВзноса КАК ДатаВзноса,
			|	ТаблицаДляЗагрузки.Активность КАК Активность,
			|	ТаблицаДляЗагрузки.Страховщик КАК Страховщик,
			|	ВЫБОР
			|		КОГДА УжеЗарегистрированныйПервыйВзнос.ДатаВзноса ЕСТЬ NULL
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК РегистрироватьПервыйВзнос,
			|	УжеЗарегистрированныйПервыйВзнос.ДатаВзноса КАК ДатаВзносаЗарегистрированная,
			|	УжеЗарегистрированныйПервыйВзнос.Страховщик КАК СтраховщикЗарегистрированный
			|ПОМЕСТИТЬ ТаблицаСОшибками
			|ИЗ
			|	ТаблицаДляЗагрузки КАК ТаблицаДляЗагрузки
			|		ЛЕВОЕ СОЕДИНЕНИЕ УжеЗарегистрированныйПервыйВзнос КАК УжеЗарегистрированныйПервыйВзнос
			|		ПО ТаблицаДляЗагрузки.ДоговорОПС = УжеЗарегистрированныйПервыйВзнос.ДоговорОПС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаСОшибками.ДоговорОПС КАК ДоговорОПС,
			|	ТаблицаСОшибками.ДатаВзноса КАК ДатаВзноса,
			|	ТаблицаСОшибками.Активность КАК Активность,
			|	ТаблицаСОшибками.Страховщик КАК Страховщик,
			|	ТаблицаСОшибками.ДатаВзносаЗарегистрированная КАК ДатаВзносаЗарегистрированная,
			|	ТаблицаСОшибками.СтраховщикЗарегистрированный КАК СтраховщикЗарегистрированный
			|ИЗ
			|	ТаблицаСОшибками КАК ТаблицаСОшибками
			|ГДЕ
			|	ТаблицаСОшибками.РегистрироватьПервыйВзнос = ИСТИНА
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаСОшибками.ДоговорОПС КАК ДоговорОПС,
			|	ТаблицаСОшибками.ДатаВзносаЗарегистрированная КАК ДатаВзносаЗарегистрированная,
			|	ТаблицаСОшибками.СтраховщикЗарегистрированный КАК СтраховщикЗарегистрированный
			|ИЗ
			|	ТаблицаСОшибками КАК ТаблицаСОшибками
			|ГДЕ
			|	ТаблицаСОшибками.РегистрироватьПервыйВзнос = ЛОЖЬ";
		
			Запрос.УстановитьПараметр("ДатаДок", Дата);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("Плательщик", ПенсионныйФонд);
			Запрос.УстановитьПараметр("СписокДоговоровОПС", СписокДоговоров);
			Результат = Запрос.ВыполнитьПакет();
		
			//еще загрузть результаты в регистр
			ТЗ = Результат[6].Выгрузить();
			#Вставка
			// +ХМНПФ 13.03.2016 ШадринАВ // ОбработкаПроведения() // №859
			// нужно убрать из ТЗ договоры, уже имеющиеся в РС.уп_ДатыПервогоВзносаПредыдущегоСтраховщика, пока разработчики не исправят этот механизм
			ТЗ = ХМ_УбратьДублиДоговоров(ТЗ);
			// -ХМНПФ 13.03.2016 ШадринАВ // ОбработкаПроведения() // №859
			#КонецВставки
		
			Движения.уп_ДатыПервогоВзносаПредыдущегоСтраховщика.Загрузить(ТЗ);
			
			ТЗОшибок = Результат[7].Выгрузить();
			Для Каждого стр из ТЗОшибок Цикл
				Если НачалоДня(стр.ДатаВзносаЗарегистрированная)> НачалоДня(Дата) Тогда
					ТекстСообщения = "Для договора:" + стр.ДоговорОПС+ " первый взнос уже зарегистрирован от "+стр.ДатаВзносаЗарегистрированная + ". Для корректного отражения в системе, документы рекомендуется проводить последовательно.";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,);
					МассивСообщений.Добавить(ТекстСообщения);
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	КонецЕсли;
	#Вставка
	// +ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаПроведения() // №1376	
	ХМ_ПеревестиВНакопительныйПериод(Движения);
	// -ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаПроведения() // №1376
	#КонецВставки
	ДополнительныеСвойства.Вставить("МассивСообщений", МассивСообщений);
	// записываем движения регистров
	Движения.уп_СостоянияДоговораОПС.Записать();
	Движения.уп_РезервыОПС.Записать();
	Движения.уп_ПоступленияОПС.Записать();
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаУдаленияПроведения")
Процедура РасшОбщ_ОбработкаУдаленияПроведения(Отказ)
		
	ОбщегоНазначенияБПВызовСервера.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, РучнаяКорректировка);
	#Вставка
	// +ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаУдаленияПроведения() // №1376
	ХМ_ПометкаНаУдалениеОперации();
	// -ХМНПФ 05.03.2018 КлименкоМИ // ОбработкаУдаленияПроведения() // №1376
	#КонецВставки

КонецПроцедуры

&ИзменениеИКонтроль("ПередЗаписью")
Процедура РасшОбщ_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЭтоНераспределенныйПлатеж Тогда
		СуммаДокумента = ДоговорыОПС.Итог("Сумма");
		//Если КатегорияВзносов = Перечисления.уп_КатегорияВзносаОПС.ВозвратСредствОтСледущегоСтраховщика Тогда
		//	Для Каждого стр из ДоговорыОПС Цикл
		//		Если НЕ ЗначениеЗаполнено(стр.ТипСредств) Тогда
		//			стр.ТипСредств = Перечисления.уп_ТипыВозвращаемыхСредств.РанееПереданныеСуммы;
		//		КонецЕсли;	
		//	КонецЦикла;	
		//КонецЕсли;	
	КонецЕсли;
	#Вставка
	// +ХМНПФ 02.02.2016 ШадринАВ // ПередЗаписью() // №169
	Если НЕ ЗначениеЗаполнено(ХМ_НомерРеестра) Тогда
		ХМ_НомерРеестра = "051"
	КонецЕсли;
	// -ХМНПФ 02.02.2016 ШадринАВ // ПередЗаписью() // №169
	#КонецВставки
	Если НЕ ЗначениеЗаполнено(уп_ВидДеятельности) Тогда
		уп_ВидДеятельности = уп_ОсновнаяПоставкаСервер.ВидДеятельностиПоТипу(Перечисления.уп_ВидыДеятельности.ОПС);
	КонецЕсли;	
КонецПроцедуры

&ИзменениеИКонтроль("ПроверкаКорректности")
Процедура РасшОбщ_ПроверкаКорректности(Отказ, Заголовок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	уп_РегистрацияПоступленийОПСДоговорыОПС.НомерСтроки,
	               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС,
	               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы
	               |ИЗ
	               |	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
	               |ГДЕ
	               |	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
	               |	И уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы = &ТипСуммы";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТипСуммы", Справочники.уп_ТипыСуммОПС.ПустаяСсылка());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Отказ = Истина;
		Сообщить("По строке №"+Выборка.НомерСтроки+" не указан тип суммы.");
	КонецЦикла;
	
	//проверяем период
	
	//Если НЕ ЭтоПереводСредствОтПредыдущегоСтраховщика Тогда
	Если КатегорияВзноса <> Перечисления.уп_КатегорияВзносаОПС.ПереводСредствОтПредыдущегоСтраховщика 
		И КатегорияВзноса <> Перечисления.уп_КатегорияВзносаОПС.ВозвратСредствОтСледущегоСтраховщика Тогда 
		Если Не ЗначениеЗаполнено(ДатаКонцаПериодаВзносов) Тогда //и ПенсионныйФонд = Константы.уп_ПенсионныйФондРФ.Получить() Тогда
			//проверяем, есть ли там ДСВ или господдержка
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС) КАК ДоговорОПС,
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы
			               |ИЗ
			               |	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
			               |ГДЕ
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
			               |	И уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы = &ТипСуммы
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("ТипСуммы", Справочники.уп_ТипыСуммОПС.ДопВзносы);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.ДоговорОПС > 0 Тогда
					Отказ = Истина;
					Сообщить("В документе присутствуют суммы ДСВ и не указан период взносов. Корректный расчет доплаты к ЕВ по такому документу невозможен.");
				КонецЕсли;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС) КАК ДоговорОПС,
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы
			               |ИЗ
			               |	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
			               |ГДЕ
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
			               |	И уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы = &ТипСуммы
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("ТипСуммы", Справочники.уп_ТипыСуммОПС.ГосПоддержка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.ДоговорОПС > 0 Тогда
					Отказ = Истина;
					Сообщить("В документе присутствуют суммы господдержки и не указан период взносов. Корректный расчет доплаты к ЕВ по такому документу невозможен.");
				КонецЕсли;
			КонецЕсли;
			
			//на мат капитал тоже поставим
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС) КАК ДоговорОПС,
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы
			               |ИЗ
			               |	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
			               |ГДЕ
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
			               |	И уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы = &ТипСуммы
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	уп_РегистрацияПоступленийОПСДоговорыОПС.ТипСуммы";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("ТипСуммы", Справочники.уп_ТипыСуммОПС.МатКапитал);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Выборка.ДоговорОПС > 0 Тогда
					Отказ = Истина;
					Сообщить("В документе присутствуют суммы материнского капитала и не указан период взносов. Корректный расчет доплаты к ЕВ по такому документу невозможен.");
				КонецЕсли;
			КонецЕсли;			
		КонецЕсли;	
	КонецЕсли;
	
	//проверяем суммы по строке
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма - уп_РегистрацияПоступленийОПСДоговорыОПС.СуммаКомпенсации - уп_РегистрацияПоступленийОПСДоговорыОПС.СуммаВосполнения КАК РазницаПоСтроке,
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Сумма КАК Сумма,
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|ГДЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.РазницаПоСтроке < 0 И ВыборкаДетальныеЗаписи.Сумма>=0 Тогда
			#Удаление
			Отказ = Истина;
			#КонецУдаления
			#Вставка
			// +ХМНПФ 26.12.2018 ШадринАВ // ПроверкаКорректности() // №1955
			#КонецВставки
			Сообщить("По строке "+ВыборкаДетальныеЗаписи.НомерСтроки+" Суммы возмещения и компенсации превышают общую сумму взноса.");
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры	

// ХМНПФ 13.03.2016 ШадринАВ // Новая ХМ_УбратьДублиДоговоров() // №859
Функция ХМ_УбратьДублиДоговоров(ТЗ)
	// нужно убрать из ТЗ договоры, уже имеющиеся в РС.уп_ДатыПервогоВзносаПредыдущегоСтраховщика
	// пока разработчики не исправят этот механизм
	Запрос = Новый Запрос;
	Запрос.Текст ="
	|ВЫБРАТЬ
	|	ТЗ.ДоговорОПС,
	|	ТЗ.ДатаВзноса,
	|	ТЗ.Активность,
	|	ТЗ.Страховщик
	|ПОМЕСТИТЬ ТЗ
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыПервогоВзносаПредСтраховщика.ДоговорОПС
	|ПОМЕСТИТЬ ДублиДоговоровОПС
	|ИЗ
	|	РегистрСведений.уп_ДатыПервогоВзносаПредыдущегоСтраховщика КАК ДатыПервогоВзносаПредСтраховщика
	|ГДЕ
	|	НЕ ДатыПервогоВзносаПредСтраховщика.Регистратор = &Ссылка
	|	И ДатыПервогоВзносаПредСтраховщика.ДоговорОПС В
	|			(ВЫБРАТЬ
	|				ТЗ.ДоговорОПС
	|			ИЗ
	|				ТЗ КАК ТЗ)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗ.ДоговорОПС,
	|	ТЗ.ДатаВзноса,
	|	ТЗ.Активность,
	|	ТЗ.Страховщик,
	|	ДублиДоговоровОПС.ДоговорОПС КАК ДубльДоговора
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТЗ КАК ТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДублиДоговоровОПС КАК ДублиДоговоровОПС
	|		ПО ТЗ.ДоговорОПС = ДублиДоговоровОПС.ДоговорОПС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Результат.ДоговорОПС,
	|	Результат.ДатаВзноса,
	|	Результат.Активность,
	|	Результат.Страховщик
	|ИЗ
	|	Результат КАК Результат
	|ГДЕ
	|	Результат.ДубльДоговора ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ТЗРезультат = Запрос.Выполнить().Выгрузить();
	Возврат ТЗРезультат;
КонецФункции // ХМ_УбратьДублиДоговоров()

// ХМНПФ 05.03.2018 КлименкоМИ // Новая ХМ_ПеревестиВНакопительныйПериод() // №1376
Процедура ХМ_ПеревестиВНакопительныйПериод(Движения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОперацияБух.Ссылка КАК Ссылка,
	|	ОперацияБух.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ОперацияБух КАК ОперацияБух
	|ГДЕ
	|	ОперацияБух.ХМ_РегистрацияПоступленийОПС = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() <> 0 Тогда
		ВыборкаДетальныеЗаписи.Следующий();
		ДокОперация = ВыборкаДетальныеЗаписи.Ссылка;
		Если ВыборкаДетальныеЗаписи.ПометкаУдаления Тогда
			ДокОперацияОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ДокОперацияОбъект.ПометкаУдаления = Ложь;
			СтрокиТЧ = ДокОперацияОбъект.ТаблицаРегистровСведений.НайтиСтроки(Новый Структура("Имя", "уп_ПричиныЗакрытияДоговоровОПС"));
			Если СтрокиТЧ.Количество() = 0 Тогда
				стрТЧ = ДокОперацияОбъект.ТаблицаРегистровСведений.Добавить();
				стрТЧ.Имя = "уп_ПричиныЗакрытияДоговоровОПС";
			КонецЕсли;
			СтрокиТЧ = ДокОперацияОбъект.ТаблицаРегистровСведений.НайтиСтроки(Новый Структура("Имя", "уп_СледующиеСтраховщикиПоДоговорамОПС"));
			Если СтрокиТЧ.Количество() = 0 Тогда
				стрТЧ = ДокОперацияОбъект.ТаблицаРегистровСведений.Добавить();
				стрТЧ.Имя = "уп_СледующиеСтраховщикиПоДоговорамОПС";
			КонецЕсли;
			ДокОперацияОбъект.Записать();
		КонецЕсли;
	Иначе
		ДокОперация = Неопределено; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних.СледующийСтраховщик КАК СледующийСтраховщик,
	|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС,
	|	уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ПричинаЗакрытия КАК ПричинаЗакрытия
	|ИЗ
	|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_ПричиныЗакрытияДоговоровОПС.СрезПоследних(&ДатаДокумента, ПричинаЗакрытия = ЗНАЧЕНИЕ(Перечисление.уп_ПричиныЗакрытияДоговораОПС.Досрочное)) КАК уп_ПричиныЗакрытияДоговоровОПССрезПоследних
	|		ПО уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС = уп_ПричиныЗакрытияДоговоровОПССрезПоследних.ДоговорОПС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СледующиеСтраховщикиПоДоговорамОПС.СрезПоследних(&ДатаДокумента, СледующийСтраховщик = &ПФ) КАК уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних
	|		ПО уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС = уп_СледующиеСтраховщикиПоДоговорамОПССрезПоследних.ДоговорОПС
	|ГДЕ
	|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("ПФ", ПенсионныйФонд);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() <> 0 Тогда
		Если ДокОперация = Неопределено тогда			
			ДокОперация = Документы.ОперацияБух.СоздатьДокумент();
			ДокОперация.Дата = Дата;
			стрТЧ = ДокОперация.ТаблицаРегистровСведений.Добавить();
			стрТЧ.Имя = "уп_ПричиныЗакрытияДоговоровОПС";
			стрТЧ = ДокОперация.ТаблицаРегистровСведений.Добавить();
			стрТЧ.Имя = "уп_СледующиеСтраховщикиПоДоговорамОПС";
			ДокОперация.Содержание = "Введен на основании документа "+Ссылка;  
			ДокОперация.ХМ_РегистрацияПоступленийОПС = Ссылка;
			ДокОперация.Записать()			
		КонецЕсли;
		РегистрПричиныЗакрытия = РегистрыСведений.уп_ПричиныЗакрытияДоговоровОПС.СоздатьНаборЗаписей();
		РегистрПричиныЗакрытия.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
		
		РегистрСлСтраховщики = РегистрыСведений.уп_СледующиеСтраховщикиПоДоговорамОПС.СоздатьНаборЗаписей();
		РегистрСлСтраховщики.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
		
		ЗапросОснЧасть = Новый Запрос;
		ЗапросОснЧасть.Текст = 
		"ВЫБРАТЬ
		|	уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Состояние КАК Состояние,
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС
		|ИЗ
		|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС КАК уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС
		|		ПО уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС = уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.ДоговорОПС
		|ГДЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &ссылка";
		
		ЗапросОснЧасть.УстановитьПараметр("ссылка", Ссылка);
		
		РезультатОснЧасть = ЗапросОснЧасть.Выполнить();
		
		ТЗОснЧасть = РезультатОснЧасть.Выгрузить();
		
		ЗапросДопЧасть = Новый Запрос;
		ЗапросДопЧасть.Текст = 
		"ВЫБРАТЬ
		|	уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Состояние КАК Состояние,
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС КАК ДоговорОПС
		|ИЗ
		|	Документ.уп_РегистрацияПоступленийОПС.ДоговорыОПС КАК уп_РегистрацияПоступленийОПСДоговорыОПС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС КАК уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС
		|		ПО уп_РегистрацияПоступленийОПСДоговорыОПС.ДоговорОПС = уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.ДоговорОПС
		|ГДЕ
		|	уп_РегистрацияПоступленийОПСДоговорыОПС.Ссылка = &Ссылка";
		
		ЗапросДопЧасть.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатДопЧасть = ЗапросДопЧасть.Выполнить();
		
		ТЗДопЧасть = РезультатДопЧасть.Выгрузить();
			
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
			Движение.ДатаУстановкиСостояния = Дата;
			Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.НакопительныйПериод;			
			
			НайденнаяОснЧасть = ТЗОснЧасть.Найти(ВыборкаДетальныеЗаписи.ДоговорОПС, "ДоговорОПС");
			Если НайденнаяОснЧасть <> Неопределено Тогда
				Движение = Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.ДатаУстановкиСостояния = Дата;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НакопительныйПериод;
			КонецЕсли;			
			
			НайденнаяДопЧасть = ТЗДопЧасть.Найти(ВыборкаДетальныеЗаписи.ДоговорОПС, "ДоговорОПС");
			Если НайденнаяДопЧасть <> Неопределено Тогда				
				Движение = Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Добавить();
				Движение.Период = Дата;
				Движение.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
				Движение.ДатаУстановкиСостояния = Дата;
				Движение.Состояние = Перечисления.уп_СостояниеЧастиПенсионногоСчетаОПС.НакопительныйПериод;
			КонецЕсли;		
			
			НовЗапПричиныЗакрытия = РегистрПричиныЗакрытия.Добавить();
			НовЗапПричиныЗакрытия.Период = Дата;
			НовЗапПричиныЗакрытия.Регистратор = ДокОперация.Ссылка;
			НовЗапПричиныЗакрытия.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
			НовЗапПричиныЗакрытия.ПричинаЗакрытия = Перечисления.уп_ПричиныЗакрытияДоговораОПС.ПустаяСсылка();
			РегистрПричиныЗакрытия.Записать();
			
			НовЗапСлСтраховщики = РегистрСлСтраховщики.Добавить();
			НовЗапСлСтраховщики.Период = Дата;
			НовЗапСлСтраховщики.Регистратор = ДокОперация.Ссылка;
			НовЗапСлСтраховщики.ДоговорОПС = ВыборкаДетальныеЗаписи.ДоговорОПС;
			НовЗапСлСтраховщики.СледующийСтраховщик = Справочники.Контрагенты.ПустаяСсылка();
			РегистрСлСтраховщики.Записать();
		КонецЦикла;
		Движения.уп_СостояниеОсновнойЧастиПенсионногоСчетаОПС.Записать();
		Движения.уп_СостояниеДополнительнойЧастиПенсионногоСчетаОПС.Записать();
	КонецЕсли;
	
КонецПроцедуры

// ХМНПФ 05.03.2018 КлименкоМИ // Новая ХМ_ПометкаНаУдалениеОперации() // №1376
Процедура ХМ_ПометкаНаУдалениеОперации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОперацияБух.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОперацияБух КАК ОперацияБух
	|ГДЕ
	|	ОперацияБух.ХМ_РегистрацияПоступленийОПС = &Ссылка
	|	И ОперацияБух.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Количество() <> 0 Тогда    
		ВыборкаДетальныеЗаписи.Следующий();
		ДокОперация = ВыборкаДетальныеЗаписи.Ссылка.Получитьобъект();
		ДокОперация.ПометкаУдаления = Истина;
		ДокОперация.Записать();
		
		РегистрПричиныЗакрытия = РегистрыСведений.уп_ПричиныЗакрытияДоговоровОПС.СоздатьНаборЗаписей();
		РегистрПричиныЗакрытия.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
		
		РегистрСлСтраховщики = РегистрыСведений.уп_СледующиеСтраховщикиПоДоговорамОПС.СоздатьНаборЗаписей();
		РегистрСлСтраховщики.Отбор.Регистратор.Значение = ДокОперация.Ссылка;	
		
		РегистрПричиныЗакрытия.Очистить();
		РегистрСлСтраховщики.Очистить();
		
		РегистрПричиныЗакрытия.Записать();
		РегистрСлСтраховщики.Записать();
	КонецЕсли;	
	
КонецПроцедуры

