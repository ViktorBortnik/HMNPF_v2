
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 28.01.2016 ШадринАВ // Новая ХМ_ВывестиИсполнителя() // №155
	// ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	// ХМНПФ 21.09.2020 ШадринАВ // Изменен макет ПФ_MXL_Приложение1_2015: // №2899
	//	- в области Таблица1 добавлен параметр "СуммаНакопленийПрописью"
	// ХМНПФ 29.01.2016 ШадринАВ // длина номера увеличена с 9 до 12 знаков // №164
	// ХМНПФ 14.05.2016 ШадринАВ // Увеличен шрифт в макете Приложение1_2015  // №348
	
	// ХМНПФ 09.11.2017 ШадринАВ // ПечатьПриложения1() // №1227 // не актуально с релиза 4.0.36.1
КонецПроцедуры

&ИзменениеИКонтроль("ПечатьПриложения1")
Функция РасшОбщ_ПечатьПриложения1()

	ТабДок = Новый ТабличныйДокумент;
	Если Дата<'20150101' Тогда
		Макет = ПолучитьМакет("Приложение1");
	Иначе
		Макет = ПолучитьМакет("Приложение1_2015");
	КонецЕсли;	
	// Заголовок

	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
	ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Число = Формат(Дата, "ДФ=дд");
	Область.Параметры.Месяц = Сред(Формат(Дата, "ДЛФ=дд"), 3, СтрДлина(Строка(Формат(Дата, "ДЛФ=дд")))-10);
	Область.Параметры.Год = Формат(Дата, "ДФ=гггг");
	Область.Параметры.НаименованиеФонда = ПредставлениеОрганизации;
	Область.Параметры.НомерРешения = Номер;
	
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;                
	Отчество = ФИОФизЛица.Отчество;
	#Удаление
	Область.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
	#КонецУдаления
	#Вставка
	// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	Область.Параметры.ФИОЗастрахованного = уп_ОбщегоНазначенияУчетРезервов.СклонятьФИО(Фамилия+" "+Имя+" "+Отчество,Лев(Строка(ДоговорОПС.Участник.уп_ФизЛицо.Пол),1),"Р");
	// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	#КонецВставки
	Область.Параметры.СтраховойНомер = уп_ОПС.ПреобразоватьНомерПФР(ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР);
	Область.Параметры.Картинка2 = "V";
	ТабДок.Вывести(Область);
	#Удаление
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	#КонецУдаления
	#Вставка
	// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	#КонецВставки
	
	СуммаНакоплений = 0;
	СуммаСрочныхВыплат = 0;
	
	СуммаНакопленийВ = 0;
	СуммаСрочныхВыплатВ = 0;
	
	Для Каждого стр из РасшифровкаСуммы Цикл
		Если стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_НЧТП 
			ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН 
			ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений Тогда
			СуммаНакоплений = СуммаНакоплений+стр.Сумма;
			СуммаНакопленийВ = СуммаНакопленийВ+стр.СуммаКВыплате;

		#Вставка
		// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
		ИначеЕсли стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты Тогда
			Если стр.ТипСуммы<>Справочники.уп_ТипыСуммОПС.МатКапитал И стр.ТипСуммы<>Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
				СуммаСрочныхВыплат = СуммаСрочныхВыплат+стр.Сумма;
				СуммаСрочныхВыплатВ = СуммаСрочныхВыплатВ+стр.СуммаКВыплате;
			КонецЕсли;	
		// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
		#КонецВставки
		ИначеЕсли стр.ТипСуммы =  Справочники.уп_ТипыСуммОПС.РезервОПС_СрочныхВыплат 
			ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРСВ
			ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты Тогда
			СуммаСрочныхВыплат = СуммаСрочныхВыплат+стр.Сумма;
			СуммаСрочныхВыплатВ = СуммаСрочныхВыплатВ+стр.СуммаКВыплате;
		КонецЕсли;	
	КонецЦикла;
	
	ОбластьТаблица1 = Макет.ПолучитьОбласть("Таблица1");
	ОбластьТаблица1.Параметры.СуммаНакоплений = СуммаНакоплений;

	#Вставка
	// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	ОбластьТаблица1.Параметры.СуммаНакопленийПрописью = ЧислоПрописью(СуммаНакоплений,"Л=ru_RU","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
	// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	#КонецВставки
	ТабДок.Вывести(ОбластьТаблица1);
	ОбластьСтрока1 = Макет.ПолучитьОбласть("Строка1");
	НомерПП = 0;
	
	Если СуммаНакоплений <> 0 Тогда
		НомерПП = НомерПП+1;
		ОбластьСтрока1.Параметры.НомерПП = НомерПП;
		ФИОПП = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", Правопреемник.уп_ФизЛицо));
		ОбластьСтрока1.Параметры.ФамилияП = ФИОПП.Фамилия;
		ОбластьСтрока1.Параметры.ИмяП = ФИОПП.Имя;
		ОбластьСтрока1.Параметры.ОтчествоП = ФИОПП.Отчество;
		ОбластьСтрока1.Параметры.ДатаЗаявления = ДокументОснование.ДатаРегистрации;
		ОбластьСтрока1.Параметры.НомерЗаявления = ДокументОснование.Номер;
		ОбластьСтрока1.Параметры.РазмерДоли = Доля;
		ОбластьСтрока1.Параметры.Сумма1 = СуммаНакопленийВ;
		
		ТабДок.Вывести(ОбластьСтрока1);
	КонецЕсли;	
	
	ОбластьТаблица2 = Макет.ПолучитьОбласть("Таблица2");
	ОбластьТаблица2.Параметры.СуммаНаСчетеСрочныхВыплат = СуммаСрочныхВыплат;
	ТабДок.Вывести(ОбластьТаблица2);
	ОбластьСтрока2 = Макет.ПолучитьОбласть("Строка2");
	НомерПП = 0;
	
	Если СуммаСрочныхВыплат<>0 Тогда
		НомерПП = НомерПП+1;
		ОбластьСтрока2.Параметры.Заполнить(стр);
		ОбластьСтрока2.Параметры.НомерПП = НомерПП;
		ФИОПП = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", Правопреемник.уп_ФизЛицо));
		ОбластьСтрока2.Параметры.ФамилияП = ФИОПП.Фамилия;
		ОбластьСтрока2.Параметры.ИмяП = ФИОПП.Имя;
		ОбластьСтрока2.Параметры.ОтчествоП = ФИОПП.Отчество;
		ОбластьСтрока2.Параметры.ДатаЗаявления = ДокументОснование.ДатаРегистрации;
		ОбластьСтрока2.Параметры.НомерЗаявления = ДокументОснование.Номер;
		ОбластьСтрока2.Параметры.РазмерДоли = Доля;
		ОбластьСтрока2.Параметры.Сумма2 = СуммаСрочныхВыплатВ;
		
		ТабДок.Вывести(ОбластьСтрока2);
	КонецЕсли;

	
	#Вставка
	// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	#КонецВставки
	Если Дата>='20150101' Тогда
		//суммы МК в резерве быть не должно - он передается в ПФР при передаче средств в резерв
		ОбластьТаблица3 = Макет.ПолучитьОбласть("Таблица3");
		ОбластьТаблица3.Параметры.СуммаМатеринскогоКапитала = 0;
		ТабДок.Вывести(ОбластьТаблица3);
		ОбластьСтрока3 = Макет.ПолучитьОбласть("Строка3");
		НомерПП = 0;
	КонецЕсли;	
	
	Область = Макет.ПолучитьОбласть("Подвал");
	
	СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОВыплатеСредствПенсионныхНакоплений.ПустаяСсылка(),Организация,ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Приложение1",Дата);
	Область.Параметры.ДолжностьОтветственного = СведенияОбОтветственном.Должность;
	Область.Параметры.ФИООтветственного = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+". "+Лев(СведенияОбОтветственном.Отчество,1)+".";
	
	ТабДок.Вывести(Область); 
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	#Вставка
	// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	ХМ_ВывестиИсполнителя(ТабДок);
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложения1() // №155
	#КонецВставки
	ТабДок.АвтоМасштаб = Истина;

	Возврат ТабДок;

КонецФункции // ПечатьПриложения1()

// ХМНПФ 28.01.2016 ШадринАВ // Новая ХМ_ВывестиИсполнителя() // №155
Процедура ХМ_ВывестиИсполнителя(ТабДок)
	ХМ_Отчет = Новый ТабличныйДокумент;
	ХМ_Макет = ПолучитьОбщийМакет("ХМ_Разное");
	ХМ_ОблИсполнитель = ХМ_Макет.ПолучитьОбласть("Исполнитель");
	ХМ_ОблИсполнитель.Параметры.ФИОИсполн = ПараметрыСеанса.ТекущийПользователь.Наименование;
	ТелефонИсполн = ХМ_ФункцииОтчетов.ПолучитьТелефонПользователя();
	ХМ_ОблИсполнитель.Параметры.ТелефонИсполн = ТелефонИсполн.Представление;
	ХМ_Отчет.Вывести(ХМ_ОблИсполнитель);
	ХМ_ФункцииОтчетов.ДобавитьОтчет(ТабДок,ХМ_Отчет);
КонецПроцедуры

&Перед("ОбработкаПроведения")
Процедура ХМОбработкаПроведения(Отказ, РежимПроведения)
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Отказ = Истина;	
	КонецЕсли;
КонецПроцедуры


&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс); 
КонецПроцедуры

