
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 28.01.2016 ШадринАВ // Новая ХМ_ВывестиИсполнителя() // №155
	// ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
	// ХМНПФ 21.09.2020 ШадринАВ // Изменен макет ПФ_MXL_Приложение1_2015: // №2899
	//	- в области Таблица1 добавлен параметр "СуммаНакопленийПрописью"
КонецПроцедуры

// ХМНПФ 28.01.2016 ШадринАВ // Новая ХМ_ВывестиИсполнителя() // №155
Процедура ХМ_ВывестиИсполнителя(ТабДок)
	ХМ_Отчет = Новый ТабличныйДокумент;
	ХМ_Макет = ПолучитьОбщийМакет("ХМ_Разное");
	ХМ_ОблИсполнитель = ХМ_Макет.ПолучитьОбласть("Исполнитель");
	ХМ_ОблИсполнитель.Параметры.ФИОИсполн = ПараметрыСеанса.ТекущийПользователь.Наименование;
	ТелефонИсполн = ХМ_ФункцииОтчетов.ПолучитьТелефонПользователя();
	ХМ_ОблИсполнитель.Параметры.ТелефонИсполн = ТелефонИсполн.Представление;
	ХМ_Отчет.Вывести(ХМ_ОблИсполнитель);
	ХМ_ФункцииОтчетов.ДобавитьОтчет(ТабДок,ХМ_Отчет);
КонецПроцедуры

&ИзменениеИКонтроль("ПечатьПриложение1")
Функция РасшОбщ_ПечатьПриложение1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ИмяПараметровПечати = ПараметрыПечати.ИмяПараметровПечати;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = ИмяПараметровПечати;
	
	//УстановитьМинимальныеПоляПечати(ТабличныйДокумент);
	
	ПервыйДокумент = Истина;
	
	Для Каждого ОбъектПечати Из МассивОбъектов Цикл	
		
		Если ОбъектПечати.ПометкаУдаления Тогда
			Продолжить;	
		КонецЕсли;
		Если ОбъектПечати.ДополнительнаяВыплата Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(ОбъектПечати.Организация, ОбъектПечати.Дата);
		
		ТаблицаДокумента = Новый ТаблицаЗначений;
		ТаблицаДокумента.Колонки.Добавить("Документ");
		ТаблицаДокумента.Колонки.Добавить("Ссылка");
		ТаблицаДокумента.Колонки.Добавить("Организация");
		ТаблицаДокумента.Колонки.Добавить("Руководитель");
		
		СведенияОДокументе = ТаблицаДокумента.Добавить();
		СведенияОДокументе.Документ = ОбъектПечати.Ссылка;
		СведенияОДокументе.Ссылка = ОбъектПечати.Ссылка;
		СведенияОДокументе.Организация = ОбъектПечати.Организация;
		СведенияОДокументе.Руководитель = Руководители.Руководитель;
		
		Дата = ОбъектПечати.Дата;
		
		//Если Дата<'20150101' Тогда
		//	Макет = Документы.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС.ПолучитьМакет("Приложение1");
		//Иначе
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС.ПФ_MXL_Приложение1_2015");
		//КонецЕсли;	
		// Заголовок
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ОбъектПечати.Организация, Дата);
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Число = Формат(Дата, "ДФ=дд");
		Область.Параметры.Месяц = Сред(Формат(Дата, "ДЛФ=дд"), 3, СтрДлина(Строка(Формат(Дата, "ДЛФ=дд")))-10);
		Область.Параметры.Год = Формат(Дата, "ДФ=гггг");
		Область.Параметры.НаименованиеФонда = ПредставлениеОрганизации;
		Область.Параметры.НомерРешения = ОбъектПечати.Номер;
		
		ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ОбъектПечати.ДоговорОПС.Участник.уп_ФизЛицо));
		Фамилия = ФИОФизЛица.Фамилия;
		Имя = ФИОФизЛица.Имя;                
		Отчество = ФИОФизЛица.Отчество;
		#Удаление
		Область.Параметры.ФИОЗастрахованного = Фамилия+" "+Имя+" "+Отчество;
		#КонецУдаления
		#Вставка
		// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		Область.Параметры.ФИОЗастрахованного = уп_ОбщегоНазначенияУчетРезервов.СклонятьФИО(Фамилия+" "+Имя+" "+Отчество,Лев(Строка(ОбъектПечати.ДоговорОПС.Участник.уп_ФизЛицо.Пол),1),"Р");
		// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		#КонецВставки
		Область.Параметры.СтраховойНомер = уп_ОПС.ПреобразоватьНомерПФР(ОбъектПечати.ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР);
		Область.Параметры.Картинка2 = "V";
		ТабличныйДокумент.Вывести(Область);
		
		СуммаНакоплений = 0;
		СуммаСрочныхВыплат = 0;
		
		СуммаНакопленийВ = 0;
		СуммаСрочныхВыплатВ = 0;
		
		Для Каждого стр из ОбъектПечати.РасшифровкаСуммы Цикл
			Если стр.ТипСуммы = Справочники.уп_ТипыСуммОПС.РезервОПС_НЧТП 
				ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРПН 
				ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервПенсионныйНакоплений Тогда
				СуммаНакоплений = СуммаНакоплений+стр.Сумма;
				СуммаНакопленийВ = СуммаНакопленийВ+стр.СуммаКВыплате;

			#Вставка
			// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
			ИначеЕсли стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты Тогда
				Если стр.ТипСуммы<>Справочники.уп_ТипыСуммОПС.МатКапитал И стр.ТипСуммы<>Справочники.уп_ТипыСуммОПС.ИДМатКапитал Тогда
					СуммаСрочныхВыплат = СуммаСрочныхВыплат+стр.Сумма;
					СуммаСрочныхВыплатВ = СуммаСрочныхВыплатВ+стр.СуммаКВыплате;
				КонецЕсли;	
			// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
			#КонецВставки
			ИначеЕсли стр.ТипСуммы =  Справочники.уп_ТипыСуммОПС.РезервОПС_СрочныхВыплат 
				ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервНаследниковРСВ
				ИЛИ стр.ТипРезерва = Перечисления.уп_ТипыРезервовОПС.РезервСрочнойВыплаты Тогда
				СуммаСрочныхВыплат = СуммаСрочныхВыплат+стр.Сумма;
				СуммаСрочныхВыплатВ = СуммаСрочныхВыплатВ+стр.СуммаКВыплате;
			КонецЕсли;	
		КонецЦикла;
		
		ОбластьТаблица1 = Макет.ПолучитьОбласть("Таблица1");
		ОбластьТаблица1.Параметры.СуммаНакоплений = СуммаНакоплений;

		#Вставка
		// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		ОбластьТаблица1.Параметры.СуммаНакопленийПрописью = ЧислоПрописью(СуммаНакоплений,"Л=ru_RU","рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
		// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		#КонецВставки
		ТабличныйДокумент.Вывести(ОбластьТаблица1);
		ОбластьСтрока1 = Макет.ПолучитьОбласть("Строка1");
		НомерПП = 0;
		
		Если СуммаНакоплений <> 0 Тогда
			НомерПП = НомерПП+1;
			ОбластьСтрока1.Параметры.НомерПП = НомерПП;
			ФИОПП = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ОбъектПечати.Правопреемник.уп_ФизЛицо));
			ОбластьСтрока1.Параметры.ФамилияП = ФИОПП.Фамилия;
			ОбластьСтрока1.Параметры.ИмяП = ФИОПП.Имя;
			ОбластьСтрока1.Параметры.ОтчествоП = ФИОПП.Отчество;
			ОбластьСтрока1.Параметры.ДатаЗаявления = ОбъектПечати.ДокументОснование.ДатаРегистрации;
			ОбластьСтрока1.Параметры.НомерЗаявления = ОбъектПечати.ДокументОснование.Номер;
			ОбластьСтрока1.Параметры.РазмерДоли = ОбъектПечати.Доля;
			ОбластьСтрока1.Параметры.Сумма1 = СуммаНакопленийВ;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока1);
		КонецЕсли;	
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ОбластьТаблица2 = Макет.ПолучитьОбласть("Таблица2");
		ОбластьТаблица2.Параметры.СуммаНаСчетеСрочныхВыплат = СуммаСрочныхВыплат;
		ТабличныйДокумент.Вывести(ОбластьТаблица2);
		ОбластьСтрока2 = Макет.ПолучитьОбласть("Строка2");
		НомерПП = 0;
		
		Если СуммаСрочныхВыплат<>0 Тогда
			НомерПП = НомерПП+1;
			ОбластьСтрока2.Параметры.Заполнить(стр);
			ОбластьСтрока2.Параметры.НомерПП = НомерПП;
			ФИОПП = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ОбъектПечати.Правопреемник.уп_ФизЛицо));
			ОбластьСтрока2.Параметры.ФамилияП = ФИОПП.Фамилия;
			ОбластьСтрока2.Параметры.ИмяП = ФИОПП.Имя;
			ОбластьСтрока2.Параметры.ОтчествоП = ФИОПП.Отчество;
			ОбластьСтрока2.Параметры.ДатаЗаявления = ОбъектПечати.ДокументОснование.ДатаРегистрации;
			ОбластьСтрока2.Параметры.НомерЗаявления = ОбъектПечати.ДокументОснование.Номер;
			ОбластьСтрока2.Параметры.РазмерДоли = ОбъектПечати.Доля;
			ОбластьСтрока2.Параметры.Сумма2 = СуммаСрочныхВыплатВ;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока2);
		КонецЕсли;
		
		Если Дата>='20150101' Тогда
			//суммы МК в резерве быть не должно - он передается в ПФР при передаче средств в резерв
			ОбластьТаблица3 = Макет.ПолучитьОбласть("Таблица3");
			ОбластьТаблица3.Параметры.СуммаМатеринскогоКапитала = 0;
			ТабличныйДокумент.Вывести(ОбластьТаблица3);
			ОбластьСтрока3 = Макет.ПолучитьОбласть("Строка3");
			НомерПП = 0;
		КонецЕсли;	
		
		Область = Макет.ПолучитьОбласть("Подвал");
		
		СведенияОбОтветственном = уп_ПользовательскиеНастройкиСервер.ПолучитьСведенияОбУполномоченномЛице(Документы.уп_РешениеОВыплатеСредствПНИзРезерваФондаПоОПС.ПустаяСсылка(),ОбъектПечати.Организация,ОбъектПечати.ДоговорОПС.Филиал,Пользователи.ТекущийПользователь(),"Приложение1",Дата);
		Область.Параметры.ДолжностьОтветственного = СведенияОбОтветственном.Должность;
		Область.Параметры.ФИООтветственного = СведенияОбОтветственном.Фамилия+" "+Лев(СведенияОбОтветственном.Имя,1)+"."+" "+Лев(СведенияОбОтветственном.Отчество,1)+".";
		
		ТабличныйДокумент.Вывести(Область); 		
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, СведенияОДокументе.Документ);
		
		//УправлениеПечатьюБП.ДополнитьДокументПодписьюИПечатью(ТабличныйДокумент, СведенияОДокументе, ОбъектыПечати, ПараметрыПечати);  
		УправлениеПечатьюБП.ДоработатьФаксимильнуюПодписьИПечать(ТабличныйДокумент, ОбъектыПечати, ОбъектПечати.Организация, СведенияОДокументе.Документ);

		#Вставка
		// +ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		ХМ_ВывестиИсполнителя(ТабличныйДокумент);
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		// -ХМНПФ 28.01.2016 ШадринАВ // ПечатьПриложение1() // №155
		#КонецВставки
	КонецЦикла;	
	Возврат ТабличныйДокумент;
КонецФункции   


