
Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 23.10.2015 Шадрин А.В. // ПечатьДокумента() // ОПС_удост_личн
	// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
	//-ХМНПФ 28.0217 Кучеров//убрал директиву на клиенте
КонецПроцедуры

// +ХМНПФ 28.0217 Кучеров// Новая Функция ХМ_ПечатьWeb()
// для получения печатных форм со стороны Web сервисов
// аналог процедуры печать типового варианта пользовательского интерфейса
Функция ХМ_ПечатьWeb(ИмяМакета) Экспорт
	ТабДокумент = Новый ТабличныйДокумент; // заглушка на случай если не сформируется ни чего 
	Если      ИмяМакета = "Заявление" Тогда
		Если Дата < Дата("20150101") Тогда
			ТабДокумент = ПечатьДокумента("Заявление");
		Иначе
			ТабДокумент = ПечатьДокумента("Заявление2015");
		КонецЕсли;
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции 

// Функция формирует табличный документ с печатной формой  документа 
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
&ИзменениеИКонтроль("ПечатьДокумента")
Функция РасшОбщ_ПечатьДокумента(ИмяМакета)
	  
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет(ИмяМакета);
  	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.НаименованиеНПФ = Организация.НаименованиеПолное;
	ФИОФизЛица = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	Фамилия = ФИОФизЛица.Фамилия;
	Имя = ФИОФизЛица.Имя;
	Отчество = ФИОФизЛица.Отчество;
	ОбластьМакета.Параметры.Фамилия = Фамилия;
	ОбластьМакета.Параметры.Имя = Имя;
	ОбластьМакета.Параметры.Отчество = Отчество;
	ОбластьМакета.Параметры.ДатаМестоРождения = Формат(ДоговорОПС.Участник.уп_ФизЛицо.ДатаРождения, "ДФ=dd.MM.yyyy")+"г. "+ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(ДоговорОПС.Участник.уп_ФизЛицо.МестоРождения);
	
	// создаем запрос для поиска в регистре сведений контактная информация
    // с параметрами юридический адрес контрагента, самого контрагента и отображаем в поле макета
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
                   |	КонтактнаяИнформация.Ссылка КАК Объект,
                   |	КонтактнаяИнформация.Вид,
                   |	КонтактнаяИнформация.Представление
                   |ИЗ
                   |	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК КонтактнаяИнформация
                   |ГДЕ
                   |	КонтактнаяИнформация.Ссылка = &ФИОЗастрахованногоЛица
                   |	И КонтактнаяИнформация.Вид.Ссылка В(&ВидАдреса)";
    			   
    Запрос.УстановитьПараметр("ФИОЗастрахованногоЛица", ДоговорОПС.Участник.уп_ФизЛицо);
	ВидАдреса = Новый СписокЗначений;
	ВидАдреса.Добавить(Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица);
	ВидАдреса.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица);
    Запрос.УстановитьПараметр("ВидАдреса",ВидАдреса);
    Результат = Запрос.Выполнить();
    ВыборкаАдресЮрЗастрахованногоЛица = Результат.Выбрать();
	Пока ВыборкаАдресЮрЗастрахованногоЛица.Следующий() Цикл
		Если ВыборкаАдресЮрЗастрахованногоЛица.Вид = Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица Тогда
    		ОбластьМакета.Параметры.Адрес = ВыборкаАдресЮрЗастрахованногоЛица.Представление;
		Иначе
    		ОбластьМакета.Параметры.Телефон = ВыборкаАдресЮрЗастрахованногоЛица.Представление;
		КонецЕсли;	
	КонецЦикла;
	
  	//находим паспортные данные застрахованного лица
	//ПаспортныеДанные = РегистрыСведений.ПаспортныеДанныеФизЛиц.ПолучитьПоследнее(Дата,Новый Структура("ФизЛицо", ДоговорОПС.Участник.уп_ФизЛицо));
	ПаспортныеДанные = уп_ОсновнаяПоставкаСервер.ПолучитьДанныеДокументаУдостоверяющегоЛичность(ДоговорОПС.Участник.уп_ФизЛицо, Дата);
	СерияПаспорта = ПаспортныеДанные.Серия;
    НомерПаспорта = ПаспортныеДанные.Номер;
    ДатаВыдачиПаспорта = ПаспортныеДанные.ДатаВыдачи;
    КемВыданПаспорт = ПаспортныеДанные.КемВыдан;
    ОбластьМакета.Параметры.СерияНомерПаспорта =  СерияПаспорта+" №"+НомерПаспорта;
    ОбластьМакета.Параметры.ДатаВыдачи = формат(ДатаВыдачиПаспорта,"ДЛФ=Д");;
    ОбластьМакета.Параметры.ПаспортКемВыдан = КемВыданПаспорт;
	
	ОбластьМакета.Параметры.НомерПФР = уп_ОПС.ПреобразоватьНомерПФР(ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР);
	
    ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для каждого стр из ЭтотОбъект.Правопреемники Цикл
		ОбластьМакета.Параметры.ФИО = стр.Фамилия+" "+Стр.Имя+" "+стр.Отчество;
		ОбластьМакета.Параметры.ДатаМестоРождения = Формат(стр.ДатаРождения, "ДЛФ=Д")+"г."+ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(стр.МестоРождения);
		ОбластьМакета.Параметры.Адрес = стр.Адрес;
		#Удаление
		ОбластьМакета.Параметры.Паспорт = "паспорт: серия "+стр.ДокументСерия+" №"+стр.ДокументНомер+", дата выдачи: "+Формат(стр.ДокументДатаВыдачи,"ДЛФ=Д")+", выдан"+стр.ДокументКемВыдан;
		#КонецУдаления
		#Вставка
		// +ХМНПФ 23.10.2015 Шадрин А.В. // ПечатьДокумента() // ОПС_удост_личн
		ОбластьМакета.Параметры.Паспорт = стр.ДокументВид.Наименование+": серия "+стр.ДокументСерия+" №"+стр.ДокументНомер+", дата выдачи: "+Формат(стр.ДокументДатаВыдачи,"ДЛФ=Д")+", выдан "+стр.ДокументКемВыдан;
		// -ХМНПФ 23.10.2015 Шадрин А.В. // ПечатьДокумента() // ОПС_удост_личн
		#КонецВставки
		ОбластьМакета.Параметры.Телефон = стр.Телефон;
		ОбластьМакета.Параметры.НомерПФР = уп_ОПС.ПреобразоватьНомерПФР(стр.СтраховойНомерПФР);
		ОбластьМакета.Параметры.Доля = стр.Доля;
    	ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.ДатаЗаявления = Дата;
    ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Оборот");
	ОбластьМакета.Параметры.ДатаЗаявления = Дата;
	ОбластьМакета.Параметры.ДатаРегистрации = ДатаРегистрации;
	ОбластьМакета.Параметры.ЧислоРегистрации = Формат(ДатаРегистрации, "ДФ=дд");
	ОбластьМакета.Параметры.МесяцРегистрации = Сред(Формат(ДатаРегистрации, "ДЛФ=дд"), 3, СтрДлина(Строка(Формат(ДатаРегистрации, "ДЛФ=дд")))-10);
	ОбластьМакета.Параметры.ГодРегистрации = Формат(ДатаРегистрации, "ДФ=гггг");
	
	ОбластьМакета.Параметры.НомерЗаявления = Номер;
	ОбластьМакета.Параметры.НаименованиеНПФ = Организация.НаименованиеПолное;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РаботникиОрганизацийСрезПоследних.Должность
	               |ИЗ
	               |	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
	               |			&Период,
	               |			Сотрудник.Физическоелицо = &ОтветственныйФизЛицо
	               |				И Организация = &Организация) КАК РаботникиОрганизацийСрезПоследних
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РаботникиОрганизацийСрезПоследних.Период УБЫВ";		
	Запрос.УстановитьПараметр("Период", ДатаРегистрации);
	Запрос.УстановитьПараметр("ОтветственныйФизЛицо", Ответственный.ФизЛицо);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТЗДолжностьОтветственного = Запрос.Выполнить().Выгрузить();
		
	//ТЗДолжностьОтветственного = РегистрыСведений.РаботникиОрганизаций.СрезПоследних(ДатаРегистрации, Новый Структура("ФизЛицо, Организация",Ответственный.ФизЛицо,Организация));
	Если ТЗДолжностьОтветственного.Количество()>0 Тогда
		ОбластьМакета.Параметры.ДолжностьОтветственного = ТЗДолжностьОтветственного.Получить(0).Должность;
	Иначе
		ОбластьМакета.Параметры.ДолжностьОтветственного = "";
	КонецЕсли;
	ОбластьМакета.Параметры.НомерРасписки = Номер;
	ОбластьМакета.Параметры.ФИОЗастрахованного = Фамилия+" "+Лев(Имя,1)+"."+Лев(Отчество,1)+".";
	ОбластьМакета.Параметры.НомерПФР = уп_ОПС.ПреобразоватьНомерПФР(ДоговорОПС.Участник.уп_ФизЛицо.СтраховойНомерПФР);
	ФИООтветственного = РегистрыСведений.ФИОФизическихЛиц.ПолучитьПоследнее(Дата, Новый Структура("ФизическоеЛицо", Ответственный.ФизЛицо));
	ОбластьМакета.Параметры.ФИООтветственного = ФИООтветственного.Фамилия+" "+Лев(ФИООтветственного.Имя,1)+"."+Лев(ФИООтветственного.Отчество,1)+".";
	ТабДокумент.Вывести(ОбластьМакета);
	Возврат ТабДокумент;
КонецФункции

&После("ОбработкаЗаполнения")
Процедура ХМ_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.уп_ДоговорыОПС") Тогда
		ДоговорОПС = ДанныеЗаполнения;
		Организация = ДоговорОПС.Организация;
	КонецЕсли;	
КонецПроцедуры

&После("ПриУстановкеНовогоНомера")
Процедура ХМ_ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)	
	ХМ_ПодпискиНаСобытия.ХМ_НомераРешенийПриУстановкеНовогоНомера(ЭтотОбъект, СтандартнаяОбработка, Префикс);	
КонецПроцедуры





