Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ 03.11.2017 ШадринАВ // ОбработкаПроведения() // №1214
	// ХМНПФ 03.11.2017 ШадринАВ // Новая ХМ_ПолучитьСостояниеДоПриостановки() // №1214
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РасшОбщ_ОбработкаПроведения(Отказ, Режим)
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначенияБПВызовСервера.ПредставлениеДокументаПриПроведении(Ссылка);

	// Проверка ручной корректировки
	Если уп_ОсновнаяПоставкаСервер.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект,Ложь) Тогда
		Возврат
	КонецЕсли;

	Отказ = Проверка(Заголовок);
	// регистр СостоянияПС 
	ТекСостояние = РегистрыСведений.уп_СостоянияДоговораОПС.ПолучитьПоследнее(Дата-1,Новый Структура("ДоговорОПС",ДоговорОПС)).Состояние;
	Если ВидОперации = Перечисления.уп_ВидыОперацийПриостановкаОПС.ПриостановкаВыплат Тогда
		#Удаление
		Если ТекСостояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод Тогда
		#КонецУдаления
		#Вставка
		// +ХМНПФ 03.11.2017 ШадринАВ // ОбработкаПроведения() // №1214		
		Если ТекСостояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод ИЛИ ТекСостояние = Перечисления.уп_СостоянияДоговоровОПС.ОбязательстваВыполнены Тогда
		// -ХМНПФ 03.11.2017 ШадринАВ // ОбработкаПроведения() // №1214
		#КонецВставки
			Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ДоговорОПС;
			Если  ТипПриостановки = 0 Тогда
				Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены;
			Иначе
				Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены;
			КонецЕсли;	
		Иначе
			Отказ = Истина;
			Сообщить("По договору ОПСу №"+ДоговорОПС.Код+" выплаты не могут быть приостановлены, т.к. счет не в выплатном периоде.")
		КонецЕсли;
	Иначе
		Если ТекСостояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены или ТекСостояние = Перечисления.уп_СостоянияДоговоровОПС.НачисленияПриостановлены Тогда
			Движение = Движения.уп_СостоянияДоговораОПС.Добавить();
			Движение.Период = Дата;
			Движение.ДоговорОПС = ДоговорОПС;
			#Удаление
			Движение.Состояние = Перечисления.уп_СостоянияДоговоровОПС.ВыплатнойПериод;
			#КонецУдаления
			#Вставка
			// +ХМНПФ 03.11.2017 ШадринАВ // ОбработкаПроведения() // №1214
			Движение.Состояние = ХМ_ПолучитьСостояниеДоПриостановки();
			// -ХМНПФ 03.11.2017 ШадринАВ // ОбработкаПроведения() // №1214
			#КонецВставки
		Иначе
			Отказ = Истина;
			Сообщить("По договору ОПСу №"+ДоговорОПС.Код+" выплаты не могут быть восстановлены, т.к. счет не приостановлен.")
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

// ХМНПФ 03.11.2017 ШадринАВ // Новая ХМ_ПолучитьСостояниеДоПриостановки() // №1214
Функция ХМ_ПолучитьСостояниеДоПриостановки()
	// - находим дату последней приостановки
	// - затем находим состояние перед приостановкой
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(СостоянияДоговораОПС.Период) КАК Период
		|ИЗ
		|	РегистрСведений.уп_СостоянияДоговораОПС КАК СостоянияДоговораОПС
		|ГДЕ
		|	СостоянияДоговораОПС.ДоговорОПС = &ДоговорОПС
		|	И СостоянияДоговораОПС.Период < &Дата
		|	И СостоянияДоговораОПС.Регистратор ССЫЛКА Документ.уп_ПриостановкаВыплатОПС
		|	И СостоянияДоговораОПС.Состояние В (ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговоровОПС.ВыплатыПриостановлены), ЗНАЧЕНИЕ(Перечисление.уп_СостоянияДоговоровОПС.НачисленияПриостановлены))";
	
	Запрос.УстановитьПараметр("ДоговорОПС", ДоговорОПС);
	Запрос.УстановитьПараметр("Дата", Дата);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		СостояниеПередПриостановкой = РегистрыСведений.уп_СостоянияДоговораОПС.ПолучитьПоследнее(Выборка.Период-1,Новый Структура("ДоговорОПС",ДоговорОПС)).Состояние;
	Иначе
		ВызватьИсключение "Не найдена приостановка выплат!";
	КонецЕсли;
	Возврат СостояниеПередПриостановкой;
КонецФункции // ХМ_ПолучитьСостояниеДоПриостановки()
