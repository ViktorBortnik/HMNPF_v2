
&НаСервере
Процедура ХМ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)

	// +ХМНПФ 07.06.2018 КлименкоМИ // ПриСозданииНаСервере() // №1686
	ХМ_УстановитьДействияФормы();
	// -ХМНПФ 07.06.2018 КлименкоМИ // ПриСозданииНаСервере() // №1686

	ХМДополнитьФорму();
	
КонецПроцедуры 

Процедура ХМДополнитьФорму() 
	
	СвойстваГруппы = Новый Структура();
	СвойстваГруппы.Вставить("ВставитьПередЭлементом",Элементы.ОтражатьБУ);
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьОбычнуюГруппу(ЭтаФорма,"ХМ_ГруппаШапкаЛевая",Элементы.ГруппаШапкаЛевая,СвойстваГруппы);
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ХМ_ВыплатаПоВозвратам",Элементы["ХМ_ГруппаШапкаЛевая"],"Объект.ХМ_ВыплатаПоВозвратам",СвойстваПоля);
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"ХМ_ЭтоВыплатаПоПереходу",Элементы["ХМ_ГруппаШапкаЛевая"],"Объект.ХМ_ЭтоВыплатаПоПереходу",СвойстваПоля);
	
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьКомандуСКнопкой(ЭтаФорма,"хм_КнопкаРасчитатьУдержания","Расчитать удержания",Элементы.СписокПенсионеров.КоманднаяПанель);
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("Вид", ВидПоляФормы.ПолеФлажка);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"СписокПенсионеровхм_Удержание",Элементы.СписокПенсионеров,"Объект.СписокПенсионеров.хм_Удержание",СвойстваПоля);
	
	СвойстваПоля = Новый Структура();
	СвойстваПоля.Вставить("ВставитьПередЭлементом", Элементы.СписокПенсионеровВыплата);  
	уп_ДинамическиеЭлементыСервер.ХМ_СоздатьПолеВвода(ЭтаФорма,"СписокПенсионеровхм_СуммаУдержания",Элементы.СписокПенсионеров,"Объект.СписокПенсионеров.хм_СуммаУдержания",СвойстваПоля);
	
КонецПроцедуры  

&НаКлиенте
Процедура хм_КнопкаРасчитатьУдержания(Команда)
	ХМЗаполнитьУдержания();
КонецПроцедуры

// ХМНПФ 11.03.2024 ПлыгунЮВ //+
&НаСервере
Процедура ХМЗаполнитьУдержания() 
	
	Если Объект.Ссылка.Проведен Тогда
		Сообщить("Расчет возможен только в непроведенном документе.");
		Возврат;
	КонецЕсли;
	Если Объект.Ссылка.Пустая() или Модифицированность Тогда
		Сообщить("Расчет возможен только в записаном документе.");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета КАК НомерСчета,
		|	уп_ВыплатныеРеестрыСписокПенсионеров.Схема КАК Схема,
		|	уп_ВыплатныеРеестрыСписокПенсионеров.Выплата КАК Выплата,
		|	хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.ВариантВозвратаИзлишнеВыплаченныхСумм КАК ВариантВозвратаИзлишнеВыплаченныхСумм,
		|	хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.ВозвращатьНаСчетДоговора КАК ВозвращатьНаСчетДоговора,
		|	хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.ПроцентУдержания КАК ПроцентУдержания,
		|	хм_УчетУдержанийНПООстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ((уп_ВыплатныеРеестрыСписокПенсионеров.Сумма - уп_ВыплатныеРеестрыСписокПенсионеров.НДФЛ - уп_ВыплатныеРеестрыСписокПенсионеров.РКО) * хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.ПроцентУдержания / 100 КАК ЧИСЛО(17, 2))) < хм_УчетУдержанийНПООстатки.СуммаОстаток
		|			ТОГДА ВЫРАЗИТЬ((уп_ВыплатныеРеестрыСписокПенсионеров.Сумма - уп_ВыплатныеРеестрыСписокПенсионеров.НДФЛ - уп_ВыплатныеРеестрыСписокПенсионеров.РКО) * хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.ПроцентУдержания / 100 КАК ЧИСЛО(17, 2))
		|		ИНАЧЕ хм_УчетУдержанийНПООстатки.СуммаОстаток
		|	КОНЕЦ КАК СуммаУдержания,
		|	уп_ВыплатныеРеестрыСписокПенсионеров.Сумма КАК Сумма,
		|	уп_ВыплатныеРеестрыСписокПенсионеров.НДФЛ КАК НДФЛ,
		|	уп_ВыплатныеРеестрыСписокПенсионеров.РКО КАК РКО
		|ИЗ
		|	Документ.уп_ВыплатныеРеестры.СписокПенсионеров КАК уп_ВыплатныеРеестрыСписокПенсионеров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.хм_ПроцентУдержанияИзВыплатыНПО.СрезПоследних(&Период, ВариантВозвратаИзлишнеВыплаченныхСумм = &Вариант) КАК хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.хм_УчетУдержанийНПО.Остатки(&Период, ) КАК хм_УчетУдержанийНПООстатки
		|			ПО хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.НомерСчета = хм_УчетУдержанийНПООстатки.НомерСчета
		|		ПО уп_ВыплатныеРеестрыСписокПенсионеров.НомерСчета = хм_ПроцентУдержанияИзВыплатыНПОСрезПоследних.НомерСчета
		|ГДЕ
		|	уп_ВыплатныеРеестрыСписокПенсионеров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Вариант", Перечисления.хм_ВариантВозвратаИзлишнеВыплаченныхСуммНПО.ПроцентОтПенсии);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = Объект.СписокПенсионеров.НайтиСтроки(Новый Структура("НомерСчета", Выборка.НомерСчета));
		СтрокаТЧ[0].хм_СуммаУдержания = Выборка.СуммаУдержания;
		//СтрокаТЧ[0].хм_Удержание = Истина;
		СтрокаТЧ[0].Выплата = СтрокаТЧ[0].Сумма-СтрокаТЧ[0].НДФЛ-СтрокаТЧ[0].РКО-СтрокаТЧ[0].хм_СуммаУдержания;
		Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
&ИзменениеИКонтроль("СписокПенсионеровСуммаПриИзменении")
Процедура ХМСписокПенсионеровСуммаПриИзменении(Элемент)
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	#Вставка
	ТекущиеДанные.Выплата = ТекущиеДанные.Сумма-ТекущиеДанные.НДФЛ-ТекущиеДанные.РКО-ТекущиеДанные.хм_СуммаУдержания;	
	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	#КонецВставки
	ТекущиеДанные.Выплата = ТекущиеДанные.Сумма-ТекущиеДанные.НДФЛ-ТекущиеДанные.РКО;
КонецПроцедуры 

// ХМНПФ 11.03.2024 ПлыгунЮВ //-
// ХМНПФ 07.06.2018 КлименкоМИ // новый ХМ_УстановитьДействияФормы() // №1686	
&НаСервере
Процедура ХМ_УстановитьДействияФормы()
	Элементы.Переместить(Элементы.СписокПенсионеров, Элементы.Страница1, Элементы.ГруппаСтраница1Подвал);
КонецПроцедуры

Процедура ХМ_НеТиповыеИзменения()
	// ХМНПФ+ 18.03.2016 ШадринАВ // новый флаг на форме ХМ_ВыплатаПоВозвратам // №281
	// ХМНПФ+ 17.10.2017 ШадринАВ // новый флаг на форме ХМ_ЭтоВыплатаПоПереходу // №1995
	// ХМНПФ 07.06.2018 КлименкоМИ // новый ХМ_УстановитьДействияФормы() // №1686	
	// ХМНПФ 07.06.2018 КлименкоМИ // ПриСозданииНаСервере() // №1686
КонецПроцедуры

